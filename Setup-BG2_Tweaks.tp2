BACKUP ~BG2_Tweaks/backup~ // location to store files for
AUTHOR ~webmaster@gibberlings3.net~ // email address displayed if install fails

/* unique combos as of v3:
SoA:  2^57 * 3^12 * 4^3 * 5^1 =    24,508,390,293,190,694,550,896,640
ToB:  2^61 * 3^12 * 4^3 * 5^1 =   392,134,244,691,051,112,814,346,240
Tutu: 2^51 * 3^12 * 4^3       =        76,588,719,666,220,920,471,552
BGT:  2^63 * 3^12 * 4^3 * 5^1 = 1,568,536,978,764,204,451,257,384,960
*/

VERSION ~v9.02~

README ~bg2_tweaks/readme-bg2tweaks.html~

ALLOW_MISSING

// p&p protection
~cdclck31.itm~
~cdring41.itm~

// storekeeper
~tem2601.sto~

~_ajanti.cre~ // tob style npcs compatibility code
~_alora.cre~
~_branwe.cre~
~_coran.cre~
~_dynahe.cre~
~_edwin.cre~
~_faldor.cre~
~_garric.cre~
~_jaheir.cre~
~_imoen1.cre~
~_kagain.cre~
~_khalid.cre~
~_kivan.cre~
~_minsc.cre~
~_montar.cre~
~_quayle.cre~
~_safana.cre~
~_sharte.cre~
~_skie.cre~
~_tiax.cre~
~_viconi.cre~
~_xan.cre~
~_xzar.cre~
~_yeslic.cre~
~ajanti.cre~
~alora.cre~
~branwe.cre~
~coran.cre~
~dynahe.cre~
~edwin.cre~
~faldor.cre~
~garric.cre~
~jaheir.cre~
~imoen.cre~
~imoen1.cre~
~kagain.cre~
~khalid.cre~
~kivan.cre~
~minsc.cre~
~montar.cre~
~bgquayle.cre~
~safana.cre~
~sharte.cre~
~skie.cre~
~tiax.cre~
~viconi.cre~
~bgxan.cre~
~bgxzar.cre~
~yeslic.cre~
~d0alassa.cre~
~m#ambr10.cre~
~subru05.cre~
~jcbruce.cre~
~chloe7.cre~
~c!delai10.cre~
~sufinch1.cre~
~fwghar04.cre~
~sc#hub.cre~
~j#indi01.cre~
~j#klsy09.cre~
~keto08.cre~
~kido.cre~
~kindrek.cre~
~r!kit07.cre~
~nath07.cre~
~sk#neht1.cre~
~sola5.cre~
~willyb.cre~
~mgwika.cre~
~tlxan.cre~
~o#xan09.cre~
~sdnpc12.cre~

~ar2812.bcs~ // miscellaneous
~_ar3601.bcs~
~_b1-3.itm~
~_bimoen.dlg~
~bimoen.dlg~
~_s1-10.itm~

~baldur25.bcs~ // tob stuff
~blun26.itm~
~blun27.itm~
~blun31.itm~
~brdflute.itm~
~chalshld.itm~
~chalarm.itm~
~chan19.itm~
~chan20.itm~
~chan21.itm~
~clck31.itm~
~elfhelm.itm~
~leat23.itm~
~leat24.itm~
~palplat.itm~
~plat21.itm~
~plat20.itm~
~plat22.itm~
~plat23.itm~
~ring41.itm~
~secret05.itm~
~shararm.itm~
~shld31.itm~
~shld32.itm~
~sw1h62.itm~
~sw1h63.itm~
~sw1h64.itm~
~sw1h65.itm~
~sw1h70.itm~
~sw1h71.itm~
~sw1h72.itm~
~sw1h75.itm~
~tamchain.itm~

~spwish07.spl~ // ToB spells
~spwish46.spl~

~clabma01.2da~ // 2das for remove xp cap
~clabma02.2da~
~clabma03.2da~
~clabma04.2da~
~clabma05.2da~
~clabma06.2da~
~clabma07.2da~
~clabma08.2da~
~clabma09.2da~

~_ar0300.bcs~ // tutu/bgt compat
~ar7400.bcs~

~_amul14.itm~ // tutu items
~cdkat2.itm~
~_chan01.itm~
~_chan02.itm~
~_chan03.itm~
~_chan04.itm~
~_chan05.itm~
~_chan06.itm~
~_chan07.itm~
~_clck01.itm~
~_clck02.itm~
~_helm01.itm~
~_helm08.itm~
~_helm09.itm~
~_helm10.itm~
~_helm11.itm~
~_helm12.itm~
~_helm13.itm~
~_helm15.itm~
~_ichan01.itm~
~_ichan04.itm~
~_ihelm01.itm~
~_ihelm10.itm~
~_ileat04.itm~
~_iplat01.itm~
~_ishld02.itm~
~_ishld03.itm~
~_leat01.itm~
~_leat02.itm~
~_leat03.itm~
~_leat04.itm~
~_leat05.itm~
~_leat07.itm~
~_leat08.itm~
~_leat09.itm~
~_misc86.itm~
~_plat01.itm~
~_plat02.itm~
~_plat04.itm~
~_plat05.itm~
~_plat06.itm~
~_plat07.itm~
~_plat08.itm~
~_plat98.itm~
~_ring06.itm~
~_ring07.itm~
~_ring25.itm~
~_shld01.itm~
~_shld02.itm~
~_shld03.itm~
~_shld04.itm~
~_shld05.itm~
~_shld06.itm~
~_shld07.itm~
~_shld08.itm~
~_shld09.itm~
~_shld10.itm~
~_shld11.itm~
~_shld12.itm~
~_shld13.itm~
~_shld14.itm~
~_shld15.itm~
~_shld16.itm~
~_shld18.itm~
~_shld19.itm~
~_shld99.itm~
~_sw1h01.itm~
~_sw1h02.itm~
~_sw1h03.itm~
~_sw1h18.itm~
~_sw1h01.itm~
~_sw1h02.itm~
~_sw1h03.itm~
~_sw1h18.itm~
~_sw1h01.itm~
~_sw1h02.itm~
~_sw1h03.itm~
~_sw1h18.itm~

~_blun09.itm~ // totsc items
~_blun11.itm~
~_chan07.itm~
~_dagg10.itm~
~_helm15.itm~
~_leat09.itm~
~_leat10.itm~
~_plat08.itm~
~_plat98.itm~
~_shld19.itm~
~_shld20.itm~
~_slng03.itm~
~_staf05.itm~
~_staf06.itm~
~_staf07.itm~
~_staf08.itm~
~_sw1h18.itm~
~_sw1h21.itm~
~_sw1h24.itm~
~_sw2h07.itm~

// ajoc's minimod
~agbladen.itm~ // blade of the night (katana)

// alex macintosh
~bitswe.itm~ // "bitter sweet" katana + 3 (katana)
~chanethe.itm~ // ethereal chain mail +5 (chain mail, no penalty to thieves or mages when wearing)
~elfmail.itm~ // elven mithril +5 (elven chain mail armour, "this superior elven chain does not interfere with your mage or thief skills. in fact, it is so light and wieldy it augments your spellcasting and thief prowess.")
~kreeplat.itm~ // full metal plate of stone +2 (full plate armour)
~serend.itm~ // "serendipity" katana + 4 (katana)

// amber
~m#asleat.itm~ // apprentice shadow armor (studded leather armour, +5% hide in shadows)
~m#buck2.itm~ // buckler +2, thorard's saucer (buckler)

// ariena
~ararmor.itm~ // ariena's armor (hide armour)

~band01.itm~   // ashes of embers
~band02.itm~
~band03.itm~
~band04.itm~
~flat01.itm~
~flat02.itm~
~flat03.itm~
~flat04.itm~
~j#spli06.itm~
~scal01.itm~
~scal02.itm~
~scal03.itm~
~scal04.itm~

// beyond the law
~kiyoarm1.itm~ // verdant vigil (scale armour)
~kiyoarm2.itm~ // verdant vigil +2 (scale armour)
~kiyoarm3.itm~ // verdant vigil +4 (scale armour)
~yoshkata.itm~ // yoshimo's katana +2 (katana)

~x#garch.itm~  // bg1 npc v7
~x#garch2.itm~
~x#ajshld.itm~
~x#fash01.itm~

~ardeadx.itm~ // bgt-bp + dsotsc + sobh
~bleat01.itm~
~bleat02.itm~
~bleat03.itm~
~blood.itm~
~bsingchn.itm~
~cb351g03.itm~
~cbbnlch8.itm~
~cbcl013.itm~
~cbcl027.itm~
~cbcl033.itm~
~cblyplat.itm~
~cblyshld.itm~
~cbmilt01.itm~
~cbmilt03.itm~
~cbmilt11.itm~
~cbmilt13.itm~
~cbmilt21.itm~
~cbsbmltk.itm~
~cbsbmltr.itm~
~cbsbmlts.itm~
~cbsbmltu.itm~
~cbsbmltv.itm~
~cbsbmltw.itm~
~cbsbmltx.itm~
~cbsbmlty.itm~
~cbshsoub.itm~
~cbuhgvar.itm~
~cbuhgvct.itm~
~celebros.itm~
~chan12zz.itm~
~chan6xx.itm~
~ddhrtles.itm~
~dplate.itm~
~dschan01.itm~
~dschan02.itm~
~dschan03.itm~
~dschan04.itm~
~dscoarm.itm~
~dscryarm.itm~
~dscuarm.itm~
~dscushld.itm~
~dsebplat.itm~
~dsfearm.itm~
~dsjearm.itm~
~dsjeshld.itm~
~dskearm.itm~
~dskorim.itm~
~dsleat01.itm~
~dsshld01.itm~
~dsshld02.itm~
~dsshld51.itm~
~dsshld52.itm~
~dsshld53.itm~
~dsshld54.itm~
~dsthrarm.itm~
~dswrkarm.itm~
~efml01.itm~
~elvarmx.itm~
~flamplat.itm~
~glory.itm~
~iceplat.itm~
~illas04.itm~
~jera.itm~
~jers.itm~
~leatraid.itm~
~maillif.itm~
~minbalpl.itm~
~minplatx.itm~
~mystery.itm~
~plat05xx.itm~
~platblue.itm~
~platraid.itm~
~platsar.itm~
~raidplat.itm~
~ritdrag.itm~
~runeplat.itm~
~samarmx1.itm~
~sgchan06.itm~
~shldgraz.itm~
~shldraid.itm~
~shldxz.itm~
~sileat.itm~
~zilfgt.itm~

// bom item pack
~o!bom001.itm~ // mother’s curse +1 (plate armour)
~o!bom002.itm~ // ranger mail +5 (chain mail armour, allows hide/move silently to be used unrestricted)
~o!bom003.itm~ // tangor’s mystical chain (chain mail)
~o!bom004.itm~ // abdel's mage chain (chain mail armour, 20% chance of miscast magic)
~o!bom005.itm~ // abdel's mage chain +1 (chain mail armour, 30% chance of miscast magic)
~o!bom006.itm~ // abdel's mage chain +2 (chain mail armour, 40% chance of miscast magic)
~o!bom007.itm~ // abdel's mage plate (plate armour, 30% chance of miscast magic)
~o!bom008.itm~ // abdel's mage plate +1 (plate armour, 40% chance of miscast magic)
~o!bom009.itm~ // abdel's mage plate +2 (plate armour, 50% chance of miscast magic)
~o!bom011.itm~ // troll hide +4 (hide armour)
~o!bom027.itm~ // beren's buckler of baleful woe (buckler)
~o!bom035.itm~ // blade of crimson cat +3 (bastard sword)
~o!bom036.itm~ // blade of the watch (bastard sword)
~o!bom037.itm~ // the black sword: "arkadion" (bastard sword)
~o!bom039.itm~ // warrior blade +3: mollusken's bone basher (katana)

~g#leat01.itm~ // cassius npc

// check the bodies
~cbbdarmr.itm~ // blue dragon plate (plate armour)
~cbchanp4.itm~ // chain mail +4 (chain mail)
~cbmhshld.itm~ // large shield +3 (large shield)
~cbmtchan.itm~ // chain of drakkas' fury (chain mail, "spell failure bonus: 10%, special: spellcasting and thieving skills can be performed without penalties while wearing drakkas' chain armor.")
~cbmtswbs.itm~ // masterwork bastard sword (bastard sword)
~cbmtswkt.itm~ // masterwork katana (katana)
~cbpdofcm.itm~ // shield of the purple dragon (medium shield)
~cbplat13.itm~ // gorgon plate +5 (plate armour)
~cbshldwk.itm~ // waukeen's defender (medium shield?)
~cbsnwmdn.itm~ // the snow maiden's reaver (bastard sword)
~cbtempus.itm~ // holy shield of tempus (large shield)
~cbtsch01.itm~ // chain mail +5 (chain mail armour)
~cbtspt01.itm~ // full plate mail +5 (full plate mail armour)
~cbtssd01.itm~ // large shield +5 (large shield)
~cbtsst01.itm~ // splint mail +4 (splint mail)
~cbwtni5a.itm~ // shield of belvin (large shield?)
~cbwtni6a.itm~ // sword of belvin (bastard sword)
~cbxtgcdd.itm~ // masterwork chain mail armor +1 (chain mail armour)
~cbxtshdd.itm~ // masterwork leather armor +4 (studded leather armour)

// com bg adventure pack
~cmleat5.itm~ // nature's divine (leather armour)
~cmshld01.itm~ // shield +3 (kite/tower/body shield)
~machan01.itm~ // splint mail, mystra's armor (splint mail)
~macmle01.itm~ // woodland armor (leather armour)
~macmle02.itm~ // studded leather, stormcloud (studded leather armour)
~maplat02.itm~ // dwarven mithril plate (plate armour)
~mashld01.itm~ // large mithril shied +3 (large shield)
~mplat01.itm~ // king's plate mail +5 (plate armour)

// com dsotc revision
~brueplt1.itm~ // bruenor's armor, plate mail +2 (plate armour)
~brueshld.itm~ // small shield +1 (small shield)
~chands01.itm~ // chain mail +2, blessed chain mail (chain mail)
~chands05.itm~ // sukien holy armor (splint mail armour)
~chands06.itm~ // elven chainmail +1 (elven chain mail)
~chands08.itm~ // crusaders armor (splint mail armour)
~chands09.itm~ // chain mail +3 (chain mail)
~dragslay.itm~ // sword of balduran (bastard sword)
~drowchn1.itm~ // drow adamantite mesh armor +1 (drow mesh armour)
~drowchn2.itm~ // drow adamantite mesh armor +2 (drow mesh armour)
~drowchn3.itm~ // drow adamantite mesh armor +3 (drow mesh armour)
~drowplt1.itm~ // drow adamantite plate armor +3 (drow plate armour)
~drowplt2.itm~ // drow adamantite plate armor +4 (drow plate armour)
~drowplt3.itm~ // drow adamantite plate armor +5 (drow plate armour)
~drowshd1.itm~ // drow adamantite buckler +1 (drow buckler)
~drowshd2.itm~ // drow adamantite buckler +2 (drow buckler)
~drowshd3.itm~ // drow adamantite buckler +3 (drow buckler)
~ebonplt.itm~ // full plate armor +1: 'ebon plate (full plate armour)
~gnwml.itm~ // gnomish workman's leather +2 (studded leather armour)
~katan1.itm~ // katana +2 "masamune" (katana 2handed)
~katan2.itm~ // katana +4, "muramasa" (katana 2handed)
~korim.itm~ // splint mail armor +4 - korim's skin (splint mail armour)
~leat08a.itm~ // improved shadow armor (shadow studded armour with +15% hide in shadows)
~leata7.itm~ // studded leather armor +3 (studded leather armour)
~leatds01.itm~ // armor of thorns (studded leather armour)
~leatds02.itm~ // cúchoinneach personal leather armor +4 (leather armour)
~leatds03.itm~ // thorfinn's hide armor (hide armour)
~leatds04.itm~ // keiria silverstring's studded leather bard armor +1 ("leather +1 protection and gives her the ability to cast spells while wearing it.")
~leatds05.itm~ // cúchoinneach personal leather armor +4 (leather armour)
~platds01.itm~ // crystal plate mail (plate armour)
~platds03.itm~ // ferthgil's dwarven mithril plate battle armor (plate armour)
~platds04.itm~ // conchobhair strongblade's blessed plate mail armor (plate armour
~platds05.itm~ // plate mail +3 : 'the practical defense (plate armour)
~shld51.itm~ // small shield +2 'sun shield' (small shield)
~shld52.itm~ // medium shield +2 [/i](medium shield)[/i]
~shld53.itm~ // medium shield +2 'heart of the dragon' (medium shield)
~shld54.itm~ // large shield +2, +7 vs. missiles: 'the wall' (large shield)
~shldds01.itm~ // cúchoinneach's small shield +3 "the moon" (small shield)
~slayer1.itm~ // small shield +2 (small shield)
~slayer3.itm~ // jet'laya's chainmail (chain mail armour)
~sw1h18.itm~ // sword of balduran (bastard sword)
~sw1hds01.itm~ // champion sword (bastard sword)
~warlkarm.itm~ // stormcloud "kraken's bane" (leather armour)
~wlfleat.itm~ // wulfgar's fur armor (leather armour)
~wulfhide.itm~ // wulfgar's fur armor (leather armour)

// com encounters
~coma11.itm~ // green dragon's plate (plate armour)
~ubswd5a.itm~ // cancerous bastard sword +4 (bastard sword)
~ucmgch01.itm~ // drow chain +3 (elven chain mail, can cast spells faster too, does not decay in sunlight)
~ucmgch02.itm~ // drow admantine chain +5 (drow elven chain mail, does not decay on surface)
~ucmgle01.itm~ // woodland armor +6 (studded leather, druid usable, +20% bonus to hide in shadows)
~ucmgpl01.itm~ // winter king's plate (plate armour)
~ucmgsh01.itm~ // medium shield +3 (medium shield)
~ucmgsh02.itm~ // drow shield +3 (medium shield)
~ucmgsw02.itm~ // katana +3, "el tel'lerian" (katana)
~ucmgsw04.itm~ // katana +4 "toran el desiade" (katana)
~ucmgsw11.itm~ // katana +4 death's bane (katana)

// com item upgrade
~maforg07.itm~ // improved chainmail +3, chainmail +3: mail of the dead (chainmail)
~mapla001.itm~ // duragons plate +4 (plate armour)

~gcmch01.itm~ // dark ritual
~gcmpl01.itm~
~gcmsh01.itm~
~gcmsh02.itm~
~cmgcha01.itm~
~cmgforg7.itm~
~cmgforg8.itm~

// tdd
~ardeadx.itm~ // armour of the dead +3 (leather armour)
~blood.itm~ // bathed in blood (plate armour)
~bsingchn.itm~ // mirathar (bladesinger chainmail)
~celebros.itm~ // mail of celebros (splint armour)
~deathreb.itm~ // death reborn (bastard sword)
~flamplat.itm~ // plate of smoldering (plate armour)
~glory.itm~ // the glory of suffering (plate armour)
~guardshz.itm~ // soul guard (tower shield)
~iceplat.itm~ // ice plate (plate armour)
~kibaxx.itm~ // tuki kiba (katana)
~king.itm~ // winter king's plate (hide armour)
~orrshld.itm~ // orrick's rhino beetle shield (tower shield)
~platblue.itm~ // blue dragon scale (scale armour)
~samarmx1.itm~ // samurai armour ("steel bands have been masterfully interlaced with strips of hardened leather for this light yet resilient suit of armor.")
~shar20.itm~ // blade master leather (leather armour)
~shldred.itm~ // the red knight's shield (medium shield)
~swanarm.itm~ // black swan armor (chain armour?)
~swzzz1.itm~ // elemental life (katana)
~swzzz2.itm~ // elemental death (katana)
~trollarm.itm~ // troll flesh (hide armour?)
~ushld3b.itm~ // storming shield +3 (tower shield)
~waukeen.itm~ // waukeen's defender (small shield)
~zzg7ts.itm~ // trollslayer (bastard sword)

~platj4.itm~ // deeper shadows of amn: red dragon grothgar component

~coma04.itm~ // desecration of souls
~coma05.itm~
~coma06.itm~
~coma07.itm~
~coms01.itm~
~coms02.itm~
~platgr01.itm~

// domains of the dead
~metcrys.itm~ // crystal sword +4 (bastard sword)

~xarm001.itm~ // exenem's addon
~xarm002.itm~
~xarm003.itm~
~xarm004.itm~
~xarm005.itm~
~xarm006.itm~

~deitm053.itm~ // fr-rov
~deitm086.itm~
~deitm087.itm~
~deitm088.itm~
~deitm089.itm~
~deitm090.itm~
~deitm096.itm~
~deitm119.itm~
~deitm120.itm~
~deitm121.itm~
~deitm122.itm~

~fwchan01.itm~  // fw item pack
~fwchan02.itm~
~fwchan03.itm~
~fwchan04.itm~
~fwleat01.itm~
~fwleat02.itm~
~fwleat03.itm~
~fwleat04.itm~
~fwleat05.itm~
~fwleat06.itm~
~fwplat01.itm~
~fwplat02.itm~
~fwplat03.itm~
~fwplat04.itm~
~fwshld01.itm~

// ghrey's im7
~j#blaz01.itm~ // bastard sword +1, "hell's maw" (bastard sword)

~juschan.itm~  // tgc v1.70
~_juschan.itm~
~grblun.itm~
~_grblun.itm~

// iiitem
~i1ch001.itm~ // smoke armor (robe type?)
~i1ch002.itm~ // voice of doom (chain mail)
~i1lt001.itm~ // skeleton ribcage (leather armour?)
~i1lt002.itm~ // anti-imprisonment armor (leather armour?, move silently: -20, lock picking: -10, find/remove traps: -10, pick pockets: -10)
~i1pl001.itm~ // gubbie armor (plate armour)

// improved anvil
~s!chan01.itm~ // supreme chain mail +6 (chain mail armour)
~s!chan02.itm~ // eternal melody +5 (chain armour, can cast spells in it for bards)
~s!jan01.itm~ // jansen adventure wear (enhanced) (as per jan jansen's existing armour)
~s!leat01.itm~ // improved human flesh (leather armour)
~s!leat02.itm~ // spirit of the night (leather armour)
~s!leat03.itm~ // improved corthala family armor (as per valygar's existing armour)
~s!plat01.itm~ // enhanced pride of the legion +4 (full plate armor)
~s!plat02.itm~ // huskar lord's armor +5 (full plate armour)
~s!plat03.itm~ // improved ankheg mail (as per improved ankheg mail in item upgrade)
~s!shld01.itm~ // delryn family legacy +4 (as per anomen's existing shield)
~s!shld02.itm~ // supreme shelter +6 (tower shield)
~s!shld03.itm~ // buckler +3 (buckler)
~s!sw01.itm~ // corthala family true blade +5 (katana)
~s!sw13.itm~ // death of a thousand cuts +4 (bastard sword)

~r#plat.itm~ // improved asylum
~r#runed.itm~
~r#swan.itm~

// improved battles
~b#firbla.itm~ // bastard sword +4: virtue's defiance (bastard sword)
~b#sw1h06.itm~ // bastard sword +4: tainted sin (bastard sword)

// indira
~j#indi01.itm~ // indi's chain vest (chainmail, can cast spells without penalties)

~c2anom01.itm~ // item upgrade v32
~c2blun01.itm~
~c2blun03.itm~
~c2chan01.itm~
~c2hide01.itm~
~c2keld01.itm~
~c2leat01.itm~
~c2plat01.itm~
~c2shld01.itm~
~c2shld02.itm~
~c2valy01.itm~

// killing wolf npc
~plat74.itm~ // full plate mail of unknown origin (full plate armour, very light)
~sw1h75.itm~ // katana of unknown origin (katana)

// kindrek
~kinplat.itm~ // kindrek's full plate (full plate armour)
~kkchain.itm~ // chain mail +3 (chain mail)

// kitanya
~r!kitplt.itm~ // bringer of vengeance: kitanya's plate mail (plate armour, specifically tailored for the npc)
~r!kitshd.itm~ // mageslayer shield (large shield/medium shield, i can't see the item icon without installing)

~tzchan01.itm~ // the magnificant magic shop
~tzchan02.itm~
~tzplat01.itm~
~tzshld01.itm~

~mdsiplat.itm~ // mordan's xmas mod

~sgchan1.itm~ // munchmod
~sgchan2.itm~
~sgchan3.itm~
~sgleat1.itm~
~sgleat2.itm~
~sgleat3.itm~
~sgleat4.itm~
~sgplat1.itm~
~sgplat2.itm~
~sgshld1.itm~
~sgsplt1.itm~
~sgsplt2.itm~
~sgsplt3.itm~

// nathaniel
~fhsw1.itm~ // aplin-fletcher sword (katana)

~shghost.itm~ // neh'taniel

// north side of the sword coast
~ntchan03.itm~ // chain mail +3: 'sage's armor' (chain mail armour)
~ntchan04.itm~ // elven chainmail of sorcery (elven chainmail)
~ntchan05.itm~ // chailmail +5: 'ebon chain' (chainmail)
~ntchan06.itm~ // chainmail of the firewine (elven chainmail)
~ntleat01.itm~ // improved shadow armor (studded leather armour, +15% hide in shadows)
~ntleat02.itm~ // will's armor with cloak (leather armour)
~ntleat03.itm~ // will's armor with cloak (leather armour)
~ntleat04.itm~ // studded leather armor +3 (studded leather armour)
~ntleat05.itm~ // studded leather of resistance (studded leather armour)
~ntplat01.itm~ // (plate armour)
~ntshld01.itm~ // small shield +2, 'shield of free action' (small shield)
~ntshld02.itm~ // shield of the firewine (large shield)
~ntshld03.itm~ // reinforced large shield (large shield)

~psq117.itm~ // planar sphere mod
~psq58.itm~
~psq59.itm~
~psq60.itm~
~psq61.itm~
~psq62.itm~
~psq63.itm~
~psq64.itm~
~psq65.itm~
~psq66.itm~

~d0leat08.itm~ // quest pack

~buck01.itm~ // rogue rebalancing, old versions
~buck02.itm~
~buck03.itm~
~buck04.itm~
~buck05.itm~
~buck06.itm~
~cyleat.itm~

// new rr
~rr#abih.itm~ // abishai hide (studded leather armor)
~rr#buc01.itm~ // kiel's buckler (buckler +1)
~rr#buc02.itm~ // buckler +1 (also present in tutu and the bg1 portion of bgt)
~rr#buc03.itm~ // buckler +2 (also present in tutu and the bg1 portion of bgt)
~rr#buc04.itm~ // buckler +3
~rr#chn01.itm~ // dark elven chain (elven chain)
~rr#guara.itm~ // the guardian (medium shield +2, non-metallic, usable by druids)
~rr#rward.itm~ // rogue's ward (buckler +2)
~rr#shl01.itm~ // last defense (buckler +1, also present in tutu and the bg1 portion of bgt)
~rr#shl02.itm~ // safeguard (buckler +2, also present in tutu and the bg1 portion of bgt)

~u#chan01.itm~ // ruad v19
~u#leat01.itm~
~u#leat02.itm~
~u#plat01.itm~
~u#plat02.itm~
~u#shld01.itm~

// saerilith
~nsshld01.itm~ // hermes' shield +1 (medium shield)
~nsshld02.itm~ // elemental shield +4 (medium shield)

// selune's armory
~mgileat1.itm~ // studded leather +3, cured leaf hauberk (studded leather armour)
~mgiplat1.itm~ // full plate +1, restless spirit (full plate armour)
~mgishld1.itm~ // medium shield +2, featherweight (medium shield)
~mgishld2.itm~ // small shield +2, shield of fortitude (small shield)

// sos
~cbcl005.itm~ // the argent shield (medium shield)
~cbcl013.itm~ // baleful mail (chainmail)
~cbcl033.itm~ // mail of life (chainmail(
~cbcl037.itm~ // mystery of the dead (large shield)
~cbcl041.itm~ // the snow maiden's reaver (bastard sword)
~cbcl055.itm~ // bastard sword +3: defender (bastard sword)
~cbcl058.itm~ // waukeen's defender (medium shield?)
~cblyplat.itm~ // lyrar's plate (plate armour)
~cblyshld.itm~ // lyrar's shield (large shield)
~cbrjr015.itm~ // dzance's guardian (medium shield?)
~cbrjr017.itm~ // laeral's storm armor (full plate armour, non-metallic)
~cbrolfsh.itm~ // rolf sureblade's shield (medium shield? or tower shield?)
~cbrolfsw.itm~ // rolf sureblade's sword (bastard sword)
~cbuhgvar.itm~ // holy armor of torm (plate armour)

// shed's im5
~leatveng.itm~ // vengeful shadows (studded leather armour)
~sdim526.itm~ // shed's iron modder 5
~shedsw01.itm~ // sinistra (katana)
~shedsw02.itm~ // dextra (katana)

// shed's mods
~sd_2sw1.itm~ // sinistra (katana)
~sd_2sw2.itm~ // dextra (katana)
~sd_vs.itm~ // vengeful shadows (studded leather armour, +10% hide in shadows)

~elemplat.itm~ // sp item pack
~elemshld.itm~

~c!gplate.itm~ // sp stuff

~a!bchan1.itm~ // song & silence
~a!bleat1.itm~
~a!bleat2.itm~
~a!brsb.itm~
~a!thcl.itm~

// turnabout
~d0t#sw11.itm~ // bastard sword +4 (bastard sword)
~d0t#sw12.itm~ // katana +4 (katana)
~d0t#leat.itm~ // studded leather armour +3 (studded leather armour)

// tsujatha
~lotharmo.itm~ // lothaullan (plate armour)

// underrep items
~u#chan07.itm~ // elven chainmail of the hand +3
~u#hfheah.itm~ // cornugan hide armour (hide armour +1)
~u#swdb94.itm~ // miasmic bastard sword
~u#swdb95.itm~ // bastard sword of wrath
~u#swdbrc.itm~ // rage of chaos (bastard sword)

~pchan.itm~  // the unusual oddities shop
~sshldmag.itm~

// the vault
~echan01.itm~ // elven chainmail +2 (elven chainmail)
~echan02.itm~ // underdark mail +1 (elven chainmail)
~leatbal.itm~ // bala's leather +4 (hide armour)
~leatbar.itm~ // barbarian hide +1 (hide armour)
~platwy.itm~ // wyvern's scale ("fashioned from the hide of a dead wyvern, the armor is as cumbersome as scalemail but provides the protection of magical platemail")

// yikari
~sdnpcsw3.itm~ // celestial fury (katana)
~sdnpcsw4.itm~ // celestial fury +5 (katana)

AUTO_TRA ~BG2_Tweaks/languages/%s~

LANGUAGE
  ~English~
  ~english~
  ~BG2_Tweaks/languages/english/description_updates.tra~
  ~BG2_Tweaks/languages/english/setup.tra~
LANGUAGE
  ~Czech (Translation by Razfallow and Vlasák)~
  ~czech~
  ~BG2_Tweaks/languages/english/setup.tra~
  ~BG2_Tweaks/languages/czech/setup.tra~
LANGUAGE
  ~Francais (Translation by Anomaly, Elgaern the Dragon Slayer, Isaya and elminster)~
  ~french~
  ~BG2_Tweaks/languages/english/setup.tra~
  ~BG2_Tweaks/languages/french/setup.tra~
LANGUAGE
  ~Deutsch (Uebersetzung von Leonardo Watson, Caswallon und Jester)~
  ~german~
  ~BG2_Tweaks/languages/english/description_updates.tra~
  ~BG2_Tweaks/languages/english/setup.tra~
  ~BG2_Tweaks/languages/german/description_updates.tra~
  ~BG2_Tweaks/languages/german/setup.tra~
LANGUAGE
  ~Italiano (translation by Andrea Colombo; original EoU by Antonio Favata)~
  ~italian~
  ~BG2_Tweaks/languages/english/description_updates.tra~
  ~BG2_Tweaks/languages/english/setup.tra~
  ~BG2_Tweaks/languages/italian/description_updates.tra~
  ~BG2_Tweaks/languages/italian/setup.tra~
LANGUAGE
  ~Korean (Translation by web2air)~
  ~korean~
  ~BG2_Tweaks/languages/english/setup.tra~
  ~BG2_Tweaks/languages/korean/setup.tra~
LANGUAGE
  ~Polski (Translation by yarpen, Artanis, dragionian, Evendur, Grjgori, and Neminus)~
  ~polish~
  ~BG2_Tweaks/languages/english/setup.tra~
  ~BG2_Tweaks/languages/polish/setup.tra~
LANGUAGE
  ~Russian (Translation by Creepin, Domi, Kulyok, Serdrick, and aerie.ru)~
  ~russian~
  ~BG2_Tweaks/languages/english/setup.tra~
  ~BG2_Tweaks/languages/russian/setup.tra~
LANGUAGE
  ~Espanol (traducido por Clan DLAN)~
  ~spanish~
  ~BG2_Tweaks/languages/english/description_updates.tra~
  ~BG2_Tweaks/languages/english/setup.tra~
  ~BG2_Tweaks/languages/spanish/description_updates.tra~
  ~BG2_Tweaks/languages/spanish/setup.tra~
LANGUAGE
  ~Chinese (translated by Miranda)~
  ~chinese~
  ~BG2_Tweaks/languages/english/description_updates.tra~
  ~BG2_Tweaks/languages/english/setup.tra~
  ~BG2_Tweaks/languages/chinese/description_updates.tra~
  ~BG2_Tweaks/languages/chinese/setup.tra~

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// Remove Helmet Animations                         \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @1000 DESIGNATED 10
GROUP @11

// this file does nothing, it just allows other mods to detect this component
COPY_EXISTING ~sw1h01.itm~ ~override/cdt00010.g3~

PRINT @1
COPY_EXISTING_REGEXP GLOB ~^.+\.itm$~ ~override~
PATCH_IF (SOURCE_SIZE > 0x71) THEN BEGIN // protects against invalid files
  READ_SHORT 0x1c "itemtype" ELSE 0
  READ_SHORT 0x22 anim
  PATCH_IF ("%itemtype%" = 7) && ! (anim=0x5759 || anim=0x575a) BEGIN
	WRITE_LONG 0x22 0
  END
END
  BUT_ONLY_IF_IT_CHANGES

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// Change Imoen's Avatar to Mage                    \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @2000 DESIGNATED 20
GROUP @11
REQUIRE_PREDICATE NOT FILE_EXISTS_IN_GAME ~_sw1h01.itm~ @10 // non-Tutu

// this file does nothing, it just allows other mods to detect this component
COPY_EXISTING ~sw1h01.itm~ ~override/cdt00020.g3~

PRINT @1
COPY_EXISTING_REGEXP GLOB ~^.+\.cre$~ ~override~ // Imoen animation adjustments
  READ_LONG   0x08 "name"  ELSE 0
  READ_SHORT  0x28 "anim"  ELSE 0
  READ_BYTE  0x273 "class" ELSE 0
  PATCH_IF ((("%name%" = 16453) OR ("%name%" = 16454)) AND ("%class%" = 13) AND ("%anim%" = 25360)) BEGIN
    WRITE_SHORT 0x28 25104
  END
  BUT_ONLY_IF_IT_CHANGES

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// Change Nalia's Avatar to Thief                   \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @3000 DESIGNATED 30
GROUP @11
GROUP @19
REQUIRE_PREDICATE NOT FILE_EXISTS_IN_GAME ~_sw1h01.itm~ @10 // non-Tutu

// this file does nothing, it just allows other mods to detect this component
COPY_EXISTING ~sw1h01.itm~ ~override/cdt00030.g3~

PRINT @1
COPY_EXISTING_REGEXP GLOB ~^.+\.cre$~ ~override~ // Nalia animation adjustments
  READ_LONG   0x08 "name"  ELSE 0
  READ_SHORT  0x28 "anim"  ELSE 0
  READ_BYTE  0x273 "class" ELSE 0
  PATCH_IF ((("%name%" = 9102) OR ("%name%" = 9103)) AND ("%class%" = 13) AND ("%anim%" = 25104)) BEGIN
    WRITE_SHORT 0x28 25360
  END
  BUT_ONLY_IF_IT_CHANGES

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// Change Viconia's Skin Color to Dark Blue         \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @4000 DESIGNATED 40
GROUP @11

// this file does nothing, it just allows other mods to detect this component
COPY_EXISTING ~sw1h01.itm~ ~override/cdt00040.g3~

PRINT @1
COPY_EXISTING_REGEXP GLOB ~^.+\.cre$~ ~override~ // Viconia skin color change
  READ_LONG 0x08 "name" ELSE 0
  PATCH_IF (("%name%" = 6132) OR ("%name%" = 9489) OR ("%name%" = 9508)) BEGIN
    WRITE_BYTE 0x2F 96
  END
  BUT_ONLY_IF_IT_CHANGES

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// Avatar Morphing Script                           \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @5000 DESIGNATED 50
GROUP @11

// adds ToB trigger/actions to SoA
INCLUDE ~BG2_Tweaks/lib/tob2soa.tpa~

COMPILE ~BG2_Tweaks/baf/ikmorph.baf~
        ~BG2_Tweaks/baf/0ikmorph.baf~
        ~BG2_Tweaks/dlg/ikmorph.d~

COPY_EXISTING ~0ikmorph.bcs~ ~scripts/0ikmorph.bs~

APPEND ~scrpdesc.2da~ ~0IKMorph IKMorph1 IKMorph2~

COPY_EXISTING ~scrpdesc.2da~ ~override~
  REPLACE ~IKMorph1~ @5001
  REPLACE ~IKMorph2~ @5002

COPY ~BG2_Tweaks/cre/ikmorph.cre~  ~override~
     ~BG2_Tweaks/eff/ikmorph.eff~  ~override~
     ~BG2_Tweaks/spl/ikmorph.spl~  ~override~

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// Weapon Animation Tweaks                          \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @6000 DESIGNATED 60
GROUP @11
GROUP @19

// this file does nothing, it just allows other mods to detect this component
COPY_EXISTING ~sw1h01.itm~ ~override/cdt00060.g3~

PRINT @1
COPY_EXISTING_REGEXP GLOB ~^.+\.itm$~ ~override~ // animation adjustments
  PATCH_IF (SOURCE_SIZE > 0x71) THEN BEGIN // protects against invalid files
    READ_BYTE  0x18 "flags"
    READ_ASCII 0x22 "anim" (2)
    READ_BYTE  0x31 "prof"
    READ_LONG  0x64 "abil_off"
    READ_SHORT 0x68 "abil_num"
    PATCH_IF ((("%prof%" = 100) OR ("%prof%" = 101)) AND ("%anim%" STRING_COMPARE_CASE "ms" = 0)) BEGIN
      WRITE_ASCII 0x22 "MC"
    END
    PATCH_IF ((("%flags%" BAND 0b00000010) = 0b00000000) AND   // one-handed...
              (("%anim%" STRING_COMPARE_CASE "cl" = 0) OR      // club
               ("%anim%" STRING_COMPARE_CASE "mc" = 0) OR      // mace
               ("%anim%" STRING_COMPARE_CASE "ms" = 0))) BEGIN // or morningstar
      FOR (index = 0 ; index < abil_num ; index = index + 1) BEGIN
        READ_BYTE ("%abil_off%" + ("%index%" * 0x38)) "type"
        PATCH_IF ("%type%" = 1) BEGIN
          WRITE_SHORT ("%abil_off%" + 0x2C + ("%index%" * 0x38)) 50 // Overhead
          WRITE_SHORT ("%abil_off%" + 0x2E + ("%index%" * 0x38)) 50 // Backhand
          WRITE_SHORT ("%abil_off%" + 0x30 + ("%index%" * 0x38))  0 // Thrusting
        END
      END
    END ELSE
    PATCH_IF ((("%anim%" STRING_COMPARE_CASE "sp" = 0) AND (("%flags%" BAND 0b00000010) = 0b00000010)) OR     // two-handed spears
              (("%anim%" STRING_COMPARE_CASE "ss" = 0) AND (("%flags%" BAND 0b00000010) = 0b00000000))) BEGIN // one-handed short swords
      FOR (index = 0 ; index < abil_num ; index = index + 1) BEGIN
        READ_BYTE ("%abil_off%" + ("%index%" * 0x38)) "type"
        PATCH_IF ("%type%" = 1) BEGIN
          WRITE_SHORT ("%abil_off%" + 0x2C + ("%index%" * 0x38)) 10 // Overhead
          WRITE_SHORT ("%abil_off%" + 0x2E + ("%index%" * 0x38)) 20 // Backhand
          WRITE_SHORT ("%abil_off%" + 0x30 + ("%index%" * 0x38)) 70 // Thrusting
        END
      END
    END ELSE
    PATCH_IF (("%anim%" STRING_COMPARE_CASE "qs" = 0) AND (("%flags%" BAND 0b00000010) = 0b00000010)) BEGIN // two-handed staves
      FOR (index = 0 ; index < abil_num ; index = index + 1) BEGIN
        READ_BYTE ("%abil_off%" + ("%index%" * 0x38)) "type"
        PATCH_IF ("%type%" = 1) BEGIN
          WRITE_SHORT ("%abil_off%" + 0x2C + ("%index%" * 0x38)) 20 // Overhead
          WRITE_SHORT ("%abil_off%" + 0x2E + ("%index%" * 0x38)) 70 // Backhand
          WRITE_SHORT ("%abil_off%" + 0x30 + ("%index%" * 0x38)) 10 // Thrusting
        END
      END
    END
  END
  BUT_ONLY_IF_IT_CHANGES

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// Icewind Dale Casting Graphics                    \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @7000 DESIGNATED 70
GROUP @11
GROUP @19

// this file does nothing, it just allows other mods to detect this component
COPY_EXISTING ~sw1h01.itm~ ~override/cdt00070.g3~

COPY ~BG2_Tweaks/bam/cgabjura.bam~ ~override~
     ~BG2_Tweaks/bam/cgaltera.bam~ ~override~
     ~BG2_Tweaks/bam/cgconjur.bam~ ~override~
     ~BG2_Tweaks/bam/cgdivina.bam~ ~override~
     ~BG2_Tweaks/bam/cgenchan.bam~ ~override~
     ~BG2_Tweaks/bam/cgillusi.bam~ ~override~
     ~BG2_Tweaks/bam/cginvoca.bam~ ~override~
     ~BG2_Tweaks/bam/cgnecrom.bam~ ~override~

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                            \\\\\
///// Restore SoA Logo for ToB Games                             \\\\\
/////                                                            \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @8000 DESIGNATED 80
GROUP @11
GROUP @19
REQUIRE_PREDICATE     FILE_EXISTS_IN_GAME ~mel01.cre~   @12 // ToB required
REQUIRE_PREDICATE NOT FILE_EXISTS_IN_GAME ~_sw1h01.itm~ @10 // non-Tutu

// this file does nothing, it just allows other mods to detect this component
COPY_EXISTING ~sw1h01.itm~ ~override/cdt00080.g3~

//Force the game to use the SoA files in the biffs
MOVE ~override/loadcntr.bam~ ~bg2_tweaks/backup~
     ~override/gprogbar.mos~ ~bg2_tweaks/backup~
     ~override/gtrspsk.mos~  ~bg2_tweaks/backup~
     ~override/gtrspsk2.mos~ ~bg2_tweaks/backup~
     ~override/stoneopt.mos~ ~bg2_tweaks/backup~


/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// Remove icons added by equipping items            \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @9000 DESIGNATED 90
GROUP @11

// this file does nothing, it just allows other mods to detect this component
COPY_EXISTING ~sw1h01.itm~ ~override/cdt00090.g3~

PRINT @1
COPY_EXISTING_REGEXP GLOB ~^.+\.itm$~ ~override~
  READ_LONG  0x6a "fx_off" ELSE 0
  READ_SHORT 0x70 "fx_num" ELSE 0
  FOR (index = 0 ; index < fx_num ; index = index + 1) BEGIN
    READ_SHORT ("%fx_off%" +        ("%index%" * 0x30)) "type"
    READ_BYTE  ("%fx_off%" + 0x0c + ("%index%" * 0x30)) "timing"
    PATCH_IF (("%type%" = 142) AND ("%timing%" = 2)) BEGIN // display portrait icon and instant/while equipped
      WRITE_BYTE  ("%fx_off%" + 0x12 + ("%index%" * 0x30)) 0 // high probability
      WRITE_BYTE  ("%fx_off%" + 0x13 + ("%index%" * 0x30)) 0 // low probability
    END
  END
  BUT_ONLY_IF_IT_CHANGES

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// Commoners use drab colors                        \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @10000 DESIGNATED 100
GROUP @11

// this file does nothing, it just allows other mods to detect this component
COPY_EXISTING ~sw1h01.itm~ ~override/cdt00100.g3~

COPY_EXISTING ~randcolr.2da~ ~override~
  SET_2DA_ENTRY_LATER ~randcolr~  3  9 ~114~ // AthPeasantMajor
  SET_2DA_ENTRY_LATER ~randcolr~  4  9 ~107~
  SET_2DA_ENTRY_LATER ~randcolr~  5  9  ~36~
  SET_2DA_ENTRY_LATER ~randcolr~  6  9  ~37~
  SET_2DA_ENTRY_LATER ~randcolr~  7  9  ~38~
  SET_2DA_ENTRY_LATER ~randcolr~  8  9  ~39~
  SET_2DA_ENTRY_LATER ~randcolr~  9  9  ~40~
  SET_2DA_ENTRY_LATER ~randcolr~ 10  9  ~41~
  SET_2DA_ENTRY_LATER ~randcolr~ 11  9  ~42~
  SET_2DA_ENTRY_LATER ~randcolr~ 12  9  ~43~
  SET_2DA_ENTRY_LATER ~randcolr~  3 10  ~63~ // AthPeasantMinor
  SET_2DA_ENTRY_LATER ~randcolr~  4 10  ~64~
  SET_2DA_ENTRY_LATER ~randcolr~  5 10  ~65~
  SET_2DA_ENTRY_LATER ~randcolr~  6 10  ~66~
  SET_2DA_ENTRY_LATER ~randcolr~  7 10  ~87~
  SET_2DA_ENTRY_LATER ~randcolr~  8 10  ~91~
  SET_2DA_ENTRY_LATER ~randcolr~  9 10  ~92~
  SET_2DA_ENTRY_LATER ~randcolr~ 10 10  ~93~
  SET_2DA_ENTRY_LATER ~randcolr~ 11 10  ~94~
  SET_2DA_ENTRY_LATER ~randcolr~ 12 10  ~95~
  SET_2DA_ENTRY_LATER ~randcolr~  3 13 ~114~ // TradePeasantMajor
  SET_2DA_ENTRY_LATER ~randcolr~  4 13 ~107~
  SET_2DA_ENTRY_LATER ~randcolr~  5 13  ~36~
  SET_2DA_ENTRY_LATER ~randcolr~  6 13  ~37~
  SET_2DA_ENTRY_LATER ~randcolr~  7 13  ~38~
  SET_2DA_ENTRY_LATER ~randcolr~  8 13  ~39~
  SET_2DA_ENTRY_LATER ~randcolr~  9 13  ~40~
  SET_2DA_ENTRY_LATER ~randcolr~ 10 13  ~41~
  SET_2DA_ENTRY_LATER ~randcolr~ 11 13  ~42~
  SET_2DA_ENTRY_LATER ~randcolr~ 12 13  ~43~
  SET_2DA_ENTRY_LATER ~randcolr~  3 14  ~63~ // TradePeasantMinor
  SET_2DA_ENTRY_LATER ~randcolr~  4 14  ~64~
  SET_2DA_ENTRY_LATER ~randcolr~  5 14  ~65~
  SET_2DA_ENTRY_LATER ~randcolr~  6 14  ~66~
  SET_2DA_ENTRY_LATER ~randcolr~  7 14  ~87~
  SET_2DA_ENTRY_LATER ~randcolr~  8 14  ~91~
  SET_2DA_ENTRY_LATER ~randcolr~  9 14  ~92~
  SET_2DA_ENTRY_LATER ~randcolr~ 10 14  ~93~
  SET_2DA_ENTRY_LATER ~randcolr~ 11 14  ~94~
  SET_2DA_ENTRY_LATER ~randcolr~ 12 14  ~95~
  SET_2DA_ENTRIES_NOW ~randcolr~ 1
  BUT_ONLY_IF_IT_CHANGES

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// Better Icons                                     \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @11000 DESIGNATED 110
GROUP @11
GROUP @19

// this file does nothing, it just allows other mods to detect this component
COPY_EXISTING ~sw1h01.itm~ ~override/cdt00110.g3~

COPY ~BG2_Tweaks/bam/cdgchan.bam~  ~override~
     ~BG2_Tweaks/bam/cdgelchn.bam~ ~override~
     ~BG2_Tweaks/bam/cdgleat.bam~  ~override~
     ~BG2_Tweaks/bam/gclck01.bam~  ~override~
     ~BG2_Tweaks/bam/gclck02.bam~  ~override~
     ~BG2_Tweaks/bam/iclck09.bam~  ~override~
     ~BG2_Tweaks/bam/iclck10.bam~  ~override~
     ~BG2_Tweaks/bam/iclck11.bam~  ~override~
     ~BG2_Tweaks/bam/iclck12.bam~  ~override~
     ~BG2_Tweaks/bam/iclck13.bam~  ~override~
     ~BG2_Tweaks/bam/iclck15.bam~  ~override~
     ~BG2_Tweaks/bam/iclck16.bam~  ~override~
     ~BG2_Tweaks/bam/iclck17.bam~  ~override~

// chain mail
COPY_EXISTING ~_chan01.itm~  ~override~
              ~_chan02.itm~  ~override~
              ~_chan03.itm~  ~override~
              ~_chan06.itm~  ~override~
              ~_chan07.itm~  ~override~
              ~_ichan01.itm~ ~override~
              ~_juschan.itm~ ~override~
              ~chan01.itm~   ~override~
              ~chan02.itm~   ~override~
              ~chan03.itm~   ~override~
              ~chan06.itm~   ~override~
              ~chan07.itm~   ~override~
              ~chan08.itm~   ~override~
              ~chan09.itm~   ~override~
              ~chan10.itm~   ~override~
              ~chan11.itm~   ~override~
              ~chan21.itm~   ~override~
              ~ichan01.itm~  ~override~
              ~juschan.itm~  ~override~
              ~npchan.itm~   ~override~
              ~shararm.itm~  ~override~
              ~tamchain.itm~ ~override~
              ~vischan1.itm~ ~override~
  READ_ASCII 0x44 "icon" ELSE 0
  PATCH_IF (("%icon%" STRING_COMPARE_CASE "gchan01" = 0) OR
            ("%icon%" STRING_COMPARE_CASE "_gchan01" = 0)) BEGIN
    WRITE_ASCII 0x44 ~cdgchan~ #8
  END
  BUT_ONLY_IF_IT_CHANGES

// elven chain
COPY_EXISTING ~a!bchan1.itm~ ~override~
              ~chan12.itm~   ~override~
              ~chan13.itm~   ~override~
              ~chan14.itm~   ~override~
              ~chan15.itm~   ~override~
              ~chan16.itm~   ~override~
              ~chan19.itm~   ~override~
              ~clolth.itm~   ~override~
              ~dwchan01.itm~ ~override~
              ~dwchan02.itm~ ~override~
              ~scal04.itm~   ~override~
              ~x#garch.itm~  ~override~
              ~x#garch2.itm~ ~override~
  READ_ASCII 0x44 "icon" ELSE 0
  PATCH_IF (("%icon%" STRING_COMPARE_CASE "gchan01" = 0) OR
            ("%icon%" STRING_COMPARE_CASE "_gchan01" = 0)) BEGIN
    WRITE_ASCII 0x44 ~cdgelchn~
  END
  BUT_ONLY_IF_IT_CHANGES

// leather armor
COPY_EXISTING ~_leat01.itm~  ~override~
              ~_leat02.itm~  ~override~
              ~_leat03.itm~  ~override~
              ~_leat09.itm~  ~override~
              ~leat01.itm~   ~override~
              ~leat02.itm~   ~override~
              ~leat03.itm~   ~override~
              ~leat09.itm~   ~override~
              ~leat11.itm~   ~override~
              ~leat12.itm~   ~override~
              ~leat13.itm~   ~override~
              ~leat14.itm~   ~override~
              ~leat21.itm~   ~override~
              ~leat22.itm~   ~override~
              ~ttleat.itm~   ~override~
              ~visleat1.itm~ ~override~
  READ_ASCII 0x44 "icon" ELSE 0
  PATCH_IF (("%icon%" STRING_COMPARE_CASE "gleat01" = 0) OR
            ("%icon%" STRING_COMPARE_CASE "_gleat01" = 0)) BEGIN
    WRITE_ASCII 0x44 ~cdgleat~ #8
  END
  BUT_ONLY_IF_IT_CHANGES

// if Tutu, move BG2 icons to Tutu defaults
ACTION_IF FILE_EXISTS_IN_GAME ~_gleat01.bam~ THEN BEGIN

  COPY_EXISTING ~gleat01.bam~ ~override/_gleat01.bam~
                ~gchan01.bam~ ~override/_gchan01.bam~

END

// Valygar's armor should use chain graphic on avatars
COPY_EXISTING ~npchan.itm~ ~override~
  WRITE_ASCII 0x22 ~3A~ #2
  BUT_ONLY_IF_IT_CHANGES

ACTION_IF FILE_EXISTS_IN_GAME ~quiver04.itm~ THEN BEGIN

  COPY_EXISTING ~quiver02.itm~ ~override~
                ~quiver04.itm~ ~override~
    WRITE_ASCII 0x3a ~iquiver0~ #8
    BUT_ONLY_IF_IT_CHANGES

END

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// Change Avatar When Wearing Robes or Armor        \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @12000 DESIGNATED 120
GROUP @11

COPY ~BG2_Tweaks/spl/lcarmor.spl~  ~override~
     ~BG2_Tweaks/spl/lcarmor2.spl~ ~override~
     ~BG2_Tweaks/spl/lcarmorc.spl~ ~override~
     ~BG2_Tweaks/spl/lcarmorf.spl~ ~override~
     ~BG2_Tweaks/spl/lcarmort.spl~ ~override~
     ~BG2_Tweaks/spl/lcrobe.spl~   ~override~
     ~BG2_Tweaks/spl/lcrobe2.spl~  ~override~

COPY ~BG2_Tweaks/eff/fc_dwar2.eff~ ~override~
     ~BG2_Tweaks/eff/fc_dwarf.eff~ ~override~
     ~BG2_Tweaks/eff/fc_el2.eff~   ~override~
     ~BG2_Tweaks/eff/fc_elf.eff~   ~override~
     ~BG2_Tweaks/eff/fc_gnom2.eff~ ~override~
     ~BG2_Tweaks/eff/fc_gnome.eff~ ~override~
     ~BG2_Tweaks/eff/fc_half2.eff~ ~override~
     ~BG2_Tweaks/eff/fc_halfl.eff~ ~override~
     ~BG2_Tweaks/eff/fc_hfor2.eff~ ~override~
     ~BG2_Tweaks/eff/fc_hforc.eff~ ~override~
     ~BG2_Tweaks/eff/fc_huma2.eff~ ~override~
     ~BG2_Tweaks/eff/fc_human.eff~ ~override~
     ~BG2_Tweaks/eff/ff_dwar2.eff~ ~override~
     ~BG2_Tweaks/eff/ff_dwarf.eff~ ~override~
     ~BG2_Tweaks/eff/ff_el2.eff~   ~override~
     ~BG2_Tweaks/eff/ff_elf.eff~   ~override~
     ~BG2_Tweaks/eff/ff_gnom2.eff~ ~override~
     ~BG2_Tweaks/eff/ff_gnome.eff~ ~override~
     ~BG2_Tweaks/eff/ff_half2.eff~ ~override~
     ~BG2_Tweaks/eff/ff_halfl.eff~ ~override~
     ~BG2_Tweaks/eff/ff_hfor2.eff~ ~override~
     ~BG2_Tweaks/eff/ff_hforc.eff~ ~override~
     ~BG2_Tweaks/eff/ff_huma2.eff~ ~override~
     ~BG2_Tweaks/eff/ff_human.eff~ ~override~
     ~BG2_Tweaks/eff/fm_dwar2.eff~ ~override~
     ~BG2_Tweaks/eff/fm_dwarf.eff~ ~override~
     ~BG2_Tweaks/eff/fm_el2.eff~   ~override~
     ~BG2_Tweaks/eff/fm_elf.eff~   ~override~
     ~BG2_Tweaks/eff/fm_gnom2.eff~ ~override~
     ~BG2_Tweaks/eff/fm_gnome.eff~ ~override~
     ~BG2_Tweaks/eff/fm_half2.eff~ ~override~
     ~BG2_Tweaks/eff/fm_halfl.eff~ ~override~
     ~BG2_Tweaks/eff/fm_huma2.eff~ ~override~
     ~BG2_Tweaks/eff/fm_human.eff~ ~override~
     ~BG2_Tweaks/eff/ft_dwar2.eff~ ~override~
     ~BG2_Tweaks/eff/ft_dwarf.eff~ ~override~
     ~BG2_Tweaks/eff/ft_el2.eff~   ~override~
     ~BG2_Tweaks/eff/ft_elf.eff~   ~override~
     ~BG2_Tweaks/eff/ft_gnom2.eff~ ~override~
     ~BG2_Tweaks/eff/ft_gnome.eff~ ~override~
     ~BG2_Tweaks/eff/ft_half2.eff~ ~override~
     ~BG2_Tweaks/eff/ft_halfl.eff~ ~override~
     ~BG2_Tweaks/eff/ft_hfor2.eff~ ~override~
     ~BG2_Tweaks/eff/ft_hforc.eff~ ~override~
     ~BG2_Tweaks/eff/ft_huma2.eff~ ~override~
     ~BG2_Tweaks/eff/ft_human.eff~ ~override~
     ~BG2_Tweaks/eff/lcarmor2.eff~ ~override~
     ~BG2_Tweaks/eff/lcarmorc.eff~ ~override~
     ~BG2_Tweaks/eff/lcarmorf.eff~ ~override~
     ~BG2_Tweaks/eff/lcarmort.eff~ ~override~
     ~BG2_Tweaks/eff/lcrobe2.eff~  ~override~
     ~BG2_Tweaks/eff/mc_dwar2.eff~ ~override~
     ~BG2_Tweaks/eff/mc_dwarf.eff~ ~override~
     ~BG2_Tweaks/eff/mc_el2.eff~   ~override~
     ~BG2_Tweaks/eff/mc_elf.eff~   ~override~
     ~BG2_Tweaks/eff/mc_gnom2.eff~ ~override~
     ~BG2_Tweaks/eff/mc_gnome.eff~ ~override~
     ~BG2_Tweaks/eff/mc_half2.eff~ ~override~
     ~BG2_Tweaks/eff/mc_halfl.eff~ ~override~
     ~BG2_Tweaks/eff/mc_hfor2.eff~ ~override~
     ~BG2_Tweaks/eff/mc_hforc.eff~ ~override~
     ~BG2_Tweaks/eff/mc_huma2.eff~ ~override~
     ~BG2_Tweaks/eff/mc_human.eff~ ~override~
     ~BG2_Tweaks/eff/mf_dwar2.eff~ ~override~
     ~BG2_Tweaks/eff/mf_dwarf.eff~ ~override~
     ~BG2_Tweaks/eff/mf_el2.eff~   ~override~
     ~BG2_Tweaks/eff/mf_elf.eff~   ~override~
     ~BG2_Tweaks/eff/mf_gnom2.eff~ ~override~
     ~BG2_Tweaks/eff/mf_gnome.eff~ ~override~
     ~BG2_Tweaks/eff/mf_half2.eff~ ~override~
     ~BG2_Tweaks/eff/mf_halfl.eff~ ~override~
     ~BG2_Tweaks/eff/mf_hfor2.eff~ ~override~
     ~BG2_Tweaks/eff/mf_hforc.eff~ ~override~
     ~BG2_Tweaks/eff/mf_huma2.eff~ ~override~
     ~BG2_Tweaks/eff/mf_human.eff~ ~override~
     ~BG2_Tweaks/eff/mm_dwar2.eff~ ~override~
     ~BG2_Tweaks/eff/mm_dwarf.eff~ ~override~
     ~BG2_Tweaks/eff/mm_el2.eff~   ~override~
     ~BG2_Tweaks/eff/mm_elf.eff~   ~override~
     ~BG2_Tweaks/eff/mm_gnom2.eff~ ~override~
     ~BG2_Tweaks/eff/mm_gnome.eff~ ~override~
     ~BG2_Tweaks/eff/mm_half2.eff~ ~override~
     ~BG2_Tweaks/eff/mm_halfl.eff~ ~override~
     ~BG2_Tweaks/eff/mm_huma2.eff~ ~override~
     ~BG2_Tweaks/eff/mm_human.eff~ ~override~
     ~BG2_Tweaks/eff/mt_dwar2.eff~ ~override~
     ~BG2_Tweaks/eff/mt_dwarf.eff~ ~override~
     ~BG2_Tweaks/eff/mt_el2.eff~   ~override~
     ~BG2_Tweaks/eff/mt_elf.eff~   ~override~
     ~BG2_Tweaks/eff/mt_gnom2.eff~ ~override~
     ~BG2_Tweaks/eff/mt_gnome.eff~ ~override~
     ~BG2_Tweaks/eff/mt_half2.eff~ ~override~
     ~BG2_Tweaks/eff/mt_halfl.eff~ ~override~
     ~BG2_Tweaks/eff/mt_hfor2.eff~ ~override~
     ~BG2_Tweaks/eff/mt_hforc.eff~ ~override~
     ~BG2_Tweaks/eff/mt_huma2.eff~ ~override~
     ~BG2_Tweaks/eff/mt_human.eff~ ~override~

// make monks immune to avatar shifting
COPY_EXISTING ~spcl812.spl~ ~override~ // movement rate bonus spell
  READ_LONG  0x64 "abil_off"
  READ_SHORT 0x68 "abil_num"
  READ_LONG  0x6a "fx_off"
  SET "new_fx" = 2
  FOR (index = 0; index < abil_num; index = index + 1) BEGIN
    READ_SHORT  ("%abil_off%" + 0x1e + (0x28 * "%index%")) "abil_fx_num"
    WRITE_SHORT ("%abil_off%" + 0x1e + (0x28 * "%index%")) ("%abil_fx_num%" + "%new_fx%")
    READ_SHORT  ("%abil_off%" + 0x20 + (0x28 * "%index%")) "abil_fx_idx"
    SET         "abil_fx_idx" = ("%abil_fx_idx%" + ("%new_fx%" * "%index%"))
    WRITE_SHORT ("%abil_off%" + 0x20 + (0x28 * "%index%")) "%abil_fx_idx%"
    INSERT_BYTES  ("%fx_off%" +        (0x30 * "%abil_fx_idx%")) 0x30
      WRITE_SHORT ("%fx_off%" +        (0x30 * "%abil_fx_idx%")) 206       // protection from spell
      WRITE_BYTE  ("%fx_off%" + 0x02 + (0x30 * "%abil_fx_idx%")) 2         // target: preset target
      SAY         ("%fx_off%" + 0x04 + (0x30 * "%abil_fx_idx%")) #-1       // suppress battle text
      WRITE_BYTE  ("%fx_off%" + 0x0c + (0x30 * "%abil_fx_idx%")) 9         // instant/permanent
      WRITE_BYTE  ("%fx_off%" + 0x12 + (0x30 * "%abil_fx_idx%")) 100       // probability
      WRITE_ASCII ("%fx_off%" + 0x14 + (0x30 * "%abil_fx_idx%")) ~lcrobe~  // spell
    INSERT_BYTES  ("%fx_off%" +        (0x30 * "%abil_fx_idx%")) 0x30
      WRITE_SHORT ("%fx_off%" +        (0x30 * "%abil_fx_idx%")) 206       // protection from spell
      WRITE_BYTE  ("%fx_off%" + 0x02 + (0x30 * "%abil_fx_idx%")) 2         // target: preset target
      SAY         ("%fx_off%" + 0x04 + (0x30 * "%abil_fx_idx%")) #-1       // suppress battle text
      WRITE_BYTE  ("%fx_off%" + 0x0c + (0x30 * "%abil_fx_idx%")) 9         // instant/permanent
      WRITE_BYTE  ("%fx_off%" + 0x12 + (0x30 * "%abil_fx_idx%")) 100       // probability
      WRITE_ASCII ("%fx_off%" + 0x14 + (0x30 * "%abil_fx_idx%")) ~lcarmor~ // spell
  END
  BUT_ONLY_IF_IT_CHANGES

PRINT @1
COPY_EXISTING_REGEXP GLOB ~^.+\.itm$~ ~override~
  READ_SHORT 0x1c "itemtype" ELSE 0
  PATCH_IF ("%itemtype%" = 2) BEGIN
    READ_ASCII 0x22 "anim" (2)
    READ_LONG  0x64 "abil_off"
    READ_SHORT 0x68 "abil_num"
    READ_LONG  0x6a "fx_off"
    READ_SHORT 0x70 "fx_num"
    SET "new_fx" = 1
    FOR (loops = 0 ; loops < abil_num ; loops = loops + 1) BEGIN // adjusts effects on abilities up by new_fx
      READ_SHORT  (0x20 + "%abil_off%" + ("%loops%" * 0x38)) "abil_fx_idx"
      WRITE_SHORT (0x20 + "%abil_off%" + ("%loops%" * 0x38)) ("%abil_fx_idx%" + "%new_fx%")
    END
    WRITE_SHORT 0x70 ("%fx_num%" + "%new_fx%")
    INSERT_BYTES  ("%fx_off%" +        ("%fx_num%" * 0x30)) 0x30 // new effect
      WRITE_SHORT ("%fx_off%" +        ("%fx_num%" * 0x30)) 146  // cast spell
      WRITE_BYTE  ("%fx_off%" + 0x02 + ("%fx_num%" * 0x30)) 1    // target self
      WRITE_LONG  ("%fx_off%" + 0x04 + ("%fx_num%" * 0x30)) 1    // cast at level
      WRITE_BYTE  ("%fx_off%" + 0x08 + ("%fx_num%" * 0x30)) 1    // cast instantly? yes
      WRITE_BYTE  ("%fx_off%" + 0x0c + ("%fx_num%" * 0x30)) 2    // instant/while equipped
      WRITE_BYTE  ("%fx_off%" + 0x12 + ("%fx_num%" * 0x30)) 100  // probability 100
    PATCH_IF (
               ("%anim%" STRING_COMPARE_CASE "2w" = 0) OR
               ("%anim%" STRING_COMPARE_CASE "3w" = 0) OR
               ("%anim%" STRING_COMPARE_CASE "4w" = 0)
             ) BEGIN // if robe
      WRITE_ASCII  ("%fx_off%" + 0x14 + ("%fx_num%" * 0x30)) ~lcrobe~  // robe spell effect
    END ELSE BEGIN
      WRITE_ASCII  ("%fx_off%" + 0x14 + ("%fx_num%" * 0x30)) ~lcarmor~ // armor spell effect
    END
  END
  BUT_ONLY_IF_IT_CHANGES

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// Force all dialogue to pause                      \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @13000 DESIGNATED 130
GROUP @11
GROUP @19

// this file does nothing, it just allows other mods to detect this component
COPY_EXISTING ~sw1h01.itm~ ~override/cdt00130.g3~

PRINT @1
COPY_EXISTING_REGEXP GLOB ~^.+\.dlg$~ ~override~
  PATCH_IF (SOURCE_SIZE > 0x2f) THEN BEGIN // protects against invalid files
    READ_LONG  0x0c "offset_state"
    PATCH_IF ("%offset_state%" = 0x30) BEGIN // adds pause field to older dlg formats
      READ_LONG 0x14 "offset_response"
      READ_LONG 0x1c "num_s_trigger"
      READ_LONG 0x18 "offset_s_trigger"
      READ_LONG 0x20 "offset_r_trigger"
      READ_LONG 0x24 "num_r_trigger"
      READ_LONG 0x28 "offset_action"
      READ_LONG 0x2c "num_action"
      FOR (index = 0 ; index < num_s_trigger ; index = index + 1) BEGIN
        READ_LONG  ("%offset_s_trigger%" + ("%index%" * 0x08)) "indiv_offset"
        WRITE_LONG ("%offset_s_trigger%" + ("%index%" * 0x08)) ("%indiv_offset%" + 0x04)
      END
      FOR (index = 0 ; index < num_r_trigger ; index = index + 1) BEGIN
        READ_LONG  ("%offset_r_trigger%" + ("%index%" * 0x08)) "indiv_offset"
        WRITE_LONG ("%offset_r_trigger%" + ("%index%" * 0x08)) ("%indiv_offset%" + 0x04)
      END
      FOR (index = 0 ; index < num_action ; index = index + 1) BEGIN
        READ_LONG  ("%offset_action%" + ("%index%" * 0x08)) "indiv_offset"
        WRITE_LONG ("%offset_action%" + ("%index%" * 0x08)) ("%indiv_offset%" + 0x04)
      END
      WRITE_LONG 0x0c ("%offset_state%"     + 0x04)
      WRITE_LONG 0x14 ("%offset_response%"  + 0x04)
      WRITE_LONG 0x18 ("%offset_s_trigger%" + 0x04)
      WRITE_LONG 0x20 ("%offset_r_trigger%" + 0x04)
      WRITE_LONG 0x28 ("%offset_action%"    + 0x04)
      INSERT_BYTES 0x30 4
    END ELSE BEGIN // otherwise just sets to pause
      WRITE_LONG 0x30 0
    END
  END
  BUT_ONLY_IF_IT_CHANGES

// allows us to regexp match tabs and newlines
INCLUDE ~BG2_Tweaks/lib/extra_regexp_vars.tpa~

// pausing actually causing issues with drow duels, so:
COPY_EXISTING ~udlesa.bcs~ ~override~
  DECOMPILE_BCS_TO_BAF
    REPLACE_TEXTUALLY ~\(IF[%tab% %lnl%%mnl%%wnl%]+!CombatCounter(0)[%tab% %lnl%%mnl%%wnl%]+Global("DuelOn","AR2202",0)\)\([%tab% %lnl%%mnl%%wnl%]+\)\(THEN\)~
      ~\1\2Global("PlayerDuelingLesaonar","GLOBAL",0)\2\3~
  COMPILE_BAF_TO_BCS
  BUT_ONLY_IF_IT_CHANGES

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// More Interjections                               \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @101000 DESIGNATED 1010
GROUP @13
GROUP @19

// this file does nothing, it just allows other mods to detect this component
COPY_EXISTING ~sw1h01.itm~ ~override/cdt01010.g3~

ACTION_IF FILE_EXISTS_IN_GAME ~mrimrin1.itm~ THEN BEGIN // imoen romance compatibility code

  COPY_EXISTING ~imoen2j.dlg~ ~override~
    DECOMPILE_DLG_TO_D
      REPLACE_TEXTUALLY ~!I[fs]ValidForPartyDialog\(ue\)?(Player\([2-6]\))~ ~!Range(Player\2,30)~
    COMPILE_D_TO_DLG
    BUT_ONLY_IF_IT_CHANGES

END

COPY_EXISTING_REGEXP GLOB ~^.+\.bcs$~ ~override~
  DECOMPILE_BCS_TO_BAF
    REPLACE_TEXTUALLY ~I[fs]ValidForPartyDialog\(ue\)?(~
                      ~InParty(~
  COMPILE_BAF_TO_BCS
  IF ~16552~
  BUT_ONLY_IF_IT_CHANGES

COPY_EXISTING ~trigger.ids~ ~override~
  REPLACE_TEXTUALLY ~0x40A8~ ~0x4043~
  BUT_ONLY_IF_IT_CHANGES

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// Alter HP Triggers for NPC Wounded Dialogues      \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @102000 DESIGNATED 1020
GROUP @13
GROUP @19
REQUIRE_PREDICATE NOT FILE_EXISTS_IN_GAME ~_sw1h01.itm~ @10 // non-Tutu

// this file does nothing, it just allows other mods to detect this component
COPY_EXISTING ~sw1h01.itm~ ~override/cdt01020.g3~

ACTION_IF NOT FILE_EXISTS_IN_GAME ~shoutids.ids~ THEN BEGIN // if no shoutids, make one

  COPY ~BG2_Tweaks/ids/shoutids.ids~ ~override~

END

// alter triggers in dialogues...
COMPILE ~BG2_Tweaks/dlg/hptriggers.d~

// and corresponding triggers in scripts
COPY_EXISTING ~edwin.bcs~ ~override~
  DECOMPILE_BCS_TO_BAF
    REPLACE_TEXTUALLY ~HPPercentLT(Myself,15)~ ~HPPercentLT(Myself,50)~
  COMPILE_BAF_TO_BCS
  BUT_ONLY_IF_IT_CHANGES

COPY_EXISTING ~jaheira.bcs~ ~override~
  DECOMPILE_BCS_TO_BAF
    REPLACE_TEXTUALLY ~HPLT("Aerie",10)~ ~HPPercentLT("Aerie",50)~
    REPLACE_TEXTUALLY ~HPLT("Nalia",10)~ ~HPPercentLT("Nalia",50)~
    REPLACE_TEXTUALLY ~HPLT(Myself,15)~  ~HPPercentLT(Myself,50)~
    REPLACE_TEXTUALLY ~HPGT(Myself,14)~  ~!HPPercentLT(Myself,50)~
  COMPILE_BAF_TO_BCS
  BUT_ONLY_IF_IT_CHANGES

COPY_EXISTING ~mazzy.bcs~ ~override~
  DECOMPILE_BCS_TO_BAF
    REPLACE_TEXTUALLY ~HPPercentLT("Valygar",20)~ ~HPPercentLT("Valygar",50)~
  COMPILE_BAF_TO_BCS
  BUT_ONLY_IF_IT_CHANGES

COPY_EXISTING ~minsc.bcs~ ~override~
  DECOMPILE_BCS_TO_BAF
    REPLACE_TEXTUALLY ~HPLT(Myself,20)~    ~HPPercentLT(Myself,50)~
    REPLACE_TEXTUALLY ~HPGT(Myself,19)~    ~!HPPercentLT(Myself,50)~
    REPLACE_TEXTUALLY ~HPLT("Keldorn",20)~ ~HPPercentLT("Keldorn",50)~
  COMPILE_BAF_TO_BCS
  BUT_ONLY_IF_IT_CHANGES

COPY_EXISTING ~nalia.bcs~ ~override~
  DECOMPILE_BCS_TO_BAF
    REPLACE_TEXTUALLY ~HPPercentLT("Aerie",25)~ ~HPPercentLT("Aerie",50)~
  COMPILE_BAF_TO_BCS
  BUT_ONLY_IF_IT_CHANGES

COPY_EXISTING ~yoshimo.bcs~ ~override~
  DECOMPILE_BCS_TO_BAF
    REPLACE_TEXTUALLY ~HPLT("Mazzy",20)~ ~HPPercentLT("Mazzy",50)~
  COMPILE_BAF_TO_BCS
  BUT_ONLY_IF_IT_CHANGES

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                            \\\\\
///// Reveal wilderness areas on map before chapter 6            \\\\\
/////                                                            \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @103000 DESIGNATED 1030
GROUP @13
REQUIRE_PREDICATE NOT FILE_EXISTS_IN_GAME ~_sw1h01.itm~ @10 // non-Tutu

// this file does nothing, it just allows other mods to detect this component
COPY_EXISTING ~sw1h01.itm~ ~override/cdt01030.g3~

ACTION_IF FILE_EXISTS_IN_GAME ~cdbehbla.pro~ THEN BEGIN // easy fix if fixed worldmap present

  COPY_EXISTING ~worldmap.wmp~ ~override~
    READ_LONG 0x30 "area_num"
    READ_LONG 0x34 "area_off"
    FOR (index = 0 ; index < area_num ; index = index + 1) BEGIN
      READ_ASCII ("%area_off%" + ("%index%" * 0xf0)) "areafile"
      PATCH_IF (("ar1700" STRING_COMPARE_CASE "%areafile%" = 0) OR     // small teeth pass
                ("ar1800" STRING_COMPARE_CASE "%areafile%" = 0) OR     // north forest
                ("ar2600" STRING_COMPARE_CASE "%areafile%" = 0)) BEGIN // forest of tethir
        READ_BYTE  ("%area_off%" + 0x30 + ("%index%" * 0xf0)) "flags"
        WRITE_BYTE ("%area_off%" + 0x30 + ("%index%" * 0xf0)) ("%flags%" BOR 0b00000010)
      END
    END
    BUT_ONLY_IF_IT_CHANGES

END ELSE BEGIN

  // adds new entrance points to wmp areas that don't have one already
  COPY_EXISTING ~ar1700.are~ ~override~
                ~ar1800.are~ ~override~
                ~ar2600.are~ ~override~
    READ_LONG 0x54 "actor_off"
    READ_LONG 0x5c "info_off"
    READ_LONG 0x60 "spawn_off"
    READ_LONG 0x68 "ent_off"
    READ_LONG 0x6c "ent_num"
    READ_LONG 0x70 "cont_off"
    READ_LONG 0x78 "item_off"
    READ_LONG 0x7c "vert_off"
    READ_LONG 0x84 "amb_off"
    READ_LONG 0x88 "var_off"
    READ_LONG 0xa0 "bmp_off"
    READ_LONG 0xa8 "door_off"
    READ_LONG 0xb0 "anim_off"
    READ_LONG 0xb8 "tiled_off"
    READ_LONG 0xbc "song_off"
    READ_LONG 0xc0 "rest_off"
    READ_LONG 0xc4 "note_off"
    PATCH_IF ("ar1700.are" STRING_COMPARE_CASE ~%SOURCE_FILE%~ = 0) BEGIN
      SET "x_coord" = 3375
      SET "y_coord" = 142
    END ELSE
    PATCH_IF ("ar1800.are" STRING_COMPARE_CASE ~%SOURCE_FILE%~ = 0) BEGIN
      SET "x_coord" = 1325
      SET "y_coord" = 56
    END ELSE BEGIN
      SET "x_coord" = 3856
      SET "y_coord" = 115
    END
    WRITE_LONG 0x6c ("%ent_num%" + 1)
    INSERT_BYTES "%ent_off%" 0x68
      WRITE_ASCII "%ent_off%" ~CDExit~ // entrance name
      WRITE_SHORT ("%ent_off%" + 0x20) "%x_coord%"
      WRITE_SHORT ("%ent_off%" + 0x22) "%y_coord%"
    PATCH_IF NOT ("%actor_off%" < "%ent_off%") BEGIN
      WRITE_LONG 0x54 ("%actor_off%" + 0x68)
    END
    PATCH_IF NOT ("%info_off%" < "%ent_off%") BEGIN
      WRITE_LONG 0x5c ("info_off" + 0x68)
    END
    PATCH_IF NOT ("%spawn_off%" < "%ent_off%") BEGIN
      WRITE_LONG 0x60 ("spawn_off" + 0x68)
    END
    PATCH_IF NOT ("%cont_off%" < "%ent_off%") BEGIN
      WRITE_LONG 0x70 ("cont_off" + 0x68)
    END
    PATCH_IF NOT ("%item_off%" < "%ent_off%") BEGIN
      WRITE_LONG 0x78 ("item_off" + 0x68)
    END
    PATCH_IF NOT ("%vert_off%" < "%ent_off%") BEGIN
      WRITE_LONG 0x7c ("vert_off" + 0x68)
    END
    PATCH_IF NOT ("%amb_off%" < "%ent_off%") BEGIN
      WRITE_LONG 0x84 ("amb_off" + 0x68)
    END
    PATCH_IF NOT ("%var_off%" < "%ent_off%") BEGIN
      WRITE_LONG 0x88 ("var_off" + 0x68)
    END
    PATCH_IF NOT ("%bmp_off%" < "%ent_off%") BEGIN
      WRITE_LONG 0xa0 ("bmp_off" + 0x68)
    END
    PATCH_IF NOT ("%door_off%" < "%ent_off%") BEGIN
      WRITE_LONG 0xa8 ("door_off" + 0x68)
    END
    PATCH_IF NOT ("%anim_off%" < "%ent_off%") BEGIN
      WRITE_LONG 0xb0 ("anim_off" + 0x68)
    END
    PATCH_IF NOT ("%tiled_off%" < "%ent_off%") BEGIN
      WRITE_LONG 0xb8 ("tiled_off" + 0x68)
    END
    PATCH_IF NOT ("%song_off%" < "%ent_off%") BEGIN
      WRITE_LONG 0xbc ("song_off" + 0x68)
    END
    PATCH_IF NOT ("%rest_off%" < "%ent_off%") BEGIN
      WRITE_LONG 0xc0 ("rest_off" + 0x68)
    END
    PATCH_IF NOT ("%note_off%" < "%ent_off%") BEGIN
      WRITE_LONG 0xc4 ("note_off" + 0x68)
    END
    BUT_ONLY_IF_IT_CHANGES

  // need to add link if fixed worldmap not installed
  COPY_EXISTING ~worldmap.wmp~ ~override~
    READ_LONG  0x30 "area_num"
    READ_LONG  0x34 "area_off"
    READ_LONG  0x38 "link_off"
    READ_LONG  0x3c "link_num"
    WRITE_LONG 0x3c ("%link_num%" + 1)
    SET "link_idx_nsrt" = 999999
    FOR (index = 0 ; index < area_num ; index = index + 1) BEGIN
      READ_ASCII ("%area_off%" + ("%index%" * 0xf0)) "areafile"
      PATCH_IF ("ar0020" STRING_COMPARE_CASE "%areafile%" = 0) BEGIN // City Gates
        SET "area_to" = "%index%"
      END
      PATCH_IF ("ar1800" STRING_COMPARE_CASE "%areafile%" = 0) BEGIN    // north forest
        READ_BYTE  ("%area_off%" + 0x30 + ("%index%" * 0xf0)) "flags"
        WRITE_BYTE ("%area_off%" + 0x30 + ("%index%" * 0xf0)) ("%flags%" BOR 0b00000010)
        SET "nf_idx" = "%index%"
      END
      PATCH_IF ("ar2600" STRING_COMPARE_CASE "%areafile%" = 0) BEGIN // forest of tethir
        READ_BYTE  ("%area_off%" + 0x30 + ("%index%" * 0xf0)) "flags"
        WRITE_BYTE ("%area_off%" + 0x30 + ("%index%" * 0xf0)) ("%flags%" BOR 0b00000010)
        SET "fot_idx" = "%index%"
      END
      READ_LONG  ("%area_off%" + 0x50 + ("%index%" * 0xf0)) "link_idx_1"
      PATCH_IF ("ar1700" STRING_COMPARE_CASE "%areafile%" = 0) BEGIN // Small Teeth Pass
        SET "link_idx_nsrt" = "%link_idx_1%"
        READ_BYTE  ("%area_off%" + 0x30 + ("%index%" * 0xf0)) "flags"
        WRITE_BYTE ("%area_off%" + 0x30 + ("%index%" * 0xf0)) ("%flags%" BOR 0b00000010)
        READ_LONG  ("%area_off%" + 0x54 + ("%index%" * 0xf0)) "link_nsrt_num"
        WRITE_LONG ("%area_off%" + 0x54 + ("%index%" * 0xf0)) ("%link_nsrt_num%" + 1)
        SET "stp_idx" = "%index%"
      END
      PATCH_IF ("%link_idx_nsrt%" < "%link_idx_1%") BEGIN // adjusts indices for links after the insert
        WRITE_LONG  ("%area_off%" + 0x50 + ("%index%" * 0xF0)) ("%link_idx_1%" + 1)
      END
      READ_LONG  ("%area_off%" + 0x58 + ("%index%" * 0xF0)) "link_idx_2"
      PATCH_IF ("%link_idx_nsrt%" < "%link_idx_2%") BEGIN // adjusts indices for links after the insert
        WRITE_LONG  ("%area_off%" + 0x58 + ("%index%" * 0xF0)) ("%link_idx_2%" + 1)
      END
      READ_LONG  ("%area_off%" + 0x60 + ("%index%" * 0xF0)) "link_idx_3"
      PATCH_IF ("%link_idx_nsrt%" < "%link_idx_3%") BEGIN // adjusts indices for links after the insert
        WRITE_LONG  ("%area_off%" + 0x60 + ("%index%" * 0xF0)) ("%link_idx_3%" + 1)
      END
      READ_LONG  ("%area_off%" + 0x68 + ("%index%" * 0xF0)) "link_idx_4"
      PATCH_IF ("%link_idx_nsrt%" < "%link_idx_4%") BEGIN // adjusts indices for links after the insert
        WRITE_LONG  ("%area_off%" + 0x68 + ("%index%" * 0xF0)) ("%link_idx_4%" + 1)
      END
    END
    FOR (index2 = 0 ; index2 < link_num ; index2 = index2 + 1) BEGIN
      READ_LONG ("%link_off%" +        ("%index2%" * 0xd8)) "target" // target index
      READ_LONG ("%link_off%" + 0x04 + ("%index2%" * 0xd8)) "exit"   // exit point
      PATCH_IF (("%exit%" = 0) AND
                (("%target%" = "%stp_idx%") OR
                 ("%target%" = "%nf_idx%") OR
                 ("%target%" = "%fot_idx%"))) BEGIN
        WRITE_ASCII ("%link_off%" + 0x04 + ("%index2%" * 0xd8)) "CDExit"   // exit point
      END
    END
    INSERT_BYTES  ("%link_off%" +        ("%link_idx_nsrt%" * 0xd8)) 0xd8
      WRITE_LONG  ("%link_off%" +        ("%link_idx_nsrt%" * 0xd8)) "%area_to%" // target index
      WRITE_ASCII ("%link_off%" + 0x04 + ("%link_idx_nsrt%" * 0xd8)) ~ExitNE~    // exit point
      WRITE_LONG  ("%link_off%" + 0x24 + ("%link_idx_nsrt%" * 0xd8)) 5           // travel time
      WRITE_LONG  ("%link_off%" + 0x28 + ("%link_idx_nsrt%" * 0xd8)) 2           // unk, changed for consistency
    FOR (index3 = 0 ; index3 < link_num ; index3 = index3 + 1) BEGIN
      READ_LONG  ("%link_off%" +        ("%index3%" * 0xd8)) "target" // target index
      READ_ASCII ("%link_off%" + 0x04 + ("%index3%" * 0xd8)) "exit"   // exit point
      PATCH_IF (("" STRING_COMPARE_CASE "%exit%" = 0) AND
                (("%target%" = "%stp_idx%") OR
                 ("%target%" = "%nf_idx%") OR
                 ("%target%" = "%fot_idx%"))) BEGIN
        WRITE_ASCII ("%link_off%" + 0x04 + ("%index3%" * 0xd8)) "CDExit"   // exit point
      END
    END
    BUT_ONLY_IF_IT_CHANGES

END

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                            \\\\\
///// Open up cloakwood                                          \\\\\
/////                                                            \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

/////                                                            \\\\\
///// first area only                                            \\\\\
/////                                                            \\\\\

BEGIN @103501 DESIGNATED 1035
SUBCOMPONENT @103500
GROUP @13
REQUIRE_PREDICATE ((FILE_EXISTS_IN_GAME ~_sw1h01.itm~) OR (FILE_EXISTS_IN_GAME ~bg1start.2da~)) @14 // Tutu or BGT only
REQUIRE_PREDICATE ((NOT FILE_EXISTS_IN_GAME ~X#BG1NPCCloakwood.G3~) AND
                   (NOT FILE_EXISTS_IN_GAME ~X#BG1NPCCloakLodge.G3~)) @16 // already installed by BG1 NPC

// this file does nothing, it just allows other mods to detect this component
COPY_EXISTING ~sw1h01.itm~ ~override/cdt01035.g3~

COPY_EXISTING ~worldmap.wmp~ ~override~
  READ_LONG  0x30 "area_num"
  READ_LONG  0x34 "area_off"
  FOR (index = 0; index < area_num; index = index + 1) BEGIN
    READ_ASCII ("%area_off%" + 0x08 + (0xf0 * "%index%")) "area_res"
    PATCH_IF (("%area_res%" STRING_COMPARE_CASE "fw2200" = 0) OR     // cloakwood 1 (tutu)
              ("%area_res%" STRING_COMPARE_CASE "ar7000" = 0)) BEGIN // cloakwood 1 (bgt)
      READ_BYTE  ("%area_off%" + 0x30 + (0xf0 * "%index%")) "flags"
      WRITE_BYTE ("%area_off%" + 0x30 + (0xf0 * "%index%")) ("%flags%" BOR 0b00000010) // adds 'reveal thru linked area' flag
    END ELSE
    PATCH_IF (("%area_res%" STRING_COMPARE_CASE "fw2100" = 0) OR     // cloakwood 2 (tutu)
              ("%area_res%" STRING_COMPARE_CASE "ar8800" = 0)) BEGIN // cloakwood 2 (bgt)
      READ_BYTE  ("%area_off%" + 0x30 + (0xf0 * "%index%")) "flags"
      WRITE_BYTE ("%area_off%" + 0x30 + (0xf0 * "%index%")) ("%flags%" BAND 0b11111101) // removes 'reveal thru linked area' flag
    END
  END
  BUT_ONLY_IF_IT_CHANGES

COPY_EXISTING_REGEXP GLOB ~^\(_ar1901\|ar8701\)\.bcs$~ ~override~
  DECOMPILE_BCS_TO_BAF
    REPLACE_TEXTUALLY ~RevealAreaOnMap("FW2200")~ ~RevealAreaOnMap("FW2200") RevealAreaOnMap("FW2100")~
    REPLACE_TEXTUALLY ~RevealAreaOnMap("AR7000")~ ~RevealAreaOnMap("AR7000") RevealAreaOnMap("AR8800")~
  COMPILE_BAF_TO_BCS
  BUT_ONLY_IF_IT_CHANGES

/////                                                            \\\\\
///// first area only                                            \\\\\
/////                                                            \\\\\

BEGIN @103600 DESIGNATED 1036
SUBCOMPONENT @103500
GROUP @13
REQUIRE_PREDICATE ((FILE_EXISTS_IN_GAME ~_sw1h01.itm~) OR (FILE_EXISTS_IN_GAME ~bg1start.2da~)) @14 // Tutu or BGT only
REQUIRE_PREDICATE ((NOT FILE_EXISTS_IN_GAME ~X#BG1NPCCloakwood.G3~) AND
                   (NOT FILE_EXISTS_IN_GAME ~X#BG1NPCCloakLodge.G3~)) @16 // already installed by BG1 NPC

// this file does nothing, it just allows other mods to detect this component
COPY_EXISTING ~sw1h01.itm~ ~override/cdt01036.g3~

COPY_EXISTING ~worldmap.wmp~ ~override~
  READ_LONG  0x30 "area_num"
  READ_LONG  0x34 "area_off"
  FOR (index = 0; index < area_num; index = index + 1) BEGIN
    READ_ASCII ("%area_off%" + 0x08 + (0xf0 * "%index%")) "area_res"
    PATCH_IF (("%area_res%" STRING_COMPARE_CASE "fw2200" = 0) OR     // cloakwood 1 (tutu)
              ("%area_res%" STRING_COMPARE_CASE "ar7000" = 0)) BEGIN // cloakwood 1 (bgt)
      READ_BYTE  ("%area_off%" + 0x30 + (0xf0 * "%index%")) "flags"
      WRITE_BYTE ("%area_off%" + 0x30 + (0xf0 * "%index%")) ("%flags%" BOR 0b00000010) // adds 'reveal thru linked area' flag
    END ELSE
    PATCH_IF (("%area_res%" STRING_COMPARE_CASE "fw1800" = 0) OR     // cloakwood mines (tutu)
              ("%area_res%" STRING_COMPARE_CASE "ar8600" = 0)) BEGIN // cloakwood mines (bgt)
      READ_BYTE  ("%area_off%" + 0x30 + (0xf0 * "%index%")) "flags"
      WRITE_BYTE ("%area_off%" + 0x30 + (0xf0 * "%index%")) ("%flags%" BAND 0b11111101) // removes 'reveal thru linked area' flag
    END
  END
  BUT_ONLY_IF_IT_CHANGES

COPY_EXISTING_REGEXP GLOB ~^\(_ar1901\|ar8701\)\.bcs$~ ~override~
  DECOMPILE_BCS_TO_BAF
    REPLACE_TEXTUALLY ~RevealAreaOnMap("FW2200")~ ~RevealAreaOnMap("FW2200") RevealAreaOnMap("FW1800")~
    REPLACE_TEXTUALLY ~RevealAreaOnMap("AR7000")~ ~RevealAreaOnMap("AR7000") RevealAreaOnMap("AR8600")~
  COMPILE_BAF_TO_BCS
  BUT_ONLY_IF_IT_CHANGES

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                            \\\\\
///// Improved Athkatla City Guard                               \\\\\
/////                                                            \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @104000 DESIGNATED 1040
GROUP @13
REQUIRE_PREDICATE NOT FILE_EXISTS_IN_GAME ~_sw1h01.itm~ @10  // non-Tutu
REQUIRE_PREDICATE NOT FILE_EXISTS_IN_GAME ~bpduearc.spl~ @16 // non-BP

COPY_EXISTING ~amncen1.cre~  ~override~ // amnish centurion
              ~amng1.cre~    ~override~ // amnish soldier
              ~amng2.cre~    ~override~ // amnish soldier
              ~amnleg01.cre~ ~override~ // amnish legionary
              ~amnleg1.cre~  ~override~ // amnish legionary
              ~amnlegs.cre~  ~override~ // amnish legionary
              ~bamng01.cre~  ~override~ // amnish guard
              ~bamng02.cre~  ~override~ // amnish guard
              ~bodyg1.cre~   ~override~ // amnish bodyguard
              ~bodyg2.cre~   ~override~ // amnish bodyguard
              ~circg1.cre~   ~override~ // amnish soldier
              ~civamng1.cre~ ~override~ // amnish soldier
              ~docsol01.cre~ ~override~ // amnish soldier
              ~gguard01.cre~ ~override~ // amnish soldier
              ~guamur.cre~   ~override~ // amnish soldier
              ~hactong.cre~  ~override~ // amnish bodyguard
              ~isamng1.cre~  ~override~ // amnish soldier
              ~isamng2.cre~  ~override~ // amnish soldier
              ~isamng3.cre~  ~override~ // amnish soldier
              ~isamng4.cre~  ~override~ // amnish soldier
              ~slamng01.cre~ ~override~ // amnish soldier
              ~slcent.cre~   ~override~ // amnish centurion
              ~stealgrd.cre~ ~override~ // amnish soldier
              ~stolegrd.cre~ ~override~ // amnish soldier
              ~temamn01.cre~ ~override~ // amnish guard
              ~vvamn1.cre~   ~override~ // amnish soldier
              ~vvamn2.cre~   ~override~ // amnish soldier
  READ_SHORT  0x24  "current_hp"
  READ_SHORT  0x26  "max_hp"
  READ_SHORT  0x46  "natural_ac"
  READ_SHORT  0x48  "effective_ac"
  READ_SHORT  0x52  "guard_thac0"
  READ_BYTE   0x54  "save_death"
  READ_BYTE   0x55  "save_wands"
  READ_BYTE   0x56  "save_polymorph"
  READ_BYTE   0x57  "save_breath"
  READ_BYTE   0x58  "save_spell"
  WRITE_SHORT 0x24  ("%current_hp%" + 10)    // Current HP
  WRITE_SHORT 0x26  ("%max_hp%" + 10)        // Max HP
  WRITE_SHORT 0x46  ("%natural_ac%" - 1)     // Natural AC
  WRITE_SHORT 0x48  ("%effective_ac%" - 1)   // Effective AC
  WRITE_SHORT 0x52  ("%guard_thac0%" - 1)    // thac0
  WRITE_BYTE  0x54  ("%save_death%" - 1)     // save vs death
  WRITE_BYTE  0x55  ("%save_wands%" - 1)     // save vs wands
  WRITE_BYTE  0x56  ("%save_polymorph%" - 1) // save vs polymorph
  WRITE_BYTE  0x57  ("%save_breath%" - 1)    // save vs breath
  WRITE_BYTE  0x58  ("%save_spell%" - 1)     // save vs spell
  //search for first empty script slot and overwrite
  FOR (index = 0 ; index < 5 ; index = index + 1) BEGIN
    READ_ASCII (0x248 + ("%index%" * 0x08)) "script"
    PATCH_IF (("None" STRING_COMPARE_CASE "%script%" = 0) OR
              (""     STRING_COMPARE_CASE "%script%" = 0)) BEGIN
      WRITE_ASCII (0x248 + ("%index%" * 0x08)) ~a#amng~ #8
      SET "index" = 5 // kills loop
    END ELSE
    PATCH_IF ("%index%" = 4) BEGIN // if we get here, no script slot open so we overwrite general script
      WRITE_ASCII 0x260 ~a#amng~ #8 // override script
    END
  END
  BUT_ONLY_IF_IT_CHANGES

// Amnish Soldier
COPY_EXISTING ~amng1.cre~ ~override/a#amng.cre~
  SAY NAME1 @104001
  SAY NAME2 @104001
  WRITE_ASCII 0x280 ~a#amng~ #8

//Amnish Soldier - Hostile
COPY_EXISTING ~a#amng.cre~ ~override/a#amngh.cre~
  WRITE_BYTE  0x270 255          // Allegiance enemy
  WRITE_ASCII 0x280 ~a#amngh~ #8 // Death Variable

//Sanctioned Wizard
COPY ~BG2_Tweaks/cre/a#amnm.cre~ ~override~
  SAY NAME1 @104002
  SAY NAME2 @104002

//Sanctioned Wizard - Hostile
COPY_EXISTING ~a#amnm.cre~ ~override/a#amnmh.cre~
  WRITE_BYTE  0x270 255          // Allegiance enemy
  WRITE_ASCII 0x280 ~a#amnmh~ #8 // Death Variable

//Sanctioned Wizard
COPY ~BG2_Tweaks/cre/a#amnp.cre~ ~override~
  SAY NAME1 @104003
  SAY NAME2 @104003

//Sanctioned Wizard - Hostile
COPY_EXISTING ~a#amnp.cre~ ~override/a#amnph.cre~
  WRITE_BYTE  0x270 255          // Allegiance enemy
  WRITE_ASCII 0x280 ~a#amnph~ // Death Variable

COMPILE ~BG2_Tweaks/baf/a#amng.baf~

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                            \\\\\
///// Gradual Drow item disintegration                           \\\\\
/////                                                            \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @105000 DESIGNATED 1050
GROUP @13
REQUIRE_PREDICATE NOT FILE_EXISTS_IN_GAME ~_sw1h01.itm~ @10 // non-Tutu

EXTEND_BOTTOM ~ar2500.bcs~ ~BG2_Tweaks/baf/ar2500.baf~
EXTEND_BOTTOM ~baldur.bcs~ ~BG2_Tweaks/baf/baldur.baf~

ACTION_IF FILE_EXISTS_IN_GAME ~baldur25.bcs~ THEN BEGIN // extend ToB game script if present

  EXTEND_BOTTOM ~baldur25.bcs~ ~BG2_Tweaks/baf/baldur.baf~

END

COPY ~BG2_Tweaks/spl/cdsw1h01.spl~ ~override~
  WRITE_ASCII 0xae ~dwsw1h01~
  SAY 0xfe @105001

COPY_EXISTING ~cdsw1h01.spl~ ~override/cdsw1h02.spl~
  WRITE_ASCII 0xae ~dwsw1h02~

COPY_EXISTING ~cdsw1h01.spl~ ~override/cdblun01.spl~
  WRITE_ASCII 0xae ~dwblun01~

COPY_EXISTING ~cdsw1h01.spl~ ~override/cdhalb01.spl~
  WRITE_ASCII 0xae ~dwhalb01~

COPY_EXISTING ~cdsw1h01.spl~ ~override/cdsper01.spl~
  WRITE_ASCII 0xae ~dwsper01~

COPY ~BG2_Tweaks/spl/cdbreak1.spl~ ~override~ // armor
  SAY 0xce @105001

COPY_EXISTING ~cdbreak1.spl~ ~override/cdbreak4.spl~  // cloak
  WRITE_LONG 0x9e 4

COPY_EXISTING ~cdbreak1.spl~ ~override/cdbreak9.spl~  // shield
  WRITE_LONG 0x9e 9

// melee weapons have chance of failing when used in combat
COPY_EXISTING ~dwsw1h01.itm~ ~override~
              ~dwsw1h02.itm~ ~override~
              ~dwblun01.itm~ ~override~
              ~dwhalb01.itm~ ~override~
              ~dwsper01.itm~ ~override~
  READ_LONG  0x64 "abil_off" ELSE 0
  READ_SHORT 0x68 "abil_num" ELSE 0
  READ_LONG  0x6a "fx_off"   ELSE 0
  SET "abil_length" = 0x38
  SET "fx_delta" = 0
  FOR (index = 0 ; index < abil_num ; index = index + 1) BEGIN // start iterating through abilities
    READ_SHORT  ("%abil_off%" +        ("%abil_length%" * "%index%")) "type"
    READ_SHORT  ("%abil_off%" + 0x1e + ("%abil_length%" * "%index%")) "abil_fx_num"
    READ_SHORT  ("%abil_off%" + 0x20 + ("%abil_length%" * "%index%")) "abil_fx_idx"
    SET "abil_fx_idx" = ("%abil_fx_idx%" + "%fx_delta%")
    WRITE_SHORT ("%abil_off%" + 0x20 + ("%abil_length%" * "%index%")) ("%abil_fx_idx%")
    PATCH_IF ("%type%" = 1) BEGIN // melee
      INSERT_BYTES            ("%fx_off%"        + (0x30 * "%abil_fx_idx%")) 0x30
        WRITE_SHORT           ("%fx_off%"        + (0x30 * "%abil_fx_idx%")) 146 // opcode: 146 cast spell
        WRITE_BYTE            ("%fx_off%" + 0x02 + (0x30 * "%abil_fx_idx%")) 1   // target: self
        WRITE_LONG            ("%fx_off%" + 0x04 + (0x30 * "%abil_fx_idx%")) 1   // cast at level: 1
        WRITE_BYTE            ("%fx_off%" + 0x0c + (0x30 * "%abil_fx_idx%")) 1   // timing: instant permanent
        WRITE_BYTE            ("%fx_off%" + 0x12 + (0x30 * "%abil_fx_idx%")) 1   // probability: 1%
        WRITE_EVALUATED_ASCII ("%fx_off%" + 0x14 + (0x30 * "%abil_fx_idx%")) ~cd%SOURCE_RES%~ // name of spell to cast (same as item name)
      SET "abil_fx_num" = "%abil_fx_num%" + 1
      SET "fx_delta" = "%fx_delta%" + 1
    END
    WRITE_SHORT  ("%abil_off%" + 0x1e + ("%abil_length%" * "%index%")) "%abil_fx_num%"
  END
  BUT_ONLY_IF_IT_CHANGES

// armor/shield/cloak has a chance to break when attacked
COPY_EXISTING ~dwchan01.itm~ ~override~
              ~dwchan02.itm~ ~override~
              ~dwclck01.itm~ ~override~
              ~dwplat01.itm~ ~override~
              ~dwshld01.itm~ ~override~
              ~misc9w.itm~   ~override~
  PATCH_IF (SOURCE_SIZE > 0x71) THEN BEGIN // protects against invalid files
    READ_SHORT  0x1c "type"
    READ_LONG   0x64 "abil_off"
    READ_SHORT  0x68 "abil_num"
    READ_LONG   0x6a "fx_off"
    READ_SHORT  0x70 "fx_num"
    WRITE_SHORT 0x70 ("%fx_num%" + 1)
    FOR (index = 0 ; index < abil_num ; index = index + 1) BEGIN
      READ_SHORT  ("%abil_off%" + 0x20 + (0x38 * "%index%")) "abil_fx_idx"
      WRITE_SHORT ("%abil_off%" + 0x20 + (0x38 * "%index%")) ("%abil_fx_idx%" + 1)
    END
    INSERT_BYTES  ("%fx_off%"       ) 0x30
      WRITE_SHORT ("%fx_off%"       ) 232          // effect type - 232 cast spell on condition
      WRITE_BYTE  ("%fx_off%" + 0x02) 1            // target - 1 targetself
      WRITE_LONG  ("%fx_off%" + 0x04) 1            // cast at level - 1
      WRITE_BYTE  ("%fx_off%" + 0x0c) 2            // duration - 2 instant while equipped
      WRITE_BYTE  ("%fx_off%" + 0x12) 1            // probability 1 - 1%
      PATCH_IF ("%type%" = 2) BEGIN                // if armor
        WRITE_ASCII ("%fx_off%" + 0x14) ~cdbreak1~ // name of spell to cast
      END ELSE
      PATCH_IF ("%type%" = 12) BEGIN               // if shield
        WRITE_ASCII ("%fx_off%" + 0x14) ~cdbreak9~ // name of spell to cast
      END ELSE BEGIN                               // if cloak
        WRITE_ASCII ("%fx_off%" + 0x14) ~cdbreak4~ // name of spell to cast
      END
  END
  BUT_ONLY_IF_IT_CHANGES

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// Breakable nonmagical shields, armor and helms    \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @106000 DESIGNATED 1060
GROUP @13
GROUP @19
REQUIRE_PREDICATE FILE_EXISTS_IN_GAME ~_sw1h01.itm~ @15 // Tutu only

COPY ~BG2_Tweaks/spl/cdbreak1.spl~ ~override~ // armor
  WRITE_ASCII 0xae ~misc58~ #8
  SAY         0xce @106001

COPY_EXISTING ~cdbreak1.spl~ ~override/cdbreak6.spl~  // helm
  WRITE_SHORT 0x90 3
  READ_ASCII  0x9a "effect" (0x30)
  WRITE_LONG  0x9e 6
  WRITE_ASCII 0xae ~cddelhlm~ #8
  SAY         0xce @106003
  INSERT_BYTES 0x9a 0x30 // insert new effect
    WRITE_EVALUATED_ASCII 0x9a "%effect%"
    WRITE_SHORT           0x9a 122         // opcode: create inventory item
    WRITE_LONG            0x9e 1           // number of items
    WRITE_ASCII           0xae ~misc59~ #8 // broken helm

COPY_EXISTING ~cdbreak1.spl~ ~override/cdbreak9.spl~  // shield
  WRITE_LONG  0x9e 9
  WRITE_ASCII 0xae ~misc57~ #8
  SAY         0xce @106002

COPY_EXISTING ~misc59.itm~ ~override/cddelhlm.itm~

EXTEND_TOP ~baldur.bcs~ ~BG2_Tweaks/baf/break.baf~

ACTION_IF FILE_EXISTS_IN_GAME ~baldur25.bcs~ THEN BEGIN

  EXTEND_TOP ~baldur25.bcs~ ~BG2_Tweaks/baf/break.baf~

END

// give nonmagical iron armor/shield/helmets a chance to break when attacked
COPY_EXISTING ~_helm01.itm~  ~override~ // helmets
              ~_helm08.itm~  ~override~
              ~_helm09.itm~  ~override~
              ~_helm10.itm~  ~override~
              ~_helm11.itm~  ~override~
              ~_helm12.itm~  ~override~
              ~_helm13.itm~  ~override~
              ~_helm15.itm~  ~override~
              ~_ihelm01.itm~ ~override~
              ~_ihelm10.itm~ ~override~
              ~dhelm01.itm~  ~override~
              ~elfhelm.itm~  ~override~
              ~helm01.itm~   ~override~
              ~helm08.itm~   ~override~
              ~helm09.itm~   ~override~
              ~helm10.itm~   ~override~
              ~helm11.itm~   ~override~
              ~helm12.itm~   ~override~
              ~helm13.itm~   ~override~
              ~helm15.itm~   ~override~
              ~helm22.itm~   ~override~
              ~helmskwa.itm~ ~override~
              ~ihelm01.itm~  ~override~
              ~ihelm10.itm~  ~override~
              ~_ishld03.itm~ ~override~  // shields
              ~_shld01.itm~  ~override~
              ~_shld03.itm~  ~override~
              ~_shld05.itm~  ~override~
              ~_shld08.itm~  ~override~
              ~_shld09.itm~  ~override~
              ~_shld10.itm~  ~override~
              ~_shld11.itm~  ~override~
              ~_shld12.itm~  ~override~
              ~_shld13.itm~  ~override~
              ~_shld14.itm~  ~override~
              ~_shld15.itm~  ~override~
              ~_shld16.itm~  ~override~
              ~_shld18.itm~  ~override~
              ~_shld99.itm~  ~override~
              ~dshld01.itm~  ~override~
              ~ishld03.itm~  ~override~
              ~shld01.itm~   ~override~
              ~shld03.itm~   ~override~
              ~shld05.itm~   ~override~
              ~shld08.itm~   ~override~
              ~shld09.itm~   ~override~
              ~shld10.itm~   ~override~
              ~shld11.itm~   ~override~
              ~shld12.itm~   ~override~
              ~shld13.itm~   ~override~
              ~shld14.itm~   ~override~
              ~shld15.itm~   ~override~
              ~shld16.itm~   ~override~
              ~shld18.itm~   ~override~
              ~shld99.itm~   ~override~
              ~_chan01.itm~  ~override~ // metal armor
              ~_chan04.itm~  ~override~
              ~_ichan01.itm~ ~override~
              ~_ichan04.itm~ ~override~
              ~_iplat01.itm~ ~override~
              ~_plat01.itm~  ~override~
              ~_plat04.itm~  ~override~
              ~_plat07.itm~  ~override~
              ~_plat98.itm~  ~override~
              ~chan01.itm~   ~override~
              ~chan04.itm~   ~override~
              ~ichan01.itm~  ~override~
              ~ichan04.itm~  ~override~
              ~iplat01.itm~  ~override~
              ~plat01.itm~   ~override~
              ~plat04.itm~   ~override~
              ~plat07.itm~   ~override~
              ~plat98.itm~   ~override~
              ~plat99.itm~   ~override~
              ~vischan1.itm~ ~override~
              ~vischan2.itm~ ~override~
              ~visplat1.itm~ ~override~
  PATCH_IF (SOURCE_SIZE > 0x71) THEN BEGIN // protects against invalid files
    READ_SHORT  0x1c "type"
    READ_LONG   0x64 "abil_off"
    READ_SHORT  0x68 "abil_num"
    READ_LONG   0x6a "fx_offset"
    READ_SHORT  0x70 "fx_num"
    WRITE_SHORT 0x70 ("%fx_num%" + 1)
    FOR (index = 0 ; index < abil_num ; index = index + 1) BEGIN
      READ_SHORT  ("%abil_off%" + 0x20 + (0x38 * "%index%")) "abil_fx_idx"
      WRITE_SHORT ("%abil_off%" + 0x20 + (0x38 * "%index%")) ("%abil_fx_idx%" + 1)
    END
    INSERT_BYTES ("%fx_offset%") 0x30
      WRITE_SHORT ("%fx_offset%" + 0x00) 232          // effect type - 232 cast spell on condition
      WRITE_BYTE  ("%fx_offset%" + 0x02) 1            // target - 1 targetself
      WRITE_LONG  ("%fx_offset%" + 0x04) 1            // cast at level - 1
      WRITE_BYTE  ("%fx_offset%" + 0x0C) 2            // duration - 2 instant while equipped
      WRITE_BYTE  ("%fx_offset%" + 0x12) 1            // probability 1 - 1%
    PATCH_IF ("%type%" = 2) BEGIN                   // if armor
      WRITE_ASCII ("%fx_offset%" + 0x14) ~cdbreak1~ // name of spell to cast
    END ELSE
    PATCH_IF ("%type%" = 12) BEGIN                  // if shield
      WRITE_ASCII ("%fx_offset%" + 0x14) ~cdbreak9~ // name of spell to cast
    END ELSE BEGIN                                  // if helm
      WRITE_ASCII ("%fx_offset%" + 0x14) ~cdbreak6~ // name of spell to cast
    END
  END
  BUT_ONLY_IF_IT_CHANGES

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// Multi-player kickout patch                       \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @107000 DESIGNATED 1070
GROUP @13

COMPILE ~BG2_Tweaks/dlg/multig.d~

ACTION_IF FILE_EXISTS_IN_GAME ~_sw1h01.itm~ THEN BEGIN // Tutu, removes CC/PP options and adds FAI option

  COMPILE ~BG2_Tweaks/dlg/multi22.d~

END

ACTION_IF FILE_EXISTS_IN_GAME ~bg1start.2da~ THEN BEGIN // BGT, adds EndOfBG checks for CC/PP and adds FAI option

  COMPILE ~BG2_Tweaks/dlg/multibgt.d~

END

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// Add Misc Bags of Holding                         \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @108000 DESIGNATED 1080
GROUP @13
GROUP @19

ACTION_IF FILE_EXISTS_IN_GAME ~mel01.cre~ THEN BEGIN // if ToB, clone existing items

  // store files
  COPY_EXISTING ~bag05.sto~ ~override/cdammo.sto~   // ammo belt
                ~bag05.sto~ ~override/cdammo2.sto~  // ammo belt #2
                ~bag06.sto~ ~override/cdpotion.sto~ // potion case
                ~bag06.sto~ ~override/cdpotio2.sto~ // potion case #2

  // item files
  COPY_EXISTING ~bag05.itm~ ~override/cdammo.itm~   // ammo belt
                ~bag05.itm~ ~override/cdammo2.itm~  // ammo belt #2
                ~bag06.itm~ ~override/cdpotion.itm~ // potion case
                ~bag06.itm~ ~override/cdpotio2.itm~ // potion case #2

END ELSE BEGIN // if no ToB, create potion case & ammo belt

  // bams
  COPY ~BG2_Tweaks/bam/ibag05.bam~ ~override~
       ~BG2_Tweaks/bam/ibag06.bam~ ~override~
       ~BG2_Tweaks/bam/pbag05.bam~ ~override~
       ~BG2_Tweaks/bam/pbag06.bam~ ~override~

  // ammo belts
  COPY ~BG2_Tweaks/sto/cdammo.sto~ ~override~
       ~BG2_Tweaks/sto/cdammo.sto~ ~override/cdammo2.sto~
    SAY 0x0c @108001

  COPY ~BG2_Tweaks/itm/cdammo.itm~ ~override~
       ~BG2_Tweaks/itm/cdammo.itm~ ~override/cdammo2.itm~
    SAY 0x08 @108001
    SAY 0x0c @108001
    SAY 0x50 @108002
    SAY 0x54 @108002

  // potion case
  COPY ~BG2_Tweaks/sto/cdpotion.sto~ ~override~
       ~BG2_Tweaks/sto/cdpotion.sto~ ~override/cdpotio2.sto~
    SAY 0x0c @108003

  COPY ~BG2_Tweaks/itm/cdpotion.itm~ ~override~
       ~BG2_Tweaks/itm/cdpotion.itm~ ~override/cdpotio2.itm~
    SAY 0x08 @108003
    SAY 0x0c @108003
    SAY 0x50 @108004
    SAY 0x54 @108004

  ACTION_IF FILE_EXISTS_IN_GAME ~cdt03040.g3~ THEN BEGIN // if bottomless bags

    COPY_EXISTING ~cdammo.sto~   ~override~
                  ~cdammo2.sto~  ~override~
                  ~cdpotion.sto~ ~override~
                  ~cdpotio2.sto~ ~override~
      WRITE_SHORT 0x22 32767
      BUT_ONLY_IF_IT_CHANGES

  END

END

COPY_EXISTING ~roger.sto~   ~override~ // roger
              ~murcrag.sto~ ~override~ // Cragmoon
              ~shop05.sto~  ~override~ // perter
              ~trmer04.sto~ ~override~ // trademeet blacksmith
  READ_LONG  0x34 "sale_offset"
  READ_LONG  0x38 "sale_number"
  WRITE_LONG 0x38 ("%sale_number%" + 1)
  READ_LONG  0x2c "purchase_offset"
  READ_LONG  0x4c "drink_offset"
  READ_LONG  0x70 "cure_offset"
  INSERT_BYTES  ("%sale_offset%" +        ("%sale_number%" * 0x1c)) 28
    PATCH_IF ("murcrag" STRING_COMPARE_CASE ~%SOURCE_RES%~ = 0) BEGIN // if store is cragmoon add potion case
      WRITE_ASCII ("%sale_offset%" +        ("%sale_number%" * 0x1C)) ~cdpotion~
    END ELSE
    PATCH_IF ("roger" STRING_COMPARE_CASE ~%SOURCE_RES%~ = 0) BEGIN // if store is roger add potion case 2
      WRITE_ASCII ("%sale_offset%" +        ("%sale_number%" * 0x1C)) ~cdpotio2~
    END ELSE
    PATCH_IF ("shop05" STRING_COMPARE_CASE ~%SOURCE_RES%~ = 0) BEGIN // if store is perter add ammo belt 1
      WRITE_ASCII ("%sale_offset%" +        ("%sale_number%" * 0x1c)) ~cdammo~
    END ELSE
    PATCH_IF ("trmer04" STRING_COMPARE_CASE ~%SOURCE_RES%~ = 0) BEGIN // if store is Trademeet blacksmith add ammo belt 2
      WRITE_ASCII ("%sale_offset%" +        ("%sale_number%" * 0x1c)) ~cdammo2~
    END
    WRITE_LONG  ("%sale_offset%" + 0x10 + ("%sale_number%" * 0x1c)) 3 // identified and unstealable
    WRITE_LONG  ("%sale_offset%" + 0x14 + ("%sale_number%" * 0x1c)) 1 // quantity
  PATCH_IF NOT ("%purchase_offset%" < "%sale_offset%") BEGIN
    WRITE_LONG 0x2c ("%purchase_offset%" + 0x1c)
  END
  PATCH_IF NOT ("%drink_offset%" < "%sale_offset%") BEGIN
    WRITE_LONG 0x4c ("%drink_offset%" + 0x1c)
  END
  PATCH_IF NOT ("%cure_offset%" < "%sale_offset%") BEGIN
    WRITE_LONG 0x70 ("%cure_offset%" + 0x1c)
  END

// if BGT or Tutu game, add bags to BG portion
ACTION_IF ((FILE_EXISTS_IN_GAME ~_sw1h01.itm~) OR (FILE_EXISTS_IN_GAME ~bg1start.2da~)) THEN BEGIN

  COPY_EXISTING ~cdammo.itm~   ~override/c!ammo.itm~   // ammo belt
                ~cdammo.sto~   ~override/c!ammo.sto~
                ~bag04.itm~    ~override/c!bag04.itm~  // bag of holding
                ~bag04.sto~    ~override/c!bag04.sto~
                ~bag02.itm~    ~override/c!bag02.itm~  // gem bag
                ~bag02.sto~    ~override/c!bag02.sto~
                ~cdpotion.itm~ ~override/c!potion.itm~ // potion case
                ~cdpotion.sto~ ~override/c!potion.sto~
                ~bag03.itm~    ~override/c!bag03.itm~  // scroll case
                ~bag03.sto~    ~override/c!bag03.sto~

  COPY_EXISTING_REGEXP GLOB ~^_?friend\.sto$~    ~override~ // FAI
                            ~^[_h]ighhedg\.sto$~ ~override~ // High Hedge
                            ~^_?taerum\.sto$~    ~override~ // Thunderhammer Smithy
                            ~^_?sto4803\.sto$~   ~override~ // Nashkel General Store
                            ~^_?sto0703\.sto$~   ~override~ // Sorcerous Sundries
    READ_LONG  0x34 "sale_offset"
    READ_LONG  0x38 "sale_number"
    WRITE_LONG 0x38 ("%sale_number%" + 1)
    READ_LONG  0x2c "purchase_offset"
    READ_LONG  0x4c "drink_offset"
    READ_LONG  0x70 "cure_offset"
    INSERT_BYTES  ("%sale_offset%" +        ("%sale_number%" * 0x1c)) 28
      PATCH_IF (~%SOURCE_RES%~ STRING_COMPARE_REGEXP ~^_?friend$~ = 0) BEGIN // if store is FAI add c!bag02
        WRITE_ASCII ("%sale_offset%" +        ("%sale_number%" * 0x1c)) ~c!bag02~
      END ELSE
      PATCH_IF (~%SOURCE_RES%~ STRING_COMPARE_REGEXP ~^[_h]ighhedg$~ = 0) BEGIN // if store is High Hedge add c!bag03
        WRITE_ASCII ("%sale_offset%" +        ("%sale_number%" * 0x1c)) ~c!bag03~
      END ELSE
      PATCH_IF (~%SOURCE_RES%~ STRING_COMPARE_REGEXP ~^_?taerum$~ = 0) BEGIN // if store is Thunderhammer Smithy add c!ammo
        WRITE_ASCII ("%sale_offset%" +        ("%sale_number%" * 0x1c)) ~c!ammo~
      END ELSE
      PATCH_IF (~%SOURCE_RES%~ STRING_COMPARE_REGEXP ~^_?sto4803$~ = 0) BEGIN // if store is Nashkel General Store add c!potion
        WRITE_ASCII ("%sale_offset%" +        ("%sale_number%" * 0x1c)) ~c!potion~
      END ELSE BEGIN // else sorcerous sundries
        WRITE_ASCII ("%sale_offset%" +        ("%sale_number%" * 0x1c)) ~c!bag04~// if store is SS add c!bag04
      END
      WRITE_LONG  ("%sale_offset%" + 0x10 + ("%sale_number%" * 0x1c)) 3 // identified and unstealable
      WRITE_LONG  ("%sale_offset%" + 0x14 + ("%sale_number%" * 0x1c)) 1 // quantity
    PATCH_IF NOT ("%purchase_offset%" < "%sale_offset%") BEGIN
      WRITE_LONG 0x2c ("%purchase_offset%" + 0x1c)
    END
    PATCH_IF NOT ("%drink_offset%" < "%sale_offset%") BEGIN
      WRITE_LONG 0x4c ("%drink_offset%" + 0x1c)
    END
    PATCH_IF NOT ("%cure_offset%" < "%sale_offset%") BEGIN
      WRITE_LONG 0x70 ("%cure_offset%" + 0x1c)
    END

END

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// Exotic Items Pack                                \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @109000 DESIGNATED 1090
GROUP @13
GROUP @19
REQUIRE_PREDICATE ((FILE_EXISTS_IN_GAME ~_sw1h01.itm~) OR (FILE_EXISTS_IN_GAME ~bg1start.2da~)) @14 // Tutu or BGT only
REQUIRE_PREDICATE NOT FILE_EXISTS_IN_GAME ~cdcal040.g3~ @16 // already installed by the calling

COPY ~BG2_Tweaks/bam/ccdioun1.bam~ ~override~
     ~BG2_Tweaks/bam/cdiammo.bam~  ~override~
     ~BG2_Tweaks/bam/cdipotbg.bam~ ~override~
     ~BG2_Tweaks/bam/cdpammo.bam~  ~override~
     ~BG2_Tweaks/bam/cdplbag1.bam~ ~override~
     ~BG2_Tweaks/bam/cdplbag2.bam~ ~override~
     ~BG2_Tweaks/bam/cdplqiv1.bam~ ~override~
     ~BG2_Tweaks/bam/cdplqiv2.bam~ ~override~
     ~BG2_Tweaks/bam/cdppotbg.bam~ ~override~
     ~BG2_Tweaks/bam/icdioun1.bam~ ~override~
     ~BG2_Tweaks/bam/icdioun2.bam~ ~override~

ACTION_IF FILE_EXISTS_IN_GAME ~bg1start.2da~ THEN BEGIN // BGT

  COMPILE ~BG2_Tweaks/dlg/exotic_bgt.d~ // fixes Gullykin cleric's dlg file to open new store

  EXTEND_BOTTOM ~ar8603.bcs~ ~BG2_Tweaks/baf/_ar1803.baf~ // adds Contagion scroll to Davaeorn's loot
  EXTEND_BOTTOM ~ar9501.bcs~ ~BG2_Tweaks/baf/_ar3601.baf~ // adds Ninja-To +0 to Sirine cave loot

  ACTION_IF NOT FILE_EXISTS_IN_GAME ~tem4003.sto~ THEN BEGIN // if Gullykin does not have a store already

    COPY_EXISTING ~tem4802.sto~ ~override/tem4003.sto~ // clones and renames Temple of Helm
      SAY 0x0C @109022

  END

END ELSE BEGIN

  COMPILE ~BG2_Tweaks/dlg/exotic_22.d~ // fixes Gullykin cleric's dlg file to open new store

  COPY_EXISTING ~fw3601.are~ ~override~ // adds area script to Sirine cave
    WRITE_ASCII 0x94 ~_ar3601~ #8

  EXTEND_BOTTOM ~_ar1803.bcs~ ~BG2_Tweaks/baf/_ar1803.baf~ // adds Contagion scroll to Davaeorn's loot
  EXTEND_BOTTOM ~_ar3601.bcs~ ~BG2_Tweaks/baf/_ar3601.baf~ // adds Ninja-To +0 to Sirine cave loot

  ACTION_IF NOT FILE_EXISTS_IN_GAME ~_tem4003.sto~ THEN BEGIN // if Gullykin does not have a store already

    COPY_EXISTING ~_tem4802.sto~ ~override/_tem4003.sto~ // clones and renames Temple of Helm
      SAY 0x0C @109022

  END

END

COPY ~BG2_Tweaks/itm/cdplquiv.itm~ ~override~ // quiver of plenty +0
  SAY NAME1 @109001
  SAY NAME2 @109001
  SAY IDENTIFIED_DESC @109002
  SAY UNIDENTIFIED_DESC @109002

COPY ~BG2_Tweaks/itm/cdplcase.itm~ ~override~ // case of plenty +0
  SAY NAME1 @109003
  SAY NAME2 @109003
  SAY IDENTIFIED_DESC @109004
  SAY UNIDENTIFIED_DESC @109004

COPY ~BG2_Tweaks/itm/cdplbag.itm~ ~override~ // bag of plenty +0
  SAY NAME1 @109005
  SAY NAME2 @109005
  SAY IDENTIFIED_DESC @109006
  SAY UNIDENTIFIED_DESC @109006

COPY ~BG2_Tweaks/itm/cdioun1.itm~ ~override~  // Deep Purple Ioun Stone
  SAY NAME1 #8181
  SAY NAME2 @109007
  SAY IDENTIFIED_DESC @109008
  SAY UNIDENTIFIED_DESC #5770

COPY ~BG2_Tweaks/itm/cdioun2.itm~ ~override~ // Flickering White Ioun Stone
  SAY NAME1 #8181
  SAY NAME2 @109009
  SAY IDENTIFIED_DESC @109010
  SAY UNIDENTIFIED_DESC #5770

COPY ~BG2_Tweaks/itm/cdkat2.itm~ ~override~ // Katana +2
  SAY NAME1 @109011
  SAY NAME2 @109013
  SAY IDENTIFIED_DESC @109014
  SAY UNIDENTIFIED_DESC @109012
  SAY 0x19e @109015
  SAY 0x1fe @109016
  SAY 0x25e @109017
  SAY 0x2be @109018
  SAY 0x31e @109019
  SAY 0x37e @109020
  SAY 0x3de @109021

COPY_EXISTING_REGEXP GLOB ~^_?meilum\.cre$~ ~override~ // adds katana +0 to Meilum by replacing primary weapon
                          ~^_?krumm\.cre$~  ~override~ // adds katana +2 to Krumm by replacing primary weapon
  READ_LONG 0x2b8 "slot_offset"
  READ_LONG 0x2bc "item_offset"
  READ_SHORT  ("%slot_offset%" + 0x12) "item_index"
  PATCH_IF (~%SOURCE_RES%~ STRING_COMPARE_REGEXP ~^_?meilum$~ = 0) BEGIN // if Melium
    WRITE_ASCII ("%item_offset%" +        (0x14 * "%item_index%")) ~sw1h43~ #8
  END ELSE BEGIN
    WRITE_ASCII ("%item_offset%" +        (0x14 * "%item_index%")) ~cdkat2~ #8
  END
  WRITE_SHORT ("%item_offset%" + 0x06 + (0x14 * "%item_index%")) 0
  WRITE_LONG  ("%item_offset%" + 0x08 + (0x14 * "%item_index%")) 0
  WRITE_LONG  ("%item_offset%" + 0x0c + (0x14 * "%item_index%")) 0
  WRITE_LONG  ("%item_offset%" + 0x10 + (0x14 * "%item_index%")) 0
  BUT_ONLY_IF_IT_CHANGES

COPY_EXISTING_REGEXP GLOB ~^_?davaeo\.cre$~ ~override~ // adds Flickering White Ioun stone to helmet slot
  READ_LONG   0x2b8 "slot_offset"
  READ_LONG   0x2bc "item_offset"
  READ_LONG   0x2c0 "item_number"
  WRITE_SHORT ("%slot_offset%") "%item_number%"
  WRITE_LONG  0x2b8 ("%slot_offset%" + 0x14)
  WRITE_LONG  0x2c0 ("%item_number%" + 1)
  INSERT_BYTES ("%slot_offset%") 0x14
    WRITE_ASCII ("%slot_offset%") ~cdioun2~ #8
  BUT_ONLY_IF_IT_CHANGES

COPY_EXISTING_REGEXP GLOB ~^_?haseo\.cre$~  ~override~ // adds katana +1 to Haseo by moving primary weapon to offhand
                          ~^_?desret\.cre$~ ~override~ // adds wakizashi +1 to Desreta by moving primary weapon to offhand
  READ_LONG    0x2b8 "slot_offset"
  READ_LONG    0x2bc "item_offset"
  READ_SHORT   0x2c0 "item_number"
  READ_SHORT   ("%slot_offset%" + 0x12) "item_index"
  WRITE_SHORT  ("%slot_offset%" + 0x12) "%item_number%"
  WRITE_SHORT  ("%slot_offset%" + 0x04) "%item_index%"
  WRITE_LONG   0x2b8 ("%slot_offset%" + 0x14)
  WRITE_SHORT  0x2c0 ("%item_number%" + 1)
  INSERT_BYTES ("%slot_offset%") 0x14
    PATCH_IF (~%SOURCE_FILE%~ STRING_COMPARE_REGEXP ~^_?haseo\.cre$~ = 0) BEGIN // if Haseo
      WRITE_ASCII ("%slot_offset%") ~sw1h44~ #8
    END ELSE BEGIN
      WRITE_ASCII ("%slot_offset%") ~sw1h47~ #8
    END
  BUT_ONLY_IF_IT_CHANGES

COPY_EXISTING_REGEXP GLOB ~^_?hakt\.cre$~   ~override~ // adds wakizashi +0 to Hakt by replacing secondary weapon
                          ~^_?maneir\.cre$~ ~override~ // adds scimitar +1 to Maneira by replacing secondary weapon
  READ_LONG 0x2b8 "slot_offset"
  READ_LONG 0x2bc "item_offset"
  READ_SHORT ("%slot_offset%" + 0x14) "item_index"
  PATCH_IF (~%SOURCE_FILE%~ STRING_COMPARE_REGEXP ~^_?hakt\.cre$~ = 0) BEGIN // if _HAKT
    WRITE_ASCII ("%item_offset%" +        (0x14 * "%item_index%")) ~sw1h46~ #8
  END ELSE BEGIN
    WRITE_ASCII ("%item_offset%" +        (0x14 * "%item_index%")) ~sw1h22~ #8
  END
  WRITE_SHORT ("%item_offset%" + 0x06 + (0x14 * "%item_index%")) 0
  WRITE_LONG  ("%item_offset%" + 0x08 + (0x14 * "%item_index%")) 0
  WRITE_LONG  ("%item_offset%" + 0x0c + (0x14 * "%item_index%")) 0
  WRITE_LONG  ("%item_offset%" + 0x10 + (0x14 * "%item_index%")) 0
  BUT_ONLY_IF_IT_CHANGES

COPY_EXISTING_REGEXP GLOB ~^\(_\|bg\)sendai\.cre$~ ~override~ // adds scimitar +1 to Sendai Argrim by replacing offhand weapon
  READ_LONG 0x2b8 "slot_offset"
  READ_LONG 0x2bc "item_offset"
  READ_SHORT ("%slot_offset%" + 0x04) "item_index"
  WRITE_ASCII ("%item_offset%" +        (0x14 * "%item_index%")) ~sw1h22~ #8
  WRITE_SHORT ("%item_offset%" + 0x06 + (0x14 * "%item_index%")) 0
  WRITE_LONG  ("%item_offset%" + 0x08 + (0x14 * "%item_index%")) 0
  WRITE_LONG  ("%item_offset%" + 0x0c + (0x14 * "%item_index%")) 0
  WRITE_LONG  ("%item_offset%" + 0x10 + (0x14 * "%item_index%")) 0
  BUT_ONLY_IF_IT_CHANGES

COPY_EXISTING_REGEXP GLOB ~^[_s]toblack\.sto$~ ~override~  // adds Ninja-To +1 to Black Lily's store
  READ_LONG 0x34 "sale_offset"
  READ_LONG 0x38 "sale_num"
  READ_LONG 0x2c "item_offset"
  READ_LONG 0x4c "drink_offset"
  READ_LONG 0x70 "cure_offset"
  FOR (index = 0 ; index < sale_num ; index = index + 1) BEGIN
    READ_ASCII ("%sale_offset%" +        ("%index%" * 0x1c)) "item"
    PATCH_IF ("%item%" STRING_COMPARE_REGEXP ~^_?SW1H07$~) BEGIN // short sword
      INSERT_BYTES  ("%sale_offset%" +        (("%index%" + 1) * 0x1c)) 28
        WRITE_ASCII ("%sale_offset%" +        (("%index%" + 1) * 0x1c)) ~sw1h49~
        WRITE_LONG  ("%sale_offset%" + 0x10 + (("%index%" + 1) * 0x1c)) 1 // identified
        WRITE_LONG  ("%sale_offset%" + 0x14 + (("%index%" + 1) * 0x1c)) 1 // quantity
      SET "index" = "%sale_num%" // kills loop
      WRITE_LONG 0x38 ("%sale_num%" + 1)
      PATCH_IF NOT ("%item_offset%" < "%sale_offset%") BEGIN
        WRITE_LONG 0x2c ("%item_offset%" + 0x1c)
      END
      PATCH_IF NOT ("%drink_offset%" < "%sale_offset%") BEGIN
        WRITE_LONG 0x4c ("%drink_offset%" + 0x1c)
      END
      PATCH_IF NOT ("%cure_offset%" < "%sale_offset%") BEGIN
        WRITE_LONG 0x70 ("%cure_offset%" + 0x1c)
      END
    END
  END
  BUT_ONLY_IF_IT_CHANGES

COPY_EXISTING_REGEXP GLOB ~^[_h]ighhedg\.sto$~ ~override~  // adds lev 1, 2 scrolls to High Hedge
  READ_LONG 0x34 "sale_offset"
  READ_LONG 0x38 "sale_num"
  READ_LONG 0x2c "item_offset"
  READ_LONG 0x4c "drink_offset"
  READ_LONG 0x70 "cure_offset"
  SET "delta" = 0
  FOR (index = 0 ; (index + delta) < (sale_num + delta) ; index = index + 1) BEGIN
    READ_ASCII ("%sale_offset%" +        ("%index%" * 0x1C)) "item"
    PATCH_IF ("%item%" STRING_COMPARE_REGEXP "^_?scrl70$" = 0) BEGIN // after Color Spray
      INSERT_BYTES  ("%sale_offset%" +        (("%index%" + 1) * 0x1c)) 28
        WRITE_ASCII ("%sale_offset%" +        (("%index%" + 1) * 0x1c)) ~scrla6~ // spook
        WRITE_LONG  ("%sale_offset%" + 0x10 + (("%index%" + 1) * 0x1c)) 1 // identified
        WRITE_LONG  ("%sale_offset%" + 0x14 + (("%index%" + 1) * 0x1c)) 2 // quantity
      INSERT_BYTES  ("%sale_offset%" +        (("%index%" + 1) * 0x1c)) 28
        WRITE_ASCII ("%sale_offset%" +        (("%index%" + 1) * 0x1c)) ~scrl5u~ // reflected image
        WRITE_LONG  ("%sale_offset%" + 0x10 + (("%index%" + 1) * 0x1c)) 1 // identified
        WRITE_LONG  ("%sale_offset%" + 0x14 + (("%index%" + 1) * 0x1c)) 2 // quantity
      INSERT_BYTES  ("%sale_offset%" +        (("%index%" + 1) * 0x1c)) 28
        WRITE_ASCII ("%sale_offset%" +        (("%index%" + 1) * 0x1c)) ~scrl6d~ // find familiar
        WRITE_LONG  ("%sale_offset%" + 0x10 + (("%index%" + 1) * 0x1c)) 1 // identified
        WRITE_LONG  ("%sale_offset%" + 0x14 + (("%index%" + 1) * 0x1c)) 2 // quantity
      SET "delta" = "%delta%" + 3
    END ELSE
    PATCH_IF ("%item%" STRING_COMPARE_REGEXP "^_?scrl89$" = 0) BEGIN // after Horror
      INSERT_BYTES  ("%sale_offset%" +        (("%index%" + 1) * 0x1c)) 28
        WRITE_ASCII ("%sale_offset%" +        (("%index%" + 1) * 0x1c)) ~scrl6e~ // power word:sleep
        WRITE_LONG  ("%sale_offset%" + 0x10 + (("%index%" + 1) * 0x1c)) 1 // identified
        WRITE_LONG  ("%sale_offset%" + 0x14 + (("%index%" + 1) * 0x1c)) 2 // quantity
      SET "delta" = "%delta%" + 1
    END
  END
  PATCH_IF ("%delta%" > 0) BEGIN
    WRITE_LONG 0x38 ("%sale_num%" + "%delta%")
    PATCH_IF NOT ("%item_offset%" < "%sale_offset%") BEGIN
      WRITE_LONG 0x2c ("%item_offset%" + ("%delta%" * 0x1c))
    END
    PATCH_IF NOT ("%drink_offset%" < "%sale_offset%") BEGIN
      WRITE_LONG 0x4c ("%drink_offset%" + ("%delta%" * 0x1c))
    END
    PATCH_IF NOT ("%cure_offset%" < "%sale_offset%") BEGIN
      WRITE_LONG 0x70 ("%cure_offset%" + ("%delta%" * 0x1c))
    END
  END
  BUT_ONLY_IF_IT_CHANGES

COPY_EXISTING_REGEXP GLOB ~^_?sto0703\.sto$~ ~override~  // adds lev 1-4 scrolls to Sorcerous Sundries
  READ_LONG 0x34 "sale_offset"
  READ_LONG 0x38 "sale_num"
  READ_LONG 0x2c "item_offset"
  READ_LONG 0x4c "drink_offset"
  READ_LONG 0x70 "cure_offset"
  SET "delta" = 0
  FOR (index = 0 ; (index + delta) < (sale_num + delta) ; index = index + 1) BEGIN
    READ_ASCII ("%sale_offset%" +        ("%index%" * 0x1c)) "item"
    PATCH_IF ("%item%" STRING_COMPARE_REGEXP "^_?scrl70$" = 0) BEGIN // after Color Spray
      INSERT_BYTES  ("%sale_offset%" +        (("%index%" + 1) * 0x1c)) 28
        WRITE_ASCII ("%sale_offset%" +        (("%index%" + 1) * 0x1c)) ~scrla6~ // spook
        WRITE_LONG  ("%sale_offset%" + 0x10 + (("%index%" + 1) * 0x1c)) 1 // identified
        WRITE_LONG  ("%sale_offset%" + 0x14 + (("%index%" + 1) * 0x1c)) 2 // quantity
      INSERT_BYTES  ("%sale_offset%" +        (("%index%" + 1) * 0x1c)) 28
        WRITE_ASCII ("%sale_offset%" +        (("%index%" + 1) * 0x1c)) ~scrl5u~ // reflected image
        WRITE_LONG  ("%sale_offset%" + 0x10 + (("%index%" + 1) * 0x1c)) 1 // identified
        WRITE_LONG  ("%sale_offset%" + 0x14 + (("%index%" + 1) * 0x1c)) 2 // quantity
      INSERT_BYTES  ("%sale_offset%" +        (("%index%" + 1) * 0x1c)) 28
        WRITE_ASCII ("%sale_offset%" +        (("%index%" + 1) * 0x1c)) ~scrl6d~ // find familiar
        WRITE_LONG  ("%sale_offset%" + 0x10 + (("%index%" + 1) * 0x1c)) 1 // identified
        WRITE_LONG  ("%sale_offset%" + 0x14 + (("%index%" + 1) * 0x1c)) 2 // quantity
      SET "delta" = "%delta%" + 3
    END ELSE
    PATCH_IF ("%item%" STRING_COMPARE_REGEXP "^_?scrl89$" = 0) BEGIN // after Horror
      INSERT_BYTES  ("%sale_offset%" +        (("%index%" + 1) * 0x1c)) 28
        WRITE_ASCII ("%sale_offset%" +        (("%index%" + 1) * 0x1c)) ~scrl6e~ // power word:sleep
        WRITE_LONG  ("%sale_offset%" + 0x10 + (("%index%" + 1) * 0x1c)) 1 // identified
        WRITE_LONG  ("%sale_offset%" + 0x14 + (("%index%" + 1) * 0x1c)) 2 // quantity
      SET "delta" = "%delta%" + 1
    END ELSE
    PATCH_IF ("%item%" STRING_COMPARE_REGEXP "^_?scrl1k$" = 0) BEGIN // after lightning bolt
      INSERT_BYTES  ("%sale_offset%" +        (("%index%" + 1) * 0x1c)) 28
        WRITE_ASCII ("%sale_offset%" +        (("%index%" + 1) * 0x1c)) ~scrla5~ // melf's minute meteors
        WRITE_LONG  ("%sale_offset%" + 0x10 + (("%index%" + 1) * 0x1c)) 1 // identified
        WRITE_LONG  ("%sale_offset%" + 0x14 + (("%index%" + 1) * 0x1c)) 2 // quantity
      INSERT_BYTES  ("%sale_offset%" +        (("%index%" + 1) * 0x1c)) 28
        WRITE_ASCII ("%sale_offset%" +        (("%index%" + 1) * 0x1c)) ~scrl6i~ // protection from cold
        WRITE_LONG  ("%sale_offset%" + 0x10 + (("%index%" + 1) * 0x1c)) 1 // identified
        WRITE_LONG  ("%sale_offset%" + 0x14 + (("%index%" + 1) * 0x1c)) 2 // quantity
      INSERT_BYTES  ("%sale_offset%" +        (("%index%" + 1) * 0x1c)) 28
        WRITE_ASCII ("%sale_offset%" +        (("%index%" + 1) * 0x1c)) ~scrl6l~ // hold undad
        WRITE_LONG  ("%sale_offset%" + 0x10 + (("%index%" + 1) * 0x1c)) 1 // identified
        WRITE_LONG  ("%sale_offset%" + 0x14 + (("%index%" + 1) * 0x1c)) 2 // quantity
      SET "delta" = "%delta%" + 3
    END ELSE
    PATCH_IF ("%item%" STRING_COMPARE_REGEXP "^_?scrl5i$" = 0) BEGIN // after greater malison
      INSERT_BYTES  ("%sale_offset%" +        (("%index%" + 1) * 0x1c)) 28
        WRITE_ASCII ("%sale_offset%" +        (("%index%" + 1) * 0x1c)) ~scrl6r~ // spider spawn
        WRITE_LONG  ("%sale_offset%" + 0x10 + (("%index%" + 1) * 0x1c)) 1 // identified
        WRITE_LONG  ("%sale_offset%" + 0x14 + (("%index%" + 1) * 0x1c)) 2 // quantity
      SET "delta" = "%delta%" + 1
    END
  END
  PATCH_IF ("%delta%" > 0) BEGIN
    WRITE_LONG 0x38 ("%sale_num%" + "%delta%")
    PATCH_IF NOT ("%item_offset%" < "%sale_offset%") BEGIN
      WRITE_LONG 0x2c ("%item_offset%" + ("%delta%" * 0x1c))
    END
    PATCH_IF NOT ("%drink_offset%" < "%sale_offset%") BEGIN
      WRITE_LONG 0x4c ("%drink_offset%" + ("%delta%" * 0x1c))
    END
    PATCH_IF NOT ("%cure_offset%" < "%sale_offset%") BEGIN
      WRITE_LONG 0x70 ("%cure_offset%" + ("%delta%" * 0x1c))
    END
  END
  BUT_ONLY_IF_IT_CHANGES

COPY_EXISTING_REGEXP GLOB ~^[_h]ighhedg\.sto$~ ~override~ // High Hedge
                          ~^_?taerum\.sto$~    ~override~ // Thunderhammer Smithy
                          ~^_?sto4803\.sto$~   ~override~ // Nashkel General Store
                          ~^_?sto4909\.sto$~   ~override~ // Carnival
  READ_LONG  0x34 "sale_offset"
  READ_LONG  0x38 "sale_number"
  WRITE_LONG 0x38 ("%sale_number%" + 1)
  READ_LONG  0x2c "purchase_offset"
  READ_LONG  0x4c "drink_offset"
  READ_LONG  0x70 "cure_offset"
  INSERT_BYTES  ("%sale_offset%" +        ("%sale_number%" * 0x1c)) 28
    PATCH_IF (~%SOURCE_FILE%~ STRING_COMPARE_REGEXP ~^[_h]ighhedg\.sto$~ = 0) BEGIN // if store is High Hedge
      WRITE_ASCII ("%sale_offset%" +        ("%sale_number%" * 0x1c)) ~cdioun1~
    END ELSE
    PATCH_IF (~%SOURCE_FILE%~ STRING_COMPARE_REGEXP ~^_?taerum\.sto$~ = 0) BEGIN // if store is Thunderhammer Smithy
      WRITE_ASCII ("%sale_offset%" +        ("%sale_number%" * 0x1c)) ~cdplcase~
    END ELSE
    PATCH_IF (~%SOURCE_FILE%~ STRING_COMPARE_REGEXP ~^_?sto4803\.sto$~ = 0) BEGIN // if store is _STO4803
      WRITE_ASCII ("%sale_offset%" +        ("%sale_number%" * 0x1c)) ~cdplbag~
    END ELSE BEGIN
      WRITE_ASCII ("%sale_offset%" +        ("%sale_number%" * 0x1c)) ~cdplquiv~ // if store is _STO4909
    END
    WRITE_LONG  ("%sale_offset%" + 0x10 + ("%sale_number%" * 0x1c)) 3 // identified and unstealable
    WRITE_LONG  ("%sale_offset%" + 0x14 + ("%sale_number%" * 0x1c)) 1 // quantity
  PATCH_IF NOT ("%purchase_offset%" < "%sale_offset%") BEGIN
    WRITE_LONG 0x2c ("%purchase_offset%" + 0x1c)
  END
  PATCH_IF NOT ("%drink_offset%" < "%sale_offset%") BEGIN
    WRITE_LONG 0x4c ("%drink_offset%" + 0x1c)
  END
  PATCH_IF NOT ("%cure_offset%" < "%sale_offset%") BEGIN
    WRITE_LONG 0x70 ("%cure_offset%" + 0x1c)
  END
  BUT_ONLY_IF_IT_CHANGES

PRINT @1
COPY_EXISTING_REGEXP GLOB ~^.+\.sto$~ ~override~  // adds generic scimitars to stores that sell short swords if not present already
  READ_LONG 0x34 "sale_offset"  ELSE 0
  READ_LONG 0x38 "sale_num"     ELSE 0
  READ_LONG 0x2c "item_offset"  ELSE 0
  READ_LONG 0x4c "drink_offset" ELSE 0
  READ_LONG 0x70 "cure_offset"  ELSE 0
  SET "scimitar_exist" = 0
  SET "scimitar_insert" = 0
  FOR (index = 0 ; index < sale_num ; index = index + 1) BEGIN
    READ_ASCII ("%sale_offset%" +        ("%index%" * 0x1c)) "item"
    PATCH_IF (("%item%" STRING_COMPARE_CASE "sw1h07" = 0) OR ("%item%" STRING_COMPARE_CASE "_sw1h07" = 0)) BEGIN // short sword
      READ_ASCII ("%sale_offset%" +        ("%index%" * 0x1c)) "clone" (28) // read short sword entry
      SET "scimitar_insert" = ("%index%" + 1) // sets right after as insert point
    END ELSE
    PATCH_IF (("%item%" STRING_COMPARE_CASE "sw1h20" = 0) OR ("%item%" STRING_COMPARE_CASE "_sw1h20" = 0)) BEGIN // scimitar
      SET "scimitar_exist" = 1 // scimitars are already present
      SET "index" = "%sale_num%" // kills loop
    END
  END
  PATCH_IF (("%scimitar_exist%" = 0) AND ("%scimitar_insert%" > 0)) BEGIN // short swords present but not scimitars
    INSERT_BYTES            ("%sale_offset%" + ("%scimitar_insert%" * 0x1C)) 28
      WRITE_EVALUATED_ASCII ("%sale_offset%" + ("%scimitar_insert%" * 0x1C)) ~%clone%~ // clones short sword entry
      WRITE_ASCII           ("%sale_offset%" + ("%scimitar_insert%" * 0x1C)) ~sw1h20~ #8  // scimitar
    // correcting offsets and such
    WRITE_LONG 0x38 ("%sale_num%" + 1)
    PATCH_IF NOT ("%item_offset%" < "%sale_offset%") BEGIN
      WRITE_LONG 0x2c ("%item_offset%" + 0x1c)
    END
    PATCH_IF NOT ("%drink_offset%" < "%sale_offset%") BEGIN
      WRITE_LONG 0x4c ("%drink_offset%" + 0x1c)
    END
    PATCH_IF NOT ("%cure_offset%" < "%sale_offset%") BEGIN
      WRITE_LONG 0x70 ("%cure_offset%" + 0x1c)
    END
  END
  BUT_ONLY_IF_IT_CHANGES

// if Ashes of Embers is installed, the katana and quivers should be usable by all
ACTION_IF ((FILE_EXISTS_IN_GAME ~j#swsoa.txt~) OR                        // SoA version of AoE (for BGT)
           (FILE_EXISTS_IN_GAME ~J#ToBAoE.txt~) OR                       // Tutu version of AoE
           (FILE_EXISTS_IN_GAME ~j#sensibleweaponsmods.txt~)) THEN BEGIN // mod-only version of AoE

  COPY_EXISTING ~cdplquiv.itm~ ~override~ // quiver of plenty +0
                ~cdplcase.itm~ ~override~ // case of plenty +0
                ~cdplbag.itm~ ~override~  // bag of plenty +0
                ~cdkat2.itm~ ~override~   // katana +2
    WRITE_LONG 0x1e 0 // remove all class/race/align unusabilities
    WRITE_BYTE 0x29 0 // remove all kit unusabilities
    WRITE_BYTE 0x2b 0 // remove all kit unusabilities
    WRITE_BYTE 0x2d 0 // remove all kit unusabilities
    WRITE_BYTE 0x2f 0 // remove all kit unusabilities

  COPY_EXISTING ~cdkat2.itm~ ~override~   // katana +2
    WRITE_BYTE 0x2b 0b00000010 // remove all kit unusabilities except for Beastmaster

END

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// Reveal City Areas                                \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @110000 DESIGNATED 1100
GROUP @13
REQUIRE_PREDICATE FILE_EXISTS_IN_GAME ~_sw1h01.itm~ @15 // tutu only

// this file does nothing, it just allows other mods to detect this component
COPY_EXISTING ~sw1h01.itm~ ~override/cdt01100.g3~

//fixes Narlen's goofiness
COPY_EXISTING ~_ar0300.bcs~ ~override~
  DECOMPILE_BCS_TO_BAF
    REPLACE_TEXTUALLY ~TimeGT(20)~ ~TimeGT(20) Global("CDNarlenExists","GLOBAL",0)~
    REPLACE_TEXTUALLY ~TimeLT(4)~  ~TimeLT(4)  Global("CDNarlenExists","GLOBAL",0)~
    REPLACE_TEXTUALLY ~TimeGT(4)~  ~TimeGT(4)  Global("CDNarlenExists","GLOBAL",1)~
    REPLACE_TEXTUALLY ~\bActivate("narlen")~   ~Activate("narlen") SetGlobal("CDNarlenExists","GLOBAL",1)~
    REPLACE_TEXTUALLY ~Deactivate("narlen")~ ~Deactivate("narlen") SetGlobal("CDNarlenExists","GLOBAL",0)~
  COMPILE_BAF_TO_BCS
  BUT_ONLY_IF_IT_CHANGES

EXTEND_BOTTOM ~_ar0100.bcs~ ~BG2_Tweaks/baf/reveal.baf~ // NW Baldur's Gate
EXTEND_BOTTOM ~_ar0200.bcs~ ~BG2_Tweaks/baf/reveal.baf~ // N Baldur's Gate
EXTEND_BOTTOM ~_ar0300.bcs~ ~BG2_Tweaks/baf/reveal.baf~ // NE Baldur's Gate
EXTEND_BOTTOM ~_ar0600.bcs~ ~BG2_Tweaks/baf/reveal.baf~ // W Baldur's Gate
EXTEND_BOTTOM ~_ar0700.bcs~ ~BG2_Tweaks/baf/reveal.baf~ // Central Baldur's Gate
EXTEND_BOTTOM ~_ar0800.bcs~ ~BG2_Tweaks/baf/reveal.baf~ // E Baldur's Gate
EXTEND_BOTTOM ~_ar1100.bcs~ ~BG2_Tweaks/baf/reveal.baf~ // SW Baldur's Gate
EXTEND_BOTTOM ~_ar1200.bcs~ ~BG2_Tweaks/baf/reveal.baf~ // S Baldur's Gate
EXTEND_BOTTOM ~_ar1300.bcs~ ~BG2_Tweaks/baf/reveal.baf~ // SE Baldur's Gate
EXTEND_BOTTOM ~_ar2600.bcs~ ~BG2_Tweaks/baf/reveal.baf~ // Candlekeep Prologue
EXTEND_BOTTOM ~_ar2626.bcs~ ~BG2_Tweaks/baf/reveal.baf~ // Candlekeep Chapter 6
EXTEND_BOTTOM ~_ar3300.bcs~ ~BG2_Tweaks/baf/reveal.baf~ // Beregost
EXTEND_BOTTOM ~_ar4800.bcs~ ~BG2_Tweaks/baf/reveal.baf~ // Nashkel

ACTION_IF FILE_EXISTS_IN_GAME ~fw1500.are~ THEN BEGIN // if TotSC is installed

  EXTEND_BOTTOM ~_ar1000.bcs~ ~BG2_Tweaks/baf/reveal.baf~ // Ulgoth's Beard

END

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// Add Map Notes                                    \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @111000 DESIGNATED 1110
GROUP @13
GROUP @19
REQUIRE_PREDICATE FILE_EXISTS_IN_GAME ~_sw1h01.itm~ @15 // tutu only

// this file does nothing, it just allows other mods to detect this component
COPY_EXISTING ~sw1h01.itm~ ~override/cdt01110.g3~

/*
Note Color key:
Gray    - Caves & Dungeon Entrances
Violet  - Inns & Taverns
Green   - Temples
Orange  - Info points
Red     - Open space locations (i.e. city squares)
Blue    - Buildings of note
Dk Blue - People
Lt Gray - Stores
*/

COPY_EXISTING ~fw0100.are~ ~override~ // NW Baldur's Gate
  ADD_MAP_NOTE #1230 #1706 ~blue~   @111010
  ADD_MAP_NOTE #3610 #3007 ~violet~ @111011
  ADD_MAP_NOTE #2940 #1716 ~violet~ @111012
  BUT_ONLY_IF_IT_CHANGES

COPY_EXISTING ~fw0123.are~ ~override~ // Temple of Bhaal
  ADD_MAP_NOTE #1668 #1297 ~blue~ @111096
  BUT_ONLY_IF_IT_CHANGES

COPY_EXISTING ~fw0146.are~ ~override~ // Thieves' Maze
  ADD_MAP_NOTE #583 #1148 ~gray~ @111095
  ADD_MAP_NOTE #414 #2763 ~gray~ @111095
  BUT_ONLY_IF_IT_CHANGES

COPY_EXISTING ~fw0200.are~ ~override~ // N Baldur's Gate
  ADD_MAP_NOTE #1550 #1123 ~green~  @111013
  ADD_MAP_NOTE #200  #1462 ~red~    @111014
  ADD_MAP_NOTE #3581 #1464 ~blue~   @111015
  ADD_MAP_NOTE #1963 #2795 ~blue~   @111016
  ADD_MAP_NOTE #3782 #2418 ~violet~ @111017
  BUT_ONLY_IF_IT_CHANGES

COPY_EXISTING ~fw0300.are~ ~override~ // NE Baldur's Gate
  ADD_MAP_NOTE #144  #1799 ~violet~ @111018
  ADD_MAP_NOTE #541  #2854 ~violet~ @111019
  ADD_MAP_NOTE #1215 #1345 ~green~  @111020
  ADD_MAP_NOTE #2276 #3134 ~blue~   @111021
  BUT_ONLY_IF_IT_CHANGES

COPY_EXISTING ~fw0400.are~ ~override~ // Zombie Farm
  ADD_MAP_NOTE #638 #454 ~blue~ @111058
  BUT_ONLY_IF_IT_CHANGES

COPY_EXISTING ~fw0600.are~ ~override~ // W Baldur's Gate
  ADD_MAP_NOTE #1110 #1080 ~green~ @111083
  ADD_MAP_NOTE #1520 #2289 ~blue~  @111084
  ADD_MAP_NOTE #3439 #873  ~blue~  @111085
  ADD_MAP_NOTE #2914 #551  ~blue~  @111086
  BUT_ONLY_IF_IT_CHANGES

COPY_EXISTING ~fw0700.are~ ~override~ // Central Baldur's Gate
  ADD_MAP_NOTE #2536 #873 ~blue~      @111087
  ADD_MAP_NOTE #730  #567 ~lightgray~ @111088
  ADD_MAP_NOTE #1729 #1230 ~violet~   @111089
  ADD_MAP_NOTE #1993 #720 ~blue~      @111090
  BUT_ONLY_IF_IT_CHANGES

COPY_EXISTING ~fw0800.are~ ~override~ // E Baldur's Gate
  ADD_MAP_NOTE #1780 #2451 ~green~     @111065
  ADD_MAP_NOTE #1055 #2015 ~lightgray~ @111066
  ADD_MAP_NOTE #3151 #1093 ~lightgray~ @111067
  ADD_MAP_NOTE #1073 #3017 ~lightgray~ @111068
  ADD_MAP_NOTE #1728 #1354 ~violet~    @111069
  ADD_MAP_NOTE #3259 #1974 ~lightgray~ @111070
  BUT_ONLY_IF_IT_CHANGES

COPY_EXISTING ~fw0900.are~ ~override~ // Wyrm's Crossing
  ADD_MAP_NOTE #2342 #2028 ~gray~ @111059
  ADD_MAP_NOTE #4093 #708  ~blue~ @111033
  BUT_ONLY_IF_IT_CHANGES

COPY_EXISTING ~fw1100.are~ ~override~ // SW Baldur's Gate
  ADD_MAP_NOTE #3535 #1327 ~blue~      @111079
  ADD_MAP_NOTE #1556 #1385 ~blue~      @111080
  ADD_MAP_NOTE #3255 #2534 ~blue~      @111081
  ADD_MAP_NOTE #2742 #1063 ~lightgray~ @111072
  ADD_MAP_NOTE #2487 #2377 ~lightgray~ @111072
  ADD_MAP_NOTE #2526 #823  ~violet~    @111082
  BUT_ONLY_IF_IT_CHANGES

COPY_EXISTING ~fw1200.are~ ~override~ // S Baldur's Gate
  ADD_MAP_NOTE #2568 #1858 ~blue~   @111074
  ADD_MAP_NOTE #3654 #2976 ~violet~ @111075
  ADD_MAP_NOTE #931  #2415 ~green~  @111076
  ADD_MAP_NOTE #400  #677  ~blue~   @111077
  ADD_MAP_NOTE #2624 #588 ~violet~  @111078
  BUT_ONLY_IF_IT_CHANGES

COPY_EXISTING ~fw1300.are~ ~override~ // SE Baldur's Gate
  ADD_MAP_NOTE #166  #729  ~violet~    @111071
  ADD_MAP_NOTE #2135 #316  ~lightgray~ @111072
  ADD_MAP_NOTE #960  #2561 ~lightgray~ @111072
  ADD_MAP_NOTE #1699 #2399 ~lightgray~ @111073
  BUT_ONLY_IF_IT_CHANGES

COPY_EXISTING ~fw1400.are~ ~override~ // Fishing Village
  ADD_MAP_NOTE #1899 #2402 ~gray~ @111057
  ADD_MAP_NOTE #3504 #2233 ~blue~ @111058
  BUT_ONLY_IF_IT_CHANGES

COPY_EXISTING ~fw1600.are~ ~override~ // Cloakwood Druids
  ADD_MAP_NOTE #2194 #1001 ~gray~ @111034
  ADD_MAP_NOTE #804  #2174 ~blue~ @111062
  BUT_ONLY_IF_IT_CHANGES

COPY_EXISTING ~fw1700.are~ ~override~ // Cloakwood Wyverns
  ADD_MAP_NOTE #4222 #1709 ~gray~ @111034
  BUT_ONLY_IF_IT_CHANGES

COPY_EXISTING ~fw1800.are~ ~override~ // Cloakwood Mines Exterior
  ADD_MAP_NOTE #3247 #793  ~blue~ @111042
  ADD_MAP_NOTE #1375 #1852 ~blue~ @111008
  ADD_MAP_NOTE #1904 #1554 ~blue~ @111004
  BUT_ONLY_IF_IT_CHANGES

COPY_EXISTING ~fw1801.are~ ~override~ // Cloakwood Mines Lvl 1
  ADD_MAP_NOTE #1975 #71   ~orange~ @111063
  ADD_MAP_NOTE #341  #133  ~gray~   @111064
  ADD_MAP_NOTE #1406 #1563 ~gray~   @111050
  BUT_ONLY_IF_IT_CHANGES

COPY_EXISTING ~fw1802.are~ ~override~ // Cloakwood Mines Lvl 3
  ADD_MAP_NOTE #2612 #2916 ~gray~ @111050
  ADD_MAP_NOTE #756  #1219 ~gray~ @111051
  BUT_ONLY_IF_IT_CHANGES

COPY_EXISTING ~fw1803.are~ ~override~ // Cloakwood Mines Lvl 4
  ADD_MAP_NOTE #205  #1177 ~orange~ @111064
  ADD_MAP_NOTE #1711 #209  ~gray~   @111051
  BUT_ONLY_IF_IT_CHANGES

COPY_EXISTING ~fw1804.are~ ~override~ // Cloakwood Mines Lvl 2
  ADD_MAP_NOTE #436  #1564 ~gray~ @111050
  ADD_MAP_NOTE #1157 #110  ~gray~ @111051
  BUT_ONLY_IF_IT_CHANGES

COPY_EXISTING ~fw1900.are~ ~override~ // Bandit Camp
  ADD_MAP_NOTE #2326 #773 ~gray~ @111034
  ADD_MAP_NOTE #3655 #957 ~blue~ @111060
  BUT_ONLY_IF_IT_CHANGES

COPY_EXISTING ~fw2100.are~ ~override~ // Cloakwood Spider Nest
  ADD_MAP_NOTE #1817 #1240 ~blue~ @111061
  BUT_ONLY_IF_IT_CHANGES

COPY_EXISTING ~fw2200.are~ ~override~ // Cloakwood Lodge
  ADD_MAP_NOTE #2477 #1790 ~blue~ @111043
  BUT_ONLY_IF_IT_CHANGES

COPY_EXISTING ~fw2300.are~ ~override~ // Friendly Arm Inn
  ADD_MAP_NOTE #1550 #2970 ~red~    @111005
  ADD_MAP_NOTE #3511 #1903 ~violet~ @111024
  ADD_MAP_NOTE #3873 #2463 ~green~  @111025
  BUT_ONLY_IF_IT_CHANGES

COPY_EXISTING ~fw2600.are~ ~override~ // Candlekeep Prologue
              ~fw2626.are~ ~override~ // Candlekeep Chapter 6
  ADD_MAP_NOTE #1071 #557  ~violet~ @111001
  ADD_MAP_NOTE #2959 #464  ~blue~   @111002
  ADD_MAP_NOTE #3843 #728  ~green~  @111003
  ADD_MAP_NOTE #4303 #1410 ~blue~   @111004
  ADD_MAP_NOTE #4396 #2561 ~red~    @111005
  ADD_MAP_NOTE #2607 #1679 ~blue~   @111006
  ADD_MAP_NOTE #2053 #2696 ~blue~   @111007
  ADD_MAP_NOTE #1569 #2487 ~blue~   @111008
  ADD_MAP_NOTE #3531 #2916 ~blue~   @111009
  ADD_MAP_NOTE #3063 #2981 ~blue~   @111022
  BUT_ONLY_IF_IT_CHANGES

COPY_EXISTING ~fw2615.are~ ~override~ // Candlekeep Catacombs Lvl 1
  ADD_MAP_NOTE #4026 #1712 ~gray~ @111049
  ADD_MAP_NOTE #3826 #921 ~gray~  @111049
  BUT_ONLY_IF_IT_CHANGES

COPY_EXISTING ~fw2619.are~ ~override~ // Candlekeep Catacombs Lvl 2
  ADD_MAP_NOTE #3552 #2308 ~gray~ @111049
  ADD_MAP_NOTE #240  #1040 ~gray~ @111049
  BUT_ONLY_IF_IT_CHANGES

COPY_EXISTING ~fw2800.are~ ~override~ // Coast Way
  ADD_MAP_NOTE #2359 #3335 ~orange~ @111023
  BUT_ONLY_IF_IT_CHANGES

COPY_EXISTING ~fw3100.are~ ~override~ // Shipwreck Coast
  ADD_MAP_NOTE #958 #1646 ~orange~ @111055
  BUT_ONLY_IF_IT_CHANGES

COPY_EXISTING ~fw3200.are~ ~override~ // High Hedge
  ADD_MAP_NOTE #2893 #2671 ~lightgray~ @111032
  ADD_MAP_NOTE #1732 #1583 ~blue~      @111033
  BUT_ONLY_IF_IT_CHANGES

COPY_EXISTING ~fw3300.are~ ~override~ // Beregost
  ADD_MAP_NOTE #3711 #3754 ~violet~    @111026
  ADD_MAP_NOTE #1470 #2731 ~violet~    @111027
  ADD_MAP_NOTE #4612 #2906 ~lightgray~ @111028
  ADD_MAP_NOTE #3403 #2039 ~violet~    @111029
  ADD_MAP_NOTE #2596 #2189 ~violet~    @111030
  ADD_MAP_NOTE #3643 #993  ~blue~      @111031
  BUT_ONLY_IF_IT_CHANGES

COPY_EXISTING ~fw3400.are~ ~override~ // Beregost Temple
  ADD_MAP_NOTE #414  #916 ~green~ @111046
  ADD_MAP_NOTE #1463 #893 ~green~ @111047
  BUT_ONLY_IF_IT_CHANGES

COPY_EXISTING ~fw3600.are~ ~override~ // Lighthouse
  ADD_MAP_NOTE #393 #991  ~gray~   @111034
  ADD_MAP_NOTE #883 #2702 ~orange~ @111056
  BUT_ONLY_IF_IT_CHANGES

COPY_EXISTING ~fw3800.are~ ~override~ // South Beregost Road
  ADD_MAP_NOTE #1861 #2269 ~gray~   @111034
  ADD_MAP_NOTE #3172 #1447 ~orange~ @111035
  BUT_ONLY_IF_IT_CHANGES

COPY_EXISTING ~fw3900.are~ ~override~ // Ulcaster Ruins
  ADD_MAP_NOTE #3159 #663 ~gray~ @111048
  BUT_ONLY_IF_IT_CHANGES

COPY_EXISTING ~fw4000.are~ ~override~ // Gullykin
  ADD_MAP_NOTE #882 #721 ~orange~ @111053
  BUT_ONLY_IF_IT_CHANGES

COPY_EXISTING ~fw4100.are~ ~override~ // Archaelogical Dig
  ADD_MAP_NOTE #3035 #1285 ~gray~   @111034
  ADD_MAP_NOTE #3751 #2173 ~orange~ @111023
  BUT_ONLY_IF_IT_CHANGES

COPY_EXISTING ~fw4200.are~ ~override~ // Fisherman's Lake
  ADD_MAP_NOTE #1379 #714 ~orange~ @111045
  BUT_ONLY_IF_IT_CHANGES

COPY_EXISTING ~fw4400.are~ ~override~ // Lonely Peaks
  ADD_MAP_NOTE #1485 #1199 ~gray~   @111034
  ADD_MAP_NOTE #2711 #2694 ~orange~ @111045
  BUT_ONLY_IF_IT_CHANGES

COPY_EXISTING ~fw4500.are~ ~override~ // Firewine Bridge
  ADD_MAP_NOTE #4027 #1983 ~gray~ @111054
  BUT_ONLY_IF_IT_CHANGES

COPY_EXISTING ~fw4700.are~ ~override~ // Xvart Village
  ADD_MAP_NOTE #4217 #1025 ~gray~ @111034
  BUT_ONLY_IF_IT_CHANGES

COPY_EXISTING ~fw4800.are~ ~override~ // Nashkel
  ADD_MAP_NOTE #2725 #949  ~green~     @111013
  ADD_MAP_NOTE #1308 #674  ~violet~    @111036
  ADD_MAP_NOTE #1665 #902  ~lightgray~ @111037
  ADD_MAP_NOTE #2689 #2441 ~violet~    @111038
  ADD_MAP_NOTE #4765 #558  ~violet~    @111039
  ADD_MAP_NOTE #3331 #1780 ~blue~      @111040
  BUT_ONLY_IF_IT_CHANGES

COPY_EXISTING ~fw5000.are~ ~override~ // Valley of the Tombs
  ADD_MAP_NOTE #944  #1793 ~gray~   @111034
  ADD_MAP_NOTE #1868 #2897 ~gray~   @111034
  ADD_MAP_NOTE #4494 #2855 ~gray~   @111034
  ADD_MAP_NOTE #1771 #886  ~orange~ @111094
  BUT_ONLY_IF_IT_CHANGES

COPY_EXISTING ~fw5100.are~ ~override~ // Gnoll Stronghold
  ADD_MAP_NOTE #591  #2147 ~gray~ @111034
  ADD_MAP_NOTE #152  #3376 ~gray~ @111034
  ADD_MAP_NOTE #818  #2420 ~gray~ @111034
  ADD_MAP_NOTE #2564 #2341 ~red~  @111041
  BUT_ONLY_IF_IT_CHANGES

COPY_EXISTING ~fw5200.are~ ~override~ // Cloudpeak Mountains
  ADD_MAP_NOTE #3163 #2674 ~gray~ @111034
  BUT_ONLY_IF_IT_CHANGES

COPY_EXISTING ~fw5201.are~ ~override~ // Firewine Ruins
  ADD_MAP_NOTE #2256 #1673 ~gray~ @111049
  ADD_MAP_NOTE #538  #215  ~gray~ @111049
  ADD_MAP_NOTE #1052 #1655 ~gray~ @111049
  BUT_ONLY_IF_IT_CHANGES

COPY_EXISTING ~fw5400.are~ ~override~ // Nashkel Mines Exterior
  ADD_MAP_NOTE #1202 #717  ~gray~   @111042
  ADD_MAP_NOTE #2796 #430  ~blue~   @111043
  ADD_MAP_NOTE #641  #2713 ~orange~ @111044
  BUT_ONLY_IF_IT_CHANGES

COPY_EXISTING ~fw5401.are~ ~override~ // Nashkel Mines Lvl 1
  ADD_MAP_NOTE #1333 #187  ~gray~ @111049
  ADD_MAP_NOTE #2076 #1884 ~gray~ @111050
  BUT_ONLY_IF_IT_CHANGES

COPY_EXISTING ~fw5402.are~ ~override~ // Nashkel Mines Lvl 2
  ADD_MAP_NOTE #2306 #2710 ~gray~ @111051
  ADD_MAP_NOTE #3610 #2516 ~gray~ @111050
  BUT_ONLY_IF_IT_CHANGES

COPY_EXISTING ~fw5403.are~ ~override~ // Nashkel Mines Lvl 3
  ADD_MAP_NOTE #1593 #120  ~gray~ @111051
  ADD_MAP_NOTE #3357 #2416 ~gray~ @111050
  BUT_ONLY_IF_IT_CHANGES

COPY_EXISTING ~fw5404.are~ ~override~ // Nashkel Mines Lvl 4
  ADD_MAP_NOTE #424  #1893 ~gray~ @111051
  ADD_MAP_NOTE #1416 #1211 ~gray~ @111034
  ADD_MAP_NOTE #2975 #908  ~gray~ @111049
  BUT_ONLY_IF_IT_CHANGES

COPY_EXISTING ~fw5506.are~ ~override~ // Candlekeep Catacombs Lvl 3
  ADD_MAP_NOTE #689 #124  ~gray~ @111049
  ADD_MAP_NOTE #279 #2107 ~gray~ @111052
  BUT_ONLY_IF_IT_CHANGES

// the following areas are TotSC only
ACTION_IF FILE_EXISTS_IN_GAME ~fw1500.are~ THEN BEGIN // if TotSC is installed

  COPY_EXISTING ~fw0500.are~ ~override~ // Durlag's Tower Exterior
    ADD_MAP_NOTE #2703 #2163 ~gray~ @111093
    BUT_ONLY_IF_IT_CHANGES

  COPY_EXISTING ~fw1000.are~ ~override~ // Ulgoth's Beard
    ADD_MAP_NOTE #2310 #612 ~blue~ @111091
    ADD_MAP_NOTE #350  #626 ~blue~ @111092
    BUT_ONLY_IF_IT_CHANGES

  COPY_EXISTING ~fw1009.are~ ~override~ // Ice Island Dungeon
    ADD_MAP_NOTE #2389 #1670 ~gray~ @111052
    ADD_MAP_NOTE #620  #170  ~gray~ @111052
    BUT_ONLY_IF_IT_CHANGES

  COPY_EXISTING ~fw1500.are~ ~override~ // N Werewolf Island
    ADD_MAP_NOTE #4200 #1683 ~blue~ @111043
    ADD_MAP_NOTE #3697 #1143 ~red~  @111055
    BUT_ONLY_IF_IT_CHANGES

  COPY_EXISTING ~fw2012.are~ ~override~ // Werewolf Caverns
    ADD_MAP_NOTE #974  #1392 ~gray~ @111052
    ADD_MAP_NOTE #2977 #401  ~gray~ @111052
    BUT_ONLY_IF_IT_CHANGES

END

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// Stores Sell Higher Stacks of Items               \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @112000 DESIGNATED 1120
GROUP @13
GROUP @19

// this file does nothing, it just allows other mods to detect this component
COPY_EXISTING ~sw1h01.itm~ ~override/cdt01120.g3~

PRINT @1
COPY_EXISTING_REGEXP GLOB ~^.+\.sto$~ ~override~
  READ_LONG 0x34 "sale_offset"
  READ_LONG 0x38 "sale_num"
  FOR (index = 0 ; index < sale_num ; index = index + 1) BEGIN
    READ_ASCII ("%sale_offset%" +        ("%index%" * 0x1c)) "item"
    PATCH_IF ("%item%" STRING_COMPARE_REGEXP "^_?\(BOLT\|DART\|AROW\|BULL\)01$" = 0) BEGIN // bolt01, dart01, arow01, bull01
      WRITE_SHORT ("%sale_offset%" + 0x0a + ("%index%" * 0x1c)) 120
    END ELSE
    PATCH_IF ("%item%" STRING_COMPARE_REGEXP "^_?\(DAGG05\|AX1H04\)$" = 0) BEGIN // dagg05, ax1h04
      WRITE_SHORT ("%sale_offset%" + 0x0a + ("%index%" * 0x1c)) 20
    END
  END
  BUT_ONLY_IF_IT_CHANGES

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// Reset rep at beginning of BG2 (BGT)              \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @113000 DESIGNATED 1130
GROUP @13
GROUP @19
REQUIRE_PREDICATE FILE_EXISTS_IN_GAME ~bg1start.2da~ @8

// this file does nothing, it just allows other mods to detect this component
COPY_EXISTING ~sw1h01.itm~ ~override/cdt01130.g3~

EXTEND_BOTTOM ~ar0602.bcs~ ~BG2_Tweaks/baf/ar0602.baf~

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// Gems and potions have lore                       \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @114000 DESIGNATED 1140
GROUP @13
GROUP @19

// this file does nothing, it just allows other mods to detect this component
COPY_EXISTING ~sw1h01.itm~ ~override/cdt01140.g3~

PRINT @1
COPY_EXISTING_REGEXP GLOB ~^.+\.itm$~ ~override~
  PATCH_IF (SOURCE_SIZE > 0x71) BEGIN
    READ_SHORT 0x1c "type"
    READ_SHORT 0x42 "lore"
    PATCH_IF ((("%type%" = 9) OR ("%type%" = 34)) AND ("%lore%" = 0)) BEGIN // potions or gems
      READ_LONG  0x34 "price"
      READ_LONG  0x64 "abil_off"
      READ_SHORT 0x68 "abil_num"
      // lore
      PATCH_IF ("%price%" > 4025) BEGIN
        WRITE_SHORT 0x42 (60 + (("%price%" - 4025) / 400))
      END ELSE
      PATCH_IF ("%price%" > 2025) BEGIN
        WRITE_SHORT 0x42 (50 + (("%price%" - 2025) / 200))
      END ELSE
      PATCH_IF ("%price%" > 1025) BEGIN
        WRITE_SHORT 0x42 (40 + (("%price%" - 1025) / 100))
      END ELSE
      PATCH_IF ("%price%" > 425) BEGIN
        WRITE_SHORT 0x42 (30 + (("%price%" - 425) / 60))
      END ELSE
      PATCH_IF ("%price%" > 175) BEGIN
        WRITE_SHORT 0x42 (20 + (("%price%" - 225) / 25))
      END ELSE
      PATCH_IF ("%price%" > 25) BEGIN
        WRITE_SHORT 0x42 (5 + (("%price%" - 50) / 10))
      END ELSE
      PATCH_IF ("%price%" > 5) BEGIN
        WRITE_SHORT 0x42 ("%price%" / 5)
      END ELSE BEGIN
        WRITE_SHORT 0x42 1
      END
      // force them to be id'd before use
      FOR (index = 0 ; index < abil_num ; index = index + 1) BEGIN
        READ_BYTE ("%abil_off%" +        ("%index%" * 0x38)) "abil_type"
        PATCH_IF ("%abil_type%" = 3) BEGIN // if magical ability
          WRITE_BYTE ("%abil_off%" + 0x01 + ("%index%" * 0x38)) 1 // force identification before use
        END
      END
      // strref adjustments
      READ_LONG 0x0c "name_id"
      READ_LONG 0x54 "desc_id"
      PATCH_IF ("%name_id%" > 2147483646) OR ("%name_id%" < 1) THEN BEGIN // if no id'd name, move un-id strings to id
        READ_LONG  0x08 "name_nid"
        WRITE_LONG 0x0c "%name_nid%"
      END
      PATCH_IF ("%desc_id%" > 2147483646) OR ("%desc_id%" < 1) THEN BEGIN // if no id'd desc, move un-id strings to id
        READ_LONG  0x50 "desc_nid"
        WRITE_LONG 0x54 "%desc_nid%"
      END
      PATCH_IF ("%type%" = 9) BEGIN // potions
        SAY 0x08  #6976
        SAY 0x50 #16264
      END ELSE BEGIN // gems
        SAY 0x08   #7106
        SAY 0x50 @114001
      END
    END
  END
  BUT_ONLY_IF_IT_CHANGES

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// Shapeshifter rebalancing                         \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @115000 DESIGNATED 1150
GROUP @13

ACTION_IF FILE_EXISTS_IN_GAME ~fw0100.are~ THEN BEGIN // Tutu

  COPY_EXISTING ~_b1-3.itm~    ~override~ // adds magical flag to misc weapons
                ~_b2-16.itm~   ~override~
                ~_b1-20.itm~   ~override~
                ~_drizzts.itm~ ~override~
                ~_fblade.itm~  ~override~
                ~_s1-10.itm~   ~override~
                ~_s1-12.itm~   ~override~
                ~_s2-16.itm~   ~override~
                ~_sarevo.itm~  ~override~
    PATCH_IF (SOURCE_SIZE > 0x71) THEN BEGIN // protects against invalid files
      READ_BYTE  0x18 "flags"
      WRITE_BYTE 0x18 ("%flags%" BOR 0b01000000)
    END
    BUT_ONLY_IF_IT_CHANGES

END

COPY_EXISTING ~sppr604.spl~ ~override~
  WRITE_SHORT 0x20 16384
  SAY IDENTIFIED_DESC @115001
  BUT_ONLY_IF_IT_CHANGES

COPY ~BG2_Tweaks/cre/wwbearwe.cre~ ~override~
  SAY NAME1 @115002
  SAY NAME2 @115002

COPY ~BG2_Tweaks/cre/wwbear.cre~ ~override~
  SAY NAME1 @115003
  SAY NAME2 @115003

COPY ~BG2_Tweaks/2da/anisum04.2da~ ~override~
     ~BG2_Tweaks/2da/anisum05.2da~ ~override~
     ~BG2_Tweaks/2da/clabdr03.2da~ ~override~
     ~BG2_Tweaks/eff/spanim5.eff~  ~override~
     ~BG2_Tweaks/spl/spcl643.spl~  ~override~
     ~BG2_Tweaks/spl/spcl644.spl~  ~override~

// if DR > v3  is installed prior to spc
ACTION_IF ((FILE_EXISTS_IN_GAME ~cdnegpp.spl~) AND (FILE_EXISTS ~Divine_Remix/lib/macro_reindex_clab.tph~)) THEN BEGIN

  INCLUDE ~Divine_Remix/lib/macro_reindex_clab.tph~
  INCLUDE ~Divine_Remix/lib/macro_add_druid_spells.tph~
  COPY_EXISTING ~clabdr03.2da~ ~override~
    LAUNCH_PATCH_MACRO ~add_druid_spells~ // add generic druid spells
    LAUNCH_PATCH_MACRO ~reindex_clab~     // re-index lines
    BUT_ONLY_IF_IT_CHANGES

END

// makes spells castable by rangers and druids
COPY_EXISTING ~sppr109.spl~ ~override~
              ~sppr203.spl~ ~override~
              ~sppr318.spl~ ~override~
              ~sppr304.spl~ ~override~
              ~sppr513.spl~ ~override~
  READ_BYTE  0x21 "priesttype"
  WRITE_BYTE 0x21 ("%priesttype%" BAND 0b01111111)
  BUT_ONLY_IF_IT_CHANGES

COPY ~BG2_Tweaks/itm/wwwere.itm~ ~override~
  SAY IDENTIFIED_DESC @115004

COPY ~BG2_Tweaks/itm/wwweregr.itm~ ~override~
  SAY IDENTIFIED_DESC @115005

// if something installed that allows shapeshifters to be non-TN alignments, adjust the paw usability
COPY_EXISTING ~alignmnt.2da~ ~override~
  READ_2DA_ENTRY 42 1 10 "lg"
  READ_2DA_ENTRY 42 2 10 "ln"
  READ_2DA_ENTRY 42 3 10 "le"
  READ_2DA_ENTRY 42 4 10 "ng"
  READ_2DA_ENTRY 42 6 10 "ne"
  READ_2DA_ENTRY 42 7 10 "cg"
  READ_2DA_ENTRY 42 8 10 "cn"
  READ_2DA_ENTRY 42 9 10 "ce"

ACTION_IF (("%lg%" = 1) OR ("%ln%" = 1) OR ("%le%" = 1)) THEN BEGIN // remove lawful flag

  COPY_EXISTING ~wwwere.itm~   ~override~
                ~wwweregr.itm~ ~override~
    READ_BYTE  0x1e "use"
    WRITE_BYTE 0x1e ("%use%" BAND 0b11101111) // removes lawful flag
    BUT_ONLY_IF_IT_CHANGES

END

ACTION_IF (("%cg%" = 1) OR ("%cn%" = 1) OR ("%ce%" = 1)) THEN BEGIN // remove chaotic flag

  COPY_EXISTING ~wwwere.itm~   ~override~
                ~wwweregr.itm~ ~override~
    READ_BYTE  0x1e "use"
    WRITE_BYTE 0x1e ("%use%" BAND 0b11111110) // removes chaotic flag
    BUT_ONLY_IF_IT_CHANGES

END

ACTION_IF (("%lg%" = 1) OR ("%ng%" = 1) OR ("%cg%" = 1)) THEN BEGIN // remove good flag

  COPY_EXISTING ~wwwere.itm~   ~override~
                ~wwweregr.itm~ ~override~
    READ_BYTE  0x1e "use"
    WRITE_BYTE 0x1e ("%use%" BAND 0b11111011) // removes good flag
    BUT_ONLY_IF_IT_CHANGES

END

ACTION_IF (("%le%" = 1) OR ("%ne%" = 1) OR ("%ce%" = 1)) THEN BEGIN // remove evil flag

  COPY_EXISTING ~wwwere.itm~   ~override~
                ~wwweregr.itm~ ~override~
    READ_BYTE  0x1e "use"
    WRITE_BYTE 0x1e ("%use%" BAND 0b11111101) // removes evil flag
    BUT_ONLY_IF_IT_CHANGES

END

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// Multiple Strongholds                             \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

/////                                                  \\\\\
///// no restrictions                                  \\\\\
/////                                                  \\\\\

BEGIN @116001 DESIGNATED 1160
GROUP @13
GROUP @19
SUBCOMPONENT @116000 // multi strongholds
REQUIRE_PREDICATE NOT FILE_EXISTS_IN_GAME ~_sw1h01.itm~ @10 // non-Tutu

// this file does nothing, it just allows other mods to detect this component
COPY_EXISTING ~sw1h01.itm~ ~override/cdt01160.g3~

COMPILE ~BG2_Tweaks/dlg/stronghold1.d~  // removes stronghold var
        ~BG2_Tweaks/dlg/stronghold2.d~  // removes class checks
        ~BG2_Tweaks/dlg/stronghold2a.d~ // allows choice for bard, ranger, druid strongholds

EXTEND_TOP ~ar0900.bcs~ ~BG2_Tweaks/baf/ar0900.baf~
EXTEND_TOP ~ar0901.bcs~ ~BG2_Tweaks/baf/ar0901.baf~
EXTEND_TOP ~ar0902.bcs~ ~BG2_Tweaks/baf/ar0902.baf~
EXTEND_TOP ~ar0904.bcs~ ~BG2_Tweaks/baf/ar0904.baf~

/////                                                  \\\\\
///// keep class restrictions                          \\\\\
/////                                                  \\\\\

BEGIN @116100 DESIGNATED 1161
GROUP @13
GROUP @19
SUBCOMPONENT @116000 // multi strongholds
REQUIRE_PREDICATE NOT FILE_EXISTS_IN_GAME ~_sw1h01.itm~ @10 // non-Tutu

// this file does nothing, it just allows other mods to detect this component
COPY_EXISTING ~sw1h01.itm~ ~override/cdt01161.g3~

COMPILE ~BG2_Tweaks/dlg/stronghold1.d~  // removes stronghold var
        ~BG2_Tweaks/dlg/stronghold1a.d~ // allows choice for bard, ranger, druid strongholds

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// Bonus Merchants                                  \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @117000 DESIGNATED 1170
GROUP @13
GROUP @19
REQUIRE_PREDICATE NOT FILE_EXISTS_IN_GAME ~_sw1h01.itm~ @10  // non-Tutu
REQUIRE_PREDICATE NOT FILE_EXISTS_IN_GAME ~bpduearc.spl~ @16 // non-BP

// this file does nothing, it just allows other mods to detect this component
COPY_EXISTING ~sw1h01.itm~ ~override/cdt01170.g3~

EXTEND_TOP ~ar0406.bcs~ ~BG2_Tweaks/baf/ar0406.baf~
EXTEND_TOP ~ar0702.bcs~ ~BG2_Tweaks/baf/ar0702.baf~

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// Edwina                                           \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @118000 DESIGNATED 1180
GROUP @13
GROUP @19
REQUIRE_PREDICATE NOT FILE_EXISTS_IN_GAME ~_sw1h01.itm~ @10 // non-Tutu

COPY ~BG2_Tweaks/bmp/royo4l.bmp~ ~override~
     ~BG2_Tweaks/bmp/royo4s.bmp~ ~override~

// add portrait change opcodes to edwin transform spells
COPY_EXISTING ~spin662.spl~ ~override~ // return to edwin
              ~spin916.spl~ ~override~ // change to edwina
  READ_LONG  0x64 "abil_off"
  READ_SHORT 0x68 "abil_num"
  READ_LONG  0x6a "fx_off"
  READ_LONG  0x34 "level"
  SET "delta" = 0
  FOR (index = 0 ; index < abil_num ; index = index + 1) BEGIN
    READ_SHORT ("%abil_off%" + 0x1e + (0x28 * "%index%")) "abil_fx_num"
    READ_SHORT ("%abil_off%" + 0x20 + (0x28 * "%index%")) "abil_fx_idx"
    SET "abil_fx_idx" = ("%abil_fx_idx%" + "%delta%")
    WRITE_SHORT ("%abil_off%" + 0x20 + (0x28 * "%index%")) "%abil_fx_idx%"
    FOR (index2 = 0 ; index2 < 2 ; index2 = index2 + 1) BEGIN
      INSERT_BYTES  ("%fx_off%" +        (0x30 * "%abil_fx_idx%")) 0x30
        WRITE_SHORT ("%fx_off%" +        (0x30 * "%abil_fx_idx%")) 107        // change portrait
        WRITE_BYTE  ("%fx_off%" + 0x02 + (0x30 * "%abil_fx_idx%")) 2          // target:preset target
        WRITE_LONG  ("%fx_off%" + 0x08 + (0x30 * "%abil_fx_idx%")) "%index2%" // small or large portrait
        WRITE_BYTE  ("%fx_off%" + 0x0c + (0x30 * "%abil_fx_idx%")) 1          // instant/permanent
        WRITE_BYTE  ("%fx_off%" + 0x12 + (0x30 * "%abil_fx_idx%")) 100        // probability
    END
    PATCH_IF ("%SOURCE_RES%" STRING_COMPARE_CASE "spin662" = 0) BEGIN
      WRITE_ASCII ("%fx_off%" + 0x14 + (0x30 * "%abil_fx_idx%")) ~nedwinm~ // portrait resource
      WRITE_ASCII ("%fx_off%" + 0x44 + (0x30 * "%abil_fx_idx%")) ~nedwins~ // portrait resource
    END ELSE BEGIN
      WRITE_ASCII ("%fx_off%" + 0x14 + (0x30 * "%abil_fx_idx%")) ~royo4l~ // portrait resource
      WRITE_ASCII ("%fx_off%" + 0x44 + (0x30 * "%abil_fx_idx%")) ~royo4s~ // portrait resource
    END
    SET "delta" = ("%delta%" + 2)
    WRITE_SHORT ("%abil_off%" + 0x1e + (0x28 * "%index%")) ("%abil_fx_num%" + 2)
  END
  BUT_ONLY_IF_IT_CHANGES

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// Ease of Use Romance Fixes                        \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @119000 DESIGNATED 1190
GROUP @13
REQUIRE_PREDICATE NOT FILE_EXISTS_IN_GAME ~_sw1h01.itm~ @10 // non-Tutu
REQUIRE_PREDICATE NOT FILE_EXISTS_IN_GAME ~cdbehbla.pro~ @16 // only if Fixpack not installed

// this file does nothing, it just allows other mods to detect this component
COPY_EXISTING ~sw1h01.itm~ ~override/cdt01190.g3~

COMPILE ~BG2_Tweaks/dlg/jagalvar.d~

// Anomen's title change; effects should be permanent and persist through resurrection
COPY_EXISTING ~spin678.spl~ ~override~
  READ_LONG  0x64 "abil_off"
  READ_SHORT 0x68 "abil_num"
  READ_LONG  0x6a "fx_off"
  WHILE ("%abil_num%" > 0) BEGIN
    SET "abil_num" = ("%abil_num%" - 1)
    READ_SHORT ("%abil_off%" + (0x28 * "%abil_num%")) "type"
    PATCH_IF ("%type%" = 1) BEGIN // if melee
      READ_SHORT ("%abil_off%" + 0x1e + (0x28 * "%abil_num%")) "abil_fx_num"
      READ_SHORT ("%abil_off%" + 0x20 + (0x28 * "%abil_num%")) "abil_fx_idx"
      WHILE ("%abil_fx_num%" > 0) BEGIN
        SET "abil_fx_num" = ("%abil_fx_num%" - 1)
        WRITE_BYTE ("%fx_off%" + 0x0c + (0x30 * ("%abil_fx_idx%" + "%abil_fx_num%"))) 1 // instant/permanent
      END
    END
  END
  BUT_ONLY_IF_IT_CHANGES

COPY_EXISTING ~aerie.bcs~ ~override~
  DECOMPILE_BCS_TO_BAF
    REPLACE_TEXTUALLY ~SetGlobal("HaerDalisRomanceActive","GLOBAL",3)~
      ~SetGlobal("HaerDalisRomanceActive","GLOBAL",3)
      END

      IF
        InParty("HaerDalis")
        !Dead("HaerDalis")
        Global("HaerDalisRomanceActive","GLOBAL",2)
        Global("AerieRomanceActive","GLOBAL",1)
      THEN
        RESPONSE #100
          SetGlobal("HaerDalisRomanceActive","GLOBAL",1)
          Continue()
      END

      IF
        InParty("HaerDalis")
        !Dead("HaerDalis")
        !Global("HaerDalisRomanceActive","GLOBAL",0)
        !Global("HaerDalisRomanceActive","GLOBAL",3)
        Global("AerieRomanceActive","GLOBAL",2)
      THEN
        RESPONSE #100
          SetGlobal("HaerDalisRomanceActive","GLOBAL",3)
          Continue()~
    REPLACE_TEXTUALLY ~AreaType(0)~ ~AreaType(OUTDOOR)~
  COMPILE_BAF_TO_BCS
EXTEND_TOP ~aerie.bcs~ ~bg2_tweaks/baf/romfix_aerie.baf~

COPY_EXISTING ~anomen.bcs~ ~override~
  DECOMPILE_BCS_TO_BAF
    REPLACE_TEXTUALLY ~SetGlobal("AnomenIsNotKnight","GLOBAL",1)~
      ~SetGlobal("AnomenIsNotKnight","GLOBAL",1)
      ChangeAlignment("Anomen",CHAOTIC_NEUTRAL)~
    REPLACE_TEXTUALLY ~"TALKEDTOCOR","GLOBAL"~ ~"TALKEDCOR","GLOBAL"~
  COMPILE_BAF_TO_BCS
EXTEND_TOP ~anomen.bcs~ ~BG2_Tweaks/baf/romfix_anomen.baf~

COPY_EXISTING ~jaheira.bcs~ ~override~
  DECOMPILE_BCS_TO_BAF
    REPLACE_TEXTUALLY ~GlobalLT("DerminSpawn","GLOBAL",5)~
      ~  GlobalLT("DerminSpawn","GLOBAL",5)
        !InParty(Myself)
      THEN
        RESPONSE #100
          RealSetGlobalTimer("JaheiraRomance","GLOBAL",3600)
          SetGlobal("DerminSpawn","GLOBAL",5)
          SetGlobalTimer("DerminAppear","GLOBAL",17280)
          StartDialogueNoSet([PC])
      END

      IF
        False()~
    REPLACE_TEXTUALLY ~\bSetGlobalTimer("TerminselAppear","GLOBAL",FIVE_DAYS)~
                      ~RealSetGlobalTimer("TerminselAppear","GLOBAL",3600)~ // originally 36000 seems to be more than random.
  COMPILE_BAF_TO_BCS

COPY_EXISTING ~viconia.bcs~ ~override~
  DECOMPILE_BCS_TO_BAF
    REPLACE_TEXTUALLY ~Global("LoveTalk","LOCALS",71)~ ~False()~
    REPLACE_TEXTUALLY ~Global("ViconiaKeldornFight","GLOBAL",1)~
      ~Global("ViconiaKeldornFight","GLOBAL",1)
      THEN
        RESPONSE #100
          SetGlobal("ViconiaKeldornFight","GLOBAL",0)
      END

      IF
        False()~
  COMPILE_BAF_TO_BCS

// If ToB installed, patch ToB BCS files:
ACTION_IF FILE_EXISTS_IN_GAME ~mel01.cre~ THEN BEGIN

  COPY_EXISTING ~anom25.bcs~ ~override~
    DECOMPILE_BCS_TO_BAF
      REPLACE_TEXTUALLY ~Global("AnomenSummoned","GLOBAL",1)~
      ~Global("AnomenSummoned","GLOBAL",1)
      !Global("AnomenIsKnight","GLOBAL",1)~
    COMPILE_BAF_TO_BCS
  EXTEND_TOP ~anom25.bcs~ ~bg2_tweaks/baf/romfix_anom25.baf~

  COPY_EXISTING ~jahe25.bcs~ ~override~
    DECOMPILE_BCS_TO_BAF
      REPLACE_TEXTUALLY ~OR(2)~ ~~
      REPLACE_TEXTUALLY ~GlobalGT("LoveTalk","LOCALS",50)~ ~~
      REPLACE_TEXTUALLY ~SetGlobal("JaheiraRomanceActive","GLOBAL",2)~ ~~
    COMPILE_BAF_TO_BCS

END

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// Imoen ToB-Dialogue Fixes                         \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @120000 DESIGNATED 1200
GROUP @13
REQUIRE_PREDICATE     FILE_EXISTS_IN_GAME ~mel01.cre~    @12 // ToB required
REQUIRE_PREDICATE NOT FILE_EXISTS_IN_GAME ~cdbehbla.pro~ @16 // only if Fixpack not installed
REQUIRE_PREDICATE NOT FILE_EXISTS_IN_GAME ~_sw1h01.itm~  @10 // non-Tutu

// this file does nothing, it just allows other mods to detect this component
COPY_EXISTING ~sw1h01.itm~ ~override/cdt01200.g3~

COMPILE ~BG2_Tweaks/dlg/immyfix.d~

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// Use BG Walking Speeds                            \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @121000 DESIGNATED 1210
GROUP @13

// this file does nothing, it just allows other mods to detect this component
COPY_EXISTING ~sw1h01.itm~ ~override/cdt01210.g3~

COPY_EXISTING_REGEXP GLOB ~^.+\.cre$~ ~override~
  READ_SHORT 0x28 "anim" ELSE 0
  PATCH_IF ((("%SOURCE_FILE%" STRING_COMPARE_CASE "charbase.cre") = 0)
    OR ("%anim%" = 0x5000)
    OR ("%anim%" = 0x5001)
    OR ("%anim%" = 0x5002)
    OR ("%anim%" = 0x5003)
    OR ("%anim%" = 0x5010)
    OR ("%anim%" = 0x5011)
    OR ("%anim%" = 0x5012)
    OR ("%anim%" = 0x5013)
    OR ("%anim%" = 0x5100)
    OR ("%anim%" = 0x5101)
    OR ("%anim%" = 0x5102)
    OR ("%anim%" = 0x5103)
    OR ("%anim%" = 0x5110)
    OR ("%anim%" = 0x5111)
    OR ("%anim%" = 0x5112)
    OR ("%anim%" = 0x5113)
    OR ("%anim%" = 0x5200)
    OR ("%anim%" = 0x5201)
    OR ("%anim%" = 0x5202)
    OR ("%anim%" = 0x5210)
    OR ("%anim%" = 0x5211)
    OR ("%anim%" = 0x5212)
    OR ("%anim%" = 0x5300)
    OR ("%anim%" = 0x5301)
    OR ("%anim%" = 0x5302)
    OR ("%anim%" = 0x5303)
    OR ("%anim%" = 0x5310)
    OR ("%anim%" = 0x5311)
    OR ("%anim%" = 0x5312)
    OR ("%anim%" = 0x5313)
    OR ("%anim%" = 0x6000)
    OR ("%anim%" = 0x6001)
    OR ("%anim%" = 0x6002)
    OR ("%anim%" = 0x6003)
    OR ("%anim%" = 0x6004)
    OR ("%anim%" = 0x6005)
    OR ("%anim%" = 0x6010)
    OR ("%anim%" = 0x6011)
    OR ("%anim%" = 0x6012)
    OR ("%anim%" = 0x6013)
    OR ("%anim%" = 0x6014)
    OR ("%anim%" = 0x6015)
    OR ("%anim%" = 0x6100)
    OR ("%anim%" = 0x6101)
    OR ("%anim%" = 0x6102)
    OR ("%anim%" = 0x6103)
    OR ("%anim%" = 0x6104)
    OR ("%anim%" = 0x6105)
    OR ("%anim%" = 0x6110)
    OR ("%anim%" = 0x6111)
    OR ("%anim%" = 0x6112)
    OR ("%anim%" = 0x6113)
    OR ("%anim%" = 0x6114)
    OR ("%anim%" = 0x6115)
    OR ("%anim%" = 0x6200)
    OR ("%anim%" = 0x6201)
    OR ("%anim%" = 0x6202)
    OR ("%anim%" = 0x6204)
    OR ("%anim%" = 0x6205)
    OR ("%anim%" = 0x6210)
    OR ("%anim%" = 0x6211)
    OR ("%anim%" = 0x6212)
    OR ("%anim%" = 0x6214)
    OR ("%anim%" = 0x6215)
    OR ("%anim%" = 0x6300)
    OR ("%anim%" = 0x6301)
    OR ("%anim%" = 0x6302)
    OR ("%anim%" = 0x6303)
    OR ("%anim%" = 0x6304)
    OR ("%anim%" = 0x6305)
    OR ("%anim%" = 0x6310)
    OR ("%anim%" = 0x6311)
    OR ("%anim%" = 0x6312)
    OR ("%anim%" = 0x6313)
    OR ("%anim%" = 0x6314)
    OR ("%anim%" = 0x6315)
    OR ("%anim%" = 0x6500)
    OR ("%anim%" = 0x6510)) BEGIN
    LPF ~ADD_CRE_EFFECT~
      INT_VAR
        opcode = 176 // movement rate bonus
        target = 1 // self
        parameter1 = 0xfffffffd // -2 walking speed
        timing = 9 // instant/permanent
  END
  END
  BUT_ONLY_IF_IT_CHANGES

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// Cromwell can upgrade WK items                    \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @122000 DESIGNATED 1220
GROUP @13
GROUP @19
REQUIRE_PREDICATE NOT FILE_EXISTS_IN_GAME ~_sw1h01.itm~ @10 // non-Tutu
REQUIRE_PREDICATE     FILE_EXISTS_IN_GAME ~mel01.cre~   @12 // ToB required

// this file does nothing, it just allows other mods to detect this component
COPY_EXISTING ~sw1h01.itm~ ~override/cdt01220.g3~

STRING_SET 66764 @122043

EXTEND_BOTTOM ~botsmith.bcs~ ~bg2_tweaks/baf/botsmith.baf~

INCLUDE ~BG2_Tweaks/lib/extra_regexp_vars.tpa~ // needed for cespy dialogue patch

COPY_EXISTING ~botsmith.dlg~ ~override~
  DECOMPILE_DLG_TO_D
    REPLACE_TEXTUALLY ~OR(2)\([%tab% %lnl%%mnl%%wnl%]*PartyHasItem("blun14")[%tab% %lnl%%mnl%%wnl%]*PartyHasItem("blun30c")\)~
                      ~OR(3)\1 PartyHasItem("blun30d")~
  COMPILE_D_TO_DLG
  BUT_ONLY_IF_IT_CHANGES

// compiled in serial so that COPY_TRANS will get the one before it
COMPILE ~bg2_tweaks/dlg/crom1.d~ // also contains cespy stuff
COMPILE ~bg2_tweaks/dlg/crom2.d~
COMPILE ~bg2_tweaks/dlg/crom3.d~
COMPILE ~bg2_tweaks/dlg/crom4.d~
COMPILE ~bg2_tweaks/dlg/crom5.d~
COMPILE ~bg2_tweaks/dlg/crom6.d~
COMPILE ~bg2_tweaks/dlg/crom7.d~
COMPILE ~bg2_tweaks/dlg/crom8.d~
COMPILE ~bg2_tweaks/dlg/crom9.d~
COMPILE ~bg2_tweaks/dlg/crom10.d~
COMPILE ~bg2_tweaks/dlg/crom11.d~
COMPILE ~bg2_tweaks/dlg/crom12.d~
COMPILE ~bg2_tweaks/dlg/crom13.d~
COMPILE ~bg2_tweaks/dlg/crom14.d~
COMPILE ~bg2_tweaks/dlg/crom15.d~

EXTEND_BOTTOM ~ar0334.bcs~ ~bg2_tweaks/baf/ar0334.baf~
/*
1      Case of Plenty +2   --  Case of Plenty quiver02
                              5k gold
2      Thieves Hood (better)-  Thieves Hood helm29
                              Ring of Invisibility ring05
                              Antidote Potion
                              10,000 gold
3      Helm of the Rock    --  Helm of the Rock
      (better)                Horn (left)
                              Horn (right)
4      Wondrous Gloves     --  Bard's Gloves
                              Star Sapphire
                              Diamond
                              Emerald
                              Rogue Stone
5      Blessed Bracers     --  Paladin's Bracers
                              10,000 gold
6      Golem Manual        --  Golem Manual
                              Clay Golem Page
                                &  5,000 gold
7      Improved Cloak of   --  Cloak of Protection +2
       Protection +2          Scroll: Invisibility
                              Scroll: Improved Haste
                              20,000 gold
8      White Dragon Scale  --  White Dragon Scales
9      Aslyerferund Elven  --  Bladesinger Chain +4    (Dragon in Suldanesselar)
       Chain +5               Scroll: Protection from Normal Weapons
                              40,000 gold
10      Erinne Sling +5     --  Erinne Sling +4
11      Quiver of Plenty +2 --  Quiver of Plenty
                              Rogue Stone
                              10,000 gold
12      Flail of Ages +4    --  Flail of Ages +3
                              Flail Head (Poison)
13      Club of Detonation +5 -  Club of Detonation +3
                              Ring of Fire Resistance
14      Ixil's Spike +6     --  Ixil's Spike
                              Ixil's Nail +4
15      Dagger of the       --  Dagger of the Star +4      (Watcher's, Demi-Lich)
       Star +5                Star Sapphire (5)
*/
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// PnP Spell Progression Tables                     \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @201000 DESIGNATED 2010
DEPRECATED @18

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// Two-Handed Bastard Swords                        \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @202000 DESIGNATED 2020
GROUP @9

INCLUDE ~BG2_Tweaks/lib/extra_regexp_vars.tpa~ // needed for description patching

COPY_EXISTING ~BG2_Tweaks/2da/bastard.2da~ ~BG2_Tweaks/2da/bastard.2da~
  COUNT_2DA_ROWS ~4~ "rows"
  FOR ( index = 0 ; index < rows ; index = index + 1 ) BEGIN
    READ_2DA_ENTRY index 0 4 "orig"
    READ_2DA_ENTRY index 1 4 "1h"
    READ_2DA_ENTRY index 2 4 "2h"
    INNER_ACTION BEGIN

      ACTION_IF FILE_EXISTS_IN_GAME ~%orig%.itm~ THEN BEGIN

        COPY_EXISTING ~%orig%.itm~ ~override/%1h%.itm~
                      ~%orig%.itm~ ~override/%2h%.itm~
                      ~%orig%.itm~ ~override/%orig%.itm~
          READ_BYTE   0x31 "prof"
          READ_LONG   0x64 "abil_off"
          READ_SHORT  0x68 "abil_num"
          WRITE_SHORT 0x68 ("%abil_num%" + 1)
          READ_LONG   0x6a "fx_off"
          WRITE_LONG  0x6a ("%fx_off%" + 0x38)
          READ_SHORT  0x70 "fx_num"
          READ_SHORT ("%abil_off%" + 0x1e + (("%abil_num%" - 1) * 0x38)) "last_fx_num"
          READ_SHORT ("%abil_off%" + 0x20 + (("%abil_num%" - 1) * 0x38)) "last_fx_idx"
          SET "new_fx" = ("%last_fx_idx%" + "%last_fx_num%")
          PATCH_IF (~%DEST_RES%~ STRING_COMPARE_CASE ~%SOURCE_RES%~ != 0) BEGIN // zero lore for new copies
            WRITE_SHORT 0x42 0
          END ELSE BEGIN
            SET "charge" = ("%abil_num%" + 1)
          END
          PATCH_IF (~%DEST_RES%~ STRING_COMPARE_CASE ~%2h%~ = 0) BEGIN // if 2h
            READ_BYTE   0x18 "flags"
            WRITE_BYTE  0x18 ("%flags%" BOR 0b00000010) // add two-handed flag
            WRITE_ASCII 0x22 ~S2~                       // paperdoll animation
            FOR (loops = 0; loops < abil_num; loops = loops + 1) BEGIN
              READ_SHORT ("%abil_off%" + ("%loops%" * 0x38)) "type"
              PATCH_IF ("%type%" = 1) BEGIN // melee abilities
                WRITE_SHORT ("%abil_off%" + 0x2c + ("%loops%" * 0x38)) 60 // overhand attacks
                WRITE_SHORT ("%abil_off%" + 0x2e + ("%loops%" * 0x38)) 40 // slashing attacks
                WRITE_SHORT ("%abil_off%" + 0x30 + ("%loops%" * 0x38)) 0  // thrusting attacks
              END
            END
            // update descriptions
            PATCH_FOR_EACH offset IN
              ~0x50~
              ~0x54~
            BEGIN
              READ_LONG ~%offset%~ desc_strref
              PATCH_IF (desc_strref >= 0 && desc_strref < 2147483646) BEGIN
                READ_STRREF ~%offset%~ desc
                INNER_PATCH_SAVE desc ~%desc%~ BEGIN
                  // replace any english lines that match
                  REPLACE_TEXTUALLY ~^[ %tab%]*Type[ %tab%]*:[ %tab%]*\(1\|one\)[- %tab%]*handed.*~ ~Type: 2-handed~
                  // replace any german lines that match
                  REPLACE_TEXTUALLY ~^[ %tab%]*Typ[ %tab%]*:[ %tab%]*\(1\|ein\)[- %tab%]*händig.*~ ~Typ: 2-händig~
                  // replace any italian lines that match
                  REPLACE_TEXTUALLY ~^[ %tab%]*Tipo[ %tab%]*:[ %tab%]*\(a[ %tab%]+\)?\(1\|una\)[- %tab%]*mano.*~ ~Tipo: a due mani~
                  // replace any spanish lines that match
                  REPLACE_TEXTUALLY ~^[ %tab%]*Tipo[ %tab%]*:[ %tab%]*\(a \|de \)?\(una\|1\)[- %tab%]*\(mano\|puño\).*~ ~Tipo: a dos manos~
                END
                SAY_EVALUATED ~%offset%~ ~%desc%~ // write changes
              END
            END
          END
          INSERT_BYTES            ("%fx_off%" +        ("%new_fx%" * 0x30)) 0x30 // new effect
            WRITE_SHORT           ("%fx_off%" +        ("%new_fx%" * 0x30)) 122  // create inventory item
            WRITE_BYTE            ("%fx_off%" + 0x02 + ("%new_fx%" * 0x30)) 1    // target: self
            WRITE_BYTE            ("%fx_off%" + 0x0c + ("%new_fx%" * 0x30)) 1    // instant/permanent till death
            WRITE_BYTE            ("%fx_off%" + 0x12 + ("%new_fx%" * 0x30)) 100  // probability
            PATCH_IF (~%DEST_RES%~ STRING_COMPARE_CASE ~%2h%~ = 0) BEGIN
              WRITE_EVALUATED_ASCII ("%fx_off%" + 0x14 + ("%new_fx%" * 0x30)) ~%1h%~ // if 2H version
            END ELSE BEGIN
              WRITE_EVALUATED_ASCII ("%fx_off%" + 0x14 + ("%new_fx%" * 0x30)) ~%2h%~ // if 1H version
            END
          INSERT_BYTES  ("%fx_off%"       ) 0x38         // new ability
            WRITE_BYTE  ("%fx_off%"       ) 3            // magical
            WRITE_BYTE  ("%fx_off%" + 0x01) 1            // ID to use
            WRITE_SHORT ("%fx_off%" + 0x02) 3            // in item slots
            PATCH_IF (~%DEST_RES%~ STRING_COMPARE_CASE ~%2h%~ = 0) BEGIN // if 2H version
              PATCH_IF ("%prof%" = 94) BEGIN // katana
                WRITE_ASCII ("%fx_off%" + 0x04) ~isw1h43~
              END ELSE BEGIN
                WRITE_ASCII ("%fx_off%" + 0x04) ~isw1h01~
              END
            END ELSE BEGIN
              WRITE_ASCII ("%fx_off%" + 0x04) ~isw2h01~   // if 1H version
            END
            WRITE_SHORT ("%fx_off%" + 0x0c) 5            // target: caster
            WRITE_SHORT ("%fx_off%" + 0x0e) 1            // range: 1
            WRITE_SHORT ("%fx_off%" + 0x1e) 1            // num effects: 1
            WRITE_SHORT ("%fx_off%" + 0x20) ("%new_fx%") // fx index
            WRITE_SHORT ("%fx_off%" + 0x22) 1            // num charges: 1
            WRITE_BYTE  ("%fx_off%" + 0x24) 1            // vanishes when drained
            WRITE_SHORT ("%fx_off%" + 0x2a) 1            // projectile: none
          BUT_ONLY_IF_IT_CHANGES

      END // action_if

    END // inner_action
    SET_2DA_ENTRY index 3 4 "%charge%"

  END // for loop
  BUT_ONLY_IF_IT_CHANGES

// ensures are regexp search doesn't die
COPY_EXISTING ~ar0087.are~ ~override/xr2400.are~
              ~ar0087.are~ ~override/xr2600.are~

// add charges to items in game
COPY_EXISTING_REGEXP GLOB ~^.+\.cre$~ ~override~
                          ~^.+\.are$~ ~override~
  PATCH_IF ("%SOURCE_FILE%" STRING_COMPARE_REGEXP "^.+\.cre$" = 0) BEGIN // if creature
    READ_LONG 0x2bc "itm_off" ELSE 0
    READ_LONG 0x2c0 "itm_num" ELSE 0
  END ELSE BEGIN
    READ_LONG  0x78 "itm_off" ELSE 0
    READ_SHORT 0x76 "itm_num" ELSE 0
  END
  FOR (index = 0 ; index < itm_num ; index = index + 1) BEGIN
    SET "offset" = 99
    READ_ASCII ("%itm_off%" +        (0x14 * "%index%")) "item" ELSE ""
//    PATCH_IF (FILE_CONTAINS ~BG2_Tweaks/2da/bastard.2da~ ~"%item%"~) BEGIN
// wanted to use FILE_CONTAINS, but it appears that %item% is not evaluated
    PATCH_IF (("%item%" STRING_COMPARE_CASE "sw1h01" = 0) OR
              ("%item%" STRING_COMPARE_CASE "sw1h02" = 0) OR
              ("%item%" STRING_COMPARE_CASE "sw1h03" = 0) OR
              ("%item%" STRING_COMPARE_CASE "sw1h18" = 0) OR
              ("%item%" STRING_COMPARE_CASE "sw1h34" = 0) OR
              ("%item%" STRING_COMPARE_CASE "sw1h37" = 0) OR
              ("%item%" STRING_COMPARE_CASE "sw1h38" = 0) OR
              ("%item%" STRING_COMPARE_CASE "sw1h39" = 0) OR
              ("%item%" STRING_COMPARE_CASE "sw1h42" = 0) OR
              ("%item%" STRING_COMPARE_CASE "sw1h62" = 0) OR
              ("%item%" STRING_COMPARE_CASE "sw1h63" = 0) OR
              ("%item%" STRING_COMPARE_CASE "sw1h64" = 0) OR
              ("%item%" STRING_COMPARE_CASE "sw1h65" = 0) OR
              ("%item%" STRING_COMPARE_CASE "sw1h72" = 0) OR
              ("%item%" STRING_COMPARE_CASE "_sw1h01" = 0) OR
              ("%item%" STRING_COMPARE_CASE "_sw1h02" = 0) OR
              ("%item%" STRING_COMPARE_CASE "_sw1h03" = 0) OR
              ("%item%" STRING_COMPARE_CASE "_sw1h18" = 0)) BEGIN
      INNER_ACTION BEGIN

        COPY_EXISTING ~BG2_Tweaks/2da/bastard.2da~ ~BG2_Tweaks/2da/bastard.2da~
          COUNT_2DA_ROWS ~4~ "rows2"
          FOR ( index2 = 0 ; index2 < rows2 ; index2 = index2 + 1 ) BEGIN
            READ_2DA_ENTRY index2 0 4 "orig"
            PATCH_IF ("%orig%" STRING_COMPARE_CASE "%item%" = 0) BEGIN
              READ_2DA_ENTRY index2 3 4 "offset"
              SET "index2" = "%rows2%" // kills loop
            END
          END
          BUT_ONLY_IF_IT_CHANGES

      END
    END
    PATCH_IF ("offset" < 4) BEGIN
      READ_SHORT ("%itm_off%" + 0x08 + ("%offset%" * 0x02) + (0x14 * "%index%")) "charges"
      PATCH_IF ("%charges%" = 0) BEGIN
        WRITE_SHORT ("%itm_off%" + 0x08 + ("%offset%" * 0x02) + (0x14 * "%index%")) 1
      END
    END
  END
  BUT_ONLY_IF_IT_CHANGES

// if breakable weapons from tutufix installed, make new generic bastard swords break as well
ACTION_IF FILE_EXISTS_IN_GAME ~_ax1h01.spl~ THEN BEGIN

  // breaking spell
  COPY_EXISTING ~_ax1h01.spl~ ~override/ikbst01c.spl~ // 1h zero-lore clone
                ~_ax1h01.spl~ ~override/ikbst01d.spl~ // 2h zero-lore clone
    READ_LONG  0x64 "abil_off"
    READ_SHORT 0x68 "abil_num"
    READ_LONG  0x6a "fx_off"
    FOR (index = 0 ; index < abil_num ; index = index + 1) BEGIN
      READ_SHORT ("%abil_off%" + 0x1e + ("%index%" * 0x28)) "abil_fx_num"
      READ_SHORT ("%abil_off%" + 0x20 + ("%index%" * 0x28)) "abil_fx_idx"
      FOR (index2 = 0 ; index2 < abil_fx_num ; index2 = index2 + 1) BEGIN
        READ_SHORT ("%fx_off%" +        (("%abil_fx_idx%" + "%index2%") * 0x30)) "opcode"
        READ_ASCII ("%fx_off%" + 0x14 + (("%abil_fx_idx%" + "%index2%") * 0x30)) "resref"
        PATCH_IF (("%opcode%" = 112) AND ("%resref%" STRING_COMPARE_CASE "_ax1h01" = 0)) BEGIN
          WRITE_ASCIIE ("%fx_off%" + 0x14 + (("%abil_fx_idx%" + "%index2%") * 0x30)) "%DEST_RES%" #8
        END
      END
    END

  COPY_EXISTING ~ikbst01c.itm~ ~override~ // 1h zero-lore clone
                ~ikbst01d.itm~ ~override~ // 2h zero-lore clone
    PATCH_IF (SOURCE_SIZE > 0x71) BEGIN
      READ_LONG  0x64 "abil_off"
      READ_SHORT 0x68 "abil_num"
      READ_LONG  0x6A "fx_off"
      FOR (index = 0 ; index < abil_num ; index = index + 1) BEGIN
        READ_SHORT ("%abil_off%" + 0x1e + ("%index%" * 0x38)) "abil_fx_num"
        READ_SHORT ("%abil_off%" + 0x20 + ("%index%" * 0x38)) "abil_fx_idx"
        FOR (index2 = 0 ; index2 < abil_fx_num ; index2 = index2 + 1) BEGIN
          READ_SHORT ("%fx_off%" +        (("%abil_fx_idx%" + "%index2%") * 0x30)) "opcode"
          READ_ASCII ("%fx_off%" + 0x14 + (("%abil_fx_idx%" + "%index2%") * 0x30)) "resref"
          PATCH_IF (("%opcode%" = 146) AND ("%resref%" STRING_COMPARE_CASE "_sw1h01" = 0)) BEGIN
            WRITE_ASCIIE ("%fx_off%" + 0x14 + (("%abil_fx_idx%" + "%index2%") * 0x30)) "%SOURCE_RES%" #8
          END
        END
      END
    END
    BUT_ONLY_IF_IT_CHANGES

END

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// Two-Handed Katanas                               \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @203000 DESIGNATED 2030
GROUP @9
REQUIRE_PREDICATE ((NOT FILE_EXISTS_IN_GAME ~_sw1h01.itm~) OR (FILE_EXISTS_IN_GAME ~cdkat2.itm~)) @5 // requires Exotic Item Pack on Tutu

INCLUDE ~BG2_Tweaks/lib/extra_regexp_vars.tpa~ // needed for description patching

COPY_EXISTING ~BG2_Tweaks/2da/katana.2da~ ~BG2_Tweaks/2da/katana.2da~
  COUNT_2DA_ROWS ~4~ "rows"
  FOR ( index = 0 ; index < rows ; index = index + 1 ) BEGIN
    READ_2DA_ENTRY index 0 4 "orig"
    READ_2DA_ENTRY index 1 4 "1h"
    READ_2DA_ENTRY index 2 4 "2h"
    INNER_ACTION BEGIN

      ACTION_IF FILE_EXISTS_IN_GAME ~%orig%.itm~ THEN BEGIN

        COPY_EXISTING ~%orig%.itm~ ~override/%1h%.itm~
                      ~%orig%.itm~ ~override/%2h%.itm~
                      ~%orig%.itm~ ~override/%orig%.itm~
          READ_BYTE   0x31 "prof"
          READ_LONG   0x64 "abil_off"
          READ_SHORT  0x68 "abil_num"
          WRITE_SHORT 0x68 ("%abil_num%" + 1)
          READ_LONG   0x6a "fx_off"
          WRITE_LONG  0x6a ("%fx_off%" + 0x38)
          READ_SHORT  0x70 "fx_num"
          READ_SHORT ("%abil_off%" + 0x1e + (("%abil_num%" - 1) * 0x38)) "last_fx_num"
          READ_SHORT ("%abil_off%" + 0x20 + (("%abil_num%" - 1) * 0x38)) "last_fx_idx"
          SET "new_fx" = ("%last_fx_idx%" + "%last_fx_num%")
          PATCH_IF (~%DEST_RES%~ STRING_COMPARE_CASE ~%SOURCE_RES%~ != 0) BEGIN // zero lore for new copies
            WRITE_SHORT 0x42 0
          END ELSE BEGIN
            SET "charge" = ("%abil_num%" + 1)
          END
          PATCH_IF (~%DEST_RES%~ STRING_COMPARE_CASE ~%2h%~ = 0) BEGIN // if 2h
            READ_BYTE   0x18 "flags"
            WRITE_BYTE  0x18 ("%flags%" BOR 0b00000010) // add two-handed flag
            WRITE_ASCII 0x22 ~S2~                       // paperdoll animation
            FOR (loops = 0; loops < abil_num; loops = loops + 1) BEGIN
              READ_SHORT ("%abil_off%" + ("%loops%" * 0x38)) "type"
              PATCH_IF ("%type%" = 1) BEGIN // melee abilities
                WRITE_SHORT ("%abil_off%" + 0x2c + ("%loops%" * 0x38)) 60 // overhand attacks
                WRITE_SHORT ("%abil_off%" + 0x2e + ("%loops%" * 0x38)) 40 // slashing attacks
                WRITE_SHORT ("%abil_off%" + 0x30 + ("%loops%" * 0x38)) 0  // thrusting attacks
              END
            END
            // update descriptions
            PATCH_FOR_EACH offset IN
              ~0x50~
              ~0x54~
            BEGIN
              READ_LONG ~%offset%~ desc_strref
              PATCH_IF (desc_strref >= 0 && desc_strref < 2147483646) BEGIN
                READ_STRREF ~%offset%~ desc
                INNER_PATCH_SAVE desc ~%desc%~ BEGIN
                  // replace any english lines that match
                  REPLACE_TEXTUALLY ~^[ %tab%]*Type[ %tab%]*:[ %tab%]*\(1\|one\)[- %tab%]*handed.*~ ~Type: 2-handed~
                  // replace any german lines that match
                  REPLACE_TEXTUALLY ~^[ %tab%]*Typ[ %tab%]*:[ %tab%]*\(1\|ein\)[- %tab%]*händig.*~ ~Typ: 2-händig~
                  // replace any italian lines that match
                  REPLACE_TEXTUALLY ~^[ %tab%]*Tipo[ %tab%]*:[ %tab%]*\(a[ %tab%]+\)?\(1\|una\)[- %tab%]*mano.*~ ~Tipo: a due mani~
                  // replace any spanish lines that match
                  REPLACE_TEXTUALLY ~^[ %tab%]*Tipo[ %tab%]*:[ %tab%]*\(a \|de \)?\(una\|1\)[- %tab%]*\(mano\|puño\).*~ ~Tipo: a dos manos~
                END
                SAY_EVALUATED ~%offset%~ ~%desc%~ // write changes
              END
            END
          END
          INSERT_BYTES            ("%fx_off%" +        ("%new_fx%" * 0x30)) 0x30 // new effect
            WRITE_SHORT           ("%fx_off%" +        ("%new_fx%" * 0x30)) 122  // create inventory item
            WRITE_BYTE            ("%fx_off%" + 0x02 + ("%new_fx%" * 0x30)) 1    // target: self
            WRITE_BYTE            ("%fx_off%" + 0x0c + ("%new_fx%" * 0x30)) 1    // instant/permanent till death
            WRITE_BYTE            ("%fx_off%" + 0x12 + ("%new_fx%" * 0x30)) 100  // probability
            PATCH_IF (~%DEST_RES%~ STRING_COMPARE_CASE ~%2h%~ = 0) BEGIN
              WRITE_EVALUATED_ASCII ("%fx_off%" + 0x14 + ("%new_fx%" * 0x30)) ~%1h%~ // if 2H version
            END ELSE BEGIN
              WRITE_EVALUATED_ASCII ("%fx_off%" + 0x14 + ("%new_fx%" * 0x30)) ~%2h%~ // if 1H version
            END
          INSERT_BYTES  ("%fx_off%"       ) 0x38         // new ability
            WRITE_BYTE  ("%fx_off%"       ) 3            // magical
            WRITE_BYTE  ("%fx_off%" + 0x01) 1            // ID to use
            WRITE_SHORT ("%fx_off%" + 0x02) 3            // in item slots
            PATCH_IF (~%DEST_RES%~ STRING_COMPARE_CASE ~%2h%~ = 0) BEGIN // if 2H version
              PATCH_IF ("%prof%" = 94) BEGIN // katana
                WRITE_ASCII ("%fx_off%" + 0x04) ~isw1h43~
              END ELSE BEGIN
                WRITE_ASCII ("%fx_off%" + 0x04) ~isw1h01~
              END
            END ELSE BEGIN
              WRITE_ASCII ("%fx_off%" + 0x04) ~isw2h01~   // if 1H version
            END
            WRITE_SHORT ("%fx_off%" + 0x0c) 5            // target: caster
            WRITE_SHORT ("%fx_off%" + 0x0e) 1            // range: 1
            WRITE_SHORT ("%fx_off%" + 0x1e) 1            // num effects: 1
            WRITE_SHORT ("%fx_off%" + 0x20) ("%new_fx%") // fx index
            WRITE_SHORT ("%fx_off%" + 0x22) 1            // num charges: 1
            WRITE_BYTE  ("%fx_off%" + 0x24) 1            // vanishes when drained
            WRITE_SHORT ("%fx_off%" + 0x2a) 1            // projectile: none
          BUT_ONLY_IF_IT_CHANGES

      END // action_if

    END // inner_action
    SET_2DA_ENTRY index 3 4 "%charge%"

  END // for loop
  BUT_ONLY_IF_IT_CHANGES

// ensures are regexp search doesn't die
COPY_EXISTING ~ar0087.are~ ~override/xr2400.are~
              ~ar0087.are~ ~override/xr2600.are~

// add charges to items in game
COPY_EXISTING_REGEXP GLOB ~^.+\.cre$~ ~override~
                          ~^.+\.are$~ ~override~
  PATCH_IF ("%SOURCE_FILE%" STRING_COMPARE_REGEXP "^.+\.cre$" = 0) BEGIN // if creature
    READ_LONG 0x2bc "itm_off" ELSE 0
    READ_LONG 0x2c0 "itm_num" ELSE 0
  END ELSE BEGIN
    READ_LONG  0x78 "itm_off" ELSE 0
    READ_SHORT 0x76 "itm_num" ELSE 0
  END
  FOR (index = 0 ; index < itm_num ; index = index + 1) BEGIN
    SET "offset" = 99
    READ_ASCII ("%itm_off%" +        (0x14 * "%index%")) "item" ELSE ""
//    PATCH_IF (FILE_CONTAINS ~BG2_Tweaks/2da/katana.2da~ ~"%item%"~) BEGIN
// wanted to use FILE_CONTAINS, but it appears that %item% is not evaluated
    PATCH_IF (("%item%" STRING_COMPARE_CASE "cdkat2" = 0) OR
              ("%item%" STRING_COMPARE_CASE "npsw02" = 0) OR
              ("%item%" STRING_COMPARE_CASE "wa2dak" = 0) OR
              ("%item%" STRING_COMPARE_CASE "npsw04" = 0) OR
              ("%item%" STRING_COMPARE_CASE "sw1h43" = 0) OR
              ("%item%" STRING_COMPARE_CASE "sw1h44" = 0) OR
              ("%item%" STRING_COMPARE_CASE "sw1h45" = 0) OR
              ("%item%" STRING_COMPARE_CASE "sw1h51" = 0) OR
              ("%item%" STRING_COMPARE_CASE "sw1h55" = 0) OR
              ("%item%" STRING_COMPARE_CASE "sw1h70" = 0) OR
              ("%item%" STRING_COMPARE_CASE "sw1h71" = 0) OR
              ("%item%" STRING_COMPARE_CASE "sw1h75" = 0)) BEGIN
      INNER_ACTION BEGIN

        COPY_EXISTING ~BG2_Tweaks/2da/katana.2da~ ~BG2_Tweaks/2da/katana.2da~
          COUNT_2DA_ROWS ~4~ "rows2"
          FOR ( index2 = 0 ; index2 < rows2 ; index2 = index2 + 1 ) BEGIN
            READ_2DA_ENTRY index2 0 4 "orig"
            PATCH_IF ("%orig%" STRING_COMPARE_CASE "%item%" = 0) BEGIN
              READ_2DA_ENTRY index2 3 4 "offset"
              SET "index2" = "%rows2%" // kills loop
            END
          END
          BUT_ONLY_IF_IT_CHANGES

      END
    END
    PATCH_IF ("offset" < 4) BEGIN
      READ_SHORT ("%itm_off%" + 0x08 + ("%offset%" * 0x02) + (0x14 * "%index%")) "charges"
      PATCH_IF ("%charges%" = 0) BEGIN
        WRITE_SHORT ("%itm_off%" + 0x08 + ("%offset%" * 0x02) + (0x14 * "%index%")) 1
      END
    END
  END
  BUT_ONLY_IF_IT_CHANGES

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// Universal Clubs                                  \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @204000 DESIGNATED 2040
GROUP @9

INCLUDE ~BG2_Tweaks/lib/extra_regexp_vars.tpa~ // needed for description patching

// this file does nothing, it just allows other mods to detect this component
COPY_EXISTING ~sw1h01.itm~ ~override/cdt02040.g3~

// this does the work of our description patching when fed a few regular expressions and replacements
DEFINE_PATCH_FUNCTION ~REMOVE_MAGE_LINE_FROM_USABILITIES~
  INT_VAR
    offset = 0 // offset to write description to
  STR_VAR
    desc = ~~ // original description 
    unusable_regexp = ~~ // regexp that matches the Unusable By/Not Usable By line
    unusable_replacement = ~~ // replacement for the Unusable By line
    mage_regexp = ~~ // regexp that matches the Mage line in the usability section
    class_prefix = ~~ // in the usability section, this precedes all class listings (ex: in english, this is one space)
BEGIN
  INNER_PATCH_SAVE desc ~%desc%~ BEGIN
    REPLACE_TEXTUALLY ~%unusable_regexp%~ ~%unusable_replacement%~
  END
  PATCH_IF ((~%desc%~ STRING_CONTAINS_REGEXP ~^%unusable_replacement%~) == 0) BEGIN
    // extract usability info
    INNER_PATCH_SAVE usab_block ~%desc%~ BEGIN
      REPLACE_TEXTUALLY ~\(.*[%lnl%%mnl%%wnl%]\)*%unusable_replacement%~ ~~
    END
    // update info
    INNER_PATCH_SAVE usab_block_new ~%usab_block%~ BEGIN
      REPLACE_TEXTUALLY ~[%lnl%%mnl%%wnl%][- %tab%]*$~ ~~ // remove any empty lines
      REPLACE_TEXTUALLY ~^[- %tab%]*~ ~%class_prefix%~ // make sure entries are indented by one space
      REPLACE_TEXTUALLY ~%mage_regexp%~ ~~ // remove mage line
    END
    // replace old usability info with new
    INNER_PATCH_SAVE desc ~%desc%~ BEGIN
      REPLACE_TEXTUALLY EXACT_MATCH ~%usab_block%~ ~%usab_block_new%~
    END
    
    // check if mage was the only thing that couldn't use
    INNER_PATCH_SAVE compare ~%desc%~ BEGIN 
      REPLACE_TEXTUALLY ~^%unusable_replacement%.*[%lnl%%mnl%%wnl%]%class_prefix%[^- %tab%]~ ~~
    END
    // if it was...
    PATCH_IF (~%desc%~ STRING_EQUAL ~%compare%~) BEGIN
      INNER_PATCH_SAVE desc ~%desc%~ BEGIN
        // remove 'not usable by' line, so it won't be sitting all by its lonesome now
        REPLACE_TEXTUALLY ~[%lnl%%mnl%%wnl%]%unusable_replacement%.*~ ~~
      END
    END
    
    // write changes
    SAY_EVALUATED ~%offset%~ ~%desc%~
  END
END

PRINT @1
COPY_EXISTING_REGEXP GLOB ~^.+\.itm$~ ~override~
  PATCH_IF (SOURCE_SIZE > 0x71) BEGIN
    READ_BYTE 0x31 "prof" ELSE 0
    PATCH_IF ("%prof%" = 115) BEGIN
      READ_BYTE 0x1f "fighter"
      PATCH_IF (("%fighter%" BAND "0b00001000") = "0b00000000") BEGIN // if usable by single-class fighter
        READ_BYTE   0x20 "mage"
        WRITE_BYTE 0x20 ("%mage%" BAND "0b11111011")
        
        // update descriptions
        PATCH_FOR_EACH offset IN
          ~0x50~
          ~0x54~
        BEGIN
          READ_LONG ~%offset%~ desc_strref
          PATCH_IF (desc_strref >= 0 && desc_strref < 2147483646) BEGIN
            READ_STRREF ~%offset%~ desc
            // remove mage line from matching english descriptions
            LAUNCH_PATCH_FUNCTION ~REMOVE_MAGE_LINE_FROM_USABILITIES~
              STR_VAR
                unusable_regexp = EVALUATE_BUFFER ~^[ %tab%]*\(Unusable By\|Not Usable By\).*~
                unusable_replacement = ~Not Usable By:~
                mage_regexp = EVALUATE_BUFFER ~[%lnl%%mnl%%wnl%].*\bMage.*~
                class_prefix = ~ ~
            END
            // remove mage line from matching german descriptions
            LAUNCH_PATCH_FUNCTION ~REMOVE_MAGE_LINE_FROM_USABILITIES~
              STR_VAR
                unusable_regexp = EVALUATE_BUFFER ~^[ %tab%]*Kann nicht verwendet werden von[ %tab%]*:.*~
                unusable_replacement = ~Kann nicht verwendet werden von:~
                mage_regexp = EVALUATE_BUFFER ~[%lnl%%mnl%%wnl%][- %tab%]*Magiern?.*~
                class_prefix = ~- ~
            END
            // remove mage line from matching italian descriptions
            LAUNCH_PATCH_FUNCTION ~REMOVE_MAGE_LINE_FROM_USABILITIES~
              STR_VAR
                unusable_regexp = EVALUATE_BUFFER ~^[ %tab%]*Non[ %tab%]*utilizzabile[ %tab%]*da[ %tab%]*:.*~
                unusable_replacement = ~Non utilizzabile da:~
                mage_regexp = EVALUATE_BUFFER ~[%lnl%%mnl%%wnl%][- %tab%]*Mago.*~
                class_prefix = ~ ~
            END
            // remove mage line from matching spanish descriptions
            LAUNCH_PATCH_FUNCTION ~REMOVE_MAGE_LINE_FROM_USABILITIES~
              STR_VAR
                unusable_regexp = EVALUATE_BUFFER ~^[ %tab%]*No \(lo puede usar\|utilizable por\|usable por\|permitido a\)[ %tab%]*:.*~
                unusable_replacement = ~No permitido a:~
                mage_regexp = EVALUATE_BUFFER ~[%lnl%%mnl%%wnl%][- %tab%]*\(El \)?Mago.*~
                class_prefix = ~ ~
            END
          END
        END
      END
    END
  END
  BUT_ONLY_IF_IT_CHANGES

COPY_EXISTING ~weapprof.2da~ ~override~ // allows mage proficiency in clubs
  FOR (column = 22; column < 30; column = column + 1) BEGIN
    SET_2DA_ENTRY_LATER ~weapprof~ 20 column ~1~ // Specialist Mage
  END
  SET_2DA_ENTRY_LATER ~weapprof~ 20  4 ~1~ // Mage
  SET_2DA_ENTRY_LATER ~weapprof~ 20 53 ~1~ // Wild Mage
  SET_2DA_ENTRIES_NOW ~weapprof~ 1
  BUT_ONLY_IF_IT_CHANGES

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// Universal Clubs description updates              \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @205000 DESIGNATED 2050
GROUP @9
REQUIRE_COMPONENT ~Setup-BG2_Tweaks.tp2~ ~2040~ @205001
REQUIRE_PREDICATE ("%LANGUAGE%" STRING_COMPARE_CASE "chinese" = 0) @7 // chinese only (english/german/italian/spanish patched above)
REQUIRE_PREDICATE NOT FILE_EXISTS_IN_GAME ~bola01.itm~ @205002

// this file does nothing, it just allows other mods to detect this component
COPY_EXISTING ~sw1h01.itm~ ~override/cdt02050.g3~

STRING_SET 6703 @99001 // generic club description string

ACTION_IF FILE_EXISTS_IN_GAME ~_blun01.itm~ THEN BEGIN
  COPY_EXISTING ~_blun01.itm~ ~override~
    SAY IDENTIFIED_DESC @99001
END

COPY_EXISTING ~_grblun.itm~ ~override~
              ~grblun.itm~  ~override~
  PATCH_IF (SOURCE_SIZE > 0x71) THEN BEGIN // protects against invalid files
    SAY IDENTIFIED_DESC @99002
  END
  BUT_ONLY_IF_IT_CHANGES

COPY_EXISTING ~blun10.itm~ ~override~
  SAY IDENTIFIED_DESC @99003

COPY_EXISTING ~blun22.itm~ ~override~
  SAY IDENTIFIED_DESC @99004

COPY_EXISTING ~blun23.itm~ ~override~
  SAY IDENTIFIED_DESC @99005

COPY_EXISTING ~blun24.itm~ ~override~
  SAY IDENTIFIED_DESC @99006

COPY_EXISTING ~blun26.itm~ ~override~
  PATCH_IF (SOURCE_SIZE > 0x71) THEN BEGIN // protects against invalid files
    SAY IDENTIFIED_DESC @99007
  END
  BUT_ONLY_IF_IT_CHANGES

COPY_EXISTING ~blun27.itm~ ~override~
  PATCH_IF (SOURCE_SIZE > 0x71) THEN BEGIN // protects against invalid files
    SAY IDENTIFIED_DESC @99008
  END
  BUT_ONLY_IF_IT_CHANGES

COPY_EXISTING ~blun31.itm~ ~override~
  PATCH_IF (SOURCE_SIZE > 0x71) THEN BEGIN // protects against invalid files
    SAY IDENTIFIED_DESC @99009
  END
  BUT_ONLY_IF_IT_CHANGES

// S&S club
COPY_EXISTING ~a!thcl.itm~ ~override~
  PATCH_IF (SOURCE_SIZE > 0x71) THEN BEGIN // protects against invalid files
    SAY IDENTIFIED_DESC @99010
  END
  BUT_ONLY_IF_IT_CHANGES

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// Weapon Styles for All (Idobek)                   \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @206000 DESIGNATED 2060
GROUP @9

// this file does nothing, it just allows other mods to detect this component
COPY_EXISTING ~sw1h01.itm~ ~override/cdt02060.g3~

OUTER_SET mage_kit_index = 0
COPY_EXISTING ~kitlist.2da~ ~override~
  READ_2DA_ENTRIES_NOW ~r2en_kitlist~ 9
  FOR (i = 0; i < r2en_kitlist; i += 1) BEGIN
    READ_2DA_ENTRY_FORMER ~r2en_kitlist~ i 8 parent_class
    PATCH_IF (~%parent_class%~ STRING_EQUAL ~1~) BEGIN // mage
      READ_2DA_ENTRY_FORMER ~r2en_kitlist~ i 1 kitname
      SPRINT $mage_kit(~%mage_kit_index%~) ~%kitname%~
      SET mage_kit_index += 1
    END
  END
  BUT_ONLY

PRINT @1
COPY_EXISTING ~weapprof.2da~ ~override~
  READ_2DA_ENTRIES_NOW ~r2en2_weapprof~ 3
  READ_2DA_ENTRY_FORMER ~r2en2_weapprof~ 30 4  mage   // sword and shield style check for mages
  READ_2DA_ENTRY_FORMER ~r2en2_weapprof~ 30 32 kensai // sword and shield style check for kensais
  FOR (i = 0; VARIABLE_IS_SET EVALUATE_BUFFER "r2en2_weapprof_1_%i%"; i+=1) BEGIN END // stolen from bg-style proficiencies component
  SET classcnt = i
  FOR (class = 4; class < classcnt; class += 1) BEGIN // for each class
    SET_2DA_ENTRY_LATER ~s2el_weapprof~ 29 class ~2~
    SET_2DA_ENTRY_LATER ~s2el_weapprof~ 30 class ~2~
    SET_2DA_ENTRY_LATER ~s2el_weapprof~ 31 class ~2~
    SET_2DA_ENTRY_LATER ~s2el_weapprof~ 32 class ~3~
    PATCH_IF (mage == 0) BEGIN // if mages can't use shields (as usual)...
      READ_2DA_ENTRY_FORMER ~r2en2_weapprof~ 0 (class - 1) classname
      FOR (j = 0; j < mage_kit_index; j += 1) BEGIN // don't let mage kits put points in sword & shield
        SPRINT kitname $mage_kit(~%j%~)
        PATCH_IF (~%classname%~ STRING_EQUAL_CASE ~%kitname%~) BEGIN
          SET_2DA_ENTRY_LATER ~s2el_weapprof~ 30 class ~0~
          SET j = mage_kit_index // exit loop
        END
      END
    END
  END
  // reset monk
  SET_2DA_ENTRY_LATER ~s2el_weapprof~ 29 51 ~0~
  SET_2DA_ENTRY_LATER ~s2el_weapprof~ 30 51 ~0~
  SET_2DA_ENTRY_LATER ~s2el_weapprof~ 32 51 ~0~
  // reset mage if sword and shield style not allowed
  PATCH_IF (mage == 0) BEGIN
    SET_2DA_ENTRY_LATER ~s2el_weapprof~ 30 4 ~0~
  END
  // reset kensai if sword and shield style not allowed
  PATCH_IF (kensai == 0) BEGIN
    SET_2DA_ENTRY_LATER ~s2el_weapprof~ 30 32 ~0~
  END
  SET_2DA_ENTRIES_NOW ~s2el_weapprof~ 3
  REPLACE_EVALUATE ~\(^ID  *\)~ BEGIN
    SPACES somespace ~%MATCH1%~
  END ~%somespace%%MATCH1%~
  BUT_ONLY_IF_IT_CHANGES

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// Druids Use Cleric Level and Spell Progression    \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @207000 DESIGNATED 2070
DEPRECATED @18

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// Delay High Level Abilities                       \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @208000 DESIGNATED 2080
GROUP @9
REQUIRE_PREDICATE     FILE_EXISTS_IN_GAME ~mel01.cre~   @12 // ToB required
REQUIRE_PREDICATE NOT FILE_EXISTS_IN_GAME ~_sw1h01.itm~ @10 // non-Tutu

// this file does nothing, it just allows other mods to detect this component
COPY_EXISTING ~sw1h01.itm~ ~override/cdt02080.g3~

COPY_EXISTING ~lunumab.2da~ ~override~
  FOR (row = 3; row < 33; row = row + 1) BEGIN
    READ_2DA_ENTRY row 1 1 level
    PATCH_IF (level < 21) BEGIN
      SET_2DA_ENTRY_LATER ~lunumab~ row 1 ~21~
    END
  END
  SET_2DA_ENTRIES_NOW ~lunumab~ 1
  BUT_ONLY_IF_IT_CHANGES

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// Change XP Cap                                    \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

/////                                                  \\\\\
///// remove cap                                       \\\\\
/////                                                  \\\\\

BEGIN @209001 DESIGNATED 2090 // remove xp cap
GROUP @9
GROUP @19
SUBCOMPONENT @209000 // change xp cap

// this file does nothing, it just allows other mods to detect this component
COPY_EXISTING ~sw1h01.itm~ ~override/cdt02090.g3~

COPY  ~BG2_Tweaks/2da/xpcap.2da~   ~override~

ACTION_IF FILE_EXISTS_IN_GAME ~cdt02070.g3~ THEN BEGIN // if druid/cleric xp progression installed
  COPY  ~BG2_Tweaks/2da/xplevel.2da~ ~override~
    REPLACE_TEXTUALLY
~CLERIC \(.*\)
DRUID \(.*\)~
~CLERIC \1
DRUID  \1~
END ELSE BEGIN
  COPY  ~BG2_Tweaks/2da/xplevel.2da~ ~override~
END

// WeiDU newline matching goodness
OUTER_INNER_PATCH ~12~ BEGIN
  WRITE_BYTE 1 0x09
  READ_ASCII 1 tab (1)  // 0x09, tab
  WRITE_BYTE 1 0x0a
  READ_ASCII 1 lnl (1)  // 0x0a, Linux
  WRITE_BYTE 0 0x0d
  READ_ASCII 0 mnl (1)  // 0x0d, Mac
  READ_ASCII 0 wnl (2)  // 0x0d0a, Windows
END

// easy replacements for 2das with levels as rows
COPY_EXISTING ~lvlmodwm.2da~ ~override~ // wild mage level modulation for wild surges
              ~mxsplbrd.2da~ ~override~ // bard spell progression table
              ~mxsplpal.2da~ ~override~ // paladin spell progression table
              ~mxsplprs.2da~ ~override~ // priest spell progression table
              ~mxsplran.2da~ ~override~ // ranger spell progression table
              ~mxsplsrc.2da~ ~override~ // sorcerer spell progression table
              ~mxsplwiz.2da~ ~override~ // wizard spell progression table
              ~skillbrd.2da~ ~override~ // bard pickpocket table
              ~skillrng.2da~ ~override~ // ranger stealth table
              ~splsrckn.2da~ ~override~ // sorcerer known spells?
  PATCH_IF (SOURCE_SIZE > 0) BEGIN
    REPLACE_TEXTUALLY ~^40\([ %tab%].*\)~
      ~40\1
41\1
42\1
43\1
44\1
45\1
46\1
47\1
48\1
49\1
50\1~
  END
  UNLESS ~^50[ %tab%]~
  BUT_ONLY_IF_IT_CHANGES

ACTION_IF FILE_EXISTS_IN_GAME ~mxspldru.2da~ THEN BEGIN

  // easy replacements for 2das with levels as rows
  COPY_EXISTING ~mxspldru.2da~ ~override~ // druid spell progression table
    REPLACE_TEXTUALLY ~^40\([ %tab%].*\)~
      ~40\1
41\1
42\1
43\1
44\1
45\1
46\1
47\1
48\1
49\1
50\1~
  UNLESS ~^50[ %tab%]~
  BUT_ONLY_IF_IT_CHANGES

END

// extend level-based abilities to 50; 2das with a zero column
COPY_EXISTING ~backstab.2da~ ~override~
  PATCH_IF (SOURCE_SIZE > 0) BEGIN
    REPLACE_TEXTUALLY ~\(0[ %tab%]+1[ %tab%]+2[ %tab%]+3[ %tab%]+4[ %tab%]+5[ %tab%]+6[ %tab%]+7[ %tab%]+8[ %tab%]+9[ %tab%]+10[ %tab%]+11[ %tab%]+12[ %tab%]+13[ %tab%]+14[ %tab%]+15[ %tab%]+16[ %tab%]+17[ %tab%]+18[ %tab%]+19[ %tab%]+20[ %tab%]+21[ %tab%]+22[ %tab%]+23[ %tab%]+24[ %tab%]+25[ %tab%]+26[ %tab%]+27[ %tab%]+28[ %tab%]+29[ %tab%]+30[ %tab%]+31[ %tab%]+32[ %tab%]+33[ %tab%]+34[ %tab%]+35[ %tab%]+36[ %tab%]+37[ %tab%]+38[ %tab%]+39\)\([ %tab%]+\)40[ %tab%]*~
      ~\1\240\241\242\243\244\245\246\247\248\249\250~
      //
     REPLACE_TEXTUALLY ~^\([A-Z]+[ %tab%]+[0-9]+[ %tab%]+[0-9]+[ %tab%]+[0-9]+[ %tab%]+[0-9]+[ %tab%]+[0-9]+[ %tab%]+[0-9]+[ %tab%]+[0-9]+[ %tab%]+[0-9]+[ %tab%]+[0-9]+[ %tab%]+[0-9]+[ %tab%]+[0-9]+[ %tab%]+[0-9]+[ %tab%]+[0-9]+[ %tab%]+[0-9]+[ %tab%]+[0-9]+[ %tab%]+[0-9]+[ %tab%]+[0-9]+[ %tab%]+[0-9]+[ %tab%]+[0-9]+[ %tab%]+[0-9]+[ %tab%]+[0-9]+[ %tab%]+[0-9]+[ %tab%]+[0-9]+[ %tab%]+[0-9]+[ %tab%]+[0-9]+[ %tab%]+[0-9]+[ %tab%]+[0-9]+[ %tab%]+[0-9]+[ %tab%]+[0-9]+[ %tab%]+[0-9]+[ %tab%]+[0-9]+[ %tab%]+[0-9]+[ %tab%]+[0-9]+[ %tab%]+[0-9]+[ %tab%]+[0-9]+[ %tab%]+[0-9]+[ %tab%]+[0-9]+[ %tab%]+[0-9]+[ %tab%]+[0-9]+[ %tab%]+[0-9]+\)\([ %tab%]+[0-9]+\)~
      ~\1\2\2\2\2\2\2\2\2\2\2\2~
  END
  UNLESS ~41[ %tab%]+42[ %tab%]+43[ %tab%]+44[ %tab%]+45[ %tab%]+46[ %tab%]+47[ %tab%]+48[ %tab%]+49[ %tab%]+50~
  BUT_ONLY_IF_IT_CHANGES

// extend level-based abilities to 50; 2das without a zero column
COPY_EXISTING ~layhands.2da~ ~override~ // paladin lay on hands ability
              ~savemonk.2da~ ~override~ // monk saving throws
              ~saveprs.2da~  ~override~ // priest saving throws
              ~saverog.2da~  ~override~ // rogue saving throws
              ~savewar.2da~  ~override~ // warrior saving throws
              ~savewiz.2da~  ~override~ // wizard saving throws
              ~thac0.2da~    ~override~ // to-hit tables
              ~xpbonus.2da~  ~override~ // xp for lockpicking, trap disarming, spell learning, etc.
  PATCH_IF (SOURCE_SIZE > 0) BEGIN
    REPLACE_TEXTUALLY ~\(1[ %tab%]+2[ %tab%]+3[ %tab%]+4[ %tab%]+5[ %tab%]+6[ %tab%]+7[ %tab%]+8[ %tab%]+9[ %tab%]+10[ %tab%]+11[ %tab%]+12[ %tab%]+13[ %tab%]+14[ %tab%]+15[ %tab%]+16[ %tab%]+17[ %tab%]+18[ %tab%]+19[ %tab%]+20[ %tab%]+21[ %tab%]+22[ %tab%]+23[ %tab%]+24[ %tab%]+25[ %tab%]+26[ %tab%]+27[ %tab%]+28[ %tab%]+29[ %tab%]+30[ %tab%]+31[ %tab%]+32[ %tab%]+33[ %tab%]+34[ %tab%]+35[ %tab%]+36[ %tab%]+37[ %tab%]+38[ %tab%]+39\)\([ %tab%]+\)40[ %tab%]*~
      ~\1\240\241\242\243\244\245\246\247\248\249\250~
     REPLACE_TEXTUALLY ~^\([A-Z_]+[ %tab%]+[0-9]+[ %tab%]+[0-9]+[ %tab%]+[0-9]+[ %tab%]+[0-9]+[ %tab%]+[0-9]+[ %tab%]+[0-9]+[ %tab%]+[0-9]+[ %tab%]+[0-9]+[ %tab%]+[0-9]+[ %tab%]+[0-9]+[ %tab%]+[0-9]+[ %tab%]+[0-9]+[ %tab%]+[0-9]+[ %tab%]+[0-9]+[ %tab%]+[0-9]+[ %tab%]+[0-9]+[ %tab%]+[0-9]+[ %tab%]+[0-9]+[ %tab%]+[0-9]+[ %tab%]+[0-9]+[ %tab%]+[0-9]+[ %tab%]+[0-9]+[ %tab%]+[0-9]+[ %tab%]+[0-9]+[ %tab%]+[0-9]+[ %tab%]+[0-9]+[ %tab%]+[0-9]+[ %tab%]+[0-9]+[ %tab%]+[0-9]+[ %tab%]+[0-9]+[ %tab%]+[0-9]+[ %tab%]+[0-9]+[ %tab%]+[0-9]+[ %tab%]+[0-9]+[ %tab%]+[0-9]+[ %tab%]+[0-9]+[ %tab%]+[0-9]+[ %tab%]+[0-9]+[ %tab%]+[0-9]+\)\([ %tab%]+[0-9]+\)~
      ~\1\2\2\2\2\2\2\2\2\2\2\2~
  END
  UNLESS ~41[ %tab%]+42[ %tab%]+43[ %tab%]+44[ %tab%]+45[ %tab%]+46[ %tab%]+47[ %tab%]+48[ %tab%]+49[ %tab%]+50~
  BUT_ONLY_IF_IT_CHANGES

// extend level-based abilities to 50; wspatck
COPY_EXISTING ~wspatck.2da~ ~override~ // warrior bonus attacks for proficiencies and levels
  PATCH_IF (SOURCE_SIZE > 0) BEGIN
    REPLACE_TEXTUALLY ~\(1[ %tab%]+2[ %tab%]+3[ %tab%]+4[ %tab%]+5[ %tab%]+6[ %tab%]+7[ %tab%]+8[ %tab%]+9[ %tab%]+10[ %tab%]+11[ %tab%]+12[ %tab%]+13[ %tab%]+14[ %tab%]+15[ %tab%]+16[ %tab%]+17[ %tab%]+18[ %tab%]+19[ %tab%]+20[ %tab%]+21[ %tab%]+22[ %tab%]+23[ %tab%]+24[ %tab%]+25[ %tab%]+26[ %tab%]+27[ %tab%]+28[ %tab%]+29[ %tab%]+30[ %tab%]+31[ %tab%]+32[ %tab%]+33[ %tab%]+34[ %tab%]+35[ %tab%]+36[ %tab%]+37[ %tab%]+38[ %tab%]+39\)\([ %tab%]+\)40[ %tab%]*~
      ~\1\240\241\242\243\244\245\246\247\248\249\250~
    REPLACE_TEXTUALLY ~^\([0-5]+[ %tab%]+-?[0-9]+[ %tab%]+-?[0-9]+[ %tab%]+-?[0-9]+[ %tab%]+-?[0-9]+[ %tab%]+-?[0-9]+[ %tab%]+-?[0-9]+[ %tab%]+-?[0-9]+[ %tab%]+-?[0-9]+[ %tab%]+-?[0-9]+[ %tab%]+-?[0-9]+[ %tab%]+-?[0-9]+[ %tab%]+-?[0-9]+[ %tab%]+-?[0-9]+[ %tab%]+-?[0-9]+[ %tab%]+-?[0-9]+[ %tab%]+-?[0-9]+[ %tab%]+-?[0-9]+[ %tab%]+-?[0-9]+[ %tab%]+-?[0-9]+[ %tab%]+-?[0-9]+[ %tab%]+-?[0-9]+[ %tab%]+-?[0-9]+[ %tab%]+-?[0-9]+[ %tab%]+-?[0-9]+[ %tab%]+-?[0-9]+[ %tab%]+-?[0-9]+[ %tab%]+-?[0-9]+[ %tab%]+-?[0-9]+[ %tab%]+-?[0-9]+[ %tab%]+-?[0-9]+[ %tab%]+-?[0-9]+[ %tab%]+-?[0-9]+[ %tab%]+-?[0-9]+[ %tab%]+-?[0-9]+[ %tab%]+-?[0-9]+[ %tab%]+-?[0-9]+[ %tab%]+-?[0-9]+[ %tab%]+-?[0-9]+[ %tab%]+-?[0-9]+\)\([ %tab%]+-?[0-9]+\)~
      ~\1\2\2\2\2\2\2\2\2\2\2\2~
  END
  UNLESS ~41[ %tab%]+42[ %tab%]+43[ %tab%]+44[ %tab%]+45[ %tab%]+46[ %tab%]+47[ %tab%]+48[ %tab%]+49[ %tab%]+50~
  BUT_ONLY_IF_IT_CHANGES

// extend clab abilities to 50
COPY_EXISTING ~clabba01.2da~ ~override~ // generic bard abilities
              ~clabdr01.2da~ ~override~ // generic druid abilities
              ~clabfi01.2da~ ~override~ // generic fighter abilities
              ~clabmo01.2da~ ~override~ // generic monk abilities
              ~clabpa01.2da~ ~override~ // generic paladin abilities
              ~clabpa05.2da~ ~override~ // fallen paladin abilities //
              ~clabpr01.2da~ ~override~ // generic priest abilities
              ~clabrn01.2da~ ~override~ // generic ranger abilities
              ~clabrn05.2da~ ~override~ // fallen ranger abilities //
              ~clabth01.2da~ ~override~ // generic thief abilities
  PATCH_IF (SOURCE_SIZE > 0) BEGIN
    REPLACE_TEXTUALLY ~[%wnl%%mnl%]+~ ~%lnl%~
    REPLACE_TEXTUALLY ~\(1[ %tab%]+2[ %tab%]+3[ %tab%]+4[ %tab%]+5[ %tab%]+6[ %tab%]+7[ %tab%]+8[ %tab%]+9[ %tab%]+10[ %tab%]+11[ %tab%]+12[ %tab%]+13[ %tab%]+14[ %tab%]+15[ %tab%]+16[ %tab%]+17[ %tab%]+18[ %tab%]+19[ %tab%]+20[ %tab%]+21[ %tab%]+22[ %tab%]+23[ %tab%]+24[ %tab%]+25[ %tab%]+26[ %tab%]+27[ %tab%]+28[ %tab%]+29[ %tab%]+30[ %tab%]+31[ %tab%]+32[ %tab%]+33[ %tab%]+34[ %tab%]+35[ %tab%]+36[ %tab%]+37[ %tab%]+38[ %tab%]+39\)\([ %tab%]+\)40[ %tab%]*~
      ~\1\240\241\242\243\244\245\246\247\248\249\250~
    REPLACE_TEXTUALLY ~^\(ABILITY[0-9A-Z]+[ %tab%]+[^ %tab%]+[ %tab%]+[^ %tab%]+[ %tab%]+[^ %tab%]+[ %tab%]+[^ %tab%]+[ %tab%]+[^ %tab%]+[ %tab%]+[^ %tab%]+[ %tab%]+[^ %tab%]+[ %tab%]+[^ %tab%]+[ %tab%]+[^ %tab%]+[ %tab%]+[^ %tab%]+[ %tab%]+[^ %tab%]+[ %tab%]+[^ %tab%]+[ %tab%]+[^ %tab%]+[ %tab%]+[^ %tab%]+[ %tab%]+[^ %tab%]+[ %tab%]+[^ %tab%]+[ %tab%]+[^ %tab%]+[ %tab%]+[^ %tab%]+[ %tab%]+[^ %tab%]+[ %tab%]+[^ %tab%]+[ %tab%]+[^ %tab%]+[ %tab%]+[^ %tab%]+[ %tab%]+[^ %tab%]+[ %tab%]+[^ %tab%]+[ %tab%]+[^ %tab%]+[ %tab%]+[^ %tab%]+[ %tab%]+[^ %tab%]+[ %tab%]+[^ %tab%]+[ %tab%]+[^ %tab%]+[ %tab%]+[^ %tab%]+\)\([ %tab%]+[^ %tab%]+[ %tab%]+[^ %tab%]+[ %tab%]+[^ %tab%]+[ %tab%]+[^ %tab%]+[ %tab%]+[^ %tab%]+[ %tab%]+[^ %tab%]+[ %tab%]+[^ %tab%]+[ %tab%]+[^ %tab%]+[ %tab%]+[^ %tab%]+[ %tab%]+[^ %tab%%lnl%]+\)[ %tab%]*$~
      ~\1\2\2~
  END
  UNLESS ~41[ %tab%]+42[ %tab%]+43[ %tab%]+44[ %tab%]+45[ %tab%]+46[ %tab%]+47[ %tab%]+48[ %tab%]+49[ %tab%]+50~
  BUT_ONLY_IF_IT_CHANGES

// extend clabs to 50 for kits
COPY_EXISTING ~kitlist.2da~ ~override~
  COUNT_2DA_ROWS ~9~ "rows"
  FOR ( index = 1 ; index < rows ; index = index + 1 ) BEGIN
    SET "exists" = 0
    READ_2DA_ENTRY "%index%" 5 9 "clab"
    INNER_PATCH_FILE ~%clab%.2da~ BEGIN

      PATCH_IF (SOURCE_SIZE > 0) BEGIN
        SET "exists" = 1
      END

    END
    PATCH_IF ("%exists%" = 1) BEGIN
      INNER_ACTION BEGIN

        COPY_EXISTING ~%clab%.2da~ ~override~
          REPLACE_TEXTUALLY ~[%wnl%%mnl%]+~ ~%lnl%~
          REPLACE_TEXTUALLY ~\(1[ %tab%]+2[ %tab%]+3[ %tab%]+4[ %tab%]+5[ %tab%]+6[ %tab%]+7[ %tab%]+8[ %tab%]+9[ %tab%]+10[ %tab%]+11[ %tab%]+12[ %tab%]+13[ %tab%]+14[ %tab%]+15[ %tab%]+16[ %tab%]+17[ %tab%]+18[ %tab%]+19[ %tab%]+20[ %tab%]+21[ %tab%]+22[ %tab%]+23[ %tab%]+24[ %tab%]+25[ %tab%]+26[ %tab%]+27[ %tab%]+28[ %tab%]+29[ %tab%]+30[ %tab%]+31[ %tab%]+32[ %tab%]+33[ %tab%]+34[ %tab%]+35[ %tab%]+36[ %tab%]+37[ %tab%]+38[ %tab%]+39\)\([ %tab%]+\)40[ %tab%]*~
            ~\1\240\241\242\243\244\245\246\247\248\249\250~
          REPLACE_TEXTUALLY ~^\(ABILITY[0-9A-Z]+[ %tab%]+[^ %tab%]+[ %tab%]+[^ %tab%]+[ %tab%]+[^ %tab%]+[ %tab%]+[^ %tab%]+[ %tab%]+[^ %tab%]+[ %tab%]+[^ %tab%]+[ %tab%]+[^ %tab%]+[ %tab%]+[^ %tab%]+[ %tab%]+[^ %tab%]+[ %tab%]+[^ %tab%]+[ %tab%]+[^ %tab%]+[ %tab%]+[^ %tab%]+[ %tab%]+[^ %tab%]+[ %tab%]+[^ %tab%]+[ %tab%]+[^ %tab%]+[ %tab%]+[^ %tab%]+[ %tab%]+[^ %tab%]+[ %tab%]+[^ %tab%]+[ %tab%]+[^ %tab%]+[ %tab%]+[^ %tab%]+[ %tab%]+[^ %tab%]+[ %tab%]+[^ %tab%]+[ %tab%]+[^ %tab%]+[ %tab%]+[^ %tab%]+[ %tab%]+[^ %tab%]+[ %tab%]+[^ %tab%]+[ %tab%]+[^ %tab%]+[ %tab%]+[^ %tab%]+[ %tab%]+[^ %tab%]+[ %tab%]+[^ %tab%]+\)\([ %tab%]+[^ %tab%]+[ %tab%]+[^ %tab%]+[ %tab%]+[^ %tab%]+[ %tab%]+[^ %tab%]+[ %tab%]+[^ %tab%]+[ %tab%]+[^ %tab%]+[ %tab%]+[^ %tab%]+[ %tab%]+[^ %tab%]+[ %tab%]+[^ %tab%]+[ %tab%]+[^ %tab%%lnl%]+\)[ %tab%]*$~
            ~\1\2\2~
          UNLESS ~41[ %tab%]+42[ %tab%]+43[ %tab%]+44[ %tab%]+45[ %tab%]+46[ %tab%]+47[ %tab%]+48[ %tab%]+49[ %tab%]+50~
          BUT_ONLY_IF_IT_CHANGES

      END
    END
  END
  BUT_ONLY_IF_IT_CHANGES

/////                                                  \\\\\
///// cap at level 20                                  \\\\\
/////                                                  \\\\\

BEGIN @209100 DESIGNATED 2091
REQUIRE_PREDICATE NOT FILE_EXISTS_IN_GAME ~_sw1h01.itm~ @10 // non-Tutu
GROUP @9
SUBCOMPONENT @209000 // change xp cap

// this file does nothing, it just allows other mods to detect this component
COPY_EXISTING ~sw1h01.itm~ ~override/cdt02091.g3~

COPY  ~BG2_Tweaks/2da/xpcap.2da~   ~override~

ACTION_IF FILE_EXISTS_IN_GAME ~cdt02070.g3~ THEN BEGIN // druids use cleric level progression
  COPY ~BG2_Tweaks/2da/xplevel20.2da~ ~override/xplevel.2da~
    REPLACE_TEXTUALLY
~CLERIC \(.*\)
DRUID \(.*\)~
~CLERIC \1
DRUID  \1~
END ELSE BEGIN
  COPY ~BG2_Tweaks/2da/xplevel20.2da~ ~override/xplevel.2da~
END

/////                                                  \\\\\
///// cap at level 30                                  \\\\\
/////                                                  \\\\\

BEGIN @209200 DESIGNATED 2092
REQUIRE_PREDICATE NOT FILE_EXISTS_IN_GAME ~_sw1h01.itm~ @10 // non-Tutu
GROUP @9
SUBCOMPONENT @209000 // change xp cap

// this file does nothing, it just allows other mods to detect this component
COPY_EXISTING ~sw1h01.itm~ ~override/cdt02092.g3~

COPY  ~BG2_Tweaks/2da/xpcap.2da~   ~override~

ACTION_IF FILE_EXISTS_IN_GAME ~cdt02070.g3~ THEN BEGIN // druids use cleric level progression
  COPY ~BG2_Tweaks/2da/xplevel30.2da~ ~override/xplevel.2da~
    REPLACE_TEXTUALLY
~CLERIC \(.*\)
DRUID \(.*\)~
~CLERIC \1
DRUID  \1~
END ELSE BEGIN
  COPY ~BG2_Tweaks/2da/xplevel30.2da~ ~override/xplevel.2da~
END

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// Allow Thief Skills and Stealth in Heavy Armor    \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @210000 DESIGNATED 2100
GROUP @9
GROUP @19

// this file does nothing, it just allows other mods to detect this component
COPY_EXISTING ~sw1h01.itm~ ~override/cdt02100.g3~

// language-specific text replacement macroes
INCLUDE ~BG2_Tweaks/languages/chinese/thieving_descripts.tpa~
INCLUDE ~BG2_Tweaks/languages/czech/thieving_descripts.tpa~
INCLUDE ~BG2_Tweaks/languages/english/thieving_descripts.tpa~
INCLUDE ~BG2_Tweaks/languages/french/thieving_descripts.tpa~
INCLUDE ~BG2_Tweaks/languages/german/thieving_descripts.tpa~
INCLUDE ~BG2_Tweaks/languages/italian/thieving_descripts.tpa~
INCLUDE ~BG2_Tweaks/languages/korean/thieving_descripts.tpa~
INCLUDE ~BG2_Tweaks/languages/polish/thieving_descripts.tpa~
INCLUDE ~BG2_Tweaks/languages/russian/thieving_descripts.tpa~
INCLUDE ~BG2_Tweaks/languages/spanish/thieving_descripts.tpa~

INCLUDE ~BG2_Tweaks/lib/common_armor_list.tpa~ // common armor types needed for both components
INCLUDE ~BG2_Tweaks/lib/extra_regexp_vars.tpa~ // needed for descript matching

// special chain that only gets thieving penalties (but is not elven)
COPY_EXISTING ~j#indi01.itm~ ~override~ // indi's chain vest (chainmail, can cast spells without penalties)
              ~o!bom004.itm~ ~override~ // abdel's mage chain (chain mail armour, 20% chance of miscast magic)
              ~o!bom005.itm~ ~override~ // abdel's mage chain +1 (chain mail armour, 30% chance of miscast magic)
              ~o!bom006.itm~ ~override~ // abdel's mage chain +2 (chain mail armour, 40% chance of miscast magic)
              ~s!chan02.itm~ ~override~ // eternal melody +5 (chain armour, can cast spells in it for bards)
  PATCH_IF (SOURCE_SIZE > 0x71) THEN BEGIN // protects against invalid files
    WRITE_SHORT 0x1C 201
  END
  BUT_ONLY_IF_IT_CHANGES

// elven chain
COPY_EXISTING ~a!bchan1.itm~ ~override~
              ~bsingchn.itm~ ~override~ // mirathar (bladesinger chainmail)
              ~c2chan01.itm~ ~override~
              ~chan12.itm~   ~override~
              ~chan13.itm~   ~override~
              ~chan14.itm~   ~override~
              ~chan15.itm~   ~override~
              ~chan16.itm~   ~override~
              ~chan19.itm~   ~override~
              ~chands06.itm~ ~override~ // elven chainmail +1 (elven chain mail)
              ~clolth.itm~   ~override~
              ~drowchn1.itm~ ~override~ // drow adamantite mesh armor +1 (drow mesh armour)
              ~drowchn2.itm~ ~override~ // drow adamantite mesh armor +2 (drow mesh armour)
              ~drowchn3.itm~ ~override~ // drow adamantite mesh armor +3 (drow mesh armour)
              ~dsjearm.itm~  ~override~
              ~dskorim.itm~  ~override~
              ~dwchan01.itm~ ~override~
              ~dwchan02.itm~ ~override~
              ~echan01.itm~  ~override~ // elven chainmail +2 (elven chainmail)
              ~echan02.itm~  ~override~ // underdark mail +1 (elven chainmail)
              ~efml01.itm~   ~override~
              ~fwchan02.itm~ ~override~
              ~fwchan03.itm~ ~override~
              ~gcmch01.itm~  ~override~
              ~illas04.itm~  ~override~
              ~ntchan04.itm~ ~override~ // elven chainmail of sorcery (elven chainmail)
              ~ntchan06.itm~ ~override~ // chainmail of the firewine (elven chainmail)
              ~pchan.itm~    ~override~
              ~rr#chn01.itm~ ~override~ // dark elven chain (elven chain)
              ~scal04.itm~   ~override~
              ~sgchan1.itm~  ~override~
              ~sgchan2.itm~  ~override~
              ~tzchan01.itm~ ~override~
              ~u#chan01.itm~ ~override~
              ~u#chan07.itm~ ~override~ // elven chainmail of the hand +3
              ~ucmgch01.itm~ ~override~ // drow chain +3 (elven chain mail, can cast spells faster too, does not decay in sunlight)
              ~ucmgch02.itm~ ~override~ // drow admantine chain +5 (drow elven chain mail, does not decay on surface)
              ~x#garch.itm~  ~override~
              ~x#garch2.itm~ ~override~
  PATCH_IF (SOURCE_SIZE > 0x71) THEN BEGIN // protects against invalid files
    WRITE_SHORT 0x1C 202
  END
  BUT_ONLY_IF_IT_CHANGES

// special plate that only gets thieving penalties
COPY_EXISTING ~o!bom007.itm~ ~override~ // abdel's mage plate (plate armour, 30% chance of miscast magic)
              ~o!bom008.itm~ ~override~ // abdel's mage plate +1 (plate armour, 40% chance of miscast magic)
              ~o!bom009.itm~ ~override~ // abdel's mage plate +2 (plate armour, 50% chance of miscast magic)
  PATCH_IF (SOURCE_SIZE > 0x71) THEN BEGIN // protects against invalid files
    WRITE_SHORT 0x1C 205
  END
  BUT_ONLY_IF_IT_CHANGES

// special studded leather that only gets thieving penalties
COPY_EXISTING ~a!bleat1.itm~ ~override~ // from s&s
              ~a!bleat2.itm~ ~override~ // from s&s
              ~dskearm.itm~  ~override~ // from dsotsc
              ~ntleat02.itm~ ~override~ // from ntotsc
              ~ntleat03.itm~ ~override~ // from ntotsc
  PATCH_IF (SOURCE_SIZE > 0x71) THEN BEGIN // protects against invalid files
    WRITE_SHORT 0x1C 208
  END
  BUT_ONLY_IF_IT_CHANGES

PRINT @1
COPY_EXISTING_REGEXP GLOB ~^.+\.itm$~ ~override~
  READ_SHORT 0x1c "temp_type" ELSE 0
  PATCH_IF ("%temp_type%" > 200) BEGIN
    READ_LONG  0x64 "abil_off"
    READ_SHORT 0x68 "abil_num"
    READ_LONG  0x6a "fx_off"
    READ_SHORT 0x70 "fx_num"
    SET "delta" = 0
    SET "patch_hide" = 0
    SET "patch_pickpocket" = 0
    SET "patch_lock" = 0
    SET "patch_trap" = 0
    SET "patch_silent" = 0
    PATCH_IF ("%temp_type%" = 201) BEGIN // chain mail
      SET "penalty_hide" = 30
      SET "penalty_pickpocket" = 40
      SET "penalty_lock" = 15
      SET "penalty_trap" = 15
      SET "penalty_silent" = 40
    END ELSE
    PATCH_IF ("%temp_type%" = 202) BEGIN // elven chain mail
      SET "penalty_hide" = 10
      SET "penalty_pickpocket" = 20
      SET "penalty_lock" = 5
      SET "penalty_trap" = 5
      SET "penalty_silent" = 10
    END ELSE
    PATCH_IF ("%temp_type%" = 203) BEGIN // full plate
      SET "penalty_hide" = 95
      SET "penalty_pickpocket" = 95
      SET "penalty_lock" = 80
      SET "penalty_trap" = 80
      SET "penalty_silent" = 95
    END ELSE
    PATCH_IF ("%temp_type%" = 204) BEGIN // hide
      SET "penalty_hide" = 20
      SET "penalty_pickpocket" = 60
      SET "penalty_lock" = 50
      SET "penalty_trap" = 50
      SET "penalty_silent" = 30
    END ELSE
    PATCH_IF ("%temp_type%" = 205) BEGIN // plate mail
      SET "penalty_hide" = 75
      SET "penalty_pickpocket" = 75
      SET "penalty_lock" = 40
      SET "penalty_trap" = 40
      SET "penalty_silent" = 80
    END ELSE
    PATCH_IF ("%temp_type%" = 206) BEGIN // scale mail
      SET "penalty_hide" = 50
      SET "penalty_pickpocket" = 50
      SET "penalty_lock" = 20
      SET "penalty_trap" = 20
      SET "penalty_silent" = 60
    END ELSE
    PATCH_IF ("%temp_type%" = 207) BEGIN // splint mail
      SET "penalty_hide" = 30
      SET "penalty_pickpocket" = 40
      SET "penalty_lock" = 15
      SET "penalty_trap" = 25
      SET "penalty_silent" = 40
    END ELSE
    PATCH_IF ("%temp_type%" = 208) BEGIN // studded leather
      SET "penalty_hide" = 20
      SET "penalty_pickpocket" = 30
      SET "penalty_lock" = 10
      SET "penalty_trap" = 10
      SET "penalty_silent" = 20
    END ELSE
    PATCH_IF ("%temp_type%" = 210) BEGIN // chain, special
      SET "penalty_hide" = 20
      SET "penalty_pickpocket" = 30
      SET "penalty_lock" = 10
      SET "penalty_trap" = 10
      SET "penalty_silent" = 20
      SET "patch_hide" = 2
      SET "patch_silent" = 2
    END ELSE BEGIN // 211, special studded leather
      SET "penalty_hide" = 20
      SET "penalty_pickpocket" = 30
      SET "penalty_lock" = 10
      SET "penalty_trap" = 10
      SET "penalty_silent" = 20
      SET "patch_hide" = 2
      SET "patch_silent" = 2
    END
    FOR (index = fx_num - 1 ; index >= 0 ; index = index - 1) BEGIN
      READ_SHORT ("%fx_off%" +        ("%index%" * 0x30)) "effect"
      READ_LONG  ("%fx_off%" + 0x04 + ("%index%" * 0x30)) "value"
      READ_LONG  ("%fx_off%" + 0x08 + ("%index%" * 0x30)) "button"
      PATCH_IF ("%value%" < 0) BEGIN // if existing thief skill penalties exist, negates them--all thieving penalties
        SET "value" = 0              // I've encountered so far have been attributed to armor, so this eliminates duplication
      END
      PATCH_IF (("%effect%" = 144) AND ("%button%" = 0)) BEGIN // this removes the disable stealth button effect, if present
        DELETE_BYTES ("%fx_off%" + ("%index%" * 0x30)) 0x30
        SET "delta" = ("%delta%" - 1)
      END ELSE
      PATCH_IF (("%effect%" = 144) AND ("%button%" = 1)) BEGIN // this removes the disable thieving skill button effect, if present
        DELETE_BYTES ("%fx_off%" + ("%index%" * 0x30)) 0x30
        SET "delta" = ("%delta%" - 1)
      END ELSE
      PATCH_IF (("%effect%" = 275) AND ("%patch_hide%" = 0) AND ("%button%" = 0)) BEGIN // if incremental hide bonus already exists, adjusts it
        WRITE_LONG ("%fx_off%" + 0x04 + ("%index%" * 0x30)) ("%value%" - "%penalty_hide%")
        SET "patch_hide" = 1
      END ELSE
      PATCH_IF (("%effect%" = 92) AND ("%patch_pickpocket%" = 0) AND ("%button%" = 0)) BEGIN // if incremental pickocket bonus already exists, adjusts it
        WRITE_LONG ("%fx_off%" + 0x04 + ("%index%" * 0x30)) ("%value%" - "%penalty_pickpocket%")
        SET "patch_pickpocket" = 1
      END ELSE
      PATCH_IF (("%effect%" = 90) AND ("%patch_lock%" = 0) AND ("%button%" = 0)) BEGIN // if incremental lockpicking bonus already exists, adjusts it
        WRITE_LONG ("%fx_off%" + 0x04 + ("%index%" * 0x30)) ("%value%" - "%penalty_lock%")
        SET "patch_lock" = 1
      END ELSE
      PATCH_IF (("%effect%" = 91) AND ("%patch_trap%" = 0) AND ("%button%" = 0)) BEGIN // if incremental trapfinding bonus already exists, adjusts it
        WRITE_LONG ("%fx_off%" + 0x04 + ("%index%" * 0x30)) ("%value%" - "%penalty_trap%")
        SET "patch_trap" = 1
      END ELSE
      PATCH_IF (("%effect%" = 59) AND ("%patch_silent%" = 0) AND ("%button%" = 0)) BEGIN // if incremental move silently bonus already exists, adjusts it
        WRITE_LONG ("%fx_off%" + 0x04 + ("%index%" * 0x30)) ("%value%" - "%penalty_silent%")
        SET "patch_silent" = 1
      END
    END
    PATCH_IF ("%patch_hide%" = 0) BEGIN // if there is no existing hide bonus, creates one
      INSERT_BYTES  ("%fx_off%"        + (("%fx_num%" + "%delta%") * 0x30)) 0x30
        WRITE_SHORT ("%fx_off%"        + (("%fx_num%" + "%delta%") * 0x30)) 275 // type
        WRITE_BYTE  ("%fx_off%" + 0x02 + (("%fx_num%" + "%delta%") * 0x30)) 1   // target
        WRITE_LONG  ("%fx_off%" + 0x04 + (("%fx_num%" + "%delta%") * 0x30)) (0 - "%penalty_hide%") // value
        WRITE_BYTE  ("%fx_off%" + 0x0c + (("%fx_num%" + "%delta%") * 0x30)) 2   // while equipped
        WRITE_BYTE  ("%fx_off%" + 0x12 + (("%fx_num%" + "%delta%") * 0x30)) 100 // probability
      SET "delta" = ("%delta%" + 1)
    END
    PATCH_IF ("%patch_pickpocket%" = 0) BEGIN // if there is no existing pickpocket bonus, creates one
      INSERT_BYTES  ("%fx_off%"        + (("%fx_num%" + "%delta%") * 0x30)) 0x30
        WRITE_SHORT ("%fx_off%"        + (("%fx_num%" + "%delta%") * 0x30)) 92  // type
        WRITE_BYTE  ("%fx_off%" + 0x02 + (("%fx_num%" + "%delta%") * 0x30)) 1   // target
        WRITE_LONG  ("%fx_off%" + 0x04 + (("%fx_num%" + "%delta%") * 0x30)) (0 - "%penalty_pickpocket%") // value
        WRITE_BYTE  ("%fx_off%" + 0x0c + (("%fx_num%" + "%delta%") * 0x30)) 2   // while equipped
        WRITE_BYTE  ("%fx_off%" + 0x12 + (("%fx_num%" + "%delta%") * 0x30)) 100 // probability
      SET "delta" = ("%delta%" + 1)
    END
    PATCH_IF ("%patch_lock%" = 0) BEGIN // if there is no existing lockpicking bonus, creates one
      INSERT_BYTES  ("%fx_off%"        + (("%fx_num%" + "%delta%") * 0x30)) 0x30
        WRITE_SHORT ("%fx_off%"        + (("%fx_num%" + "%delta%") * 0x30)) 90  // type
        WRITE_BYTE  ("%fx_off%" + 0x02 + (("%fx_num%" + "%delta%") * 0x30)) 1   // target
        WRITE_LONG  ("%fx_off%" + 0x04 + (("%fx_num%" + "%delta%") * 0x30)) (0 - "%penalty_lock%") // value
        WRITE_BYTE  ("%fx_off%" + 0x0c + (("%fx_num%" + "%delta%") * 0x30)) 2   // while equipped
        WRITE_BYTE  ("%fx_off%" + 0x12 + (("%fx_num%" + "%delta%") * 0x30)) 100 // probability
      SET "delta" = ("%delta%" + 1)
    END
    PATCH_IF ("%patch_trap%" = 0) BEGIN // if there is no existing trapfinding bonus, creates one
      INSERT_BYTES  ("%fx_off%"        + (("%fx_num%" + "%delta%") * 0x30)) 0x30
        WRITE_SHORT ("%fx_off%"        + (("%fx_num%" + "%delta%") * 0x30)) 91  // type
        WRITE_BYTE  ("%fx_off%" + 0x02 + (("%fx_num%" + "%delta%") * 0x30)) 1   // target
        WRITE_LONG  ("%fx_off%" + 0x04 + (("%fx_num%" + "%delta%") * 0x30)) (0 - "%penalty_trap%") // value
        WRITE_BYTE  ("%fx_off%" + 0x0c + (("%fx_num%" + "%delta%") * 0x30)) 2   // while equipped
        WRITE_BYTE  ("%fx_off%" + 0x12 + (("%fx_num%" + "%delta%") * 0x30)) 100 // probability
      SET "delta" = ("%delta%" + 1)
    END
    PATCH_IF ("%patch_silent%" = 0) BEGIN // if there is no existing silent bonus, creates one
      INSERT_BYTES  ("%fx_off%"        + (("%fx_num%" + "%delta%") * 0x30)) 0x30
        WRITE_SHORT ("%fx_off%"        + (("%fx_num%" + "%delta%") * 0x30)) 59  // type
        WRITE_BYTE  ("%fx_off%" + 0x02 + (("%fx_num%" + "%delta%") * 0x30)) 1   // target
        WRITE_LONG  ("%fx_off%" + 0x04 + (("%fx_num%" + "%delta%") * 0x30)) (0 - "%penalty_silent%") // value
        WRITE_BYTE  ("%fx_off%" + 0x0c + (("%fx_num%" + "%delta%") * 0x30)) 2   // while equipped
        WRITE_BYTE  ("%fx_off%" + 0x12 + (("%fx_num%" + "%delta%") * 0x30)) 100 // probability
      SET "delta" = ("%delta%" + 1)
    END
    PATCH_IF ("%delta%" != 0) BEGIN
      FOR (index2 = 0 ; index2 < abil_num ; index2 = index2 + 1) BEGIN // if abilities are present, need to adjust their effects index
        READ_SHORT  ("%abil_off%" + 0x20 + ("%index2%" * 0x38)) "abil_fx_index"
        WRITE_SHORT ("%abil_off%" + 0x20 + ("%index2%" * 0x38)) ("%abil_fx_index%" + "%delta%")
      END
      WRITE_SHORT 0x70 ("%fx_num%" + "%delta%")
    END
    // description updates
    FOR (index = 0x50 ; index < 0x55 ; index = index + 0x04) BEGIN
      READ_LONG "%index%" "valid"
      PATCH_IF ("%valid%" < 2147483646) AND ("%valid%" >= 0) BEGIN
        READ_STRREF "%index%" "description"
        INNER_PATCH ~%description%~ BEGIN
          PATCH_IF ("%LANGUAGE%" STRING_COMPARE_CASE "chinese" = 0) BEGIN // if chinese
            LAUNCH_PATCH_MACRO ~thieving_descripts_chinese~
          END ELSE
          PATCH_IF ("%LANGUAGE%" STRING_COMPARE_CASE "czech" = 0) BEGIN // if czech
            LAUNCH_PATCH_MACRO ~thieving_descripts_czech~
          END ELSE
          PATCH_IF ("%LANGUAGE%" STRING_COMPARE_CASE "english" = 0) BEGIN // if english
            LAUNCH_PATCH_MACRO ~thieving_descripts_english~
          END ELSE
          PATCH_IF ("%LANGUAGE%" STRING_COMPARE_CASE "french" = 0) BEGIN // if french
            LAUNCH_PATCH_MACRO ~thieving_descripts_french~
          END ELSE
          PATCH_IF ("%LANGUAGE%" STRING_COMPARE_CASE "german" = 0) BEGIN // if german
            LAUNCH_PATCH_MACRO ~thieving_descripts_german~
          END ELSE
          PATCH_IF ("%LANGUAGE%" STRING_COMPARE_CASE "italian" = 0) BEGIN // if italian
            LAUNCH_PATCH_MACRO ~thieving_descripts_italian~
          END ELSE
          PATCH_IF ("%LANGUAGE%" STRING_COMPARE_CASE "korean" = 0) BEGIN // if korean
            LAUNCH_PATCH_MACRO ~thieving_descripts_korean~
          END ELSE
          PATCH_IF ("%LANGUAGE%" STRING_COMPARE_CASE "polish" = 0) BEGIN // if Polish
            LAUNCH_PATCH_MACRO ~thieving_descripts_polish~
          END ELSE
          PATCH_IF ("%LANGUAGE%" STRING_COMPARE_CASE "russian" = 0) BEGIN // if russian
            LAUNCH_PATCH_MACRO ~thieving_descripts_russian~
          END ELSE
          PATCH_IF ("%LANGUAGE%" STRING_COMPARE_CASE "spanish" = 0) BEGIN // if Spanish
            LAUNCH_PATCH_MACRO ~thieving_descripts_spanish~
          END
          REPLACE_TEXTUALLY ~CDPERCENTAGE~ ~%~
          SET char = 0 // different than the check
          FOR (i = 0; char != 0xfafa; i += 1) BEGIN
            READ_BYTE i char ELSE 0xfafa // if out of bounds, read my custom EOF signal/value
          END
          READ_ASCII 0 description (i - 1)
        END
        SAY_EVALUATED "%index%" ~%description%~
      END
    END

    WRITE_SHORT 0x1c 2
  END
  BUT_ONLY_IF_IT_CHANGES

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                            \\\\\
///// Adjust item descriptions for thieving skills               \\\\\
/////                                                            \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @211000 DESIGNATED 2110
DEPRECATED @18

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// Allow Arcane Spellcasting in Armor               \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @212000 DESIGNATED 2120
GROUP @9
GROUP @19

// this file does nothing, it just allows other mods to detect this component
COPY_EXISTING ~sw1h01.itm~ ~override/cdt02120.g3~

// language-specific text replacement macroes
INCLUDE ~BG2_Tweaks/languages/chinese/arcane_descripts.tpa~
INCLUDE ~BG2_Tweaks/languages/czech/arcane_descripts.tpa~
INCLUDE ~BG2_Tweaks/languages/english/arcane_descripts.tpa~
INCLUDE ~BG2_Tweaks/languages/french/arcane_descripts.tpa~
INCLUDE ~BG2_Tweaks/languages/german/arcane_descripts.tpa~
INCLUDE ~BG2_Tweaks/languages/italian/arcane_descripts.tpa~
INCLUDE ~BG2_Tweaks/languages/korean/arcane_descripts.tpa~
INCLUDE ~BG2_Tweaks/languages/polish/arcane_descripts.tpa~
INCLUDE ~BG2_Tweaks/languages/russian/arcane_descripts.tpa~
INCLUDE ~BG2_Tweaks/languages/spanish/arcane_descripts.tpa~

/*
Rather than a series of huge CEs, we temporarily set the item types here so we only need one REGEXP.
190 - Buckler
191 - Small Shield
192 - Medium Shield
193 - Large Shield
194 - Tower Shield

201 - Chainmail
202 - Elven Chainmail
203 - Full Plate
204 - Hide Armor
205 - Plate Mail
206 - Scale Mail
207 - Splint Mail
208 - Studded Leather
209 - Leather
*/

// most armor (save leather) same as thieving tweak
INCLUDE ~BG2_Tweaks/lib/common_armor_list.tpa~
INCLUDE ~BG2_Tweaks/lib/extra_regexp_vars.tpa~ // needed for descript matching

// bucklers
COPY_EXISTING ~_shld08.itm~  ~override~
              ~_shld09.itm~  ~override~
              ~_shld10.itm~  ~override~
              ~_shld20.itm~  ~override~
              ~a!brsb.itm~   ~override~
              ~buck01.itm~   ~override~
              ~buck02.itm~   ~override~
              ~buck03.itm~   ~override~
              ~buck04.itm~   ~override~
              ~buck05.itm~   ~override~
              ~buck06.itm~   ~override~
              ~c2shld01.itm~ ~override~
              ~deitm121.itm~ ~override~
              ~drowshd1.itm~ ~override~ // drow adamantite buckler +1 (drow buckler)
              ~drowshd2.itm~ ~override~ // drow adamantite buckler +2 (drow buckler)
              ~drowshd3.itm~ ~override~ // drow adamantite buckler +3 (drow buckler)
              ~dsshld01.itm~ ~override~
              ~m#buck2.itm~  ~override~ // buckler +2, thorard's saucer (buckler)
              ~o!bom027.itm~ ~override~ // beren's buckler of baleful woe (buckler)
              ~rr#buc01.itm~ ~override~ // kiel's buckler (buckler +1)
              ~rr#buc02.itm~ ~override~ // buckler +1 (also present in tutu and the bg1 portion of bgt)
              ~rr#buc03.itm~ ~override~ // buckler +2 (also present in tutu and the bg1 portion of bgt)
              ~rr#buc04.itm~ ~override~ // buckler +3
              ~rr#rward.itm~ ~override~ // rogue's ward (buckler +2)
              ~rr#shl01.itm~ ~override~ // last defense (buckler +1, also present in tutu and the bg1 portion of bgt)
              ~rr#shl02.itm~ ~override~ // safeguard (buckler +2, also present in tutu and the bg1 portion of bgt)
              ~s!shld03.itm~ ~override~ // buckler +3 (buckler)
              ~shld08.itm~   ~override~
              ~shld09.itm~   ~override~
              ~shld10.itm~   ~override~
              ~shld17.itm~   ~override~
              ~shld20.itm~   ~override~
              ~skelshld.itm~ ~override~
  PATCH_IF (SOURCE_SIZE > 0x71) THEN BEGIN // protects against invalid files
    WRITE_SHORT 0x1C 190
  END
  BUT_ONLY_IF_IT_CHANGES

// small shields
COPY_EXISTING ~_ishld02.itm~ ~override~
              ~_shld01.itm~  ~override~
              ~_shld02.itm~  ~override~
              ~_shld11.itm~  ~override~
              ~_shld12.itm~  ~override~
              ~_shld99.itm~  ~override~
              ~brueshld.itm~ ~override~ // small shield +1 (small shield)
              ~c2shld02.itm~ ~override~
              ~cbcl058.itm~  ~override~ // waukeen's defender
              ~cbsbmltw.itm~ ~override~
              ~dscushld.itm~ ~override~
              ~dshld01.itm~  ~override~
              ~dsjeshld.itm~ ~override~
              ~dsshld51.itm~ ~override~
              ~ishld02.itm~  ~override~
              ~mgishld2.itm~ ~override~ // small shield +2, shield of fortitude (small shield)
              ~ntshld01.itm~ ~override~ // small shield +2, 'shield of free action' (small shield)
              ~psq60.itm~    ~override~
              ~shld01.itm~   ~override~
              ~shld02.itm~   ~override~
              ~shld11.itm~   ~override~
              ~shld12.itm~   ~override~
              ~shld24.itm~   ~override~
              ~shld25.itm~   ~override~
              ~shld28.itm~   ~override~
              ~shld51.itm~   ~override~ // small shield +2 'sun shield' (small shield)
              ~shld99.itm~   ~override~
              ~shldds01.itm~ ~override~ // cúchoinneach's small shield +3 "the moon" (small shield)
              ~slayer1.itm~  ~override~ // small shield +2 (small shield)
              ~waukeen.itm~  ~override~ // waukeen's defender (small shield)
  PATCH_IF (SOURCE_SIZE > 0x71) THEN BEGIN // protects against invalid files
    WRITE_SHORT 0x1C 191
  END
  BUT_ONLY_IF_IT_CHANGES

// medium shields
COPY_EXISTING ~_ishld03.itm~ ~override~
              ~_shld03.itm~  ~override~
              ~_shld04.itm~  ~override~
              ~_shld13.itm~  ~override~
              ~_shld14.itm~  ~override~
              ~c2anom01.itm~ ~override~
              ~cbcl005.itm~  ~override~ // the argent shield (medium shield)
              ~cbpdofcm.itm~ ~override~ // shield of the purple dragon (medium shield)
              ~cbrjr015.itm~ ~override~ // dzance's guardian (medium shield?)
              ~cbshldwk.itm~ ~override~ // waukeen's defender (medium shield?)
              ~cbshsoub.itm~ ~override~
              ~chalshld.itm~ ~override~
              ~cmgforg8.itm~ ~override~
              ~coms01.itm~   ~override~
              ~coms02.itm~   ~override~
              ~deitm119.itm~ ~override~
              ~deitm120.itm~ ~override~
              ~dsshld52.itm~ ~override~
              ~dsshld53.itm~ ~override~
              ~dwshld01.itm~ ~override~
              ~elemshld.itm~ ~override~
              ~gcmsh01.itm~  ~override~
              ~ishld03.itm~  ~override~
              ~mgishld1.itm~ ~override~ // medium shield +2, featherweight (medium shield)
              ~npshld.itm~   ~override~
              ~nsshld01.itm~ ~override~ // hermes' shield +1 (medium shield)
              ~nsshld02.itm~ ~override~ // elemental shield +4 (medium shield)
              ~psq59.itm~    ~override~
              ~psq61.itm~    ~override~
              ~r!kitshd.itm~ ~override~ // mageslayer shield (large shield/medium shield, i can't see the item icon without installing)
              ~r#runed.itm~  ~override~
              ~rr#guara.itm~ ~override~ // the guardian (medium shield +2, non-metallic, usable by druids)
              ~s!shld01.itm~ ~override~ // delryn family legacy +4 (as per anomen's existing shield)
              ~sgshld1.itm~  ~override~
              ~shghost.itm~  ~override~
              ~shld03.itm~   ~override~
              ~shld04.itm~   ~override~
              ~shld13.itm~   ~override~
              ~shld14.itm~   ~override~
              ~shld21.itm~   ~override~
              ~shld22.itm~   ~override~
              ~shld26.itm~   ~override~
              ~shld27.itm~   ~override~
              ~shld29.itm~   ~override~
              ~shld32.itm~   ~override~
              ~shld52.itm~   ~override~ // medium shield +2 (medium shield)
              ~shld53.itm~   ~override~ // medium shield +2 'heart of the dragon' (medium shield)
              ~shldgraz.itm~ ~override~
              ~shldred.itm~  ~override~ // the red knight's shield (medium shield)
              ~sshldmag.itm~ ~override~
              ~ttshld01.itm~ ~override~
              ~u#shld01.itm~ ~override~
              ~ucmgsh01.itm~ ~override~ // medium shield +3 (medium shield)
              ~ucmgsh02.itm~ ~override~ // drow shield +3 (medium shield)
              ~wa2shiel.itm~ ~override~
              ~x#ajshld.itm~ ~override~
  PATCH_IF (SOURCE_SIZE > 0x71) THEN BEGIN // protects against invalid files
    WRITE_SHORT 0x1C 192
  END
  BUT_ONLY_IF_IT_CHANGES

// tower shields
COPY_EXISTING ~_shld05.itm~  ~override~
              ~_shld06.itm~  ~override~
              ~_shld07.itm~  ~override~
              ~_shld15.itm~  ~override~
              ~_shld16.itm~  ~override~
              ~_shld18.itm~  ~override~
              ~_shld19.itm~  ~override~
              ~cb351g03.itm~ ~override~
              ~cbcl037.itm~  ~override~ // mystery of the dead (large shield)
              ~cblyshld.itm~ ~override~ // lyrar's shield (large shield)
              ~cbmhshld.itm~ ~override~ // large shield +3 (large shield)
              ~cbmilt03.itm~ ~override~
              ~cbmilt13.itm~ ~override~
              ~cbrolfsh.itm~ ~override~ // rolf sureblade's shield (medium shield? or tower shield?)
              ~cbsbmltx.itm~ ~override~
              ~cbsbmlty.itm~ ~override~
              ~cbtempus.itm~ ~override~ // holy shield of tempus (large shield)
              ~cbtssd01.itm~ ~override~ // large shield +5 (large shield)
              ~cbwtni5a.itm~ ~override~ // shield of belvin (large shield?)
              ~cmshld01.itm~ ~override~ // shield +3 (kite/tower/body shield)
              ~deitm122.itm~ ~override~
              ~dsshld02.itm~ ~override~
              ~dsshld54.itm~ ~override~
              ~fwshld01.itm~ ~override~
              ~gcmsh02.itm~  ~override~
              ~guardshz.itm~ ~override~ // soul guard (tower shield)
              ~jers.itm~     ~override~
              ~mashld01.itm~ ~override~ // large mithril shied +3 (large shield)
              ~mystery.itm~  ~override~
              ~ntshld02.itm~ ~override~ // shield of the firewine (large shield)
              ~ntshld03.itm~ ~override~ // reinforced large shield (large shield)
              ~orrshld.itm~  ~override~ // orrick's rhino beetle shield (tower shield)
              ~psq117.itm~   ~override~
              ~psq58.itm~    ~override~
              ~s!shld02.itm~ ~override~ // supreme shelter +6 (tower shield)
              ~shld05.itm~   ~override~
              ~shld06.itm~   ~override~
              ~shld07.itm~   ~override~
              ~shld15.itm~   ~override~
              ~shld16.itm~   ~override~
              ~shld18.itm~   ~override~
              ~shld19.itm~   ~override~
              ~shld23.itm~   ~override~
              ~shld30.itm~   ~override~
              ~shld31.itm~   ~override~
              ~shld54.itm~   ~override~ // large shield +2, +7 vs. missiles: 'the wall' (large shield)
              ~shldraid.itm~ ~override~
              ~shldxz.itm~   ~override~
              ~tzshld01.itm~ ~override~
              ~ushld3b.itm~  ~override~ // storming shield +3 (tower shield)
              ~x#fash01.itm~ ~override~
  PATCH_IF (SOURCE_SIZE > 0x71) THEN BEGIN // protects against invalid files
    WRITE_SHORT 0x1C 194
  END
  BUT_ONLY_IF_IT_CHANGES

// leather armor
COPY_EXISTING ~_leat01.itm~  ~override~
              ~_leat02.itm~  ~override~
              ~_leat03.itm~  ~override~
              ~_leat09.itm~  ~override~
              ~ardeadx.itm~  ~override~ // armour of the dead +3 (leather armour)
              ~c2leat01.itm~ ~override~
              ~cbsbmltr.itm~ ~override~
              ~cbsbmlts.itm~ ~override~
              ~cmleat5.itm~  ~override~ // nature's divine (leather armour)
              ~ddhrtles.itm~ ~override~
              ~deitm089.itm~ ~override~
              ~fwleat01.itm~ ~override~
              ~i1lt001.itm~  ~override~ // skeleton ribcage (leather armour?)
              ~i1lt002.itm~  ~override~ // anti-imprisonment armor (leather armour?, move silently: -20, lock picking: -10, find/remove traps: -10, pick pockets: -10)
              ~leat01.itm~   ~override~
              ~leat02.itm~   ~override~
              ~leat03.itm~   ~override~
              ~leat09.itm~   ~override~
              ~leat11.itm~   ~override~
              ~leat12.itm~   ~override~
              ~leat13.itm~   ~override~
              ~leat14.itm~   ~override~
              ~leat21.itm~   ~override~
              ~leat22.itm~   ~override~
              ~leatds02.itm~ ~override~ // cúchoinneach personal leather armor +4 (leather armour)
              ~leatds05.itm~ ~override~ // cúchoinneach personal leather armor +4 (leather armour)
              ~macmle01.itm~ ~override~ // woodland armor (leather armour)
              ~psq66.itm~    ~override~
              ~s!leat01.itm~ ~override~ // improved human flesh (leather armour)
              ~s!leat02.itm~ ~override~ // spirit of the night (leather armour)
              ~samarmx1.itm~ ~override~
              ~sgleat1.itm~  ~override~
              ~sgleat3.itm~  ~override~
              ~sgleat4.itm~  ~override~
              ~shar20.itm~   ~override~ // blade master leather (leather armour)
              ~ttleat.itm~   ~override~
              ~visleat1.itm~ ~override~
              ~warlkarm.itm~ ~override~ // stormcloud "kraken's bane" (leather armour)
              ~wlfleat.itm~  ~override~ // wulfgar's fur armor (leather armour)
              ~wulfhide.itm~ ~override~ // wulfgar's fur armor (leather armour)
              ~xarm004.itm~  ~override~
  PATCH_IF (SOURCE_SIZE > 0x71) THEN BEGIN // protects against invalid files
    WRITE_SHORT 0x1C 209
  END
  BUT_ONLY_IF_IT_CHANGES

PRINT @1
COPY_EXISTING_REGEXP GLOB ~^.+\.itm$~ ~override~
  READ_SHORT 0x1c "temp_type" ELSE 0
  PATCH_IF ("%temp_type%" > 189) BEGIN // master patch check--patching only occurs for items above
    READ_LONG  0x64 "abil_off"
    READ_SHORT 0x68 "abil_num"
    READ_LONG  0x6a "fx_off"
    READ_SHORT 0x70 "fx_num"
    SET "delta" = 0
    SET "patch_miscast" = 0
    SET "patch_disable" = 0
    PATCH_IF (("%temp_type%" = 190) OR ("%temp_type%" = 191)) BEGIN // buckler, small shield
      SET "patch_miscast" = 5
    END ELSE
    PATCH_IF ("%temp_type%" = 209) BEGIN // leather armor
      SET "patch_miscast" = 10
    END ELSE
    PATCH_IF (("%temp_type%" = 192) OR ("%temp_type%" = 193) OR ("%temp_type%" = 208) OR ("%temp_type%" = 211)) BEGIN // medium shield, large shield, studded leather
      SET "patch_miscast" = 15
    END ELSE
    PATCH_IF ("%temp_type%" = 204) BEGIN // hide armor
      SET "patch_miscast" = 20
    END ELSE
    PATCH_IF ("%temp_type%" = 206) BEGIN // scale armor
      SET "patch_miscast" = 25
    END ELSE
    PATCH_IF (("%temp_type%" = 201) OR ("%temp_type%" = 210)) BEGIN // chain armor
      SET "patch_miscast" = 30
    END ELSE
    PATCH_IF ("%temp_type%" = 203) BEGIN // full plate
      SET "patch_miscast" = 35
    END ELSE
    PATCH_IF (("%temp_type%" = 205) OR ("%temp_type%" = 207)) BEGIN // plate mail, splint mail
      SET "patch_miscast" = 40
    END ELSE BEGIN // tower shield
      SET "patch_miscast" = 50
    END
    FOR (index = 0 ; index < fx_num ; index = index + 1) BEGIN // this major FOR loop examines existing effects and adjusts them accordingly
      READ_SHORT ("%fx_off%" + (("%index%") * 0x30)) "effect"
      PATCH_IF ("%effect%" = 145) BEGIN // this loop converts the disable spellcasting to a miscast penalty
        WRITE_ASCII ("%fx_off%" +        ("%index%" * 0x30)) ~~ #48           // zeroes existing effect
        WRITE_SHORT ("%fx_off%" +        ("%index%" * 0x30)) 60                // casting failure
        WRITE_BYTE  ("%fx_off%" + 0x02 + ("%index%" * 0x30)) 1                 // target self
        WRITE_LONG  ("%fx_off%" + 0x04 + ("%index%" * 0x30)) "%patch_miscast%" // penalty
        WRITE_BYTE  ("%fx_off%" + 0x0c + ("%index%" * 0x30)) 2                 // instant/while equipped
        WRITE_BYTE  ("%fx_off%" + 0x12 + ("%index%" * 0x30)) 100               // probability 1
        SET "patch_disable" = 1
      END
    END
    PATCH_IF ("%patch_disable%" = 0) BEGIN // if there is no disable spellcasting, creates new effect
      INSERT_BYTES  ("%fx_off%"        + ("%fx_num%" * 0x30)) 0x30
        WRITE_SHORT ("%fx_off%"        + ("%fx_num%" * 0x30)) 60                // casting failure
        WRITE_BYTE  ("%fx_off%" + 0x02 + ("%fx_num%" * 0x30)) 1                 // target self
        WRITE_LONG  ("%fx_off%" + 0x04 + ("%fx_num%" * 0x30)) "%patch_miscast%" // penalty
        WRITE_BYTE  ("%fx_off%" + 0x0c + ("%fx_num%" * 0x30)) 2                 // instant/while equipped
        WRITE_BYTE  ("%fx_off%" + 0x12 + ("%fx_num%" * 0x30)) 100               // probability 1
      SET "delta" = ("%delta%" + 1)
    END
    PATCH_IF ("%delta%" != 0) BEGIN
      WRITE_SHORT 0x70 ("%fx_num%" + "%delta%")
      FOR (index2 = 0 ; index2 < abil_num ; index2 = index2 + 1) BEGIN // if abilities are present, need to adjust their effects index
        READ_SHORT  ("%abil_off%" + 0x20 + ("%index2%" * 0x38)) "abil_fx_idx"
        WRITE_SHORT ("%abil_off%" + 0x20 + ("%index2%" * 0x38)) ("%abil_fx_idx%" + "%delta%")
      END
    END
    // adjust descriptions
    FOR (index = 0x50 ; index < 0x55 ; index = index + 0x04) BEGIN
      READ_LONG "%index%" "valid"
      PATCH_IF ("%valid%" < 2147483646) AND ("%valid%" >= 0) BEGIN
        READ_STRREF "%index%" "description"
        INNER_PATCH ~%description%~ BEGIN
          PATCH_IF ("%LANGUAGE%" STRING_COMPARE_CASE "chinese" = 0) BEGIN // if chinese
            LAUNCH_PATCH_MACRO ~arcane_descripts_chinese~
          END ELSE
          PATCH_IF ("%LANGUAGE%" STRING_COMPARE_CASE "czech" = 0) BEGIN // if czech
            LAUNCH_PATCH_MACRO ~arcane_descripts_czech~
          END ELSE
          PATCH_IF ("%LANGUAGE%" STRING_COMPARE_CASE "english" = 0) BEGIN // if english
            LAUNCH_PATCH_MACRO ~arcane_descripts_english~
          END ELSE
          PATCH_IF ("%LANGUAGE%" STRING_COMPARE_CASE "french" = 0) BEGIN // if french
            LAUNCH_PATCH_MACRO ~arcane_descripts_french~
          END ELSE
          PATCH_IF ("%LANGUAGE%" STRING_COMPARE_CASE "german" = 0) BEGIN // if german
            LAUNCH_PATCH_MACRO ~arcane_descripts_german~
          END ELSE
          PATCH_IF ("%LANGUAGE%" STRING_COMPARE_CASE "italian" = 0) BEGIN // if italian
            LAUNCH_PATCH_MACRO ~arcane_descripts_italian~
          END ELSE
          PATCH_IF ("%LANGUAGE%" STRING_COMPARE_CASE "korean" = 0) BEGIN // if korean
            LAUNCH_PATCH_MACRO ~arcane_descripts_korean~
          END ELSE
          PATCH_IF ("%LANGUAGE%" STRING_COMPARE_CASE "polish" = 0) BEGIN // if Polish
            LAUNCH_PATCH_MACRO ~arcane_descripts_polish~
          END ELSE
          PATCH_IF ("%LANGUAGE%" STRING_COMPARE_CASE "russian" = 0) BEGIN // if russian
            LAUNCH_PATCH_MACRO ~arcane_descripts_russian~
          END ELSE
          PATCH_IF ("%LANGUAGE%" STRING_COMPARE_CASE "spanish" = 0) BEGIN // if Spanish
            LAUNCH_PATCH_MACRO ~arcane_descripts_spanish~
          END
          SET char = 0 // different than the check
          FOR (i = 0; char != 0xfafa; i += 1) BEGIN
            READ_BYTE i char ELSE 0xfafa // if out of bounds, read my custom EOF signal/value
          END
          READ_ASCII 0 description (i - 1)
        END
        SAY_EVALUATED "%index%" ~%description%~
      END
    END
    PATCH_IF ("%temp_type%" > 199) BEGIN // resets item type for armor
      WRITE_SHORT 0x1c 2
    END ELSE BEGIN                       // or for shields
      WRITE_SHORT 0x1c 12
    END
  END
  BUT_ONLY_IF_IT_CHANGES

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                            \\\\\
///// Adjust item descriptions for arcane casting                \\\\\
/////                                                            \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @213000 DESIGNATED 2130
DEPRECATED @18

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                            \\\\\
///// More dual-classes                                          \\\\\
/////                                                            \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @214000 DESIGNATED 2140
GROUP @9
GROUP @19

INCLUDE ~BG2_Tweaks/lib/kit_ids_fixer.tpa~
INCLUDE ~BG2_Tweaks/lib/tob2soa.tpa~

// first it replaces WILDMAGE and BARBARIAN values in these files if they're present
COPY_EXISTING ~dualclas.2da~ ~override~
  REPLACE_TEXTUALLY
    ~\bWILDMAGE\b.*~
    ~WILDMAGE                1       1       0       1       0       0~
  REPLACE_TEXTUALLY
    ~\bBARBARIAN\b.*~
    ~BARBARIAN               0       1       1       1       1       0~
  BUT_ONLY_IF_IT_CHANGES

COPY_EXISTING ~abdcdsrq.2da~ ~override~
  REPLACE_TEXTUALLY
    ~\bWILDMAGE\b.*~
    ~WILDMAGE                0       0       0       17      0       0~
  REPLACE_TEXTUALLY
    ~\bBARBARIAN\b.*~
    ~BARBARIAN               17      0       0       0       0       0~
  BUT_ONLY_IF_IT_CHANGES

COPY_EXISTING ~abdcscrq.2da~ ~override~
  REPLACE_TEXTUALLY
    ~\bWILDMAGE\b.*~
    ~WILDMAGE                0       0       0       15      0       0~
  REPLACE_TEXTUALLY
    ~\bBARBARIAN\b.*~
    ~BARBARIAN               15      0       0       0       0       0~
  BUT_ONLY_IF_IT_CHANGES

// if they're not then it appends them to the files
APPEND ~dualclas.2da~ ~WILDMAGE                1       1       0       1       0       0~
  UNLESS ~\bWILDMAGE\b~

APPEND ~abdcdsrq.2da~ ~WILDMAGE                0       0       0       17      0       0~
  UNLESS ~\bWILDMAGE\b~

APPEND ~abdcscrq.2da~ ~WILDMAGE                0       0       0       15      0       0~
  UNLESS ~\bWILDMAGE\b~

APPEND ~dualclas.2da~ ~BARBARIAN               0       1       1       1       1       0~
  UNLESS ~\bBARBARIAN\b~

APPEND ~abdcdsrq.2da~ ~BARBARIAN               17      0       0       0       0       0~
  UNLESS ~\bBARBARIAN\b~

APPEND ~abdcscrq.2da~ ~BARBARIAN               15      0       0       0       0       0~
  UNLESS ~\bBARBARIAN\b~

COPY ~BG2_Tweaks/spl/cdtitle0.spl~ ~override~ // spell to fix title and class name for d/c barbarians. Calls one of the EFFs below as appropriate

COPY ~BG2_Tweaks/eff/cdtitle1.eff~ ~override~
  SAY 0x1C @214001

COPY_EXISTING ~cdtitle1.eff~ ~override/cdtitle2.eff~
  SAY 0x1C @214002

COPY_EXISTING ~cdtitle1.eff~ ~override/cdtitle3.eff~
  SAY 0x1C @214003

COPY_EXISTING ~cdtitle1.eff~ ~override/cdtitle4.eff~
  SAY 0x1C @214004

PRINT @1
COPY_EXISTING_REGEXP GLOB ~^.+\.itm$~ ~override~
  PATCH_IF (SOURCE_SIZE > 0x71) THEN BEGIN // protects against invalid files
    READ_BYTE 0x1f "usa2"
    READ_BYTE 0x29 "kit1"
    PATCH_IF ((("%kit1%" BAND 0b01000000) = 0b01000000) AND // if unusable by fighters and barbarians
              (("%usa2%" BAND 0b00001000) = 0b00001000)) THEN BEGIN
      WRITE_BYTE 0x29 ("%kit1%" BAND 0b10111111) //remove redundant barb flag
    END
  END
  BUT_ONLY_IF_IT_CHANGES


ACTION_IF FILE_EXISTS ~Refinements/Hlab/Thief/clab/CLABEMPT.2DA~ AND NOT FILE_EXISTS ~Refinements/lib/kit.tpa~ THEN BEGIN

  ADD_KIT ~barbtF~
    ~barbtF   1 1 1 1 1 0 0 0~
    ~barbtF  0 1 0 0 1 0 0 1 0 1 1 0 0 1 1 1 0 1 0 0 0 0 1 1 0 1 1 1 1 1 1 3 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0~
    ~barbtF     0       9       0       0       0       0~
    ~barbtF     0       0       0       0       0       0~
    ~barbtF     0       17      0       0       0       0~
    ~barbtF     0       15      0       0       0       0~
    ~barbtF     0       1       1       1       1       1       1       1       1~
    ~barbtF     1       1       1       0       0       0~
    ~Refinements/Hlab/Thief/clab/CLABEMPT.2DA~
    ~~
    ~0x40000000 2~
    ~BRB~
    ~* * * * * * * * * * * * * * * * * * * *~
    SAY #45855
    SAY #45859
    SAY #45869

  ADD_KIT ~barbtw~
    ~barbtw   1 1 1 1 1 0 0 0~
    ~barbtw  0 1 0 0 1 0 0 1 0 1 1 0 0 1 1 1 0 1 0 0 0 0 1 1 0 1 1 1 1 1 1 3 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0~
    ~barbtw     0       9       0       0       0       0~
    ~barbtw     0       0       0       0       0       0~
    ~barbtw     0       17      0       0       0       0~
    ~barbtw     0       15      0       0       0       0~
    ~barbtw     0       1       1       1       1       1       1       1       1~
    ~barbtw     1       1       1       0       0       0~
    ~Refinements/Hlab/Thief/clab/CLABTEMP.2DA~
    ~~
    ~0x40000000 2~
    ~BRB~
    ~* * * * * * * * * * * * * * * * * * * *~
    SAY #45855
    SAY #45859
    SAY #45869

  COPY_EXISTING ~kitlist.2da~ ~override~
    REPLACE_TEXTUALLY ~45855 45859 45869 CLABTEMP ~     ~45855 45859 45869 CLABFI05 ~
    SET_2DA_ENTRY 34 7 1 0x400000020

  EXTEND_TOP ~dplayer.bcs~  ~BG2_Tweaks/baf/dplayer.baf~
  EXTEND_TOP ~dplayer2.bcs~ ~BG2_Tweaks/baf/dplayer.baf~
  EXTEND_TOP ~dplayer3.bcs~ ~BG2_Tweaks/baf/dplayer.baf~
  //EXTEND_BOTTOM ~baldur.bcs~   ~BG2_Tweaks/baf/Runcheck.baf~
  //EXTEND_BOTTOM ~baldur25.bcs~ ~BG2_Tweaks/baf/Runcheck.baf~
  EXTEND_BOTTOM ~li#stri.bcs~  ~BG2_Tweaks/baf/li#stri.baf~

  COPY_EXISTING ~weapprof.2da~ ~override~
  // Full compatibility with all weapprof modifiers (like, say, AOE)
    FOR (row = 3; row < 35; row = row + 1) BEGIN
      READ_2DA_ENTRY "%row%" 52 1 "barb"
      SET_2DA_ENTRY "%row%" (22+"%barbtF%") 1 "%barb%"
      SET_2DA_ENTRY "%row%" (22+"%barbtw%") 1 "%barb%"
    END

  COPY_EXISTING  ~KITLIST.2DA~ ~override~
    READ_2DA_ENTRY 34 2 1 "lower"
    READ_2DA_ENTRY 34 3 1 "mixed"
    READ_2DA_ENTRY 34 4 1 "help"
    SET_2DA_ENTRY "%barbtF%"+3 2 1 "%lower%"
    SET_2DA_ENTRY "%barbtw%"+3 2 1 "%lower%"
    SET_2DA_ENTRY "%barbtF%"+3 3 1 "%mixed%"
    SET_2DA_ENTRY "%barbtw%"+3 3 1 "%mixed%"
    SET_2DA_ENTRY "%barbtF%"+3 4 1 "%help%"
    SET_2DA_ENTRY "%barbtw%"+3 4 1 "%help%"

END

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// wear multiple protection items                   \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

/////                                                  \\\\\
///// PnP restrictions                                 \\\\\
/////                                                  \\\\\

BEGIN @215001 DESIGNATED 2150 // PnP restrictions
GROUP @9
SUBCOMPONENT @215000 // wear multiple protection items

INCLUDE ~BG2_Tweaks/lib/extra_regexp_vars.tpa~ // needed for description patching

// this file does nothing, it just allows other mods to detect this component
COPY_EXISTING ~sw1h01.itm~ ~override/cdt02150.g3~

APPEND ~itemexcl.2da~
~amul14cd  1
clck01cd  1
clck02cd  1
clck31cd  1
ring06cd  1
ring07cd  1
ring25cd  1
ring41cd  1
amul14c!  1
clck01c!  1
clck02c!  1
clck31c!  1
ring06c!  1
ring07c!  1
ring25c!  1
ring41c!  1~

//add ability and effect
COPY_EXISTING ~amul14.itm~ ~override/cdamul14.itm~ // no AC bonus clone
              ~clck01.itm~ ~override/cdclck01.itm~
              ~clck02.itm~ ~override/cdclck02.itm~
              ~clck31.itm~ ~override/cdclck31.itm~
              ~ring06.itm~ ~override/cdring06.itm~
              ~ring07.itm~ ~override/cdring07.itm~
              ~ring25.itm~ ~override/cdring25.itm~
              ~ring41.itm~ ~override/cdring41.itm~
              ~amul14.itm~ ~override/amul14cd.itm~ // zero-lore clone
              ~clck01.itm~ ~override/clck01cd.itm~
              ~clck02.itm~ ~override/clck02cd.itm~
              ~clck31.itm~ ~override/clck31cd.itm~
              ~ring06.itm~ ~override/ring06cd.itm~
              ~ring07.itm~ ~override/ring07cd.itm~
              ~ring25.itm~ ~override/ring25cd.itm~
              ~ring41.itm~ ~override/ring41cd.itm~
              ~amul14.itm~ ~override/amul14.itm~   // originals with new ability
              ~clck01.itm~ ~override/clck01.itm~
              ~clck02.itm~ ~override/clck02.itm~
              ~clck31.itm~ ~override/clck31.itm~
              ~ring06.itm~ ~override/ring06.itm~
              ~ring07.itm~ ~override/ring07.itm~
              ~ring25.itm~ ~override/ring25.itm~
              ~ring41.itm~ ~override/ring41.itm~
              // Tutu items
              ~_amul14.itm~ ~override/c!amul14.itm~ // no AC bonus clone
              ~_clck01.itm~ ~override/c!clck01.itm~
              ~_clck02.itm~ ~override/c!clck02.itm~
              ~_ring06.itm~ ~override/c!ring06.itm~
              ~_ring07.itm~ ~override/c!ring07.itm~
              ~_ring25.itm~ ~override/c!ring25.itm~
              ~_amul14.itm~ ~override/amul14c!.itm~ // zero-lore clone
              ~_clck01.itm~ ~override/clck01c!.itm~
              ~_clck02.itm~ ~override/clck02c!.itm~
              ~_ring06.itm~ ~override/ring06c!.itm~
              ~_ring07.itm~ ~override/ring07c!.itm~
              ~_ring25.itm~ ~override/ring25c!.itm~
              ~_amul14.itm~ ~override/_amul14.itm~   // originals with new ability
              ~_clck01.itm~ ~override/_clck01.itm~
              ~_clck02.itm~ ~override/_clck02.itm~
              ~_ring06.itm~ ~override/_ring06.itm~
              ~_ring07.itm~ ~override/_ring07.itm~
              ~_ring25.itm~ ~override/_ring25.itm~
  PATCH_IF (SOURCE_SIZE > 0x71) THEN BEGIN // protects against invalid files
    READ_ASCII  0x3a "bam" (8)
    READ_LONG   0x64 "abil_off"
    READ_SHORT  0x68 "abil_num"
    WRITE_SHORT 0x68 ("%abil_num%" + 1)
    READ_LONG   0x6a "fx_off"
    WRITE_LONG  0x6a ("%fx_off%" + 0x38)
    READ_SHORT  0x70 "fx_num"
    PATCH_IF ("%abil_num%" > 0) BEGIN
      READ_SHORT ("%abil_off%" + 0x1e + (("%abil_num%" - 1) * 0x38)) "last_fx_num"
      READ_SHORT ("%abil_off%" + 0x20 + (("%abil_num%" - 1) * 0x38)) "last_fx_idx"
      SET "new_fx" = ("%last_fx_idx%" + "%last_fx_num%")
    END ELSE BEGIN
      SET "new_fx" = "%fx_num%"
    END
    PATCH_IF (~%DEST_RES%~ STRING_COMPARE_CASE ~%SOURCE_RES%~ != 0) BEGIN // zero lore for new copies
      WRITE_SHORT 0x42 0
    END
    PATCH_IF (~%DEST_RES%~ STRING_MATCHES_REGEXP ~^[Cc][Dd!]......$~ = 0) BEGIN // if no AC bonus
      FOR (loops = 0; loops < fx_num; loops = loops + 1) BEGIN
        READ_SHORT ("%fx_off%" + ("%loops%" * 0x30)) "type"
        PATCH_IF ("%type%" = 0) BEGIN // ac bonus
          WRITE_BYTE ("%fx_off%" + 0x12 + ("%loops%" * 0x38)) 0 // probability
        END
      END
    END
    INSERT_BYTES            ("%fx_off%" +        ("%new_fx%" * 0x30)) 0x30 // new effect
      WRITE_SHORT           ("%fx_off%" +        ("%new_fx%" * 0x30)) 122  // create inventory item
      WRITE_BYTE            ("%fx_off%" + 0x02 + ("%new_fx%" * 0x30)) 1    // target: self
      WRITE_BYTE            ("%fx_off%" + 0x0c + ("%new_fx%" * 0x30)) 1    // instant/permanent till death
      WRITE_BYTE            ("%fx_off%" + 0x12 + ("%new_fx%" * 0x30)) 100  // probability
      PATCH_IF ((~%DEST_RES%~ STRING_MATCHES_REGEXP ~[Cc][Dd]......~ = 0) OR
                (~%DEST_RES%~ STRING_MATCHES_REGEXP ~[Cc]!......~ = 0)) BEGIN // if no AC version (either non-tutu or tutu)
        
        PATCH_IF (~%DEST_RES%~ STRING_MATCHES_REGEXP ~[Cc][Dd]......~ = 0) BEGIN // (non-tutu)
          WRITE_EVALUATED_ASCII ("%fx_off%" + 0x14 + ("%new_fx%" * 0x30)) ~%SOURCE_RES%CD~
        END
        ELSE PATCH_IF (~%DEST_RES%~ STRING_MATCHES_REGEXP ~[Cc]!......~ = 0) BEGIN // (tutu)
          WRITE_EVALUATED_ASCII ("%fx_off%" + 0x13 + ("%new_fx%" * 0x30)) ~%SOURCE_RES%C!~
          WRITE_BYTE            ("%fx_off%" + 0x13 + ("%new_fx%" * 0x30)) 0
        END
        
        // update descriptions
        SET offset = 0x54 // identified description
        READ_LONG ~%offset%~ desc_strref
        PATCH_IF (desc_strref >= 0 && desc_strref < 2147483646) BEGIN
          READ_STRREF ~%offset%~ desc
          // replace any english lines that match
          INNER_PATCH_SAVE desc ~%desc%~ BEGIN
            REPLACE_TEXTUALLY ~[%lnl%%mnl%%wnl%][ %tab%]*Armou?r Class.*:.*~ ~~
          END
          // replace any german lines that match
          INNER_PATCH_SAVE desc ~%desc%~ BEGIN
            REPLACE_TEXTUALLY ~[%lnl%%mnl%%wnl%][ %tab%]*R\(K\|üstungsklasse\)[- %tab%]*\(Bonus\)?[ %tab%]*:.*~ ~~
          END
          // replace any italian lines that match
          INNER_PATCH_SAVE desc ~%desc%~ BEGIN
            REPLACE_TEXTUALLY ~[%lnl%%mnl%%wnl%][ %tab%]*\(Bonus[ %tab%]*\|Bonus[ %tab%]*alla[ %tab%]*\)?\(C[- %tab%]*A\|Classe[- %tab%]*Armatura\)[ %tab%]*:.*~ ~~
          END
          // replace any spanish lines that match
          INNER_PATCH_SAVE desc ~%desc%~ BEGIN
            REPLACE_TEXTUALLY ~[%lnl%%mnl%%wnl%][ %tab%]*\(Bonificador de \|Bonificador a la \)?Clase de armadura[ %tab%]*:.*~ ~~
          END
          // write changes
          SAY_EVALUATED ~%offset%~ ~%desc%~
        END
      END ELSE
      PATCH_IF (~%SOURCE_RES%~ STRING_MATCHES_REGEXP ~_......~ = 0) BEGIN // if AC version (tutu)
        WRITE_EVALUATED_ASCII ("%fx_off%" + 0x15 + ("%new_fx%" * 0x30)) ~%SOURCE_RES%~
        WRITE_EVALUATED_ASCII ("%fx_off%" + 0x14 + ("%new_fx%" * 0x30)) ~C!~
        PATCH_IF (~%SOURCE_RES%~ STRING_COMPARE_CASE ~_amul14~ = 0) BEGIN
          SET "_amul14_abil_num" = ("%abil_num%" + 1)
        END ELSE
        PATCH_IF (~%SOURCE_RES%~ STRING_COMPARE_CASE ~_clck01~ = 0) BEGIN
          SET "_clck01_abil_num" = ("%abil_num%" + 1)
        END ELSE
        PATCH_IF (~%SOURCE_RES%~ STRING_COMPARE_CASE ~_clck02~ = 0) BEGIN
          SET "_clck02_abil_num" = ("%abil_num%" + 1)
        END ELSE
        PATCH_IF (~%SOURCE_RES%~ STRING_COMPARE_CASE ~_ring06~ = 0) BEGIN
          SET "_ring06_abil_num" = ("%abil_num%" + 1)
        END ELSE
        PATCH_IF (~%SOURCE_RES%~ STRING_COMPARE_CASE ~_ring07~ = 0) BEGIN
          SET "_ring07_abil_num" = ("%abil_num%" + 1)
        END ELSE
        PATCH_IF (~%SOURCE_RES%~ STRING_COMPARE_CASE ~_ring25~ = 0) BEGIN
          SET "_ring25_abil_num" = ("%abil_num%" + 1)
        END
      END ELSE BEGIN // if AC version (non-tutu)
        WRITE_EVALUATED_ASCII ("%fx_off%" + 0x14 + ("%new_fx%" * 0x30)) ~CD%SOURCE_RES%~
        PATCH_IF (~%SOURCE_RES%~ STRING_COMPARE_CASE ~amul14~ = 0) BEGIN
          SET "amul14_abil_num" = ("%abil_num%" + 1)
        END ELSE
        PATCH_IF (~%SOURCE_RES%~ STRING_COMPARE_CASE ~clck01~ = 0) BEGIN
          SET "clck01_abil_num" = ("%abil_num%" + 1)
        END ELSE
        PATCH_IF (~%SOURCE_RES%~ STRING_COMPARE_CASE ~clck02~ = 0) BEGIN
          SET "clck02_abil_num" = ("%abil_num%" + 1)
        END ELSE
        PATCH_IF (~%SOURCE_RES%~ STRING_COMPARE_CASE ~clck31~ = 0) BEGIN
          SET "clck31_abil_num" = ("%abil_num%" + 1)
        END ELSE
        PATCH_IF (~%SOURCE_RES%~ STRING_COMPARE_CASE ~ring06~ = 0) BEGIN
          SET "ring06_abil_num" = ("%abil_num%" + 1)
        END ELSE
        PATCH_IF (~%SOURCE_RES%~ STRING_COMPARE_CASE ~ring07~ = 0) BEGIN
          SET "ring07_abil_num" = ("%abil_num%" + 1)
        END ELSE
        PATCH_IF (~%SOURCE_RES%~ STRING_COMPARE_CASE ~ring25~ = 0) BEGIN
          SET "ring25_abil_num" = ("%abil_num%" + 1)
        END ELSE
        PATCH_IF (~%SOURCE_RES%~ STRING_COMPARE_CASE ~ring41~ = 0) BEGIN
          SET "ring41_abil_num" = ("%abil_num%" + 1)
        END
      END
    INSERT_BYTES            ("%fx_off%"       ) 0x38         // new ability
      WRITE_BYTE            ("%fx_off%"       ) 3            // magical
      WRITE_BYTE            ("%fx_off%" + 0x01) 1            // ID to use
      WRITE_SHORT           ("%fx_off%" + 0x02) 3            // in item slots
      WRITE_EVALUATED_ASCII ("%fx_off%" + 0x04) ~%bam%~ #8   // item bam
      WRITE_SHORT           ("%fx_off%" + 0x0c) 5            // target: caster
      WRITE_SHORT           ("%fx_off%" + 0x0e) 1            // range: 1
      WRITE_SHORT           ("%fx_off%" + 0x1e) 1            // num effects: 1
      WRITE_SHORT           ("%fx_off%" + 0x20) ("%new_fx%") // fx index
      WRITE_SHORT           ("%fx_off%" + 0x22) 1            // num charges: 1
      WRITE_BYTE            ("%fx_off%" + 0x24) 1            // vanishes when drained
      WRITE_SHORT           ("%fx_off%" + 0x2a) 1            // projectile: none
  END
  BUT_ONLY_IF_IT_CHANGES

// ensures are regexp search doesn't die
COPY_EXISTING ~ar0087.are~ ~override/xr2400.are~
              ~ar0087.are~ ~override/xr2600.are~

// add charges to items in game
COPY_EXISTING_REGEXP GLOB ~^.+\.cre$~ ~override~
                          ~^.+\.are$~ ~override~
  PATCH_IF ("%SOURCE_FILE%" STRING_COMPARE_REGEXP "^.+\.cre$" = 0) BEGIN // if creature
    READ_LONG 0x2bc "itm_off" ELSE 0
    READ_LONG 0x2c0 "itm_num" ELSE 0
  END ELSE BEGIN
    READ_LONG  0x78 "itm_off" ELSE 0
    READ_SHORT 0x76 "itm_num" ELSE 0
  END
  FOR (index = 0 ; index < itm_num ; index = index + 1) BEGIN
    SET "offset" = 0
    READ_ASCII ("%itm_off%" +        (0x14 * "%index%")) "item"
    PATCH_IF ("%item%" STRING_COMPARE_CASE ~_amul14~ = 0) BEGIN
      SET "offset" = ("%_amul14_abil_num%" * 0x02)
    END ELSE
    PATCH_IF ("%item%" STRING_COMPARE_CASE ~_clck01~ = 0) BEGIN
      SET "offset" = ("%_clck01_abil_num%" * 0x02)
    END ELSE
    PATCH_IF ("%item%" STRING_COMPARE_CASE ~_clck02~ = 0) BEGIN
      SET "offset" = ("%_clck02_abil_num%" * 0x02)
    END ELSE
    PATCH_IF ("%item%" STRING_COMPARE_CASE ~_ring06~ = 0) BEGIN
      SET "offset" = ("%_ring06_abil_num%" * 0x02)
    END ELSE
    PATCH_IF ("%item%" STRING_COMPARE_CASE ~_ring07~ = 0) BEGIN
      SET "offset" = ("%_ring07_abil_num%" * 0x02)
    END ELSE
    PATCH_IF ("%item%" STRING_COMPARE_CASE ~_ring25~ = 0) BEGIN
      SET "offset" = ("%_ring25_abil_num%" * 0x02)
    END ELSE
    PATCH_IF ("%item%" STRING_COMPARE_CASE ~amul14~ = 0) BEGIN
      SET "offset" = ("%amul14_abil_num%" * 0x02)
    END ELSE
    PATCH_IF ("%item%" STRING_COMPARE_CASE ~clck01~ = 0) BEGIN
      SET "offset" = ("%clck01_abil_num%" * 0x02)
    END ELSE
    PATCH_IF ("%item%" STRING_COMPARE_CASE ~clck02~ = 0) BEGIN
      SET "offset" = ("%clck02_abil_num%" * 0x02)
    END ELSE
    PATCH_IF ("%item%" STRING_COMPARE_CASE ~clck31~ = 0) BEGIN
      SET "offset" = ("%clck31_abil_num%" * 0x02)
    END ELSE
    PATCH_IF ("%item%" STRING_COMPARE_CASE ~ring06~ = 0) BEGIN
      SET "offset" = ("%ring06_abil_num%" * 0x02)
    END ELSE
    PATCH_IF ("%item%" STRING_COMPARE_CASE ~ring07~ = 0) BEGIN
      SET "offset" = ("%ring07_abil_num%" * 0x02)
    END ELSE
    PATCH_IF ("%item%" STRING_COMPARE_CASE ~ring25~ = 0) BEGIN
      SET "offset" = ("%ring25_abil_num%" * 0x02)
    END ELSE
    PATCH_IF ("%item%" STRING_COMPARE_CASE ~ring41~ = 0) BEGIN
      SET "offset" = ("%ring41_abil_num%" * 0x02)
    END
    PATCH_IF ("offset" != 0) BEGIN
      READ_SHORT ("%itm_off%" + 0x08 + "%offset%" + (0x14 * "%index%")) "charges"
      PATCH_IF ("%charges%" = 0) BEGIN
        WRITE_SHORT ("%itm_off%" + 0x08 + "%offset%" + (0x14 * "%index%")) 1
      END
    END
  END
  BUT_ONLY_IF_IT_CHANGES

// description updates
ACTION_IF ("%LANGUAGE%" STRING_COMPARE_CASE "chinese" = 0) THEN BEGIN // chinese only (english/german/italian/spanish patched above)

  COPY_EXISTING_REGEXP GLOB ~^c[d!]amul14\.itm$~ ~override~
    SAY IDENTIFIED_DESC @99030
    BUT_ONLY_IF_IT_CHANGES

  COPY_EXISTING_REGEXP GLOB ~^c[d!]clck01\.itm$~ ~override~
    SAY IDENTIFIED_DESC @99031
    BUT_ONLY_IF_IT_CHANGES

  COPY_EXISTING_REGEXP GLOB ~^c[d!]clck02\.itm$~ ~override~
    SAY IDENTIFIED_DESC @99032
    BUT_ONLY_IF_IT_CHANGES

  COPY_EXISTING ~cdclck31.itm~ ~override~
    PATCH_IF (SOURCE_SIZE > 0x71) THEN BEGIN // protects against invalid files
      SAY IDENTIFIED_DESC @99033
    END
    BUT_ONLY_IF_IT_CHANGES

  COPY_EXISTING_REGEXP GLOB ~^c[d!]ring06\.itm$~ ~override~
    SAY IDENTIFIED_DESC @99034
    BUT_ONLY_IF_IT_CHANGES

  COPY_EXISTING_REGEXP GLOB ~^c[d!]ring07\.itm$~ ~override~
    SAY IDENTIFIED_DESC @99035
    BUT_ONLY_IF_IT_CHANGES

  COPY_EXISTING_REGEXP GLOB ~^c[d!]ring25\.itm$~ ~override~
    SAY IDENTIFIED_DESC @99036
    BUT_ONLY_IF_IT_CHANGES

  COPY_EXISTING ~cdring41.itm~ ~override~
    PATCH_IF (SOURCE_SIZE > 0x71) THEN BEGIN // protects against invalid files
      SAY IDENTIFIED_DESC @99037
     END
    BUT_ONLY_IF_IT_CHANGES

END

/////                                                  \\\\\
///// no restrictions                                  \\\\\
/////                                                  \\\\\

BEGIN @215100 DESIGNATED 2151 // no restrictions
GROUP @9
GROUP @19
SUBCOMPONENT @215000 // wear multiple protection items

// this file does nothing, it just allows other mods to detect this component
COPY_EXISTING ~sw1h01.itm~ ~override/cdt02151.g3~

COPY  ~BG2_Tweaks/2da/itemexcl.2da~ ~override~

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// Altered weapon proficiencies                     \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

/////                                                  \\\\\
///// Rebalanced weapon proficiencies                  \\\\\
/////                                                  \\\\\

BEGIN @216000 DESIGNATED 2160
GROUP @9
GROUP @19
SUBCOMPONENT @216006

// this file does nothing, it just allows other mods to detect this component
COPY_EXISTING ~sw1h01.itm~ ~override/cdt02160.g3~

// animation corrections
COPY_EXISTING ~misc9q.itm~ ~override~
  WRITE_ASCII 0x22 "sc" #2

ACTION_IF FILE_EXISTS_IN_GAME ~mel01.cre~ THEN BEGIN // ToB

  COPY_EXISTING ~sw1h66.itm~ ~override~
    WRITE_ASCII 0x22 "ss" #2

  COPY_EXISTING ~sw1h67.itm~ ~override~
    WRITE_ASCII 0x22 "s1" #2

  COPY_EXISTING ~sw1h68a.itm~ ~override~
    WRITE_ASCII 0x22 "sc" #2

END

//item changes
PRINT @1
COPY_EXISTING_REGEXP GLOB ~^.+\.itm$~ ~override~ // item changes
  PATCH_IF (SOURCE_SIZE > 0x71) THEN BEGIN // protects against invalid files
    READ_ASCII 0x22 "anim" (2)
    READ_BYTE  0x31 "prof"
    PATCH_IF ("%prof%" = 100 AND (("%anim%" STRING_COMPARE_CASE "ms" = 0) OR ("%anim%" STRING_COMPARE_CASE "mc" = 0))) BEGIN // morningstars
      SET "prof" = 101
      WRITE_BYTE 0x31 "%prof%" // mace prof
    END ELSE
    PATCH_IF (("%prof%" = 95) AND ("%anim%" STRING_COMPARE_CASE "s1" = 0)) BEGIN // ninja-tos
      SET "prof" = 91
      WRITE_BYTE 0x31 "%prof%" // short sword prof
    END ELSE
    PATCH_IF (("%prof%" = 95) AND ("%anim%" STRING_COMPARE_CASE "ss" = 0)) BEGIN // wakizashis
      SET "prof" = 94
      WRITE_BYTE 0x31 "%prof%" // katana prof
    END

    PATCH_IF ("%LANGUAGE%" STRING_COMPARE_CASE "ENGLISH" = 0) THEN BEGIN // only update descripts if English
      PATCH_IF ("%prof%" = 101) BEGIN // morningstars/maces
        READ_LONG UNIDENTIFIED_DESC "valid"
        PATCH_IF ("%valid%" > 0) BEGIN
          READ_STRREF UNIDENTIFIED_DESC "description"
          INNER_PATCH ~%description%~ BEGIN
            REPLACE_TEXTUALLY ~Proficiency\( Type\)?[:-] *\(\(Flail */ *Morningstar\)\|\(Mace\)\)~ ~Proficiency Type: Mace/Morningstar~
            SET char = 0 // different than the check
            FOR (i = 0; char != 0xfafa; i += 1) BEGIN
              READ_BYTE i char ELSE 0xfafa // if out of bounds, read my custom EOF signal/value
            END
            READ_ASCII 0 description (i - 1)
          END
          SAY_EVALUATED UNIDENTIFIED_DESC ~%description%~
        END
        READ_LONG IDENTIFIED_DESC "valid"
        PATCH_IF ("%valid%" > 0) BEGIN
          READ_STRREF IDENTIFIED_DESC "description"
          INNER_PATCH ~%description%~ BEGIN
            REPLACE_TEXTUALLY ~Proficiency\( Type\)?[:-] *\(\(Flail */ *Morningstar\)\|\(Mace\)\)~ ~Proficiency Type: Mace/Morningstar~
            SET char = 0 // different than the check
            FOR (i = 0; char != 0xfafa; i += 1) BEGIN
              READ_BYTE i char ELSE 0xfafa // if out of bounds, read my custom EOF signal/value
            END
            READ_ASCII 0 description (i - 1)
          END
          SAY_EVALUATED IDENTIFIED_DESC ~%description%~
        END
      END ELSE
      PATCH_IF ("%prof%" = 100) BEGIN // flails
        READ_LONG UNIDENTIFIED_DESC "valid"
        PATCH_IF ("%valid%" > 0) BEGIN
          READ_STRREF UNIDENTIFIED_DESC "description"
          INNER_PATCH ~%description%~ BEGIN
            REPLACE_TEXTUALLY ~Proficiency\( Type\)?[:-] *Flail */ *Morningstar~ ~Proficiency Type: Flail~
            SET char = 0 // different than the check
            FOR (i = 0; char != 0xfafa; i += 1) BEGIN
              READ_BYTE i char ELSE 0xfafa // if out of bounds, read my custom EOF signal/value
            END
            READ_ASCII 0 description (i - 1)
          END
          SAY_EVALUATED UNIDENTIFIED_DESC ~%description%~
        END
        READ_LONG IDENTIFIED_DESC "valid"
        PATCH_IF ("%valid%" > 0) BEGIN
          READ_STRREF IDENTIFIED_DESC "description"
          INNER_PATCH ~%description%~ BEGIN
            REPLACE_TEXTUALLY ~Proficiency\( Type\)?[:-] *Flail */ *Morningstar~ ~Proficiency Type: Flail~
            SET char = 0 // different than the check
            FOR (i = 0; char != 0xfafa; i += 1) BEGIN
              READ_BYTE i char ELSE 0xfafa // if out of bounds, read my custom EOF signal/value
            END
            READ_ASCII 0 description (i - 1)
          END
          SAY_EVALUATED IDENTIFIED_DESC ~%description%~
        END
      END ELSE
      PATCH_IF ("%prof%" = 94) BEGIN // katanas/wakizashis
        READ_LONG UNIDENTIFIED_DESC "valid"
        PATCH_IF ("%valid%" > 0) BEGIN
          READ_STRREF UNIDENTIFIED_DESC "description"
          INNER_PATCH ~%description%~ BEGIN
            REPLACE_TEXTUALLY ~Proficiency\( Type\)?[:-] *\(\(Scimitar */ *Wakizashi */ *Ninja-?[Tt]o\)\|\(Katana\)\)~ ~Proficiency Type: Katana/Wakizashi~
            SET char = 0 // different than the check
            FOR (i = 0; char != 0xfafa; i += 1) BEGIN
              READ_BYTE i char ELSE 0xfafa // if out of bounds, read my custom EOF signal/value
            END
            READ_ASCII 0 description (i - 1)
          END
          SAY_EVALUATED UNIDENTIFIED_DESC ~%description%~
        END
        READ_LONG IDENTIFIED_DESC "valid"
        PATCH_IF ("%valid%" > 0) BEGIN
          READ_STRREF IDENTIFIED_DESC "description"
          INNER_PATCH ~%description%~ BEGIN
            REPLACE_TEXTUALLY ~Proficiency\( Type\)?[:-] *\(\(Scimitar */ *Wakizashi */ *Ninja-?[Tt]o\)\|\(Katana\)\)~ ~Proficiency Type: Katana/Wakizashi~
            SET char = 0 // different than the check
            FOR (i = 0; char != 0xfafa; i += 1) BEGIN
              READ_BYTE i char ELSE 0xfafa // if out of bounds, read my custom EOF signal/value
            END
            READ_ASCII 0 description (i - 1)
          END
          SAY_EVALUATED IDENTIFIED_DESC ~%description%~
        END
      END ELSE
      PATCH_IF ("%prof%" = 91) BEGIN // short swords/ninja-tos
        READ_LONG UNIDENTIFIED_DESC "valid"
        PATCH_IF ("%valid%" > 0) BEGIN
          READ_STRREF UNIDENTIFIED_DESC "description"
          INNER_PATCH ~%description%~ BEGIN
            REPLACE_TEXTUALLY ~Proficiency\( Type\)?[:-] *\(\(Scimitar */ *Wakizashi */ *Ninja-?[Tt]o\)\|\(Short Sword\)\)~ ~Proficiency Type: Short Sword/Ninja-To~
            SET char = 0 // different than the check
            FOR (i = 0; char != 0xfafa; i += 1) BEGIN
              READ_BYTE i char ELSE 0xfafa // if out of bounds, read my custom EOF signal/value
            END
            READ_ASCII 0 description (i - 1)
          END
          SAY_EVALUATED UNIDENTIFIED_DESC ~%description%~
        END
        READ_LONG IDENTIFIED_DESC "valid"
        PATCH_IF ("%valid%" > 0) BEGIN
          READ_STRREF IDENTIFIED_DESC "description"
          INNER_PATCH ~%description%~ BEGIN
            REPLACE_TEXTUALLY ~Proficiency\( Type\)?[:-] *\(\(Scimitar */ *Wakizashi */ *Ninja-?[Tt]o\)\|\(Short Sword\)\)~ ~Proficiency Type: Short Sword/Ninja-To~
            SET char = 0 // different than the check
            FOR (i = 0; char != 0xfafa; i += 1) BEGIN
              READ_BYTE i char ELSE 0xfafa // if out of bounds, read my custom EOF signal/value
            END
            READ_ASCII 0 description (i - 1)
          END
          SAY_EVALUATED IDENTIFIED_DESC ~%description%~
        END
      END ELSE
      PATCH_IF ("%prof%" = 95) BEGIN // scimitars
        READ_LONG UNIDENTIFIED_DESC "valid"
        PATCH_IF ("%valid%" > 0) BEGIN
          READ_STRREF UNIDENTIFIED_DESC "description"
          INNER_PATCH ~%description%~ BEGIN
            REPLACE_TEXTUALLY ~Proficiency\( Type\)?[:-] *Scimitar */ *Wakizashi */ *Ninja-?[Tt]o~ ~Proficiency Type: Scimitar~
            SET char = 0 // different than the check
            FOR (i = 0; char != 0xfafa; i += 1) BEGIN
              READ_BYTE i char ELSE 0xfafa // if out of bounds, read my custom EOF signal/value
            END
            READ_ASCII 0 description (i - 1)
          END
          SAY_EVALUATED UNIDENTIFIED_DESC ~%description%~
        END
        READ_LONG IDENTIFIED_DESC "valid"
        PATCH_IF ("%valid%" > 0) BEGIN
          READ_STRREF IDENTIFIED_DESC "description"
          INNER_PATCH ~%description%~ BEGIN
            REPLACE_TEXTUALLY ~Proficiency\( Type\)?[:-] *Scimitar */ *Wakizashi */ *Ninja-?[Tt]o~ ~Proficiency Type: Scimitar~
            SET char = 0 // different than the check
            FOR (i = 0; char != 0xfafa; i += 1) BEGIN
              READ_BYTE i char ELSE 0xfafa // if out of bounds, read my custom EOF signal/value
            END
            READ_ASCII 0 description (i - 1)
          END
          SAY_EVALUATED IDENTIFIED_DESC ~%description%~
        END
      END
    END
  END
  BUT_ONLY_IF_IT_CHANGES

COPY_EXISTING ~weapprof.2da~ ~override~
  SET_2DA_ENTRY 11 3 51 ~cdssdescript~
  SET_2DA_ENTRY 14 3 51 ~cdkatdescript~
  SET_2DA_ENTRY 15 3 51 ~cdscimdescript~
  SET_2DA_ENTRY 21 3 51 ~cdfldescript~
  SET_2DA_ENTRY 22 3 51 ~cdmacedescript~
  REPLACE ~cdssdescript~   @216001
  REPLACE ~cdkatdescript~  @216002
  REPLACE ~cdscimdescript~ @216003
  REPLACE ~cdfldescript~   @216004
  REPLACE ~cdmacedescript~ @216005
  BUT_ONLY_IF_IT_CHANGES

// if Maurolava's samurai kit is installed, old version
// only wants katanas and wakizashis; since they're now combined need to remove scimitars for the kit
ACTION_IF FILE_EXISTS_IN_GAME ~samurai.2da~ THEN BEGIN

  // find samurai kit in kitlist
  COPY_EXISTING ~kitlist.2da~ ~override~
    COUNT_2DA_ROWS ~9~ "rows"
    FOR ( index = 31 ; index < rows ; index = index + 1 ) BEGIN
      READ_2DA_ENTRY "%index%" 5 9 "clab"
      PATCH_IF ("%clab%" STRING_COMPARE_CASE "samurai" = 0) BEGIN
        SET "samurai" = "%index%"
        SET "rows" = 0 // kills loop
      END
    END
    BUT_ONLY_IF_IT_CHANGES
    
  COPY_EXISTING ~weapprof.2da~ ~override~
    SET_2DA_ENTRY 14 ("%samurai%" + 22) ("%samurai%" + 23) 1
    BUT_ONLY_IF_IT_CHANGES

END

// if Maurolava's samurai kit is installed, new version
// only wants katanas and wakizashis; since they're now combined need to remove scimitars for the kit
ACTION_IF FILE_EXISTS_IN_GAME ~mlsamura.2da~ THEN BEGIN

  // find samurai kit in kitlist
  COPY_EXISTING ~kitlist.2da~ ~override~
    COUNT_2DA_ROWS ~9~ "rows"
    FOR ( index = 31 ; index < rows ; index = index + 1 ) BEGIN
      READ_2DA_ENTRY "%index%" 5 9 "clab"
      PATCH_IF ("%clab%" STRING_COMPARE_CASE "mlsamura" = 0) BEGIN
        SET "samurai" = "%index%"
        SET "rows" = 0 // kills loop
      END
    END
    BUT_ONLY_IF_IT_CHANGES
    
  COPY_EXISTING ~weapprof.2da~ ~override~
    SET_2DA_ENTRY 14 ("%samurai%" + 22) ("%samurai%" + 23) 1
    BUT_ONLY_IF_IT_CHANGES

END

/////                                                  \\\\\
///// BG Weapon profs with weapon styles               \\\\\
/////                                                  \\\\\

BEGIN @216100 DESIGNATED 2161
GROUP @9
SUBCOMPONENT @216006

// this file does nothing, it just allows other mods to detect this component
COPY_EXISTING ~sw1h01.itm~ ~override/cdt02161.g3~

INCLUDE ~BG2_Tweaks/lib/bg_style_profs.tpa~
OUTER_SET "tb#tutu_realloc_style" = 0
LAUNCH_MACRO_ACTION "tb#tutu_weapprof"

/////                                                  \\\\\
///// BG Weapon profs without weapon styles            \\\\\
/////                                                  \\\\\

BEGIN @216200 DESIGNATED 2162
GROUP @9
SUBCOMPONENT @216006

// this file does nothing, it just allows other mods to detect this component
COPY_EXISTING ~sw1h01.itm~ ~override/cdt02162.g3~

INCLUDE ~BG2_Tweaks/lib/bg_style_profs.tpa~
OUTER_SET "tb#tutu_realloc_style" = 1
LAUNCH_MACRO_ACTION "tb#tutu_weapprof"

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// Cast Spells from Scrolls at Character Level      \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @217000 DESIGNATED 2170
GROUP @9

// this file does nothing, it just allows other mods to detect this component
COPY_EXISTING ~sw1h01.itm~ ~override/cdt02170.g3~

COPY_EXISTING_REGEXP GLOB ~^.+\.itm$~ ~override~
  READ_LONG  0x64 "abil_off" ELSE 0
  READ_SHORT 0x68 "abil_num" ELSE 0
  READ_LONG  0x6a "fx_off"   ELSE 0
  FOR (index = 0 ; index < abil_num ; index = index + 1) BEGIN
    READ_BYTE ("%abil_off%" + (0x38 * "%index%")) "type"
    PATCH_IF ("%type%" = 3) BEGIN // magical
      READ_SHORT ("%abil_off%" + 0x1e + (0x38 * "%index%")) "abil_fx_num"
      READ_SHORT ("%abil_off%" + 0x20 + (0x38 * "%index%")) "abil_fx_idx"
      FOR (index2 = 0 ; index2 < abil_fx_num ; index2 = index2 + 1) BEGIN
        READ_SHORT ("%fx_off%" +        (0x30 * ("%abil_fx_idx%" + "%index2%"))) "opcode"
        PATCH_IF (("%opcode%" = 146) OR ("%opcode%" = 148)) BEGIN // cast spell
          WRITE_LONG ("%fx_off%" + 0x04 + (0x30 * ("%abil_fx_idx%" + "%index2%"))) 0 // cast at character's level
        END
      END
    END
  END
  BUT_ONLY_IF_IT_CHANGES

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// Un-Nerfed THAC0 Table, Saving Throws, etc.       \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @218000 DESIGNATED 2180
DEPRECATED @18

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// limited storekeeper id ability                   \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

/////                                                  \\\\\
///// only mages and bards                             \\\\\
/////                                                  \\\\\

BEGIN @219001 DESIGNATED 2190 // only mages n bards
GROUP @9
SUBCOMPONENT @219000 // limited storekeeper id ability

// this file does nothing, it just allows other mods to detect this component
COPY_EXISTING ~sw1h01.itm~ ~override/cdt02190.g3~

// builds cdstores.2da
INCLUDE ~BG2_Tweaks/lib/cdstores.tpa~

  // with cdstores.2da built, use it as a lookup table
COPY_EXISTING ~cdstores.2da~ ~override~
  COUNT_2DA_ROWS ~2~ "rows"
  FOR ( index = 1 ; index < rows ; index = index + 1 ) BEGIN
    READ_2DA_ENTRY "%index%" 0 1 "store"
    READ_2DA_ENTRY "%index%" 1 1 "creature"
    SET "exists" = 0 // sanity check to make sure store and creature exist

    INNER_PATCH_FILE ~%store%~ BEGIN
      SET "exists" = 1
    END

    PATCH_IF ("%exists%" = 1) BEGIN // don't bother if store doesn't exist

      INNER_PATCH_FILE ~%creature%~ BEGIN
        READ_BYTE 0x273 "class"
        PATCH_IF (("%class%" =   1) OR     // mage
                  ("%class%" =   5) OR     // bard
                  ("%class%" =   7) OR     // fighter-mage
                  ("%class%" =  10) OR     // fighter-mage-thief
                  ("%class%" =  13) OR     // mage-thief
                  ("%class%" =  14) OR     // cleric-mage
                  ("%class%" =  17) OR     // fighter-mage-cleric
                  ("%class%" =  19) OR     // sorcerer
                  ("%class%" = 202) OR     // mage-all
                  ("%class%" = 206)) BEGIN // bard-all
          SET "lore" = 200
        END ELSE BEGIN
          SET "lore" = 0
        END
      END // end inner_patch_file

      INNER_ACTION BEGIN

        COPY_EXISTING ~%store%~ ~override~
          PATCH_IF (SOURCE_SIZE > 0x9a) BEGIN
            WRITE_LONG 0x3c "%lore%" // lore value
          END
          BUT_ONLY_IF_IT_CHANGES

      END
    END // end patch_if
  END
  BUT_ONLY_IF_IT_CHANGES

/////                                                  \\\\\
///// based on shopkeeper                              \\\\\
/////                                                  \\\\\

BEGIN @219100 DESIGNATED 2191 // only mages n bards
GROUP @9
SUBCOMPONENT @219000 // limited storekeeper id ability

// this file does nothing, it just allows other mods to detect this component
COPY_EXISTING ~sw1h01.itm~ ~override/cdt02191.g3~

// builds cdstores.2da
INCLUDE ~BG2_Tweaks/lib/cdstores.tpa~

  // with cdstores.2da built, use it as a lookup table
COPY_EXISTING ~cdstores.2da~ ~override~
  COUNT_2DA_ROWS ~2~ "rows"
  FOR (index = 1 ; index < rows ; index = index + 1 ) BEGIN
    READ_2DA_ENTRY "%index%" 0 1 "store"
    READ_2DA_ENTRY "%index%" 1 1 "creature"
    SET "exists" = 0 // sanity check to make sure store and creature exist

    INNER_PATCH_FILE ~%store%~ BEGIN
      SET "exists" = 1
    END

    PATCH_IF ("%exists%" = 1) BEGIN // don't bother if store doesn't exist

      INNER_PATCH_FILE ~%creature%~ BEGIN
        READ_BYTE 0x234 "level1" // level of primary class
        READ_BYTE 0x235 "level2" // Highest attained level in secondary class (0-100)
        READ_BYTE 0x236 "level3" // Highest attained level in tertiary class (0-100)
        READ_BYTE 0x273 "class"  // corresponds to values in class.ids

        // lore due to class and level
        PATCH_IF ("%class%" = 5) BEGIN // bard
          SET "lore" = (10 * "%level1%")
        END ELSE
        PATCH_IF (("%class%" =  1) OR     // single-class mage
                  ("%class%" =  4) OR     // single-class thief
                  ("%class%" = 19)) BEGIN // single-class sorcerer
          SET "lore" = (3 * "%level1%")
        END ELSE
        PATCH_IF (("%class%" =  7) OR     // multi-dual fighter-thief
                  ("%class%" =  9) OR     // multi-dual fighter-mage
                  ("%class%" = 14) OR     // multi-dual cleric-mage
                  ("%class%" = 15)) BEGIN // multi-dual cleric-thief
          PATCH_IF ((3 * "%level2%") < ("%level1%")) BEGIN
            SET "lore" = "%level1%"
          END ELSE BEGIN
            SET "lore" = (3 * "%level2%")
          END
        END ELSE
        PATCH_IF (("%class%" =  13)) BEGIN // multi-dual mage-thief
          PATCH_IF (("%level2%") < ("%level1%")) BEGIN
            SET "lore" = (3 * "%level1%")
          END ELSE BEGIN
            SET "lore" = (3 * "%level2%")
          END
        END ELSE
        PATCH_IF (("%class%" =  10)) BEGIN // fighter-mage-thief
          PATCH_IF (("%level2%") < ("%level3%")) BEGIN
            SET "lore" = (3 * "%level3%")
          END ELSE BEGIN
            SET "lore" = (3 * "%level2%")
          END
          PATCH_IF ("%lore%" < "%level1%") BEGIN
            SET "lore" = "%level1%"
          END
        END ELSE
        PATCH_IF (("%class%" =  17)) BEGIN // fighter-mage-cleric
          PATCH_IF (("%level1%") < ("%level3%")) BEGIN
            SET "lore" = "%level3%"
          END ELSE BEGIN
            SET "lore" = "%level1%"
          END
          PATCH_IF ("%lore%" < (3 * "%level2%")) BEGIN
            SET "lore" = (3 * "%level2%")
          END
        END ELSE BEGIN // any other single, multi, or dual class non-mage, non-thief, non-bard
          PATCH_IF ("%level1%" > "%level2%") BEGIN
            SET "lore" = "%level1%"
          END ELSE BEGIN
            SET "lore" = "%level2%"
          END
          PATCH_IF ("%lore%" < "%level3%") BEGIN
            SET "lore" = "%level3%"
          END
        END

        SET "lore" = (40 + "%lore%") // occupational bonus--also, just enough for Ribald to ID everything in game

        // assign bonuses for high/low int and wis
        FOR (index2 = 0 ; index2 < 2 ; index2 = index2 + 1) BEGIN
          READ_BYTE (0x23a + "%index2%") "attr" // int, wis on the two loops
          PATCH_IF (("%attr%" > 9) AND ("%attr%" < 15)) BEGIN // kills sequence if of average value
          END ELSE
          PATCH_IF ("%attr%" < 7) BEGIN
            SET "lore" = "%lore%" - 20
          END ELSE
          PATCH_IF (("%attr%" > 6) AND ("%attr%" < 10)) BEGIN
            SET "lore" = "%lore%" - 10
          END ELSE
          PATCH_IF (("%attr%" > 14) AND ("%attr%" < 17)) BEGIN
            SET "lore" = "%lore%" + 5
          END ELSE
          PATCH_IF ("%attr%" = 17) BEGIN
            SET "lore" = "%lore%" + 7
          END ELSE
          PATCH_IF ("%attr%" = 18) BEGIN
            SET "lore" = "%lore%" + 10
          END ELSE
          PATCH_IF ("%attr%" = 19) BEGIN
            SET "lore" = "%lore%" + 12
          END ELSE
          PATCH_IF ("%attr%" = 20) BEGIN
            SET "lore" = "%lore%" + 15
          END ELSE
          PATCH_IF ("%attr%" = 21) BEGIN
            SET "lore" = "%lore%" + 20
          END ELSE
          PATCH_IF ("%attr%" = 22) BEGIN
            SET "lore" = "%lore%" + 25
          END ELSE
          PATCH_IF ("%attr%" = 23) BEGIN
            SET "lore" = "%lore%" + 30
          END ELSE
          PATCH_IF ("%attr%" = 24) BEGIN
            SET "lore" = "%lore%" + 35
          END ELSE
          PATCH_IF ("%attr%" = 25) BEGIN
            SET "lore" = "%lore%" + 40
          END
        END

        // enforce min-max limits of 0, 200
        PATCH_IF ("%lore%" < 0) BEGIN
          SET "lore" = 0
        END ELSE
        PATCH_IF ("%lore%" > 200) BEGIN
          SET "lore" = 200
        END
      END // end inner_patch_file

      INNER_ACTION BEGIN

        COPY_EXISTING ~%store%~ ~override~
          READ_LONG 0x3c "old_lore" ELSE 0
          PATCH_IF ("%old_lore%" > 0) BEGIN
            WRITE_LONG 0x3c "%lore%" // lore value
          END
          BUT_ONLY_IF_IT_CHANGES

      END
    END
  END
  BUT_ONLY_IF_IT_CHANGES

/////                                                  \\\\\
///// hybrid approach                                  \\\\\
/////                                                  \\\\\

BEGIN @219200 DESIGNATED 2192 // only mages n bards
GROUP @9
SUBCOMPONENT @219000 // limited storekeeper id ability

// this file does nothing, it just allows other mods to detect this component
COPY_EXISTING ~sw1h01.itm~ ~override/cdt02192.g3~

// builds cdstores.2da
INCLUDE ~BG2_Tweaks/lib/cdstores.tpa~

  // with cdstores.2da built, use it as a lookup table
COPY_EXISTING ~cdstores.2da~ ~override~
  COUNT_2DA_ROWS ~2~ "rows"
  FOR (index = 1 ; index < rows ; index = index + 1 ) BEGIN
    READ_2DA_ENTRY "%index%" 0 1 "store"
    READ_2DA_ENTRY "%index%" 1 1 "creature"
    SET "exists" = 0 // sanity check to make sure store and creature exist

    INNER_PATCH_FILE ~%store%~ BEGIN
      SET "exists" = "%exists%" + 1
    END

    PATCH_IF ("%exists%" = 1) BEGIN // don't bother if store doesn't exist

      INNER_PATCH_FILE ~%creature%~ BEGIN
        SET "exists" = "%exists%" + 1
        READ_BYTE 0x273 "class"  // corresponds to values in class.ids
        PATCH_IF (("%class%" =   1) OR     // mage
                  ("%class%" =   5) OR     // bard
                  ("%class%" =   7) OR     // fighter-mage
                  ("%class%" =  10) OR     // fighter-mage-thief
                  ("%class%" =  13) OR     // mage-thief
                  ("%class%" =  14) OR     // cleric-mage
                  ("%class%" =  17) OR     // fighter-mage-cleric
                  ("%class%" =  19) OR     // sorcerer
                  ("%class%" = 202) OR     // mage-all
                  ("%class%" = 206)) BEGIN // bard-all
          SET "lore" = 200
        END ELSE BEGIN
          READ_BYTE 0x234 "level1"  // level of primary class
          READ_BYTE 0x235 "level2" // Highest attained level in secondary class (0-100)
          READ_BYTE 0x236 "level3" // Highest attained level in tertiary class (0-100)
          PATCH_IF ("%class%" =  4) BEGIN // single-class thief
            SET "lore" = (3 * "%level1%")
          END ELSE
          PATCH_IF (("%class%" =  9) OR     // multi-dual fighter-thief
                    ("%class%" = 15)) BEGIN // multi-dual cleric-thief
            PATCH_IF ((3 * "%level2%") < ("%level1%")) BEGIN
              SET "lore" = "%level1%"
            END ELSE BEGIN
              SET "lore" = (3 * "%level2%")
            END
          END ELSE BEGIN // any other single, multi, or dual class non-mage, non-thief, non-bard
            PATCH_IF ("%level1%" > "%level2%") BEGIN
              SET "lore" = "%level1%"
            END ELSE BEGIN
              SET "lore" = "%level2%"
            END
            PATCH_IF ("%lore%" < "%level3%") BEGIN
              SET "lore" = "%level3%"
            END
          END

          SET "lore" = (40 + "%lore%") // occupational bonus--also, just enough for Ribald to ID everything in game

          // assign bonuses for high/low int and wis
          FOR (index2 = 0 ; index2 < 2 ; index2 = index2 + 1) BEGIN
            READ_BYTE (0x23a + "%index2%") "attr" // int, wis on the two loops
            PATCH_IF (("%attr%" > 9) AND ("%attr%" < 15)) BEGIN // kills sequence if of average value
            END ELSE
            PATCH_IF ("%attr%" < 7) BEGIN
              SET "lore" = "%lore%" - 20
            END ELSE
            PATCH_IF (("%attr%" > 6) AND ("%attr%" < 10)) BEGIN
              SET "lore" = "%lore%" - 10
            END ELSE
            PATCH_IF (("%attr%" > 14) AND ("%attr%" < 17)) BEGIN
              SET "lore" = "%lore%" + 5
            END ELSE
            PATCH_IF ("%attr%" = 17) BEGIN
              SET "lore" = "%lore%" + 7
            END ELSE
            PATCH_IF ("%attr%" = 18) BEGIN
              SET "lore" = "%lore%" + 10
            END ELSE
            PATCH_IF ("%attr%" = 19) BEGIN
              SET "lore" = "%lore%" + 12
            END ELSE
            PATCH_IF ("%attr%" = 20) BEGIN
              SET "lore" = "%lore%" + 15
            END ELSE
            PATCH_IF ("%attr%" = 21) BEGIN
              SET "lore" = "%lore%" + 20
            END ELSE
            PATCH_IF ("%attr%" = 22) BEGIN
              SET "lore" = "%lore%" + 25
            END ELSE
            PATCH_IF ("%attr%" = 23) BEGIN
              SET "lore" = "%lore%" + 30
            END ELSE
            PATCH_IF ("%attr%" = 24) BEGIN
              SET "lore" = "%lore%" + 35
            END ELSE
            PATCH_IF ("%attr%" > 24) BEGIN
              SET "lore" = "%lore%" + 40
            END
          END

          // enforce min-max limits of 0, 200
          PATCH_IF ("%lore%" < 0) BEGIN
            SET "lore" = 0
          END ELSE
          PATCH_IF ("%lore%" > 200) BEGIN
            SET "lore" = 200
          END
        END
      END // end inner_patch_file

      INNER_ACTION BEGIN

        COPY_EXISTING ~%store%~ ~override~
          READ_LONG 0x3c "old_lore" ELSE 0
          PATCH_IF ("%old_lore%" > 0) BEGIN
            WRITE_LONG 0x3c "%lore%" // lore value
          END
          BUT_ONLY_IF_IT_CHANGES

      END
    END
  END
  BUT_ONLY_IF_IT_CHANGES

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// Multi-Class Grand Mastery                        \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @220000 DESIGNATED 2200
GROUP @9
GROUP @19

// this file does nothing, it just allows other mods to detect this component
COPY_EXISTING ~sw1h01.itm~ ~override/cdt02200.g3~

PRINT @1
COPY_EXISTING ~weapprof.2da~  ~override~
  FOR (column = 12; column < 21; column = column + 1) BEGIN
    FOR (row = 11; row < 31; row = row + 1) BEGIN
      READ_2DA_ENTRY row column 1 stars
      PATCH_IF (stars > 1) BEGIN
        SET_2DA_ENTRY_LATER ~multigrand~ row column ~5~
      END
    END
  END
  SET_2DA_ENTRIES_NOW ~multigrand~ 1
  BUT_ONLY_IF_IT_CHANGES

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// True Grand Mastery                               \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @221000 DESIGNATED 2210
GROUP @9

// this file does nothing, it just allows other mods to detect this component
COPY_EXISTING ~sw1h01.itm~ ~override/cdt02210.g3~

COPY ~BG2_Tweaks/2da/wspatck.2da~  ~override~
     ~BG2_Tweaks/2da/wspecial.2da~ ~override~

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// Change Magically Created Weapons to Zero Weight  \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @222000 DESIGNATED 2220
GROUP @9
REQUIRE_PREDICATE NOT FILE_EXISTS_IN_GAME ~cdbehbla.pro~ @16

// this file does nothing, it just allows other mods to detect this component
COPY_EXISTING ~sw1h01.itm~ ~override/cdt02220.g3~

COPY_EXISTING ~bolt07.itm~   ~override~ // jan's flashers
              ~brdflute.itm~ ~override~ // bard flute
              ~enmace.itm~   ~override~ // enchanted weapon: mace
              ~enmorn.itm~   ~override~ // enchanted weapon: morningstar
              ~enstaff.itm~  ~override~ // enchanted weapon: staff
              ~ensw1h01.itm~ ~override~ // enchanted weapon: long sword
              ~ensw1h02.itm~ ~override~ // enchanted weapon: short sword
              ~ensw2h.itm~   ~override~ // enchanted weapon: battle axe
              ~melfmet.itm~  ~override~ // melf's minute meteors
              ~plymstar.itm~ ~override~ // ogre's morningstar from polymorph self
              ~plysala.itm~  ~override~ // salamander's spear from polymorph self
              ~shille.itm~   ~override~ // shillegagh
              ~sorb.itm~     ~override~ // sol's searing orb
              ~stardart.itm~ ~override~ // darts created by cloak of the stars
  PATCH_IF (SOURCE_SIZE > 0x71) THEN BEGIN // protects against invalid files
    WRITE_LONG 0x4C 0
  END
  BUT_ONLY_IF_IT_CHANGES

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                            \\\\\
///// Make +x/+y weapons consistent                              \\\\\
/////                                                            \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @223000 DESIGNATED 2230
GROUP @9
REQUIRE_PREDICATE NOT FILE_EXISTS_IN_GAME ~cdbehbla.pro~ @16
FORBID_COMPONENT ~item_rev/item_rev.tp2~ ~0~ @16

// this file does nothing, it just allows other mods to detect this component
COPY_EXISTING ~sw1h01.itm~ ~override/cdt02230.g3~

COPY_EXISTING ~hamm04.itm~ ~override~ // hammer +1/+4 vs giantkin
              ~sw1h31.itm~ ~override~ // daystar
  WRITE_LONG 0x60 4
  BUT_ONLY_IF_IT_CHANGES

COPY_EXISTING ~blun10.itm~ ~override~ // Root of the Problem +1/+3 vs unnatural
              ~blun23.itm~ ~override~ // Bone Club +2/+3 vs undead
              ~sw1h03.itm~ ~override~ // bastard sword +1/+3 vs shapeshifters
              ~sw1h54.itm~ ~override~ // Equalizer
  WRITE_LONG 0x60 3
  BUT_ONLY_IF_IT_CHANGES

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                            \\\\\
///// Description updates for consistent +x/+y weapons           \\\\\
/////                                                            \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @223100 DESIGNATED 2231
GROUP @9
REQUIRE_COMPONENT ~BG2_Tweaks/Setup-BG2_Tweaks.tp2~ ~2230~ @17
REQUIRE_PREDICATE (("%LANGUAGE%" STRING_COMPARE_CASE "chinese" = 0) OR 
                   ("%LANGUAGE%" STRING_COMPARE_CASE "english" = 0) OR 
                   ("%LANGUAGE%" STRING_COMPARE_CASE "german" = 0) OR 
                   ("%LANGUAGE%" STRING_COMPARE_CASE "italian" = 0) OR 
                   ("%LANGUAGE%" STRING_COMPARE_CASE "spanish" = 0)) @7 // chinese/english/german/italian/spanish only

// this file does nothing, it just allows other mods to detect this component
COPY_EXISTING ~sw1h01.itm~ ~override/cdt02231.g3~

ACTION_IF FILE_EXISTS_IN_GAME ~bola01.itm~ THEN BEGIN // if AoE installed

  COPY_EXISTING ~blun10.itm~ ~override~ // Root of the Problem +1/+3 vs unnatural
    SAY IDENTIFIED_DESC @99057
    BUT_ONLY_IF_IT_CHANGES

  COPY_EXISTING ~blun23.itm~ ~override~ // Bone Club +2/+3 vs undead
    SAY IDENTIFIED_DESC @99058
    BUT_ONLY_IF_IT_CHANGES

  COPY_EXISTING ~hamm04.itm~ ~override~ // Hammer +1/+4 vs Giantkin
    SAY IDENTIFIED_DESC @99059
    BUT_ONLY_IF_IT_CHANGES

  COPY_EXISTING ~sw1h03.itm~ ~override~ // bastard sword +1/+3 vs shapeshifters
    SAY IDENTIFIED_DESC @99060
    BUT_ONLY_IF_IT_CHANGES

  COPY_EXISTING ~SW1H31.itm~ ~override~ // Daystar
    SAY IDENTIFIED_DESC @99061
    BUT_ONLY_IF_IT_CHANGES

  COPY_EXISTING ~sw1h54.itm~ ~override~ // Equalizer
    SAY IDENTIFIED_DESC @99062
    BUT_ONLY_IF_IT_CHANGES

END ELSE BEGIN // if no AoE

  ACTION_IF FILE_EXISTS ~override/cdt02040.g3~ THEN BEGIN // if universal clubs

    COPY_EXISTING ~blun10.itm~ ~override~ // Root of the Problem +1/+3 vs unnatural
      SAY IDENTIFIED_DESC @99063
      BUT_ONLY_IF_IT_CHANGES

    COPY_EXISTING ~blun23.itm~ ~override~ // Bone Club +2/+3 vs undead
      SAY IDENTIFIED_DESC @99064
      BUT_ONLY_IF_IT_CHANGES

  END ELSE BEGIN // if

    COPY_EXISTING ~blun10.itm~ ~override~ // Root of the Problem +1/+3 vs unnatural
      SAY IDENTIFIED_DESC @99051
      BUT_ONLY_IF_IT_CHANGES

    COPY_EXISTING ~blun23.itm~ ~override~ // Bone Club +2/+3 vs undead
      SAY IDENTIFIED_DESC @99052
      BUT_ONLY_IF_IT_CHANGES

  END

  COPY_EXISTING ~hamm04.itm~ ~override~ // Hammer +1/+4 vs Giantkin
    SAY IDENTIFIED_DESC @99053
    BUT_ONLY_IF_IT_CHANGES

  COPY_EXISTING ~sw1h03.itm~ ~override~ // bastard sword +1/+3 vs shapeshifters
    SAY IDENTIFIED_DESC @99054
    BUT_ONLY_IF_IT_CHANGES

  COPY_EXISTING ~sw1h31.itm~ ~override~ // Daystar
    SAY IDENTIFIED_DESC @99055
    BUT_ONLY_IF_IT_CHANGES

  COPY_EXISTING ~sw1h54.itm~ ~override~ // Equalizer
    SAY IDENTIFIED_DESC @99056
    BUT_ONLY_IF_IT_CHANGES

END

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                            \\\\\
///// Alter THAC0 Table                                          \\\\\
/////                                                            \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @224000 DESIGNATED 2240
GROUP @9
REQUIRE_PREDICATE NOT FILE_EXISTS_IN_GAME ~bpduearc.spl~ @16      // non-BP
REQUIRE_PREDICATE NOT FILE_EXISTS         ~data/rot-rule.bif~ @16 // non-RoT
REQUIRE_PREDICATE NOT FILE_EXISTS         ~data/tdd-rule.bif~ @16 // non-TDD

// this file does nothing, it just allows other mods to detect this component
COPY_EXISTING ~sw1h01.itm~ ~override/cdt02240.g3~

COPY ~BG2_Tweaks/2da/thac0.2da~ ~override~

/////                                                            \\\\\
///// capped at -10                                              \\\\\
/////                                                            \\\\\

BEGIN @224100 DESIGNATED 2241
DEPRECATED @18

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                            \\\\\
///// Alter sorcerer spell progression                           \\\\\
/////                                                            \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @225000 DESIGNATED 2250
GROUP @9
REQUIRE_PREDICATE NOT FILE_EXISTS_IN_GAME ~bpduearc.spl~ @16      // non-BP
REQUIRE_PREDICATE NOT FILE_EXISTS         ~data/rot-rule.bif~ @16 // non-RoT
REQUIRE_PREDICATE NOT FILE_EXISTS         ~data/tdd-rule.bif~ @16 // non-TDD

// this file does nothing, it just allows other mods to detect this component
COPY_EXISTING ~sw1h01.itm~ ~override/cdt02250.g3~

COPY ~BG2_Tweaks/2da/un_mxsplsrc.2da~ ~override/mxsplsrc.2da~

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                            \\\\\
///// Alter Mage Spell Progression Table                         \\\\\
/////                                                            \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

/////                                                            \\\\\
///// un-nerfed                                                  \\\\\
/////                                                            \\\\\

BEGIN @226001 DESIGNATED 2260
GROUP @9
SUBCOMPONENT @226000 // alter mage progression
REQUIRE_PREDICATE NOT FILE_EXISTS_IN_GAME ~bpduearc.spl~ @16      // non-BP
REQUIRE_PREDICATE NOT FILE_EXISTS         ~data/rot-rule.bif~ @16 // non-RoT
REQUIRE_PREDICATE NOT FILE_EXISTS         ~data/tdd-rule.bif~ @16 // non-TDD

// this file does nothing, it just allows other mods to detect this component
COPY_EXISTING ~sw1h01.itm~ ~override/cdt02260.g3~

COPY ~BG2_Tweaks/2da/un_mxsplwiz.2da~ ~override/mxsplwiz.2da~

ACTION_IF FILE_EXISTS_IN_GAME ~mel01.cre~ THEN BEGIN

  // add mage alchemy, scribe scrolls
  COPY_EXISTING ~lucm0.2da~ ~override~
                ~luma0.2da~ ~override~
    COUNT_2DA_ROWS ~9~ "rows"
    SET "patch" = 0
    FOR ( index = 1 ; index < rows ; index = index + 1 ) BEGIN
      READ_2DA_ENTRY "%index%" 1 9 "abil"
      PATCH_IF ("%abil%" STRING_COMPARE_CASE "*" = 0) BEGIN
        PATCH_IF ("%patch%" = 0) BEGIN
          SET_2DA_ENTRY "%index%" 1 9 ~GA_GBSCRBMG~
          SET_2DA_ENTRY "%index%" 4 9 ~1~
          SET_2DA_ENTRY "%index%" 5 9 ~99~
          SET_2DA_ENTRY "%index%" 6 9 ~99~
          SET "patch" = 1
        END ELSE BEGIN
          SET_2DA_ENTRY "%index%" 1 9 ~GA_GBALCHMG~
          SET_2DA_ENTRY "%index%" 4 9 ~1~
          SET_2DA_ENTRY "%index%" 5 9 ~99~
          SET_2DA_ENTRY "%index%" 6 9 ~99~
          SET "index" = "%rows%" // kills loop
        END
      END
    END
    BUT_ONLY_IF_IT_CHANGES

  COPY ~BG2_Tweaks/spl/gbalchmg.spl~ ~override~
    SAY 0x08 @218001
    SAY 0x50 @218004

  COPY ~BG2_Tweaks/spl/gbscrbmg.spl~ ~override~
    SAY 0x08 @218007
    SAY 0x50 @218010

  ACTION_IF NOT FILE_EXISTS_IN_GAME ~cdt02280.g3~ THEN BEGIN // if un-nerfed cleric progression hasn't already made this change...

    COPY_EXISTING ~spcl918.spl~ ~override~
      SAY        0x08 @218003
      SAY        0x50 @218006
      READ_LONG  0x64 "abil_off"
      READ_SHORT 0x68 "abil_num"
      READ_LONG  0x6a "fx_off"
      FOR (index = 0 ; index < abil_num ; index = index + 1) BEGIN
        READ_SHORT ("%abil_off%" + 0x1e + (0x28 * "%index%")) "abil_fx_num"
        READ_SHORT ("%abil_off%" + 0x20 + (0x28 * "%index%")) "abil_fx_idx"
        FOR (index2 = 0 ; index2 < abil_fx_num ; index2 = index2 + 1) BEGIN
          READ_SHORT ("%fx_off%" +        (0x30 * ("%abil_fx_idx%" + "%index2%"))) "opcode"
          PATCH_IF ("%opcode%" = 122) BEGIN // create item
            READ_ASCII ("%fx_off%" + 0x14 + (0x30 * ("%abil_fx_idx%" + "%index2%"))) "item"
            PATCH_IF ("%item%" STRING_COMPARE_CASE "potn52" = 0) BEGIN // potion of extra healing
              WRITE_ASCII ("%fx_off%" + 0x14 + (0x30 * ("%abil_fx_idx%" + "%index2%"))) ~potn10~ #8 // potion of invis
            END ELSE
            PATCH_IF ("%item%" STRING_COMPARE_CASE "potn36" = 0) BEGIN // potion of master thieving
              WRITE_ASCII ("%fx_off%" + 0x14 + (0x30 * ("%abil_fx_idx%" + "%index2%"))) ~potn37~ #8 // potion of mind focusing
            END
          END
        END
      END
      BUT_ONLY_IF_IT_CHANGES

    COPY_EXISTING ~spcl919.spl~ ~override~
      SAY 0x008 @218009
      SAY 0x050 @218012

  END

END

/////                                                            \\\\\
///// pnp                                                        \\\\\
/////                                                            \\\\\

BEGIN @226100 DESIGNATED 2261
GROUP @9
SUBCOMPONENT @226000 // alter mage progression

// this file does nothing, it just allows other mods to detect this component
COPY_EXISTING ~sw1h01.itm~ ~override/cdt02261.g3~

COPY ~BG2_Tweaks/2da/mxsplwiz.2da~ ~override~

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                            \\\\\
///// Alter bard Spell Progression Table                         \\\\\
/////                                                            \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

/////                                                            \\\\\
///// un-nerfed                                                  \\\\\
/////                                                            \\\\\

BEGIN @226001 DESIGNATED 2270
GROUP @9
SUBCOMPONENT @227000 // alter bard progression
REQUIRE_PREDICATE NOT FILE_EXISTS_IN_GAME ~bpduearc.spl~ @16      // non-BP
REQUIRE_PREDICATE NOT FILE_EXISTS         ~data/rot-rule.bif~ @16 // non-RoT
REQUIRE_PREDICATE NOT FILE_EXISTS         ~data/tdd-rule.bif~ @16 // non-TDD

// this file does nothing, it just allows other mods to detect this component
COPY_EXISTING ~sw1h01.itm~ ~override/cdt02270.g3~

COPY ~BG2_Tweaks/2da/un_mxsplbrd.2da~ ~override/mxsplbrd.2da~

/////                                                            \\\\\
///// pnp                                                        \\\\\
/////                                                            \\\\\

BEGIN @226100 DESIGNATED 2271
GROUP @9
SUBCOMPONENT @227000 // alter bard progression

// this file does nothing, it just allows other mods to detect this component
COPY_EXISTING ~sw1h01.itm~ ~override/cdt02271.g3~

COPY ~BG2_Tweaks/2da/mxsplbrd.2da~ ~override~

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                            \\\\\
///// Alter cleric Spell Progression Table                       \\\\\
/////                                                            \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

/////                                                            \\\\\
///// un-nerfed                                                  \\\\\
/////                                                            \\\\\

BEGIN @226001 DESIGNATED 2280
GROUP @9
SUBCOMPONENT @228000 // alter cleric progression
REQUIRE_PREDICATE NOT FILE_EXISTS_IN_GAME ~bpduearc.spl~ @16      // non-BP
REQUIRE_PREDICATE NOT FILE_EXISTS         ~data/rot-rule.bif~ @16 // non-RoT
REQUIRE_PREDICATE NOT FILE_EXISTS         ~data/tdd-rule.bif~ @16 // non-TDD

// this file does nothing, it just allows other mods to detect this component
COPY_EXISTING ~sw1h01.itm~ ~override/cdt02280.g3~

COPY ~BG2_Tweaks/2da/un_mxsplprs.2da~ ~override/mxsplprs.2da~

ACTION_IF FILE_EXISTS_IN_GAME ~mel01.cre~ THEN BEGIN

  // add clerical alchemy, scribe scrolls
  COPY_EXISTING ~lucl0.2da~ ~override~
                ~lucm0.2da~ ~override~
    COUNT_2DA_ROWS ~9~ "rows"
    SET "patch" = 0
    FOR ( index = 1 ; index < rows ; index = index + 1 ) BEGIN
      READ_2DA_ENTRY "%index%" 1 9 "abil"
      PATCH_IF ("%abil%" STRING_COMPARE_CASE "*" = 0) BEGIN
        PATCH_IF ("%patch%" = 0) BEGIN
          SET_2DA_ENTRY "%index%" 1 9 ~GA_GBSCRBPR~
          SET_2DA_ENTRY "%index%" 4 9 ~1~
          SET_2DA_ENTRY "%index%" 5 9 ~99~
          SET_2DA_ENTRY "%index%" 6 9 ~99~
          SET "patch" = 1
        END ELSE BEGIN
          SET_2DA_ENTRY "%index%" 1 9 ~GA_GBALCHPR~
          SET_2DA_ENTRY "%index%" 4 9 ~1~
          SET_2DA_ENTRY "%index%" 5 9 ~99~
          SET_2DA_ENTRY "%index%" 6 9 ~99~
          SET "index" = "%rows%" // kills loop
        END
      END
    END
    BUT_ONLY_IF_IT_CHANGES

  COPY ~BG2_Tweaks/spl/gbalchpr.spl~ ~override~
    SAY 0x08 @218002
    SAY 0x50 @218005

  COPY ~BG2_Tweaks/spl/gbscrbpr.spl~ ~override~
    SAY 0x08 @218008
    SAY 0x50 @218011

  ACTION_IF NOT FILE_EXISTS_IN_GAME ~cdt02260.g3~ THEN BEGIN // if un-nerfed mage progression hasn't already made this change...

    COPY_EXISTING ~spcl918.spl~ ~override~
      SAY        0x08 @218003
      SAY        0x50 @218006
      READ_LONG  0x64 "abil_off"
      READ_SHORT 0x68 "abil_num"
      READ_LONG  0x6a "fx_off"
      FOR (index = 0 ; index < abil_num ; index = index + 1) BEGIN
        READ_SHORT ("%abil_off%" + 0x1e + (0x28 * "%index%")) "abil_fx_num"
        READ_SHORT ("%abil_off%" + 0x20 + (0x28 * "%index%")) "abil_fx_idx"
        FOR (index2 = 0 ; index2 < abil_fx_num ; index2 = index2 + 1) BEGIN
          READ_SHORT ("%fx_off%" +        (0x30 * ("%abil_fx_idx%" + "%index2%"))) "opcode"
          PATCH_IF ("%opcode%" = 122) BEGIN // create item
            READ_ASCII ("%fx_off%" + 0x14 + (0x30 * ("%abil_fx_idx%" + "%index2%"))) "item"
            PATCH_IF ("%item%" STRING_COMPARE_CASE "potn52" = 0) BEGIN // potion of extra healing
              WRITE_ASCII ("%fx_off%" + 0x14 + (0x30 * ("%abil_fx_idx%" + "%index2%"))) ~potn10~ #8 // potion of invis
            END ELSE
            PATCH_IF ("%item%" STRING_COMPARE_CASE "potn36" = 0) BEGIN // potion of master thieving
              WRITE_ASCII ("%fx_off%" + 0x14 + (0x30 * ("%abil_fx_idx%" + "%index2%"))) ~potn37~ #8 // potion of mind focusing
            END
          END
        END
      END
      BUT_ONLY_IF_IT_CHANGES

    COPY_EXISTING ~spcl919.spl~ ~override~
      SAY 0x008 @218009
      SAY 0x050 @218012

  END

END

/////                                                            \\\\\
///// pnp                                                        \\\\\
/////                                                            \\\\\

BEGIN @226100 DESIGNATED 2281
GROUP @9
SUBCOMPONENT @228000 // alter cleric progression

// this file does nothing, it just allows other mods to detect this component
COPY_EXISTING ~sw1h01.itm~ ~override/cdt02281.g3~

COPY ~BG2_Tweaks/2da/mxsplprs.2da~ ~override~

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                            \\\\\
///// Alter druid Spell/level Progression Table                  \\\\\
/////                                                            \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

/////                                                            \\\\\
///// druid level, un-nerfed druid spells                        \\\\\
/////                                                            \\\\\

BEGIN @229001 DESIGNATED 2290
GROUP @9
SUBCOMPONENT @229000 // alter druid progression
REQUIRE_PREDICATE NOT FILE_EXISTS_IN_GAME ~bpduearc.spl~ @16      // non-BP
REQUIRE_PREDICATE NOT FILE_EXISTS         ~data/rot-rule.bif~ @16 // non-RoT
REQUIRE_PREDICATE NOT FILE_EXISTS         ~data/tdd-rule.bif~ @16 // non-TDD

// this file does nothing, it just allows other mods to detect this component
COPY_EXISTING ~sw1h01.itm~ ~override/cdt02290.g3~

COPY ~BG2_Tweaks/2da/un_mxspldru.2da~ ~override/mxspldru.2da~

/////                                                            \\\\\
///// druid level, pnp cleric/druid spells                       \\\\\
/////                                                            \\\\\

BEGIN @229100 DESIGNATED 2291
GROUP @9
SUBCOMPONENT @229000 // alter druid progression

// this file does nothing, it just allows other mods to detect this component
COPY_EXISTING ~sw1h01.itm~ ~override/cdt02291.g3~

COPY ~BG2_Tweaks/2da/mxsplprs.2da~ ~override/mxspldru.2da~

/////                                                            \\\\\
///// cleric level, normal druid spells                          \\\\\
/////                                                            \\\\\

BEGIN @229200 DESIGNATED 2292
GROUP @9
SUBCOMPONENT @229000 // alter druid progression


// this file does nothing, it just allows other mods to detect this component
COPY_EXISTING ~sw1h01.itm~ ~override/cdt02292.g3~

// use cleric level progression
INCLUDE ~BG2_Tweaks/lib/druid_2_cleric_levels.tpa~

/////                                                            \\\\\
///// cleric level, un-nerfed druid spells                       \\\\\
/////                                                            \\\\\

BEGIN @229300 DESIGNATED 2293
GROUP @9
SUBCOMPONENT @229000 // alter druid progression


// this file does nothing, it just allows other mods to detect this component
COPY_EXISTING ~sw1h01.itm~ ~override/cdt02293.g3~

COPY ~BG2_Tweaks/2da/un_mxspldru.2da~ ~override/mxspldru.2da~

// use cleric level progression
INCLUDE ~BG2_Tweaks/lib/druid_2_cleric_levels.tpa~

/////                                                            \\\\\
///// cleric level, pnp cleric/druid spells                      \\\\\
/////                                                            \\\\\

BEGIN @229400 DESIGNATED 2294
GROUP @9
SUBCOMPONENT @229000 // alter druid progression


// this file does nothing, it just allows other mods to detect this component
COPY_EXISTING ~sw1h01.itm~ ~override/cdt02294.g3~

COPY ~BG2_Tweaks/2da/mxsplprs.2da~ ~override/mxspldru.2da~

// use cleric level progression
INCLUDE ~BG2_Tweaks/lib/druid_2_cleric_levels.tpa~

/////                                                            \\\\\
///// cleric level, normal cleic spells                          \\\\\
/////                                                            \\\\\

BEGIN @229500 DESIGNATED 2295
GROUP @9
SUBCOMPONENT @229000 // alter druid progression


// this file does nothing, it just allows other mods to detect this component
COPY_EXISTING ~sw1h01.itm~ ~override/cdt02295.g3~

COPY_EXISTING ~mxsplprs.2da~ ~override/mxspldru.2da~

// use cleric level progression
INCLUDE ~BG2_Tweaks/lib/druid_2_cleric_levels.tpa~

/////                                                            \\\\\
///// cleric level, un-nerfed cleric spells                      \\\\\
/////                                                            \\\\\

BEGIN @229600 DESIGNATED 2296
GROUP @9
SUBCOMPONENT @229000 // alter druid progression


// this file does nothing, it just allows other mods to detect this component
COPY_EXISTING ~sw1h01.itm~ ~override/cdt02296.g3~

COPY ~BG2_Tweaks/2da/un_mxsplprs.2da~ ~override/mxspldru.2da~

// use cleric level progression
INCLUDE ~BG2_Tweaks/lib/druid_2_cleric_levels.tpa~

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// triple-class HLA tables                           \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @230000 DESIGNATED 2300
GROUP @9
GROUP @19
REQUIRE_PREDICATE     FILE_EXISTS_IN_GAME ~cdt02090.g3~ @21 // xp cap must be removed
REQUIRE_PREDICATE     FILE_EXISTS_IN_GAME ~mel01.cre~   @12 // ToB required
REQUIRE_PREDICATE NOT FILE_EXISTS_IN_GAME ~_sw1h01.itm~ @10 // non-Tutu

// this file does nothing, it just allows other mods to detect this component
COPY_EXISTING ~sw1h01.itm~ ~override/cdt02300.g3~

// gives f/m/c and f/m/t unique HLAs; tables created below
COPY_EXISTING ~LUABBR.2DA~ ~override~
  SET_2DA_ENTRY 14 1 1 ~FMT~
  SET_2DA_ENTRY 19 1 1 ~FMC~
BUT_ONLY_IF_IT_CHANGES

ACTION_IF (NOT FILE_EXISTS_IN_GAME ~lufmc.2da~) BEGIN
  COPY ~BG2_Tweaks/2da/lufmc.2da~ ~override~
END
ELSE BEGIN
COPY_EXISTING ~lufmc.2da~ ~override~
  READ_2DA_ENTRIES_NOW ~r2en_lufmc~ 10
  SET planetar = 0
  SET dark_planetar = 0
  SET improved_alacrity = 0
  SET dragons_breath = 0
  SET comet = 0
  FOR (i = 0; i < r2en_lufmc; i += 1) BEGIN // for each row
    READ_2DA_ENTRY_FORMER ~r2en_lufmc~ i 1 old_ability
    SPRINT new_ability ~~
    PATCH_IF (~%old_ability%~ STRING_EQUAL_CASE ~GA_SPCL902~) BEGIN // deathblow
      PATCH_IF (planetar == 0) BEGIN
        SPRINT new_ability ~GA_SPWI923~ // summon planetar
        SET planetar = 1
      END
    END
    ELSE PATCH_IF (~%old_ability%~ STRING_EQUAL_CASE ~GA_SPCL903~) BEGIN // greater deathblow
      PATCH_IF (dark_planetar == 0) BEGIN
        SPRINT new_ability ~GA_SPWI924~ // summon dark planetar
        SET dark_planetar = 1
      END
    END
    ELSE PATCH_IF (~%old_ability%~ STRING_EQUAL_CASE ~GA_SPCL908~) BEGIN // war cry
      PATCH_IF (improved_alacrity == 0) BEGIN
        SPRINT new_ability ~GA_SPWI921~ // improved alacrity
        SET improved_alacrity = 1
      END
    END
    ELSE PATCH_IF (~%old_ability%~ STRING_EQUAL_CASE ~*~) BEGIN // unused
      PATCH_IF (dragons_breath == 0) BEGIN
        SPRINT new_ability ~GA_SPWI922~ // dragon's breath
        SET dragons_breath = 1
      END
      ELSE PATCH_IF (comet == 0) BEGIN
        SPRINT new_ability ~GA_SPWI925~ // comet
        SET comet = 1
      END
    END
    PATCH_IF (STRING_LENGTH ~%new_ability%~ > 0) BEGIN // ability to be replaced here
      SET_2DA_ENTRY_LATER ~s2el_lufmc~ i 1 ~%new_ability%~ // ability
      SET_2DA_ENTRY_LATER ~s2el_lufmc~ i 2 ~*~ // icon
      SET_2DA_ENTRY_LATER ~s2el_lufmc~ i 3 ~*~ // strref
      SET_2DA_ENTRY_LATER ~s2el_lufmc~ i 4 ~32~ // min_level
      SET_2DA_ENTRY_LATER ~s2el_lufmc~ i 5 ~99~ // max_level
      SET_2DA_ENTRY_LATER ~s2el_lufmc~ i 6 ~1~ // num_allowed
      SET_2DA_ENTRY_LATER ~s2el_lufmc~ i 7 ~*~ // prerequisite
      PATCH_IF (~%new_ability%~ STRING_EQUAL_CASE ~GA_SPWI923~) BEGIN // summon planetar restrictions
        SET_2DA_ENTRY_LATER ~s2el_lufmc~ i 8 ~GA_SPWI924~ // excluded_by
        SET_2DA_ENTRY_LATER ~s2el_lufmc~ i 9 ~ALL_EVIL~ // alignment_restrict
      END
      ELSE PATCH_IF (~%new_ability%~ STRING_EQUAL_CASE ~GA_SPWI924~) BEGIN // summon dark planetar restrictions
        SET_2DA_ENTRY_LATER ~s2el_lufmc~ i 8 ~GA_SPWI923~ // excluded_by
        SET_2DA_ENTRY_LATER ~s2el_lufmc~ i 9 ~ALL_GOOD~ // alignment_restrict
      END
      ELSE BEGIN // restrictions for others
        SET_2DA_ENTRY_LATER ~s2el_lufmc~ i 8 ~*~ // excluded_by
        SET_2DA_ENTRY_LATER ~s2el_lufmc~ i 9 ~*~ // alignment_restrict
      END
    END
  END
  SET_2DA_ENTRIES_NOW ~s2el_lufmc~ 10 // actually write changes!
  PRETTY_PRINT_2DA
END

ACTION_IF (NOT FILE_EXISTS_IN_GAME ~lufmt.2da~) BEGIN
  COPY ~BG2_Tweaks/2da/lufmt.2da~ ~override~
END
ELSE BEGIN
  COPY_EXISTING ~lufmt.2da~ ~override~
    READ_2DA_ENTRIES_NOW ~r2en_lufmt~ 10
    SET planetar = 0
    SET dark_planetar = 0
    SET improved_alacrity = 0
    SET dragons_breath = 0
    SET energy_blades = 0
    SET comet = 0
    FOR (i = 0; i < r2en_lufmt; i += 1) BEGIN // for each row
      READ_2DA_ENTRY_FORMER ~r2en_lufmt~ i 1 old_ability
      SPRINT new_ability ~~
      PATCH_IF (~%old_ability%~ STRING_EQUAL_CASE ~GA_SPCL902~) BEGIN // deathblow
        PATCH_IF (planetar == 0) BEGIN
          SPRINT new_ability ~GA_SPWI923~ // summon planetar
          SET planetar = 1
        END
      END
      ELSE PATCH_IF (~%old_ability%~ STRING_EQUAL_CASE ~GA_SPCL903~) BEGIN // greater deathblow
        PATCH_IF (dark_planetar == 0) BEGIN
          SPRINT new_ability ~GA_SPWI924~ // summon dark planetar
          SET dark_planetar = 1
        END
      END
      ELSE PATCH_IF (~%old_ability%~ STRING_EQUAL_CASE ~GA_SPCL908~) BEGIN // war cry
        PATCH_IF (improved_alacrity == 0) BEGIN
          SPRINT new_ability ~GA_SPWI921~ // improved alacrity
          SET improved_alacrity = 1
        END
      END
      ELSE PATCH_IF (~%old_ability%~ STRING_EQUAL_CASE ~GA_SPCL918~) BEGIN // alchemy
        PATCH_IF (energy_blades == 0) BEGIN
          SPRINT new_ability ~GA_SPWI920~ // energy blades
          SET energy_blades = 1
        END
      END
      ELSE PATCH_IF (~%old_ability%~ STRING_EQUAL_CASE ~GA_SPCL919~) BEGIN // scribe scrolls
        PATCH_IF (comet == 0) BEGIN
          SPRINT new_ability ~GA_SPWI925~ // comet
          SET comet = 1
        END
      END
      ELSE PATCH_IF (~%old_ability%~ STRING_EQUAL_CASE ~*~) BEGIN // unused
        PATCH_IF (dragons_breath == 0) BEGIN
          SPRINT new_ability ~GA_SPWI922~ // dragon's breath
          SET dragons_breath = 1
        END
      END
      PATCH_IF (STRING_LENGTH ~%new_ability%~ > 0) BEGIN // ability to be replaced here
        SET_2DA_ENTRY_LATER ~s2el_lufmt~ i 1 ~%new_ability%~ // ability
        SET_2DA_ENTRY_LATER ~s2el_lufmt~ i 2 ~*~ // icon
        SET_2DA_ENTRY_LATER ~s2el_lufmt~ i 3 ~*~ // strref
        SET_2DA_ENTRY_LATER ~s2el_lufmt~ i 4 ~32~ // min_level
        SET_2DA_ENTRY_LATER ~s2el_lufmt~ i 5 ~99~ // max_level
        SET_2DA_ENTRY_LATER ~s2el_lufmt~ i 6 ~1~ // num_allowed
        SET_2DA_ENTRY_LATER ~s2el_lufmt~ i 7 ~*~ // prerequisite
        PATCH_IF (~%new_ability%~ STRING_EQUAL_CASE ~GA_SPWI923~) BEGIN // summon planetar restrictions
          SET_2DA_ENTRY_LATER ~s2el_lufmt~ i 8 ~GA_SPWI924~ // excluded_by
          SET_2DA_ENTRY_LATER ~s2el_lufmt~ i 9 ~ALL_EVIL~ // alignment_restrict
        END
        ELSE PATCH_IF (~%new_ability%~ STRING_EQUAL_CASE ~GA_SPWI924~) BEGIN // summon dark planetar restrictions
          SET_2DA_ENTRY_LATER ~s2el_lufmt~ i 8 ~GA_SPWI923~ // excluded_by
          SET_2DA_ENTRY_LATER ~s2el_lufmt~ i 9 ~ALL_GOOD~ // alignment_restrict
        END
        ELSE BEGIN // restrictions for others
          SET_2DA_ENTRY_LATER ~s2el_lufmt~ i 8 ~*~ // excluded_by
          SET_2DA_ENTRY_LATER ~s2el_lufmt~ i 9 ~*~ // alignment_restrict
        END
      END
    END
    SET_2DA_ENTRIES_NOW ~s2el_lufmt~ 10 // actually write changes!
    PRETTY_PRINT_2DA
END

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                            \\\\\
///// save penalties for powerful spellcasters                   \\\\\
/////                                                            \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

/////                                                            \\\\\
///// arcane only                                                \\\\\
/////                                                            \\\\\

BEGIN @231001 DESIGNATED 2310
GROUP @9
SUBCOMPONENT @231000 // arcane save penalties

// this file does nothing, it just allows other mods to detect this component
COPY_EXISTING ~sw1h01.itm~ ~override/cdt02310.g3~

INCLUDE ~BG2_Tweaks/lib/saves_macro.tpa~

COPY_EXISTING_REGEXP GLOB ~^.+\.spl$~ ~override~
  PATCH_IF (SOURCE_SIZE > 0x71) BEGIN
    READ_SHORT 0x1c "spell_type" ELSE 0
    PATCH_IF ("%spell_type%" = 1) BEGIN // arcane only
      LAUNCH_PATCH_MACRO ~save_via_level~ // contains rest of patch
    END
  END
  BUT_ONLY_IF_IT_CHANGES

/////                                                            \\\\\
///// divine only                                                \\\\\
/////                                                            \\\\\

BEGIN @231100 DESIGNATED 2311
GROUP @9
SUBCOMPONENT @231000 // divine

// this file does nothing, it just allows other mods to detect this component
COPY_EXISTING ~sw1h01.itm~ ~override/cdt02311.g3~

INCLUDE ~BG2_Tweaks/lib/saves_macro.tpa~

COPY_EXISTING_REGEXP GLOB ~^.+\.spl$~ ~override~
//COPY_EXISTING ~spin692.spl~ ~override~
  PATCH_IF (SOURCE_SIZE > 0x71) BEGIN
    READ_SHORT 0x1c "spell_type" ELSE 0
    PATCH_IF ("%spell_type%" = 2) BEGIN // divine only
      LAUNCH_PATCH_MACRO ~save_via_level~ // contains rest of patch
    END
  END
  BUT_ONLY_IF_IT_CHANGES

/////                                                            \\\\\
///// arcane & divine                                            \\\\\
/////                                                            \\\\\

BEGIN @231200 DESIGNATED 2312
GROUP @9
SUBCOMPONENT @231000 // both

// this file does nothing, it just allows other mods to detect this component
COPY_EXISTING ~sw1h01.itm~ ~override/cdt02312.g3~

INCLUDE ~BG2_Tweaks/lib/saves_macro.tpa~

// first fix spin692 so it won't muck up the search
COPY_EXISTING ~spin692.spl~ ~override~
  PATCH_IF (SOURCE_SIZE > 0x71) BEGIN
    READ_LONG 0x64 "abil_off"
    WRITE_SHORT ("%abil_off%" + 0x10) 1 // set first ability header to have minimum level of 1
  END
  BUT_ONLY_IF_IT_CHANGES

COPY_EXISTING_REGEXP GLOB ~^.+\.spl$~ ~override~
  PATCH_IF (SOURCE_SIZE > 0x71) BEGIN
    READ_SHORT 0x1c "spell_type" ELSE 0
    PATCH_IF (("%spell_type%" = 1) OR ("%spell_type%" = 2)) BEGIN // arcane & divine only
      LAUNCH_PATCH_MACRO ~save_via_level~ // contains rest of patch
    END
  END
  BUT_ONLY_IF_IT_CHANGES

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                            \\\\\
///// trap cap removal                                           \\\\\
/////                                                            \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @232000 DESIGNATED 2320
GROUP @9
GROUP @19

// this file does nothing, it just allows other mods to detect this component
COPY_EXISTING ~sw1h01.itm~ ~override/cdt02320.g3~

COPY_EXISTING ~trapsnar.pro~ ~override/ag#snar.pro~
ADD_PROJECTILE ~override/ag#snar.pro~

COPY_EXISTING ~spcl411.spl~ ~override~
              ~spcl415.spl~ ~override~
  READ_LONG  0x64 ab_off
  READ_SHORT 0x68 ab_num
  FOR(i=0; i<ab_num; i+=1) BEGIN
    WRITE_SHORT (ab_off+i*0x28+0x26) ag#snar
  END
  BUT_ONLY_IF_IT_CHANGES

ACTION_IF FILE_EXISTS_IN_GAME ~mel01.cre~ THEN BEGIN // if ToB

  COPY_EXISTING ~trapboom.pro~ ~override/ag#boom.pro~
  COPY_EXISTING ~trapspik.pro~ ~override/ag#spik.pro~
  COPY_EXISTING ~traptime.pro~ ~override/ag#time.pro~

  ADD_PROJECTILE ~override/ag#boom.pro~
  ADD_PROJECTILE ~override/ag#spik.pro~
  ADD_PROJECTILE ~override/ag#time.pro~

  COPY_EXISTING ~spcl910b.spl~ ~override~
    READ_LONG  0x64 ab_off
    READ_SHORT 0x68 ab_num
    FOR(i=0; i<ab_num; i+=1) BEGIN
      WRITE_SHORT (ab_off+i*0x28+0x26) ag#spik
    END

  COPY_EXISTING ~spcl911b.spl~ ~override~
    READ_LONG  0x64 ab_off
    READ_SHORT 0x68 ab_num
    FOR(i=0; i<ab_num; i+=1) BEGIN
      WRITE_SHORT (ab_off+i*0x28+0x26) ag#boom
    END
    BUT_ONLY_IF_IT_CHANGES

  COPY_EXISTING ~spcl912b.spl~ ~override~
    READ_LONG  0x64 ab_off
    READ_SHORT 0x68 ab_num
    FOR(i=0; i<ab_num; i+=1) BEGIN
      WRITE_SHORT (ab_off+i*0x28+0x26) ag#time
    END
    BUT_ONLY_IF_IT_CHANGES

END

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                            \\\\\
///// remove delay on magical traps                              \\\\\
/////                                                            \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @233000 DESIGNATED 2330
GROUP @9

// this file does nothing, it just allows other mods to detect this component
COPY_EXISTING ~sw1h01.itm~ ~override/cdt02330.g3~

COPY_EXISTING ~dfirebl.pro~  ~override~
              ~iceglyp.pro~  ~override~
              ~trapglyp.pro~ ~override~
              ~trapskul.pro~ ~override~
  WRITE_SHORT 0x210 1
  BUT_ONLY_IF_IT_CHANGES

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                            \\\\\
///// celestial cap removal                                      \\\\\
/////                                                            \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @234000 DESIGNATED 2340
REQUIRE_PREDICATE     FILE_EXISTS_IN_GAME ~mel01.cre~   @12 // ToB required
REQUIRE_PREDICATE NOT FILE_EXISTS_IN_GAME ~_sw1h01.itm~ @10 // non-Tutu
GROUP @9

// this file does nothing, it just allows other mods to detect this component
COPY_EXISTING ~sw1h01.itm~ ~override/cdt02340.g3~

COPY_EXISTING ~devagood.cre~ ~override/ag#dgood.cre~
              ~devaevil.cre~ ~override/ag#devil.cre~
              ~plangood.cre~ ~override/ag#pgood.cre~
              ~planevil.cre~ ~override/ag#pevil.cre~

COPY_EXISTING ~spdeva.eff~ ~override~
  WRITE_ASCII 0x30 ag#dgood

COPY_EXISTING ~spdeva2.eff~ ~override~
  WRITE_ASCII 0x30 ag#devil

COPY_EXISTING ~spplan.eff~ ~override~
  WRITE_ASCII 0x30 ag#pgood

COPY_EXISTING ~spplan2.eff~ ~override~
  WRITE_ASCII 0x30 ag#pevil

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// Higher HP on Level Up                            \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

/////                                                  \\\\\
///// maximum                                          \\\\\
/////                                                  \\\\\

BEGIN @300001 DESIGNATED 3000 // maximum
GROUP @4
GROUP @19
SUBCOMPONENT @300000 // Higher HP on Level Up

// this file does nothing, it just allows other mods to detect this component
COPY_EXISTING ~sw1h01.itm~ ~override/cdt03000.g3~

COPY_EXISTING ~hpmonk.2da~ ~override~
              ~hpprs.2da~  ~override~
              ~hpwar.2da~  ~override~
  FOR (row = 3; row < 12; row = row + 1) BEGIN
    READ_2DA_ENTRY row 1 1 dice
    SET_2DA_ENTRY_LATER ~hpmonk~ row 2 0
    SET_2DA_ENTRY_LATER ~hpmonk~ row 3 dice
  END
  SET_2DA_ENTRIES_NOW ~hpmonk~ 1
  BUT_ONLY_IF_IT_CHANGES

COPY_EXISTING ~hprog.2da~ ~override~
              ~hpwiz.2da~ ~override~
  FOR (row = 3; row < 13; row = row + 1) BEGIN
    READ_2DA_ENTRY row 1 1 dice
    SET_2DA_ENTRY_LATER ~hprog~ row 2 0
    SET_2DA_ENTRY_LATER ~hprog~ row 3 dice
  END
  SET_2DA_ENTRIES_NOW ~hprog~ 1
  BUT_ONLY_IF_IT_CHANGES

ACTION_IF FILE_EXISTS_IN_GAME ~hpbarb.2da~ BEGIN                                   // if TobEx is installed and the Barbarian HP table is externalized
COPY_EXISTING ~hpbarb.2da~ ~override~                                              // Barbarian HP table
  FOR (row = 3; row < 12; row = row + 1) BEGIN
    READ_2DA_ENTRY row 1 1 dice
    SET_2DA_ENTRY_LATER ~hpbarb~ row 2 0
    SET_2DA_ENTRY_LATER ~hpbarb~ row 3 dice
  END
  SET_2DA_ENTRIES_NOW ~hpbarb~ 1
BUT_ONLY_IF_IT_CHANGES
END

/////                                                  \\\\\
///// nwn-style                                        \\\\\
/////                                                  \\\\\

BEGIN @300100 DESIGNATED 3001 // nwn-style
GROUP @4
SUBCOMPONENT @300000 // Higher HP on Level Up

// this file does nothing, it just allows other mods to detect this component
COPY_EXISTING ~sw1h01.itm~ ~override/cdt03001.g3~

COPY_EXISTING ~hpmonk.2da~ ~override~
              ~hpprs.2da~  ~override~
              ~hpwar.2da~  ~override~
  FOR (row = 3; row < 12; row = row + 1) BEGIN
    READ_2DA_ENTRY row 1 1 dice
    SET_2DA_ENTRY_LATER ~hpmonk~ row 1 2
    SET_2DA_ENTRY_LATER ~hpmonk~ row 2 (dice / 2)
    SET_2DA_ENTRY_LATER ~hpmonk~ row 3 0
  END
  SET_2DA_ENTRIES_NOW ~hpmonk~ 1
  BUT_ONLY_IF_IT_CHANGES

COPY_EXISTING ~hprog.2da~ ~override~
              ~hpwiz.2da~ ~override~
  FOR (row = 3; row < 13; row = row + 1) BEGIN
    READ_2DA_ENTRY row 1 1 dice
    SET_2DA_ENTRY_LATER ~hprog~ row 1 2
    SET_2DA_ENTRY_LATER ~hprog~ row 2 (dice / 2)
    SET_2DA_ENTRY_LATER ~hprog~ row 3 0
  END
  SET_2DA_ENTRIES_NOW ~hprog~ 1
  BUT_ONLY_IF_IT_CHANGES

ACTION_IF FILE_EXISTS_IN_GAME ~hpbarb.2da~ BEGIN                                   // if TobEx is installed and the Barbarian HP table is externalized
COPY_EXISTING ~hpbarb.2da~ ~override~                                              // Barbarian HP table
  FOR (row = 3; row < 12; row = row + 1) BEGIN
    READ_2DA_ENTRY row 1 1 dice
    SET_2DA_ENTRY_LATER ~hpbarb~ row 1 2
    SET_2DA_ENTRY_LATER ~hpbarb~ row 2 (dice / 2)
    SET_2DA_ENTRY_LATER ~hpbarb~ row 3 0
  END
  SET_2DA_ENTRIES_NOW ~hpbarb~ 1
BUT_ONLY_IF_IT_CHANGES
END

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// Maximum HP for NPCs                              \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

/////                                                  \\\\\
///// all npcs                                         \\\\\
/////                                                  \\\\\

BEGIN @301001 DESIGNATED 3010 // maximum hp for npcs
GROUP @4
SUBCOMPONENT @301000 // all npcs

// this file does nothing, it just allows other mods to detect this component
COPY_EXISTING ~sw1h01.itm~ ~override/cdt03010.g3~

// load patch macro
INCLUDE ~BG2_Tweaks/lib/max_hp_creatures.tpa~

PRINT @1
COPY_EXISTING_REGEXP GLOB ~^.+\.cre$~ ~override~
  PATCH_IF (SOURCE_SIZE > 0x2d3) BEGIN
    PATCH_IF (NOT ~%SOURCE_RES%~ STRING_EQUAL_CASE ~riftcr04~) BEGIN // skip creatures meant to have lower hp
      READ_SHORT 0x24 "currenthp" ELSE 0
      READ_SHORT 0x26 "maxhp"     ELSE 0
      PATCH_IF (("%maxhp%" > 0) AND ("%currenthp%" > 0)) BEGIN
        LAUNCH_PATCH_MACRO ~max_hp_creatures~ // contains rest of patch
      END
    END
  END
  BUT_ONLY_IF_IT_CHANGES

/////                                                  \\\\\
///// Maximum HP for Non-Party-Joinable NPCs           \\\\\
/////                                                  \\\\\

BEGIN @301100 DESIGNATED 3011 // maximum hp for npcs
GROUP @4
SUBCOMPONENT @301000 // all npcs

// this file does nothing, it just allows other mods to detect this component
COPY_EXISTING ~sw1h01.itm~ ~override/cdt03011.g3~

// load patch macro
INCLUDE ~BG2_Tweaks/lib/max_hp_creatures.tpa~

PRINT @1
COPY_EXISTING_REGEXP GLOB ~^.+\.cre$~ ~override~
  PATCH_IF (SOURCE_SIZE > 0x2d3) BEGIN
    PATCH_IF (NOT ~%SOURCE_RES%~ STRING_EQUAL_CASE ~riftcr04~) BEGIN // skip creatures meant to have lower hp
      READ_SHORT 0x24  "currenthp" ELSE 0
      READ_SHORT 0x26  "maxhp"     ELSE 0
      READ_LONG  0x1cc "biography" ELSE 0
      PATCH_IF (("%maxhp%" > 0) AND ("%currenthp%" > 0) AND (("%biography%" > 2147483646) OR ("%biography%" < 1))) BEGIN
        LAUNCH_PATCH_MACRO ~max_hp_creatures~ // contains rest of patch
      END
    END
  END
  BUT_ONLY_IF_IT_CHANGES

/////                                                  \\\\\
///// Maximum HP for Party-Joinable NPCs               \\\\\
/////                                                  \\\\\

BEGIN @301200 DESIGNATED 3012 // maximum hp for npcs
GROUP @4
SUBCOMPONENT @301000 // all npcs

// this file does nothing, it just allows other mods to detect this component
COPY_EXISTING ~sw1h01.itm~ ~override/cdt03012.g3~

// load patch macro
INCLUDE ~BG2_Tweaks/lib/max_hp_creatures.tpa~

PRINT @1
COPY_EXISTING_REGEXP GLOB ~^.+\.cre$~ ~override~
  PATCH_IF (SOURCE_SIZE > 0x2d3) BEGIN
    READ_SHORT 0x24  "currenthp" ELSE 0
    READ_SHORT 0x26  "maxhp"     ELSE 0
    READ_LONG  0x1cc "biography" ELSE 0
    PATCH_IF (("%maxhp%" > 0) AND ("%currenthp%" > 0) AND ("%biography%" < 2147483647) AND ("%biography%" > 0)) BEGIN
      LAUNCH_PATCH_MACRO ~max_hp_creatures~ // contains rest of patch
    END
  END
  BUT_ONLY_IF_IT_CHANGES

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// Identify All Items                               \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @302000 DESIGNATED 3020
GROUP @4

// this file does nothing, it just allows other mods to detect this component
COPY_EXISTING ~sw1h01.itm~ ~override/cdt03020.g3~

// SoS item needs to be un-ID'd for one quest - part 1
ACTION_IF FILE_EXISTS_IN_GAME ~cbltcnt2.itm~ THEN BEGIN

  COPY_EXISTING ~cbltcnt2.itm~ ~override~
    READ_SHORT 0x42 "sos_lore"
    BUT_ONLY_IF_IT_CHANGES

END

PRINT @1
COPY_EXISTING_REGEXP GLOB ~^.+\.itm$~ ~override~
  PATCH_IF (SOURCE_SIZE > 0x71) THEN BEGIN // protects against invalid files
    WRITE_SHORT 0x42 0
  END
  BUT_ONLY_IF_IT_CHANGES

// SoS item needs to be un-ID'd for one quest - part 2
ACTION_IF FILE_EXISTS_IN_GAME ~cbltcnt2.itm~ THEN BEGIN

  COPY_EXISTING ~cbltcnt2.itm~ ~override~
    WRITE_SHORT 0x42 "%sos_lore%"
    BUT_ONLY_IF_IT_CHANGES

END

// ensures are regexp search doesn't die
COPY_EXISTING ~ar0087.are~ ~override/xr2400.are~
              ~ar0087.are~ ~override/xr2600.are~

// add charges to items in game
COPY_EXISTING_REGEXP GLOB ~^.+\.cre$~ ~override~
                          ~^.+\.are$~ ~override~
  PATCH_IF ("%SOURCE_FILE%" STRING_COMPARE_REGEXP "^.+\.cre$" = 0) BEGIN // if creature
    READ_LONG 0x2bc "itm_off" ELSE 0
    READ_LONG 0x2c0 "itm_num" ELSE 0
  END ELSE BEGIN
    READ_LONG  0x78 "itm_off" ELSE 0
    READ_SHORT 0x76 "itm_num" ELSE 0
  END
  FOR (index = 0 ; index < itm_num ; index = index + 1) BEGIN
    READ_BYTE  ("%itm_off%" + 0x10 + (0x14 * "%index%")) "flag"
    WRITE_BYTE ("%itm_off%" + 0x10 + (0x14 * "%index%")) ("%flag%" BOR 0b00000001) // adds identified flag
  END
  BUT_ONLY_IF_IT_CHANGES

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// Easy Spell Learning                              \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

/////                                                  \\\\\
///// 100% learn spells                                \\\\\
/////                                                  \\\\\

BEGIN @303001 DESIGNATED 3030 // 100% learn spells
GROUP @4
SUBCOMPONENT @303000 // easy spell learning

// this file does nothing, it just allows other mods to detect this component
COPY_EXISTING ~sw1h01.itm~ ~override/cdt03030.g3~

PRINT @1
COPY_EXISTING ~intmod.2da~ ~override~
  COUNT_2DA_ROWS ~5~ "rows"
  FOR (index = 3; index < rows ; index = index + 1) BEGIN
    SET_2DA_ENTRY_LATER ~intmod~ index 1 ~150~
  END
  SET_2DA_ENTRIES_NOW ~intmod~ 1
  BUT_ONLY_IF_IT_CHANGES

/////                                                  \\\\\
///// 100% learn spells & no spell caps                \\\\\
/////                                                  \\\\\

BEGIN @303100 DESIGNATED 3031 // 100% learn spells, remove caps
GROUP @4
GROUP @19
SUBCOMPONENT @303000 // easy spell learning

// this file does nothing, it just allows other mods to detect this component
COPY_EXISTING ~sw1h01.itm~ ~override/cdt03031.g3~

PRINT @1
COPY_EXISTING ~intmod.2da~ ~override~
  COUNT_2DA_ROWS ~5~ "rows"
  FOR (index = 3; index < rows ; index = index + 1) BEGIN
    SET_2DA_ENTRY_LATER ~intmod~ index 1 ~150~
    SET_2DA_ENTRY_LATER ~intmod~ index 2 ~9~
    SET_2DA_ENTRY_LATER ~intmod~ index 3 ~99~
  END
  SET_2DA_ENTRIES_NOW ~intmod~ 1
  BUT_ONLY_IF_IT_CHANGES

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// Make Misc Bags of Holding Bottomless             \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @304000 DESIGNATED 3040
GROUP @4
GROUP @19
REQUIRE_PREDICATE ((NOT FILE_EXISTS_IN_GAME ~_sw1h01.itm~) OR (FILE_EXISTS_IN_GAME ~c!ammo.sto~))  @2 // requires bags of holding on Tutu

// this file does nothing, it just allows other mods to detect this component
COPY_EXISTING ~sw1h01.itm~ ~override/cdt03040.g3~

PRINT @1
COPY_EXISTING_REGEXP GLOB ~^.+\.sto$~ ~override~
  READ_LONG 0x08 "itemtype" ELSE 0
  PATCH_IF ("%itemtype%" = 5) BEGIN
    WRITE_SHORT 0x22 32767
  END
  BUT_ONLY_IF_IT_CHANGES

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// Remove fatigue from restoration spells           \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @305000 DESIGNATED 3050
GROUP @4
REQUIRE_PREDICATE NOT FILE_EXISTS_IN_GAME ~_sw1h01.itm~ @10 // non-Tutu

// this file does nothing, it just allows other mods to detect this component
COPY_EXISTING ~sw1h01.itm~ ~override/cdt03050.g3~

// remove fatigue from restoration spells
COPY_EXISTING ~sppr417.spl~  ~override~
              ~sppr713.spl~  ~override~
              ~spwish07.spl~ ~override~
              ~spwish46.spl~ ~override~
  READ_LONG  0x64 "abil_off" ELSE 0
  READ_SHORT 0x68 "abil_num" ELSE 0
  READ_LONG  0x6a "fx_off"   ELSE 0
  FOR (index = 0 ; index < abil_num ; index = index + 1) BEGIN
    READ_SHORT ("%abil_off%" + (0x28 * "%index%")) "type"
    PATCH_IF ("%type%" = 1) BEGIN // if melee
      READ_SHORT ("%abil_off%" + 0x1e + (0x28 * "%index%")) "abil_fx_num"
      READ_SHORT ("%abil_off%" + 0x20 + (0x28 * "%index%")) "abil_fx_idx"
      FOR (index2 = 0 ; index2 < abil_fx_num ; index2 = index2 + 1) BEGIN
        READ_SHORT ("%fx_off%" +        (0x30 * ("%abil_fx_idx%" + "%index2%"))) "opcode"
        PATCH_IF ("%opcode%" = 93) BEGIN // fatigue
          WRITE_BYTE ("%fx_off%" + 0x12 + (0x30 * ("%abil_fx_idx%" + "%index2%"))) 0 // probability 0
        END
      END
    END
  END
  BUT_ONLY_IF_IT_CHANGES

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// Remove "You Must Gather..."                      \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @306000 DESIGNATED 3060
GROUP @4

// this file does nothing, it just allows other mods to detect this component
COPY_EXISTING ~sw1h01.itm~ ~override/cdt03060.g3~

COPY  ~BG2_Tweaks/wav/error01.wav~ ~override~

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// Store-rep tweaks                                 \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

/////                                                  \\\\\
///// discounts for low rep                            \\\\\
/////                                                  \\\\\

BEGIN @307000 DESIGNATED 3070 // low-rep discounts
GROUP @4
SUBCOMPONENT @307001 // store-rep tweaks

// this file does nothing, it just allows other mods to detect this component
COPY_EXISTING ~sw1h01.itm~ ~override/cdt03070.g3~

COPY ~BG2_Tweaks/2da/repmodst.2da~ ~override~

/////                                                  \\\\\
///// no rep effect, 100%                              \\\\\
/////                                                  \\\\\

BEGIN @307100 DESIGNATED 3071 // low-rep discounts
GROUP @4
SUBCOMPONENT @307001 // store-rep tweaks

// this file does nothing, it just allows other mods to detect this component
COPY_EXISTING ~sw1h01.itm~ ~override/cdt03071.g3~

COPY ~BG2_Tweaks/2da/repmodst_100.2da~ ~override/repmodst.2da~

/////                                                  \\\\\
///// no rep effect, 80%                               \\\\\
/////                                                  \\\\\

BEGIN @307200 DESIGNATED 3072 // low-rep discounts
GROUP @4
SUBCOMPONENT @307001 // store-rep tweaks

// this file does nothing, it just allows other mods to detect this component
COPY_EXISTING ~sw1h01.itm~ ~override/cdt03072.g3~

COPY ~BG2_Tweaks/2da/repmodst_080.2da~ ~override/repmodst.2da~

/////                                                  \\\\\
///// no rep effect, 60%                               \\\\\
/////                                                  \\\\\

BEGIN @307300 DESIGNATED 3073 // low-rep discounts
GROUP @4
SUBCOMPONENT @307001 // store-rep tweaks

// this file does nothing, it just allows other mods to detect this component
COPY_EXISTING ~sw1h01.itm~ ~override/cdt03073.g3~

COPY ~BG2_Tweaks/2da/repmodst_060.2da~ ~override/repmodst.2da~

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// Unlimited Ammo Stacks                            \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @308000 DESIGNATED 3080
GROUP @4
GROUP @19

// this file does nothing, it just allows other mods to detect this component
COPY_EXISTING ~sw1h01.itm~ ~override/cdt03080.g3~

PRINT @1
COPY_EXISTING_REGEXP GLOB ~^.+\.itm$~ ~override~ // looking for ammo
  READ_SHORT  0x1C "type" ELSE 0
  READ_SHORT  0x38 "max"  ELSE 0
  PATCH_IF (
             ("%max%" > 1) AND // if item can already stack and is of the type...
             (
               ("%type%" =  5) OR // arrows,
               ("%type%" = 14) OR // bullets,
               ("%type%" = 31) OR // bolts,
               ("%type%" = 24) OR // darts,
               ("%type%" = 25) OR // axe, or
               ("%type%" = 16)    // dagger
             )
           ) BEGIN
    WRITE_SHORT 0x38 999
  END
  BUT_ONLY_IF_IT_CHANGES

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// Unlimited Gem and Jewelry Stacking               \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @309000 DESIGNATED 3090
GROUP @4
GROUP @19

// this file does nothing, it just allows other mods to detect this component
COPY_EXISTING ~sw1h01.itm~ ~override/cdt03090.g3~

PRINT @1
COPY_EXISTING_REGEXP GLOB ~^.+\.itm$~ ~override~ // looking for gems and jewelry
  PATCH_IF (SOURCE_SIZE > 0x71) THEN BEGIN // protects against invalid files
    READ_SHORT 0x1c "type"     ELSE 0
    READ_SHORT 0x68 "max_abil" ELSE 0
    READ_SHORT 0x70 "max_fx"   ELSE 0
    PATCH_IF (
               ("%max_abil%" = 0) AND // maximum abilities must be zero and
               ("%max_fx%" = 0) AND   // maximum effects must be zero and type must be one of
               (
                 ("%type%" =  1) OR // amulets
                 ("%type%" = 34) OR // gems
                 ("%type%" = 33) OR // gold
                 ("%type%" = 10)    // or rings
               )
             ) BEGIN
      WRITE_SHORT 0x38 999
    END
  END
  BUT_ONLY_IF_IT_CHANGES

// bandit scalps, too
COPY_EXISTING ~_misc86.itm~ ~override~
              ~misc86.itm~  ~override~
  PATCH_IF (SOURCE_SIZE > 0x71) THEN BEGIN // protects against invalid files
    WRITE_SHORT 0x38 9999
  END
  BUT_ONLY_IF_IT_CHANGES

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// Unlimited Potion Stacking                        \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @310000 DESIGNATED 3100
GROUP @4
GROUP @19

// this file does nothing, it just allows other mods to detect this component
COPY_EXISTING ~sw1h01.itm~ ~override/cdt03100.g3~

PRINT @1
COPY_EXISTING_REGEXP GLOB ~^.+\.itm$~ ~override~ // looking for potions
  READ_SHORT  0x1C "type" ELSE 0
  PATCH_IF ("%type%" = 9) BEGIN // potions
    WRITE_SHORT 0x38 999
  END
  BUT_ONLY_IF_IT_CHANGES

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// Unlimited Scroll Stacking                        \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @311000 DESIGNATED 3110
GROUP @4
GROUP @19

// this file does nothing, it just allows other mods to detect this component
COPY_EXISTING ~sw1h01.itm~ ~override/cdt03110.g3~

PRINT @1
COPY_EXISTING_REGEXP GLOB ~^.+\.itm$~ ~override~ // looking for scrolls
  READ_SHORT  0x1C "type" ELSE 0
  PATCH_IF ("%type%" = 11) BEGIN // scrolls
    WRITE_SHORT 0x38 999
  END
  BUT_ONLY_IF_IT_CHANGES

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// Happy patch                                      \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

/////                                                  \\\\\
///// never angry                                      \\\\\
/////                                                  \\\\\

BEGIN @312001 DESIGNATED 3120
GROUP @4
SUBCOMPONENT @312000

// this file does nothing, it just allows other mods to detect this component
COPY_EXISTING ~sw1h01.itm~ ~override/cdt03120.g3~

// stop rep complaints
COPY_EXISTING ~happy.2da~ ~override~
  FOR (row = 0 ; row < 8 ; row = row + 1) BEGIN
    SET_2DA_ENTRY %row% 1 4 ~0~
  END
  FOR (row = 0 ; row < 5 ; row = row + 1) BEGIN
    SET_2DA_ENTRY %row% 2 4 ~0~
  END
  FOR (row = 18 ; row < 20 ; row = row + 1) BEGIN
    SET_2DA_ENTRY %row% 2 4 ~0~
  END
  FOR (row = 12 ; row < 20 ; row = row + 1) BEGIN
    SET_2DA_ENTRY %row% 3 4 ~0~
  END
  BUT_ONLY_IF_IT_CHANGES

// remove NPC conflicts
//INCLUDE ~BG2_Tweaks/lib/happy.tpa~

/////                                                  \\\\\
///// angry but never leave                            \\\\\
/////                                                  \\\\\

BEGIN @312100 DESIGNATED 3121
GROUP @4
SUBCOMPONENT @312000

// this file does nothing, it just allows other mods to detect this component
COPY_EXISTING ~sw1h01.itm~ ~override/cdt03121.g3~

// stop rep complaints
COPY_EXISTING ~happy.2da~ ~override~
  SET_2DA_ENTRY  0 1 4 ~-160~
  SET_2DA_ENTRY  1 1 4 ~-160~
  SET_2DA_ENTRY  0 2 4 ~-160~
  SET_2DA_ENTRY 18 3 4 ~-160~
  SET_2DA_ENTRY 19 3 4 ~-160~
  BUT_ONLY_IF_IT_CHANGES

// remove NPC conflicts
//INCLUDE ~BG2_Tweaks/lib/happy.tpa~

/////                                                  \\\\\
///// always neutral                                   \\\\\
/////                                                  \\\\\

BEGIN @312200 DESIGNATED 3122
GROUP @4
SUBCOMPONENT @312000

// this file does nothing, it just allows other mods to detect this component
COPY_EXISTING ~sw1h01.itm~ ~override/cdt03122.g3~

// stop rep complaints
COPY_EXISTING ~happy.2da~ ~override~
  FOR (col = 1 ; col < 4 ; col = col + 1) BEGIN
    FOR (row = 0 ; row < 20 ; row = row + 1) BEGIN
      SET_2DA_ENTRY %row% %col% 4 "0"
    END
  END
  BUT_ONLY_IF_IT_CHANGES

// remove NPC conflicts
//INCLUDE ~BG2_Tweaks/lib/happy.tpa~

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// NPCs don't fight                                 \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @312300 DESIGNATED 3123
GROUP @4

// remove NPC conflicts
INCLUDE ~BG2_Tweaks/lib/happy.tpa~


/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// Stop Haer'Dalis-Aerie romance from starting      \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @312400 DESIGNATED 3124
GROUP @4

  COPY_EXISTING ~haerdali.bcs~ ~override~
    DECOMPILE_BCS_TO_BAF
      REPLACE_TEXTUALLY ~Global("RomanceConflict","GLOBAL",0)~ ~False()~
    COMPILE_BAF_TO_BCS
    BUT_ONLY_IF_IT_CHANGES
  EXTEND_TOP ~haerdali.bcs~ ~BG2_Tweaks/baf/haerdali.baf~ 


/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// Happy neutrals                                   \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @312500 DESIGNATED 3125 // happy neutrals
GROUP @4
GROUP @19

// this file does nothing, it just allows other mods to detect this component
COPY_EXISTING ~sw1h01.itm~ ~override/cdt03125.g3~

COPY_EXISTING ~happy.2da~ ~override~
  FOR (row = 6; row < 12; row = row + 1) BEGIN
    SET_2DA_ENTRY "%row%" 2 4 ~80~
  END
  BUT_ONLY_IF_IT_CHANGES

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// No traps or locks                                \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @313000 DESIGNATED 3130
GROUP @4

// this file does nothing, it just allows other mods to detect this component
COPY_EXISTING ~sw1h01.itm~ ~override/cdt03130.g3~

COPY_EXISTING ~ar0087.are~ ~override/xr2400.are~
              ~ar0087.are~ ~override/xr2600.are~

COPY_EXISTING_REGEXP GLOB ~^.+\.are$~ ~override~
  READ_SHORT 0x5a "trig_num"
  READ_LONG  0x5c "trig_off"
  READ_SHORT 0x74 "cont_num"
  READ_LONG  0x70 "cont_off"
  READ_SHORT 0xa4 "door_num"
  READ_LONG  0xa8 "door_off"
  FOR (index = 0 ; index < trig_num ; index = index + 1) BEGIN // cycle through triggers
    READ_SHORT ("%trig_off%" + 0x6a + (0xc4 * "%index%")) "trap_diff"
    PATCH_IF (("%trap_diff%" > 0) AND ("%trap_diff%" < 100)) BEGIN
      WRITE_SHORT ("%trig_off%" + 0x6c + (0xc4 * "%index%")) 0 // is not a trap
    END
  END
  FOR (index2 = 0 ; index2 < cont_num ; index2 = index2 + 1) BEGIN // cycle through containers
    WRITE_LONG  ("%cont_off%" + 0x26 + ("%index2%" * 0xc0)) 0 // lock difficulty
    WRITE_SHORT ("%cont_off%" + 0x30 + ("%index2%" * 0xc0)) 0 // is not a trap
  END
  FOR (index3 = 0 ; index3 < door_num ; index3 = index3 + 1) BEGIN // cycle through doors
    WRITE_SHORT ("%door_off%" + 0x70 + ("%index3%" * 0xc8)) 0 // is not a trap
    WRITE_LONG  ("%door_off%" + 0x88 + ("%index3%" * 0xc8)) 0 // detect diff
    WRITE_LONG  ("%door_off%" + 0x8c + ("%index3%" * 0xc8)) 0 // lock diff
  END
  BUT_ONLY_IF_IT_CHANGES

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// Faster chapter 1&2 cutscenes and dreams          \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

/////                                                  \\\\\
///// original EoU stuff                               \\\\\
/////                                                  \\\\\

BEGIN @314001 DESIGNATED 3140 // original EoU
GROUP @4
SUBCOMPONENT @314000 // faster chap 1&2 cutscenes
REQUIRE_PREDICATE NOT FILE_EXISTS_IN_GAME ~_sw1h01.itm~ @10 // non-Tutu
REQUIRE_PREDICATE !GAME_IS ~bgt~ @10                        // Forbid installation on BGT

// this file does nothing, it just allows other mods to detect this component
COPY_EXISTING ~sw1h01.itm~ ~override/cdt03140.g3~

COMPILE ~BG2_Tweaks/baf/newgame.baf~
        ~BG2_Tweaks/baf/cut01c.baf~
        ~BG2_Tweaks/baf/cut01d.baf~
        ~BG2_Tweaks/baf/cut01e.baf~
        ~BG2_Tweaks/baf/cut01f.baf~
        ~BG2_Tweaks/baf/cut01g.baf~

INCLUDE ~BG2_Tweaks/lib/faster_start.tpa~

/////                                                  \\\\\
///// non-silly version                                \\\\\
/////                                                  \\\\\

BEGIN @314100 DESIGNATED 3141 // original EoU
GROUP @4
SUBCOMPONENT @314000 // faster chap 1&2 cutscenes
REQUIRE_PREDICATE NOT FILE_EXISTS_IN_GAME ~_sw1h01.itm~ @10 // non-Tutu
REQUIRE_PREDICATE !GAME_IS ~bgt~ @10                        // Forbid installation on BGT

// this file does nothing, it just allows other mods to detect this component
COPY_EXISTING ~sw1h01.itm~ ~override/cdt03141.g3~

COPY ~BG2_Tweaks/baf/newgame_nonsilly.baf~ ~override/newgame.bcs~
     ~BG2_Tweaks/baf/cut01c_nonsilly.baf~  ~override/cut01c.bcs~
     ~BG2_Tweaks/baf/cut01d_nonsilly.baf~  ~override/cut01d.bcs~
     ~BG2_Tweaks/baf/cut01e_nonsilly.baf~  ~override/cut01e.bcs~
     ~BG2_Tweaks/baf/cut01f_nonsilly.baf~  ~override/cut01f.bcs~
     ~BG2_Tweaks/baf/cut01g_nonsilly.baf~  ~override/cut01g.bcs~
  COMPILE_BAF_TO_BCS

INCLUDE ~BG2_Tweaks/lib/faster_start.tpa~

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// Remove Cloak-of-Mirroring & Spell-Trap Animation \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @315000 DESIGNATED 3150
GROUP @4
REQUIRE_PREDICATE NOT FILE_EXISTS_IN_GAME ~_sw1h01.itm~ @10 // non-Tutu

// this file does nothing, it just allows other mods to detect this component
COPY_EXISTING ~sw1h01.itm~ ~override/cdt03150.g3~

COPY ~BG2_Tweaks/bam/spmagglo.bam~ ~override~
     ~BG2_Tweaks/bam/spmagglo.bam~ ~override/spsturni.bam~
     ~BG2_Tweaks/bam/spmagglo.bam~ ~override/spturni2.bam~

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// Keep Drizzt's Loot, Disable Malchor Harpell      \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @316000 DESIGNATED 3160
GROUP @4
REQUIRE_PREDICATE NOT FILE_EXISTS_IN_GAME ~_sw1h01.itm~ @10 // non-Tutu

// this file does nothing, it just allows other mods to detect this component
COPY_EXISTING ~sw1h01.itm~ ~override/cdt03160.g3~

COPY_EXISTING ~baldur.bcs~ ~override~
  DECOMPILE_BCS_TO_BAF
    REPLACE_TEXTUALLY ~!Exists("c6harp")~ ~False()~
  COMPILE_BAF_TO_BCS
  BUT_ONLY_IF_IT_CHANGES

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// No Drow Avatars On Party In Underdark            \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @317000 DESIGNATED 3170
GROUP @4
REQUIRE_PREDICATE NOT FILE_EXISTS_IN_GAME ~_sw1h01.itm~ @10 // non-Tutu

// this file does nothing, it just allows other mods to detect this component
COPY_EXISTING ~sw1h01.itm~ ~override/cdt03170.g3~

COPY_EXISTING ~ar2102.bcs~ ~override~
  DECOMPILE_BCS_TO_BAF
    REPLACE_TEXTUALLY ~ActionOverride(Player[1-6],ApplySpell(Myself,DROW_CHANGE))~ ~~
  COMPILE_BAF_TO_BCS
  BUT_ONLY_IF_IT_CHANGES

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// Romance Cheats                                   \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @318300 DESIGNATED 3183
GROUP @4
REQUIRE_PREDICATE NOT FILE_EXISTS_IN_GAME ~_sw1h01.itm~ @10 // non-Tutu

// this file does nothing, it just allows other mods to detect this component
COPY_EXISTING ~sw1h01.itm~ ~override/cdt3183.g3~

// fixes for Anomen's join dialogue
COMPILE ~bg2_tweaks/dlg/ano_rom_fix.d~

OUTER_SET "proceed" = 0
OUTER_SET "req_race" = 0
OUTER_SET "req_gender" = 0
OUTER_SET "multiple" = 0
OUTER_SET "no_kill" = 0
OUTER_SET "start_tob" = 0

OUTER_WHILE ("%proceed%" STRING_COMPARE_REGEXP "[Aa]") BEGIN
  OUTER_WHILE ("%req_race%" STRING_COMPARE_REGEXP "[AaBb]") BEGIN
    PRINT @318301
    ACTION_READLN req_race
  END
  OUTER_WHILE ("%req_gender%" STRING_COMPARE_REGEXP "[AaBb]") BEGIN
    PRINT @318303
    ACTION_READLN req_gender
  END
  OUTER_WHILE ("%multiple%" STRING_COMPARE_REGEXP "[AaBb]") BEGIN
    PRINT @318305
    ACTION_READLN multiple
  END
  ACTION_IF ("%multiple%" STRING_COMPARE_CASE "a" = 0) THEN BEGIN // only offer no kill if multi-romance selected
    OUTER_WHILE ("%no_kill%" STRING_COMPARE_REGEXP "[AaBb]") BEGIN
      PRINT @318307
      ACTION_READLN no_kill
    END
  END
  ACTION_IF FILE_EXISTS_IN_GAME ~mel01.cre~ THEN BEGIN // tob
    OUTER_WHILE ("%start_tob%" STRING_COMPARE_REGEXP "[AaBb]") BEGIN
      PRINT @318311
      ACTION_READLN start_tob
    END
  END
  // print summary of options before proceeding
  PRINT @318309
  ACTION_IF ("%req_race%" STRING_COMPARE_CASE "a" = 0) THEN BEGIN
    PRINT @318302
  END
  ACTION_IF ("%req_gender%" STRING_COMPARE_CASE "a" = 0) THEN BEGIN
    PRINT @318304
  END
  ACTION_IF ("%multiple%" STRING_COMPARE_CASE "a" = 0) THEN BEGIN
    PRINT @318306
  END
  ACTION_IF ("%no_kill%" STRING_COMPARE_CASE "a" = 0) THEN BEGIN
    PRINT @318308
  END
  ACTION_IF ("%start_tob%" STRING_COMPARE_CASE "a" = 0) THEN BEGIN
    PRINT @318312
  END
  OUTER_WHILE ("%proceed%" STRING_COMPARE_REGEXP "[AaBb]") BEGIN
    PRINT @318310
    ACTION_READLN proceed
  END
  ACTION_IF ("%proceed%" STRING_COMPARE_CASE "b" = 0) THEN BEGIN
    OUTER_SET "proceed" = 0
    OUTER_SET "req_race" = 0
    OUTER_SET "req_gender" = 0
    OUTER_SET "multiple" = 0
    OUTER_SET "no_kill" = 0
    OUTER_SET "start_tob" = 0
  END
END

// take care of tob_start
// this will also take care of any romance requirement adjustments on the ToB side while we're here
ACTION_IF ("%start_tob%" STRING_COMPARE_CASE "a" = 0) THEN BEGIN

  EXTEND_TOP ~aeri25.bcs~ ~bg2_tweaks/baf/aeri25_start_tob.baf~

  COMPILE ~BG2_Tweaks/dlg/rom_start_tob.d~

  ACTION_IF ("%req_gender%" STRING_COMPARE_CASE "b" = 0) THEN BEGIN // if gender reqs intact

    COMPILE ~BG2_Tweaks/dlg/rom_start_tob_gender.d~

  END

  ACTION_IF ("%req_race%" STRING_COMPARE_CASE "b" = 0) THEN BEGIN // if  race reqs intact

    COMPILE ~BG2_Tweaks/dlg/rom_start_tob_race.d~

  END

  ACTION_IF ("%multiple%" STRING_COMPARE_CASE "b" = 0) THEN BEGIN // if no multi allowed

    COMPILE ~BG2_Tweaks/dlg/rom_start_tob_multi.d~

  END

END

// removing gender and/or racial requirements
// note that ToB doesn't need to be adjusted here--romance either continues from SoA, or is adjusted in the ToB start patches above
ACTION_IF (("%req_race%" STRING_COMPARE_CASE "a" = 0) AND ("%req_gender%" STRING_COMPARE_CASE "a")) THEN BEGIN // if only gender reqs intact

  EXTEND_TOP ~aerie.bcs~   ~bg2_tweaks/baf/aerie_rom_reqs.baf~
  EXTEND_TOP ~anomen.bcs~  ~bg2_tweaks/baf/anomen_rom_reqs.baf~
  EXTEND_TOP ~jaheira.bcs~ ~bg2_tweaks/baf/jaheira_rom_reqs.baf~
  EXTEND_TOP ~viconia.bcs~ ~bg2_tweaks/baf/viconia_rom_reqs.baf~

  COPY_EXISTING ~aerie.bcs~   ~override~
                ~jaheira.bcs~ ~override~
                ~viconia.bcs~ ~override~
    DECOMPILE_BCS_TO_BAF
      REPLACE_TEXTUALLY ~Global("CDRomanceFoo","LOCALS",0)~
      ~Gender(Player1,MALE)~
    COMPILE_BAF_TO_BCS
    BUT_ONLY_IF_IT_CHANGES

  COPY_EXISTING ~anomen.bcs~ ~override~
    DECOMPILE_BCS_TO_BAF
      REPLACE_TEXTUALLY ~Global("CDRomanceFoo","LOCALS",0)~
      ~Gender(Player1,FEMALE)~
    COMPILE_BAF_TO_BCS
    BUT_ONLY_IF_IT_CHANGES

END

ACTION_IF (("%req_race%" STRING_COMPARE_CASE "a") AND ("%req_gender%" STRING_COMPARE_CASE "a" = 0)) THEN BEGIN // if only race reqs intact

  EXTEND_TOP ~aerie.bcs~   ~bg2_tweaks/baf/aerie_rom_reqs.baf~
  EXTEND_TOP ~anomen.bcs~  ~bg2_tweaks/baf/anomen_rom_reqs.baf~
  EXTEND_TOP ~jaheira.bcs~ ~bg2_tweaks/baf/jaheira_rom_reqs.baf~
  EXTEND_TOP ~viconia.bcs~ ~bg2_tweaks/baf/viconia_rom_reqs.baf~

  COPY_EXISTING ~aerie.bcs~ ~override~
    DECOMPILE_BCS_TO_BAF
      REPLACE_TEXTUALLY ~Global("CDRomanceFoo","LOCALS",0)~
      ~OR(5) Race(Player1,HUMAN) Race(Player1,HALF_ELF) Race(Player1,ELF) Race(Player1,HALFLING) Race(Player1,GNOME)~
    COMPILE_BAF_TO_BCS
    BUT_ONLY_IF_IT_CHANGES

  COPY_EXISTING ~anomen.bcs~  ~override~
                ~jaheira.bcs~ ~override~
    DECOMPILE_BCS_TO_BAF
      REPLACE_TEXTUALLY ~Global("CDRomanceFoo","LOCALS",0)~
      ~OR(4) Race(Player1,HUMAN) Race(Player1,HALF_ELF) Race(Player1,ELF) Race(Player1,HALFLING)~
    COMPILE_BAF_TO_BCS
    BUT_ONLY_IF_IT_CHANGES

  COPY_EXISTING ~viconia.bcs~ ~override~
    DECOMPILE_BCS_TO_BAF
      REPLACE_TEXTUALLY ~Global("CDRomanceFoo","LOCALS",0)~
      ~OR(4) Race(Player1,HUMAN) Race(Player1,HALF_ELF) Race(Player1,HALFLING) Race(Player1,HALFORC)~
    COMPILE_BAF_TO_BCS
    BUT_ONLY_IF_IT_CHANGES

END

ACTION_IF (("%req_race%" STRING_COMPARE_CASE "a" = 0) AND ("%req_gender%" STRING_COMPARE_CASE "a" = 0)) THEN BEGIN // if no reqs, just clean up

  EXTEND_TOP ~aerie.bcs~   ~bg2_tweaks/baf/aerie_rom_reqs.baf~
  EXTEND_TOP ~anomen.bcs~  ~bg2_tweaks/baf/anomen_rom_reqs.baf~
  EXTEND_TOP ~jaheira.bcs~ ~bg2_tweaks/baf/jaheira_rom_reqs.baf~
  EXTEND_TOP ~viconia.bcs~ ~bg2_tweaks/baf/viconia_rom_reqs.baf~

  COPY_EXISTING ~aerie.bcs~   ~override~
                ~anomen.bcs~  ~override~
                ~jaheira.bcs~ ~override~
                ~viconia.bcs~ ~override~
    DECOMPILE_BCS_TO_BAF
      REPLACE_TEXTUALLY ~Global("CDRomanceFoo","LOCALS",0)~ ~~
    COMPILE_BAF_TO_BCS
    BUT_ONLY_IF_IT_CHANGES

END

// allow multiple romances
ACTION_IF ("%multiple%" STRING_COMPARE_CASE "a" = 0) THEN BEGIN // if no reqs, just clean up

  // stop disabling aerie's romance
  COPY_EXISTING ~jaheira.bcs~ ~override~
                ~viconia.bcs~ ~override~
    DECOMPILE_BCS_TO_BAF
      REPLACE_TEXTUALLY ~\bGlobal("AerieRomanceActive","GLOBAL",[12])~ ~False()~
    COMPILE_BAF_TO_BCS
    BUT_ONLY_IF_IT_CHANGES

  // stop disabling jaheira's romance
  COPY_EXISTING ~aerie.bcs~   ~override~
                ~viconia.bcs~ ~override~
    DECOMPILE_BCS_TO_BAF
      REPLACE_TEXTUALLY ~\bGlobal("JaheiraRomanceActive","GLOBAL",[12])~ ~False()~
    COMPILE_BAF_TO_BCS
    BUT_ONLY_IF_IT_CHANGES

  // stop disabling viconia's romance
  COPY_EXISTING ~aerie.bcs~   ~override~
                ~jaheira.bcs~ ~override~
    DECOMPILE_BCS_TO_BAF
      REPLACE_TEXTUALLY ~\bGlobal("ViconiaRomanceActive","GLOBAL",[12])~ ~False()~
    COMPILE_BAF_TO_BCS
    BUT_ONLY_IF_IT_CHANGES

  ACTION_IF FILE_EXISTS_IN_GAME ~mel01.cre~ THEN BEGIN

    // stop disabling aerie's romance
    COPY_EXISTING ~jahe25.bcs~ ~override~
                  ~vico25.bcs~ ~override~
      DECOMPILE_BCS_TO_BAF
        REPLACE_TEXTUALLY ~\bGlobal("AerieRomanceActive","GLOBAL",[12])~ ~False()~
      COMPILE_BAF_TO_BCS
      BUT_ONLY_IF_IT_CHANGES

    // stop disabling jaheira's romance
    COPY_EXISTING ~aeri25.bcs~ ~override~
                  ~vico25.bcs~ ~override~
      DECOMPILE_BCS_TO_BAF
        REPLACE_TEXTUALLY ~\bGlobal("JaheiraRomanceActive","GLOBAL",[12])~ ~False()~
      COMPILE_BAF_TO_BCS
      BUT_ONLY_IF_IT_CHANGES

    // stop disabling viconia's romance
    COPY_EXISTING ~aeri25.bcs~ ~override~
                  ~jahe25.bcs~ ~override~
      DECOMPILE_BCS_TO_BAF
        REPLACE_TEXTUALLY ~\bGlobal("ViconiaRomanceActive","GLOBAL",[12])~ ~False()~
      COMPILE_BAF_TO_BCS
      BUT_ONLY_IF_IT_CHANGES

    // wraith punish
    COPY_EXISTING ~aeri25.bcs~ ~override~
                  ~anom25.bcs~ ~override~
                  ~jahe25.bcs~ ~override~
                  ~vico25.bcs~ ~override~
      DECOMPILE_BCS_TO_BAF
        REPLACE_TEXTUALLY ~GlobalGT("WraithPunish","GLOBAL",0)~ ~False()~
      COMPILE_BAF_TO_BCS
      BUT_ONLY_IF_IT_CHANGES

    EXTEND_TOP ~aeri25.bcs~ ~BG2_Tweaks/baf/aeri25_rom2.baf~
    EXTEND_TOP ~anom25.bcs~ ~BG2_Tweaks/baf/anom25_rom2.baf~
    EXTEND_TOP ~jahe25.bcs~ ~BG2_Tweaks/baf/jahe25_rom2.baf~
    EXTEND_TOP ~vico25.bcs~ ~BG2_Tweaks/baf/vico25_rom2.baf~

  END

  // no kill
  ACTION_IF ("%no_kill%" STRING_COMPARE_CASE "a" = 0) THEN BEGIN // if no reqs, just clean up

    EXTEND_TOP ~aerie.bcs~   ~BG2_Tweaks/baf/aerie_rom3.baf~
    EXTEND_TOP ~anomen.bcs~  ~BG2_Tweaks/baf/anomen_rom3.baf~
    EXTEND_TOP ~jaheira.bcs~ ~BG2_Tweaks/baf/jaheira_rom3.baf~
    EXTEND_TOP ~viconia.bcs~ ~BG2_Tweaks/baf/viconia_rom3.baf~

    ACTION_IF FILE_EXISTS_IN_GAME ~mel01.cre~ THEN BEGIN

      EXTEND_TOP ~aeri25.bcs~ ~BG2_Tweaks/baf/aeri25_rom3.baf~
      EXTEND_TOP ~anom25.bcs~ ~BG2_Tweaks/baf/anom25_rom3.baf~
      EXTEND_TOP ~jahe25.bcs~ ~BG2_Tweaks/baf/jahe25_rom3.baf~
      EXTEND_TOP ~vico25.bcs~ ~BG2_Tweaks/baf/vico25_rom3.baf~

    END

  END

END

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// rest anywhere                                    \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @319000 DESIGNATED 3190
GROUP @4

// this file does nothing, it just allows other mods to detect this component
COPY_EXISTING ~sw1h01.itm~ ~override/cdt03190.g3~

COPY_EXISTING ~ar0087.are~ ~override/xr2400.are~
              ~ar0087.are~ ~override/xr2600.are~

COPY_EXISTING_REGEXP GLOB ~^.+\.are$~ ~override~
  READ_BYTE  0x48 "flags"
  WRITE_BYTE 0x48 ("%flags%" BOR 0b10000000)
  BUT_ONLY_IF_IT_CHANGES

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                            \\\\\
///// sellable items                                             \\\\\
/////                                                            \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @320000 DESIGNATED 3200
GROUP @4
GROUP @19

// this file does nothing, it just allows other mods to detect this component
COPY_EXISTING ~sw1h01.itm~ ~override/cdt03200.g3~

PRINT @1
COPY_EXISTING_REGEXP GLOB ~^.+\.itm$~ ~override~
  READ_SHORT 0x1c "type"  ELSE 0 // 17 - mace, 18 - sling, 26 - staff, 37 - book
  READ_LONG  0x34 "price" ELSE 0
  PATCH_IF ((("%type%" = 17) OR ("%type%" = 18) OR ("%type%" = 26) OR ("%type%" = 37)) AND ("%price%" = 0)) BEGIN
    WRITE_LONG 0x34 1 // set price to 1 gp
  END
  BUT_ONLY_IF_IT_CHANGES

// stores which buy arrows now buy bolts (mainly affects BGT/Tutu)
COPY_EXISTING_REGEXP GLOB ~^.+\.sto$~ ~override~
  READ_LONG 0x2c "buy_off"   ELSE 0
  READ_LONG 0x30 "buy_num"   ELSE 0
  READ_LONG 0x34 "sale_off"  ELSE 0
  READ_LONG 0x4c "drink_off" ELSE 0
  READ_LONG 0x70 "cure_off"  ELSE 0
  SET "bolts" = 0
  FOR (index = 0 ; index < buy_num ; index = index + 1) BEGIN
    READ_LONG ("%buy_off%" + ("%index%" * 0x04)) "item"
    PATCH_IF ("%item%" = 5) BEGIN // arrows
      SET "bolts" = 1
    END ELSE
    PATCH_IF ("%item%" = 31) BEGIN // bolts
      SET "bolts" = 2
      SET "index" = "%buy_num%" // kills loop
    END
  END
  PATCH_IF ("%bolts%" = 1) BEGIN // if arrows but no bolts
    INSERT_BYTES "%buy_off%" 0x04
      WRITE_LONG "%buy_off%" 31
    WRITE_LONG 0x30 ("%buy_num%" + 1)
    // adjust offsets for older-formatted stores
    PATCH_IF ("%sale_off%" > "%buy_off%") BEGIN
      WRITE_LONG 0x34 ("%sale_off%" + 0x04)
    END
      PATCH_IF ("%drink_off%" > "%buy_off%") BEGIN
    WRITE_LONG 0x4c ("%drink_off%" + 0x04)
    END
    PATCH_IF ("%cure_off%" > "%buy_off%") BEGIN
      WRITE_LONG 0x70 ("%cure_off%" + 0x04)
    END
  END
  BUT_ONLY_IF_IT_CHANGES

COPY_EXISTING_REGEXP GLOB ~^_?blun10\.itm$~ ~override~ // root of the problem
  WRITE_LONG 0x34 2750 // sets root of problem to BG price
  BUT_ONLY_IF_IT_CHANGES

COPY_EXISTING ~flolth.itm~ ~override~ // drow flail +3
  WRITE_LONG 0x34 575 // same as drow flail +3, dwblun01
  BUT_ONLY_IF_IT_CHANGES

COPY_EXISTING ~key20.itm~ ~override~ // king strohm iii's burial mask
  WRITE_LONG 0x34 20 // like a helmet, but universal
  BUT_ONLY_IF_IT_CHANGES

COPY_EXISTING ~kuobolt.itm~  ~override~ // kuo-toa bolts
              ~sahbolt.itm~  ~override~ // sahuagin bolts
  WRITE_LONG 0x34 30 // guess
  BUT_ONLY_IF_IT_CHANGES

COPY_EXISTING ~misc3a1.itm~ ~override~ // book of infinite spells
              ~misc3a2.itm~ ~override~
              ~misc3a3.itm~ ~override~
              ~misc3a4.itm~ ~override~
              ~misc3a5.itm~ ~override~
              ~misc3a6.itm~ ~override~
              ~misc3a7.itm~ ~override~
              ~misc3a8.itm~ ~override~
  WRITE_LONG 0x34 3200 // supopsed to be artifact
  BUT_ONLY_IF_IT_CHANGES

COPY_EXISTING ~misc3a9.itm~ ~override~ // book of infinite spells, last page
  WRITE_LONG 0x34 800 // last page and burning hands? weak
  BUT_ONLY_IF_IT_CHANGES

COPY_EXISTING ~misc3c.itm~ ~override~ // efreeti bottle
  WRITE_LONG 0x34 2000

COPY_EXISTING ~misc3h.itm~ ~override~ // horn of blasting
  WRITE_LONG 0x34 1000
  BUT_ONLY_IF_IT_CHANGES

COPY_EXISTING ~misc3l.itm~ ~override~ // horn of silence
  WRITE_LONG 0x34 2000
  BUT_ONLY_IF_IT_CHANGES

COPY_EXISTING ~misc3o.itm~ ~override~ // methlid's harp
  WRITE_LONG 0x34 1000
  BUT_ONLY_IF_IT_CHANGES

COPY_EXISTING ~misc9q.itm~ ~override~ // habib's scimitar
  WRITE_LONG 0x34 55
  BUT_ONLY_IF_IT_CHANGES

COPY_EXISTING ~npmisc1.itm~ ~override~ // jan jansen's goggles
  WRITE_LONG 0x34 200
  BUT_ONLY_IF_IT_CHANGES

COPY_EXISTING ~npmisc2.itm~ ~override~ // jan jansen's gloves
  WRITE_LONG 0x34 150
  BUT_ONLY_IF_IT_CHANGES

COPY_EXISTING ~npshld.itm~ ~override~ // anomen's shield
  WRITE_LONG 0x34 2000
  BUT_ONLY_IF_IT_CHANGES

ACTION_IF FILE_EXISTS_IN_GAME ~mel01.cre~ THEN BEGIN // if ToB installed

  COPY_EXISTING ~deck.itm~ ~override~ // deck of many things
    WRITE_LONG 0x34 2500
    BUT_ONLY_IF_IT_CHANGES

  COPY_EXISTING ~kuobolt2.itm~ ~override~ // kuo-toa bolts
                ~kuobolt3.itm~ ~override~ // kuo-toa bolts
    WRITE_LONG 0x34 30 // guess
    BUT_ONLY_IF_IT_CHANGES

END

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// Min Stats Tweak                                  \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @321000 DESIGNATED 3210
GROUP @4
GROUP @19

// this file does nothing, it just allows other mods to detect this component
COPY_EXISTING ~sw1h01.itm~ ~override/cdt03210.g3~

OUTER_SET "min_stat" = 0
OUTER_WHILE ((NOT IS_AN_INT "%min_stat%") OR ("%min_stat%" < 6) OR ("%min_stat%" > 15)) BEGIN
    PRINT @321001
    ACTION_READLN min_stat
  END

COPY_EXISTING ~abclasrq.2da~ ~override~
  COUNT_2DA_ROWS ~7~ "rows"
  FOR (index = 0 ; index < rows ; index = index + 1) BEGIN
    FOR (index2 = 1 ; index2 < 7 ; index2 = index2 + 1) BEGIN
      READ_2DA_ENTRY index index2 7 "min"
      PATCH_IF ("%min%" < "%min_stat%") BEGIN
        SET_2DA_ENTRY index index2 7 "%min_stat%"
      END
    END
  END
  BUT_ONLY_IF_IT_CHANGES

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// Adjust Evil joinable NPC reaction rolls          \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @400000 DESIGNATED 4000
GROUP @0
REQUIRE_PREDICATE ((FILE_EXISTS_IN_GAME ~_sw1h01.itm~) OR (FILE_EXISTS_IN_GAME ~bg1start.2da~)) @14 // Tutu or BGT only

// this file does nothing, it just allows other mods to detect this component
COPY_EXISTING ~sw1h01.itm~ ~override/cdt04000.g3~

ACTION_IF FILE_EXISTS_IN_GAME ~bg1start.2da~ THEN BEGIN // BGT

  COMPILE ~BG2_Tweaks/dlg/bgt_evilnpc.d~

END ELSE BEGIN // Tutu

  COMPILE ~BG2_Tweaks/dlg/evilnpc.d~

END

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// Improved Fate Spirit Summoning                   \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @401000 DESIGNATED 4010
GROUP @0
GROUP @19
REQUIRE_PREDICATE     FILE_EXISTS_IN_GAME ~mel01.cre~   @12 // ToB required
REQUIRE_PREDICATE NOT FILE_EXISTS_IN_GAME ~_sw1h01.itm~ @10 // non-Tutu

// this file does nothing, it just allows other mods to detect this component
COPY_EXISTING ~sw1h01.itm~ ~override/cdt04010.g3~

// end of SoA cutscenes set extra variable
COPY_EXISTING ~cut100a.bcs~ ~override~
              ~cut100b.bcs~ ~override~
  DECOMPILE_BCS_TO_BAF
    REPLACE_TEXTUALLY ~MultiPlayerSync()~ ~SetGlobal("G#G3.CompletedSoA","GLOBAL",1) MultiPlayerSync()~
  COMPILE_BAF_TO_BCS
  UNLESS ~SetGlobal("G#G3.CompletedSoA","GLOBAL",1)~
  BUT_ONLY_IF_IT_CHANGES

// extend SoA scripts to set per-NPC variables if player meets Bioware NPC
EXTEND_BOTTOM ~aerie.bcs~    ~BG2_Tweaks/baf/aerie.baf~
EXTEND_BOTTOM ~anomen.bcs~   ~BG2_Tweaks/baf/anomen.baf~
EXTEND_BOTTOM ~cernd.bcs~    ~BG2_Tweaks/baf/cernd.baf~
EXTEND_BOTTOM ~edwin.bcs~    ~BG2_Tweaks/baf/fsp_edwin.baf~
EXTEND_BOTTOM ~haerdali.bcs~ ~BG2_Tweaks/baf/haer.baf~
EXTEND_BOTTOM ~imoen.bcs~    ~BG2_Tweaks/baf/imoen.baf~
EXTEND_BOTTOM ~imoen2.bcs~   ~BG2_Tweaks/baf/imoen.baf~
EXTEND_BOTTOM ~jaheira.bcs~  ~BG2_Tweaks/baf/jaheira.baf~
EXTEND_BOTTOM ~jan.bcs~      ~BG2_Tweaks/baf/jan.baf~
EXTEND_BOTTOM ~keldorn.bcs~  ~BG2_Tweaks/baf/keldorn.baf~
EXTEND_BOTTOM ~korgan.bcs~   ~BG2_Tweaks/baf/fsp_korgan.baf~
EXTEND_BOTTOM ~mazzy.bcs~    ~BG2_Tweaks/baf/mazzy.baf~
EXTEND_BOTTOM ~minsc.bcs~    ~BG2_Tweaks/baf/fsp_minsc.baf~
EXTEND_BOTTOM ~nalia.bcs~    ~BG2_Tweaks/baf/nalia.baf~
EXTEND_BOTTOM ~valygar.bcs~  ~BG2_Tweaks/baf/fsp_valygar.baf~
EXTEND_BOTTOM ~viconia.bcs~  ~BG2_Tweaks/baf/viconia.baf~

// mod NPCs
// can't do xan (talon) and wikaede; no SoA scripts
// anything else not listed: fatesp tie-in not found
ACTION_IF FILE_EXISTS_IN_GAME ~d0alassa.bcs~ THEN BEGIN // alassa

  EXTEND_BOTTOM ~d0alassa.bcs~ ~BG2_Tweaks/baf/alassa.baf~

END

ACTION_IF FILE_EXISTS_IN_GAME ~subru.bcs~ THEN BEGIN // bruce (bons)

  EXTEND_BOTTOM ~subru.bcs~ ~BG2_Tweaks/baf/bruce_bons.baf~

END

ACTION_IF FILE_EXISTS_IN_GAME ~jcbru.bcs~ THEN BEGIN // bruce (jc)

  EXTEND_BOTTOM ~jcbru.bcs~ ~BG2_Tweaks/baf/bruce_jc.baf~

END

ACTION_IF FILE_EXISTS_IN_GAME ~p#deh.bcs~ THEN BEGIN // deheriana

  EXTEND_BOTTOM ~p#deh.bcs~ ~BG2_Tweaks/baf/deheriana.baf~

END

ACTION_IF FILE_EXISTS_IN_GAME ~fwghar.bcs~ THEN BEGIN // ghareth

  EXTEND_BOTTOM ~fwghar.bcs~ ~BG2_Tweaks/baf/ghareth.baf~

END

ACTION_IF FILE_EXISTS_IN_GAME ~sc#hub.bcs~ THEN BEGIN // hubelpot

  EXTEND_BOTTOM ~sc#hub.bcs~ ~BG2_Tweaks/baf/hubelpot.baf~

END

ACTION_IF FILE_EXISTS_IN_GAME ~j#klsy.bcs~ THEN BEGIN // kelsey

  EXTEND_BOTTOM ~j#klsy.bcs~ ~BG2_Tweaks/baf/kelsey.baf~

END

ACTION_IF FILE_EXISTS_IN_GAME ~kido.bcs~ THEN BEGIN // kido

  EXTEND_BOTTOM ~kido.bcs~ ~BG2_Tweaks/baf/kido.baf~

END

ACTION_IF FILE_EXISTS_IN_GAME ~kindrek.bcs~ THEN BEGIN // kindrek

  EXTEND_BOTTOM ~kindrek.bcs~ ~BG2_Tweaks/baf/kindrek.baf~

END

ACTION_IF FILE_EXISTS_IN_GAME ~p#kiv01.bcs~ THEN BEGIN // kivan

  EXTEND_BOTTOM ~p#kiv01.bcs~ ~BG2_Tweaks/baf/fsp_kivan.baf~

END

ACTION_IF FILE_EXISTS_IN_GAME ~d#silver.bcs~ THEN BEGIN // silverstar

  EXTEND_BOTTOM ~d#silver.bcs~ ~BG2_Tweaks/baf/silverstar.baf~

END

ACTION_IF FILE_EXISTS_IN_GAME ~sola.bcs~ THEN BEGIN // solaufein (weimer)

  EXTEND_BOTTOM ~sola.bcs~ ~BG2_Tweaks/baf/sola.baf~

END

ACTION_IF FILE_EXISTS_IN_GAME ~tashia.bcs~ THEN BEGIN // tashia

  EXTEND_BOTTOM ~tashia.bcs~ ~BG2_Tweaks/baf/tashia.baf~

END

ACTION_IF FILE_EXISTS_IN_GAME ~vanim.bcs~ THEN BEGIN // vanim

  EXTEND_BOTTOM ~vanim.bcs~ ~BG2_Tweaks/baf/vanim.baf~

END

ACTION_IF FILE_EXISTS_IN_GAME ~o#xans.bcs~ THEN BEGIN // xan (kulyok)

  EXTEND_BOTTOM ~o#xans.bcs~ ~BG2_Tweaks/baf/xan.baf~

END

// WeiDU newline matching goodness
OUTER_INNER_PATCH ~12~ BEGIN
  WRITE_BYTE 1 0x09
  READ_ASCII 1 tab (1)  // 0x09, tab
  WRITE_BYTE 1 0x0a
  READ_ASCII 1 lnl (1)  // 0x0a, Linux
  WRITE_BYTE 0 0x0d
  READ_ASCII 0 mnl (1)  // 0x0d, Mac
  READ_ASCII 0 wnl (2)  // 0x0d0a, Windows
END

COPY_EXISTING ~fatesp.dlg~ ~override~
  DECOMPILE_DLG_TO_D
    REPLACE_TEXTUALLY ~~~~~!Dead("\([^"]+\)")[ |%tab%|%lnl%|%mnl%|%wnl%]*Global("\([^"]+\)","GLOBAL",0)~ THEN REPLY #\([0-9]+\)~~~~~
    ~~~~~Dead("\1")~ THEN REPLY #\3 DO ~SetGlobal("\2","GLOBAL",2)~ GOTO 7
    IF ~!Dead("\1") Global("\2","GLOBAL",0)~ THEN REPLY #\3~~~~~
  COMPILE_D_TO_DLG
  BUT_ONLY_IF_IT_CHANGES

// add extra triggers to fate spirit dialogue
COMPILE ~BG2_Tweaks/dlg/fatesp.d~

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// ToB-Style NPCs                                   \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @402000 DESIGNATED 4020
GROUP @0

// this file does nothing, it just allows other mods to detect this component
COPY_EXISTING ~sw1h01.itm~ ~override/cdt04020.g3~

// load patch macro
INCLUDE ~bg2_tweaks/lib/tob_style_npcs.tpa~

COPY_EXISTING ~aerie6.cre~   ~override/aerie7.cre~
              ~aerie6.cre~   ~override/aerie9.cre~
              ~aerie6.cre~   ~override/aerie10.cre~
              ~aerie6.cre~   ~override/aerie11.cre~
              ~aerie6.cre~   ~override/aerie12.cre~ // ToB
              ~anomen6.cre~  ~override/anomen7.cre~
              ~anomen6.cre~  ~override/anomen8.cre~
              ~anomen6.cre~  ~override/anomen9.cre~
              ~anomen6.cre~  ~override/anomen10.cre~
              ~anomen6.cre~  ~override/anomen12.cre~ // ToB
              ~cernd10.cre~  ~override/cernd12.cre~
              ~cernd10.cre~  ~override/cernd13.cre~
              ~cernd10.cre~  ~override/cernd13b.cre~
              ~cernd10.cre~  ~override/cernd14.cre~ // ToB
              ~edwin7.cre~   ~override/edwin9.cre~
              ~edwin7.cre~   ~override/edwin11.cre~
              ~edwin7.cre~   ~override/edwin12.cre~
              ~edwin7.cre~   ~override/edwin13.cre~
              ~edwin7.cre~   ~override/edwin15.cre~ // ToB
              ~haer10.cre~   ~override/haer11.cre~
              ~haer10.cre~   ~override/haer13.cre~
              ~haer10.cre~   ~override/haer15.cre~
              ~haer10.cre~   ~override/haer19.cre~  // ToB
              ~imoen10.cre~  ~override/imoen15.cre~ // ToB
              ~imoen10.cre~  ~override/imoen211.cre~
              ~imoen10.cre~  ~override/imoen213.cre~
              ~jaheir7.cre~  ~override/jaheir8.cre~
              ~jaheir7.cre~  ~override/jaheir11.cre~
              ~jaheir7.cre~  ~override/jaheir12.cre~
              ~jaheir7.cre~  ~override/jahei14.cre~ // ToB
              ~jan8.cre~     ~override/jan10.cre~
              ~jan8.cre~     ~override/jan11.cre~
              ~jan8.cre~     ~override/jan12.cre~
              ~jan8.cre~     ~override/jan15.cre~ // ToB
              ~keldor8.cre~  ~override/keldor9.cre~
              ~keldor8.cre~  ~override/keldor10.cre~
              ~keldor8.cre~  ~override/keldor12.cre~
              ~keldor8.cre~  ~override/keldor14.cre~ // ToB
              ~korgan8.cre~  ~override/korgan9.cre~
              ~korgan8.cre~  ~override/korgan11.cre~
              ~korgan8.cre~  ~override/korgan12.cre~
              ~korgan8.cre~  ~override/korgan15.cre~ // ToB
              ~mazzy8.cre~   ~override/mazzy9.cre~
              ~mazzy8.cre~   ~override/mazzy11.cre~
              ~mazzy8.cre~   ~override/mazzy12.cre~
              ~mazzy8.cre~   ~override/mazzy15.cre~ // ToB
              ~minsc7.cre~   ~override/minsc8.cre~
              ~minsc7.cre~   ~override/minsc9.cre~
              ~minsc7.cre~   ~override/minsc10.cre~
              ~minsc7.cre~   ~override/minsc12.cre~
              ~minsc7.cre~   ~override/minsc15.cre~ // ToB
              ~nalia8.cre~   ~override/nalia10.cre~
              ~nalia8.cre~   ~override/nalia11.cre~
              ~nalia8.cre~   ~override/nalia13.cre~
              ~nalia8.cre~   ~override/nalia15.cre~
              ~nalia8.cre~   ~override/nalia18.cre~ // ToB
              ~valyg8.cre~   ~override/valyg9.cre~
              ~valyg8.cre~   ~override/valyg11.cre~
              ~valyg8.cre~   ~override/valyg12.cre~
              ~valyg8.cre~   ~override/valyg14.cre~ // ToB
              ~viconi6.cre~  ~override/viconi8.cre~
              ~viconi6.cre~  ~override/viconi9.cre~
              ~viconi6.cre~  ~override/viconi11.cre~
              ~viconi6.cre~  ~override/viconi13.cre~
              ~viconi6.cre~  ~override/viconi16.cre~ // ToB
              ~yoshi7.cre~   ~override/yoshi8.cre~
              ~yoshi7.cre~   ~override/yoshi10.cre~
              ~yoshi7.cre~   ~override/yoshi11.cre~
              ~yoshi7.cre~   ~override/yoshi12.cre~
              // Tutu creatures
              ~_ajanti.cre~  ~override/_ajanti4.cre~
              ~_ajanti.cre~  ~override/_ajanti6.cre~
              ~_alora.cre~   ~override/_alora6.cre~
              ~_branwe.cre~  ~override/_branwe5.cre~
              ~_coran.cre~   ~override/_coran5.cre~
              ~_dynahe.cre~  ~override/_dynahe2.cre~
              ~_dynahe.cre~  ~override/_dynahe4.cre~
              ~_dynahe.cre~  ~override/_dynahe6.cre~
              ~_edwin.cre~   ~override/_edwin2.cre~
              ~_edwin.cre~   ~override/_edwin4.cre~
              ~_edwin.cre~   ~override/_edwin6.cre~
              ~_faldor.cre~  ~override/_faldor5.cre~
              ~_garric.cre~  ~override/_garric2.cre~
              ~_garric.cre~  ~override/_garric4.cre~
              ~_garric.cre~  ~override/_garric6.cre~
              ~_jaheir.cre~  ~override/_jaheir2.cre~
              ~_jaheir.cre~  ~override/_jaheir4.cre~
              ~_jaheir.cre~  ~override/_jaheir6.cre~
              ~_imoen1.cre~  ~override/_imoen2.cre~
              ~_imoen1.cre~  ~override/_imoen4.cre~
              ~_imoen1.cre~  ~override/_imoen6.cre~
              ~_kagain.cre~  ~override/_kagain2.cre~
              ~_kagain.cre~  ~override/_kagain4.cre~
              ~_kagain.cre~  ~override/_kagain6.cre~
              ~_khalid.cre~  ~override/_khalid2.cre~
              ~_khalid.cre~  ~override/_khalid4.cre~
              ~_khalid.cre~  ~override/_khalid6.cre~
              ~_kivan.cre~   ~override/_kivan4.cre~
              ~_kivan.cre~   ~override/_kivan6.cre~
              ~_minsc.cre~   ~override/_minsc2.cre~
              ~_minsc.cre~   ~override/_minsc4.cre~
              ~_minsc.cre~   ~override/_minsc6.cre~
              ~_montar.cre~  ~override/_montar2.cre~
              ~_montar.cre~  ~override/_montar4.cre~
              ~_montar.cre~  ~override/_montar6.cre~
              ~_quayle.cre~  ~override/_quayle4.cre~
              ~_quayle.cre~  ~override/_quayle6.cre~
              ~_safana.cre~  ~override/_safana4.cre~
              ~_safana.cre~  ~override/_safana6.cre~
              ~_sharte.cre~  ~override/_sharte4.cre~
              ~_sharte.cre~  ~override/_sharte6.cre~
              ~_skie.cre~    ~override/_skie6.cre~
              ~_tiax.cre~    ~override/_tiax4.cre~
              ~_tiax.cre~    ~override/_tiax6.cre~
              ~_viconi.cre~  ~override/_viconi4.cre~
              ~_viconi.cre~  ~override/_viconi6.cre~
              ~_xan.cre~     ~override/_xan4.cre~
              ~_xan.cre~     ~override/_xan6.cre~
              ~_xzar.cre~    ~override/_xzar2.cre~
              ~_xzar.cre~    ~override/_xzar4.cre~
              ~_xzar.cre~    ~override/_xzar6.cre~
              ~_yeslic.cre~  ~override/_yeslic5.cre~
              // BGT
              ~ajanti.cre~   ~override/ajanti4.cre~
              ~ajanti.cre~   ~override/ajanti6.cre~
              ~alora.cre~    ~override/alora6.cre~
              ~branwe.cre~   ~override/branwe5.cre~
              ~coran.cre~    ~override/coran5.cre~
              ~dynahe.cre~   ~override/dynahe2.cre~
              ~dynahe.cre~   ~override/dynahe4.cre~
              ~dynahe.cre~   ~override/dynahe6.cre~
              ~edwin.cre~    ~override/edwin2.cre~
              ~edwin.cre~    ~override/edwin4.cre~
              ~edwin.cre~    ~override/edwin6.cre~
              ~faldor.cre~   ~override/faldor5.cre~
              ~garric.cre~   ~override/garric2.cre~
              ~garric.cre~   ~override/garric4.cre~
              ~garric.cre~   ~override/garric6.cre~
              ~jaheir.cre~   ~override/jaheir2.cre~
              ~jaheir.cre~   ~override/jaheir4.cre~
              ~jaheir.cre~   ~override/jaheir6.cre~
              ~imoen1.cre~   ~override/imoen2.cre~
              ~imoen1.cre~   ~override/imoen4.cre~
              ~imoen1.cre~   ~override/imoen61.cre~
              ~kagain.cre~   ~override/kagain2.cre~
              ~kagain.cre~   ~override/kagain4.cre~
              ~kagain.cre~   ~override/kagain6.cre~
              ~khalid.cre~   ~override/khalid2.cre~
              ~khalid.cre~   ~override/khalid4.cre~
              ~khalid.cre~   ~override/khalid6.cre~
              ~kivan.cre~    ~override/kivan4.cre~
              ~kivan.cre~    ~override/kivan6.cre~
              ~minsc.cre~    ~override/minsc2.cre~
              ~minsc.cre~    ~override/minsc4.cre~
              ~minsc.cre~    ~override/minsc6.cre~
              ~montar.cre~   ~override/montar2.cre~
              ~montar.cre~   ~override/montar4.cre~
              ~montar.cre~   ~override/montar6.cre~
              ~bgquayle.cre~ ~override/quayle4.cre~
              ~bgquayle.cre~ ~override/quayle6.cre~
              ~safana.cre~   ~override/safana4.cre~
              ~safana.cre~   ~override/safana6.cre~
              ~sharte.cre~   ~override/sharte4.cre~
              ~sharte.cre~   ~override/sharte6.cre~
              ~skie.cre~     ~override/skie6.cre~
              ~tiax.cre~     ~override/tiax4.cre~
              ~tiax.cre~     ~override/tiax6.cre~
              ~viconi.cre~   ~override/viconi4.cre~
              ~viconi.cre~   ~override/viconi61.cre~
              ~bgxan.cre~    ~override/xan4.cre~
              ~bgxan.cre~    ~override/xan6.cre~
              ~bgxzar.cre~   ~override/xzar2.cre~
              ~bgxzar.cre~   ~override/xzar4.cre~
              ~bgxzar.cre~   ~override/xzar6.cre~
              ~yeslic.cre~   ~override/yeslic5.cre~
              // mod npcs
              ~d0alassa.cre~  ~override/d0alas25.cre~  // alassa
              ~m#ambr10.cre~  ~override/m#ambr12.cre~  // amber
              ~m#ambr10.cre~  ~override/m#ambr14.cre~
              ~m#ambr10.cre~  ~override/m#ambr15.cre~
              ~subru05.cre~   ~override/subru09.cre~   // bruce (bons)
              ~subru05.cre~   ~override/subru17.cre~
              ~jcbruce.cre~   ~override/jcbruce2.cre~  // bruce (jcompton)
              ~chloe7.cre~    ~override/chloe9.cre~    // chloe
              ~chloe7.cre~    ~override/chloe13.cre~
              ~chloe7.cre~    ~override/chloe15.cre~
              ~c!delai10.cre~ ~override/c!delai11.cre~ // delainy
              ~c!delai10.cre~ ~override/c!delai13.cre~
              ~c!delai10.cre~ ~override/c!delai15.cre~
              ~sufinch1.cre~  ~override/sufinch2.cre~  // finch
              ~sufinch1.cre~  ~override/sufinch4.cre~
              ~fwghar04.cre~  ~override/fwghar25.cre~  // ghareth
              ~sc#hub.cre~    ~override/sc#hub25.cre~  // hubelpot
              ~j#indi01.cre~  ~override/j#indi03.cre~  // indira
              ~j#indi01.cre~  ~override/j#indi04.cre~
              ~j#klsy09.cre~  ~override/j#klsy11.cre~  // kelsey
              ~j#klsy09.cre~  ~override/j#klsy12.cre~
              ~j#klsy09.cre~  ~override/j#klsy13.cre~
              ~j#klsy09.cre~  ~override/j#klsy25.cre~
              ~keto08.cre~    ~override/keto10.cre~    // keto
              ~keto08.cre~    ~override/keto12.cre~
              ~kido.cre~      ~override/kido25.cre~    // kido
              ~kindrek.cre~   ~override/kindre25.cre~  // kindrek
              ~r!kit07.cre~   ~override/r!kit09.cre~   // kitanya
              ~r!kit07.cre~   ~override/r!kit11.cre~
              ~r!kit07.cre~   ~override/r!kit13.cre~
              ~r!kit07.cre~   ~override/r!kit15.cre~
              ~nath07.cre~    ~override/nath08.cre~    // nathaniel
              ~nath07.cre~    ~override/nath10.cre~
              ~sk#neht1.cre~  ~override/sk#neht3.cre~  // neh'taniel
              ~sk#neht1.cre~  ~override/sk#neht5.cre~
              ~sola5.cre~     ~override/sola7.cre~     // solaufein (weimer)
              ~sola5.cre~     ~override/sola10.cre~
              ~sola5.cre~     ~override/sola12.cre~
              ~sola5.cre~     ~override/sola15.cre~
              ~sola5.cre~     ~override/sola17.cre~
              ~willyb.cre~    ~override/willyb2.cre~   // willie bruce
              ~mgwika.cre~    ~override/mgwika25.cre~  // wikaede
              ~tlxan.cre~     ~override/tlxantob.cre~  // xan (talon)
              ~o#xan09.cre~   ~override/o#xan11.cre~   // xan (kulyok)
              ~o#xan09.cre~   ~override/o#xan12.cre~
              ~o#xan09.cre~   ~override/o#xan13.cre~
              ~sdnpc12.cre~   ~override/sdnpc18.cre~   // yikari
  LAUNCH_PATCH_MACRO ~tob_style_npcs~
  BUT_ONLY_IF_IT_CHANGES

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// Stat changes: Edwin                              \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

/////                                                  \\\\\
///// BG Values                                        \\\\\
/////                                                  \\\\\

BEGIN @403001 DESIGNATED 4030 // Use BG Stats
GROUP @0
SUBCOMPONENT @403000 // Stat Changes: Edwin

// this file does nothing, it just allows other mods to detect this component
COPY_EXISTING ~sw1h01.itm~ ~override/cdt04030.g3~

COPY_EXISTING_REGEXP GLOB ~^_?edwin[0-9]*\.cre$~ ~override~
  PATCH_IF (SOURCE_SIZE > 0x2d3) THEN BEGIN // protects against invalid files
    WRITE_BYTE 0x238 9 // 9 STR
    WRITE_BYTE 0x23b 9 // 9 WIS
  END
  BUT_ONLY_IF_IT_CHANGES

/////                                                  \\\\\
///// BG2 Values                                       \\\\\
/////                                                  \\\\\

BEGIN @403100 DESIGNATED 4031 // Use BG2 Stats
GROUP @0
SUBCOMPONENT @403000 // Stat Changes: Edwin

// this file does nothing, it just allows other mods to detect this component
COPY_EXISTING ~sw1h01.itm~ ~override/cdt04031.g3~

COPY_EXISTING_REGEXP GLOB ~^_?edwin[0-9]*\.cre$~ ~override~
  PATCH_IF (SOURCE_SIZE > 0x2d3) THEN BEGIN // protects against invalid files
    WRITE_BYTE 0x238 10 // 10 STR
    WRITE_BYTE 0x23b 10 // 10 WIS
  END
  BUT_ONLY_IF_IT_CHANGES

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// Stat changes: Jaheira                            \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

/////                                                  \\\\\
///// BG Values                                        \\\\\
/////                                                  \\\\\

BEGIN @403001 DESIGNATED 4040 // Use BG Stats
GROUP @0
SUBCOMPONENT @404000 // Stat Changes: Jaheira

// this file does nothing, it just allows other mods to detect this component
COPY_EXISTING ~sw1h01.itm~ ~override/cdt04040.g3~

COPY_EXISTING_REGEXP GLOB ~^_?jahei.*\.cre$~ ~override~
  READ_LONG 0x08 "strref" ELSE 0
  PATCH_IF (("%strref%" = 9456) OR ("%strref%" = 9475)) THEN BEGIN // exclude harper cre files
    WRITE_BYTE 0x23c 14 // 14 DEX
  END
  BUT_ONLY_IF_IT_CHANGES

/////                                                  \\\\\
///// BG2 Values                                       \\\\\
/////                                                  \\\\\

BEGIN @403100 DESIGNATED 4041 // Use BG2 Stats
GROUP @0
SUBCOMPONENT @404000 // Stat Changes: Jaheira

// this file does nothing, it just allows other mods to detect this component
COPY_EXISTING ~sw1h01.itm~ ~override/cdt04041.g3~

COPY_EXISTING_REGEXP GLOB ~^_?jahei.*\.cre$~ ~override~
  READ_LONG 0x08 "strref" ELSE 0
  PATCH_IF (("%strref%" = 9456) OR ("%strref%" = 9475)) THEN BEGIN // exclude harper cre files
    WRITE_BYTE 0x23c 17 // 17 DEX
  END
  BUT_ONLY_IF_IT_CHANGES

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// Change Jaheira to NG                             \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @405000 DESIGNATED 4050
GROUP @19
GROUP @0

// this file does nothing, it just allows other mods to detect this component
COPY_EXISTING ~sw1h01.itm~ ~override/cdt04050.g3~

COPY_EXISTING_REGEXP GLOB ~^_?jahei.*\.cre$~ ~override~
  READ_LONG 0x08  "strref" ELSE 0
  READ_BYTE 0x27b "align"  ELSE 0
  PATCH_IF ((("%strref%" = 9456) OR ("%strref%" = 9475)) AND ("%align%" = 34)) THEN BEGIN // exclude harper cre files
    WRITE_BYTE 0x27b 33 // neutral good
  END
  BUT_ONLY_IF_IT_CHANGES

COPY_EXISTING ~misc5x.itm~ ~override~ // harper pin
  READ_BYTE  0x1e "use1"
  READ_BYTE  0x21 "use2"
  WRITE_BYTE 0x1e (("%use1%" BOR 0b00001000) BAND 0b11111011) // adds GE-Neutral flag, removes good flag
  WRITE_BYTE 0x21 ("%use2%" BOR 0b10000000) // adds half-orc flag
  BUT_ONLY_IF_IT_CHANGES

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// Stat changes: Minsc                              \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

/////                                                  \\\\\
///// BG Values                                        \\\\\
/////                                                  \\\\\

BEGIN @403001 DESIGNATED 4060 // Use BG Stats
GROUP @0
SUBCOMPONENT @406000 // Stat Changes: Minsc

// this file does nothing, it just allows other mods to detect this component
COPY_EXISTING ~sw1h01.itm~ ~override/cdt04060.g3~

COPY_EXISTING_REGEXP GLOB ~^_?minsc[0-9]*\.cre$~ ~override~
  PATCH_IF (SOURCE_SIZE > 0x2d3) THEN BEGIN // protects against invalid files
    WRITE_BYTE 0x23c 15 // 15 DEX
    WRITE_BYTE 0x23d 15 // 15 CON
  END
  BUT_ONLY_IF_IT_CHANGES

/////                                                  \\\\\
///// BG2 Values                                       \\\\\
/////                                                  \\\\\

BEGIN @403100 DESIGNATED 4061 // Use BG2 Stats
GROUP @0
SUBCOMPONENT @406000 // Stat Changes: Minsc

// this file does nothing, it just allows other mods to detect this component
COPY_EXISTING ~sw1h01.itm~ ~override/cdt04061.g3~

COPY_EXISTING_REGEXP GLOB ~^_?minsc[0-9]*\.cre$~ ~override~
  PATCH_IF (SOURCE_SIZE > 0x2d3) THEN BEGIN // protects against invalid files
    WRITE_BYTE 0x23c 16 // 16 DEX
    WRITE_BYTE 0x23d 16 // 16 CON
  END
  BUT_ONLY_IF_IT_CHANGES

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// Stat changes: Viconia                            \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

/////                                                  \\\\\
///// BG Values                                        \\\\\
/////                                                  \\\\\

BEGIN @403001 DESIGNATED 4070 // Use BG Stats
GROUP @0
SUBCOMPONENT @407000 // Stat Changes: Viconia

// this file does nothing, it just allows other mods to detect this component
COPY_EXISTING ~sw1h01.itm~ ~override/cdt04070.g3~

COPY_EXISTING_REGEXP GLOB ~^\(bg\|_\)?viconi[0-9]*\.cre$~ ~override~
  READ_BYTE  0x5d  "mr" ELSE 50
  PATCH_IF ("%mr%" != 50) BEGIN
    WRITE_BYTE 0x5d 50
  END
  READ_BYTE  0x23b "wis" ELSE 15
  PATCH_IF ("%wis%" != 15) BEGIN
    WRITE_BYTE 0x23b 15 // 15 WIS
    READ_LONG  0x2a8 "mem_info"
    FOR (index = 1 ; index < 4 ; index = index + 1) BEGIN // adds level 2, 3, 4 bonus spell for 15 >> 18 WIS
      READ_LONG ("%mem_info%" + 0x04 + ("%index%" * 0x10)) "spells"
      PATCH_IF ("%spells%" > 0) BEGIN
        WRITE_LONG ("%mem_info%" + 0x04 + ("%index%" * 0x10)) ("%spells%" + 1)
      END
    END
  END
  BUT_ONLY_IF_IT_CHANGES

/////                                                  \\\\\
///// BG2 Values                                       \\\\\
/////                                                  \\\\\

BEGIN @403100 DESIGNATED 4071 // Use BG2 Stats
GROUP @0
SUBCOMPONENT @407000 // Stat Changes: Viconia

// this file does nothing, it just allows other mods to detect this component
COPY_EXISTING ~sw1h01.itm~ ~override/cdt04071.g3~

COPY_EXISTING_REGEXP GLOB ~^\(bg\|_\)?viconi[0-9]*\.cre$~ ~override~
  READ_BYTE  0x5d  "mr" ELSE 65
  PATCH_IF ("%mr%" != 65) BEGIN
    WRITE_BYTE 0x5d 65
  END
  READ_BYTE  0x23b "wis" ELSE 18
  PATCH_IF ("%wis%" != 18) BEGIN
    WRITE_BYTE 0x23b 18 // 18 WIS
    READ_LONG  0x2a8 "mem_info"
    FOR (index = 1 ; index < 4 ; index = index + 1) BEGIN // removes level 2, 3, 4 bonus spell for 18 >> 15 WIS
      READ_LONG ("%mem_info%" + 0x04 + ("%index%" * 0x10)) "spells"
      PATCH_IF ("%spells%" > 0) BEGIN
        WRITE_LONG ("%mem_info%" + 0x04 + ("%index%" * 0x10)) ("%spells%" - 1)
      END
    END
  END
  BUT_ONLY_IF_IT_CHANGES

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// Make Khalid a Fighter-Mage                       \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @408000 DESIGNATED 4080
GROUP @0
REQUIRE_PREDICATE ((FILE_EXISTS_IN_GAME ~_sw1h01.itm~) OR (FILE_EXISTS_IN_GAME ~bg1start.2da~)) @14 // Tutu or BGT only

ACTION_IF FILE_EXISTS_IN_GAME ~bg1start.2da~ THEN BEGIN // BGT

  // this file does nothing, it just allows other mods to detect this component
  COPY_EXISTING ~khalid2.cre~ ~override/cdt04080.g3~
    READ_ASCII 0xa4 "soundset" (396)

  COPY ~BG2_Tweaks/cre/p#khali1.cre~ ~override/khalid.cre~
       ~BG2_Tweaks/cre/p#khali2.cre~ ~override/khalid2.cre~
       ~BG2_Tweaks/cre/p#khali3.cre~ ~override/khalid4.cre~
       ~BG2_Tweaks/cre/p#khali4.cre~ ~override/khalid6.cre~
    SAY NAME1 #3138
    SAY NAME2 #3138
    WRITE_EVALUATED_ASCII 0xa4 "%soundset%" #396
    SAY BIO @408001
    WRITE_ASCII 0x280 ~khalid~  // death variable
    WRITE_ASCII 0x248 ~khalid~  // override script
    WRITE_ASCII 0x250 ~shout~   // class script
    WRITE_ASCII 0x260 ~wtasight~// general script
    WRITE_ASCII 0x268 ~dplayer~ // default script
    WRITE_ASCII 0x2cc ~khalid~  // dialogue file
    ADD_CRE_ITEM ~BOW03~  #0  #0 #0 ~IDENTIFIED~ ~WEAPON2~ EQUIP TWOHANDED
    ADD_CRE_ITEM ~AROW01~ #40 #0 #0 ~IDENTIFIED~ ~QUIVER2~
    ADD_CRE_ITEM ~HELM01~ #0  #0 #0 ~IDENTIFIED~ ~HELMET~
    ADD_CRE_ITEM ~SW1H01~ #0  #0 #0 ~IDENTIFIED~ ~INV5~
    ADD_CRE_ITEM ~POTN08~ #0  #0 #0 ~IDENTIFIED~ ~QITEM1~

  COPY_EXISTING ~khalid2.cre~ ~override~
                ~khalid4.cre~ ~override~
                ~khalid6.cre~ ~override~
    ADD_CRE_ITEM ~BRAC03~ #0 #0 #0 ~IDENTIFIED~ ~GLOVES~

  COPY_EXISTING ~khalid4.cre~ ~override~
                ~khalid6.cre~ ~override~
    ADD_CRE_ITEM ~CLCK14~ #0 #0 #0 ~IDENTIFIED~ ~CLOAK~

  COPY_EXISTING ~khalid6.cre~ ~override~
    ADD_CRE_ITEM ~RING06~ #0 #0 #0 ~IDENTIFIED~ ~LRING~

  COMPILE ~BG2_Tweaks/dlg/p#khalid_bgt.d~ USING ~BG2_Tweaks/languages/%s/p#khalid.tra~

  OUTER_SPRINT tutu_var ~~
  ACTION_IF (GAME_IS ~tutu tutu_totsc~) BEGIN
    OUTER_SPRINT tutu_var ~_~
  END
  EXTEND_BOTTOM ~ar6801.bcs~ ~BG2_Tweaks/baf/p#fw2301.baf~ EVALUATE_BUFFER
  EXTEND_TOP    ~khalid.bcs~ ~BG2_Tweaks/baf/p#khalid.baf~

END ELSE BEGIN // Tutu

  // this file does nothing, it just allows other mods to detect this component
  // extra code is to detect walking speeds from tutufix
  COPY_EXISTING ~_KHALID2.cre~ ~override/cdt04080.g3~
    READ_ASCII 0xa4 "soundset" (396) // copies existing soundset
    READ_BYTE  0x33 "eff_version"
    READ_LONG  0x2c4 "effects_offset"
    READ_LONG  0x2c8 "fx_num"
    PATCH_IF ("%eff_version%" = 0) BEGIN
      FOR (loops = 0 ; loops < fx_num ; loops = loops + 1) BEGIN
        READ_SHORT ("%effects_offset%" + ("%loops%" * 0x30)) "type"
        PATCH_IF (("%type%" = 176) OR ("%type%" = 126)) BEGIN
          SET "loops" = "%fx_num%"
          SET "eff_version" = 4
        END
      END
    END ELSE BEGIN
      FOR (loops = 0 ; loops < fx_num ; loops = loops + 1) BEGIN
        READ_LONG ("%effects_offset%" + 8 + ("%loops%" * 264)) "type"
        PATCH_IF (("%type%" = 176) OR ("%type%" = 126)) BEGIN
          SET "loops" = "%fx_num%"
          SET "eff_version" = 4
        END
      END
    END

  COPY ~BG2_Tweaks/cre/p#khali1.cre~ ~override/_khalid.cre~
       ~BG2_Tweaks/cre/p#khali2.cre~ ~override/_khalid2.cre~
       ~BG2_Tweaks/cre/p#khali3.cre~ ~override/_khalid4.cre~
       ~BG2_Tweaks/cre/p#khali4.cre~ ~override/_khalid6.cre~
    SAY NAME1 #3138
    SAY NAME2 #3138
    WRITE_EVALUATED_ASCII 0xa4 "%soundset%" #396
    SAY BIO @408001
    WRITE_ASCII 0x280 ~khalid~   // death variable
    WRITE_ASCII 0x248 ~_khalid~  // override script
    WRITE_ASCII 0x250 ~_shout~   // class script
    WRITE_ASCII 0x260 ~_tasight~ // general script
    WRITE_ASCII 0x268 ~_dplayer~ // default script
    WRITE_ASCII 0x2cc ~_khalid~  // dialogue file
    ADD_CRE_ITEM ~_bow03~  #0  #0 #0 ~IDENTIFIED~ ~WEAPON2~ EQUIP TWOHANDED
    ADD_CRE_ITEM ~_arow01~ #40 #0 #0 ~IDENTIFIED~ ~QUIVER2~
    ADD_CRE_ITEM ~_helm01~ #0  #0 #0 ~IDENTIFIED~ ~HELMET~
    ADD_CRE_ITEM ~_sw1h01~ #0  #0 #0 ~IDENTIFIED~ ~INV5~
    ADD_CRE_ITEM ~_potn08~ #0  #0 #0 ~IDENTIFIED~ ~QITEM1~
    PATCH_IF ("%eff_version%" = 4) BEGIN
      READ_LONG  0x2b0 "known_offset"
      READ_LONG  0x2b8 "slots_offset"
      READ_LONG  0x2bc "items_offset"
      READ_LONG  0x2c4 "effects_offset"
      READ_LONG  0x2c8 "fx_num"
      WRITE_LONG 0x2c8 ("%fx_num%" + 1)
      PATCH_IF ("%known_offset%" >= "%effects_offset%") BEGIN
        WRITE_LONG 0x2b0 ("%known_offset%" + 264)
      END
      PATCH_IF ("%slots_offset%" >= "%effects_offset%") BEGIN
        WRITE_LONG 0x2b8 ("%slots_offset%" + 264)
      END
      PATCH_IF ("%items_offset%" >= "%effects_offset%") BEGIN
        WRITE_LONG 0x2bc ("%items_offset%" + 264)
      END
      INSERT_BYTES  ("%effects_offset%"     ) 264
        WRITE_LONG  ("%effects_offset%" +  8) 176
        WRITE_LONG  ("%effects_offset%" + 12) 1
        WRITE_LONG  ("%effects_offset%" + 20) 0xfffffffd
        WRITE_LONG  ("%effects_offset%" + 24) 0
        WRITE_LONG  ("%effects_offset%" + 28) 9
        WRITE_SHORT ("%effects_offset%" + 36) 100
    END

  COPY_EXISTING ~_khalid2.cre~ ~override~
                ~_khalid4.cre~ ~override~
                ~_khalid6.cre~ ~override~
    ADD_CRE_ITEM ~_brac03~ #0 #0 #0 ~IDENTIFIED~ ~GLOVES~

  COPY_EXISTING ~_khalid4.cre~ ~override~
                ~_khalid6.cre~ ~override~
    ADD_CRE_ITEM ~_clck14~ #0 #0 #0 ~IDENTIFIED~ ~CLOAK~

  COPY_EXISTING ~_khalid6.cre~ ~override~
    ADD_CRE_ITEM ~_ring06~ #0 #0 #0 ~IDENTIFIED~ ~LRING~

  COMPILE ~BG2_Tweaks/dlg/p#khalid.d~
  
  OUTER_SPRINT tutu_var ~~
  ACTION_IF (GAME_IS ~tutu tutu_totsc~) BEGIN
    OUTER_SPRINT tutu_var ~_~
  END
  EXTEND_BOTTOM ~_ar2301.bcs~ ~BG2_Tweaks/baf/p#fw2301.baf~ EVALUATE_BUFFER  
  EXTEND_TOP    ~_khalid.bcs~ ~BG2_Tweaks/baf/p#khalid.baf~

END

// if bg-style proficiencies are installed, adjust proficiencies to match new system
ACTION_IF FILE_EXISTS_IN_GAME ~cdt02161.g3~ OR FILE_EXISTS_IN_GAME ~cdt02162.g3~ THEN BEGIN
  OUTER_SPRINT tutu_var ~~
  ACTION_IF (GAME_IS ~tutu tutu_totsc~) BEGIN
    OUTER_SPRINT tutu_var ~_~
  END
  
  COPY_EXISTING ~%tutu_var%khalid.cre~  ~override~
                ~%tutu_var%khalid2.cre~ ~override~
                ~%tutu_var%khalid4.cre~ ~override~
                ~%tutu_var%khalid6.cre~ ~override~
    PATCH_IF (SOURCE_SIZE > 0x2d3) BEGIN
      SET_BG2_PROFICIENCY ~UNASSIGNED104~ 0
      SET_BG2_PROFICIENCY ~PROFICIENCYLARGESWORD~ 2
    END
    BUT_ONLY
  
  COPY_EXISTING ~%tutu_var%khalid2.cre~ ~override~
                ~%tutu_var%khalid4.cre~ ~override~
                ~%tutu_var%khalid6.cre~ ~override~
    PATCH_IF (SOURCE_SIZE > 0x2d3) BEGIN
      SET_BG2_PROFICIENCY ~UNASSIGNED106~ 0
      SET_BG2_PROFICIENCY ~PROFICIENCYMISSILEWEAPONS~ 1
    END
    BUT_ONLY
END

// if tob-style npcs are installed, then adjust higher level creatures
ACTION_IF FILE_EXISTS_IN_GAME ~cdt04020.g3~ THEN BEGIN

  // load patch macro
  INCLUDE ~bg2_tweaks/lib/tob_style_npcs.tpa~
  
  COPY_EXISTING ~_khalid.cre~  ~override/_khalid2.cre~
                ~_khalid.cre~  ~override/_khalid4.cre~
                ~_khalid.cre~  ~override/_khalid6.cre~
                ~khalid.cre~   ~override/khalid2.cre~
                ~khalid.cre~   ~override/khalid4.cre~
                ~khalid.cre~   ~override/khalid6.cre~
    LAUNCH_PATCH_MACRO ~tob_style_npcs~
    BUT_ONLY_IF_IT_CHANGES              

END

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// Make Montaron an Assassin                        \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @409000 DESIGNATED 4090
GROUP @0
REQUIRE_PREDICATE ((FILE_EXISTS_IN_GAME ~_sw1h01.itm~) OR (FILE_EXISTS_IN_GAME ~bg1start.2da~)) @14 // Tutu or BGT only

ACTION_IF FILE_EXISTS_IN_GAME ~bg1start.2da~ THEN BEGIN // BGT

  // this file does nothing, it just allows other mods to detect this component
  COPY_EXISTING ~montar.cre~ ~override/cdt04090.g3~
    READ_ASCII 0xa4 "soundset" (396)

  COPY ~BG2_Tweaks/cre/_montar.cre~  ~override/montar.cre~
       ~BG2_Tweaks/cre/_montar2.cre~ ~override/montar2.cre~
       ~BG2_Tweaks/cre/_montar4.cre~ ~override/montar4.cre~
       ~BG2_Tweaks/cre/_montar6.cre~ ~override/montar6.cre~
    SAY NAME1 #2425
    SAY NAME2 #2425
    WRITE_EVALUATED_ASCII 0xa4 "%soundset%" #396
    WRITE_ASCII 0x34  ~montars~ #8   // small portrait
    WRITE_ASCII 0x3c  ~montarm~ #8   // large portrait
    WRITE_ASCII 0x248 ~montaron~     // Override Script
    WRITE_ASCII 0x250 ~shout~        // Class Script
    WRITE_ASCII 0x258 ~~ #8          // Race Script
    WRITE_ASCII 0x260 ~wtasight~     // General Script
    WRITE_ASCII 0x268 ~dplayer~ #8   // default Script
    WRITE_ASCII 0x280 ~montaron~ #18 // Death Variable
    WRITE_ASCII 0x2cc ~montar~ #8    // Dialogue file
    // use BGT item references instead of Tutu
    READ_LONG  0x2bc "itm_off" ELSE 0
    READ_LONG  0x2c0 "itm_num" ELSE 0
    FOR (index = 0 ; index < itm_num ; index = index + 1) BEGIN // searches through items
      READ_ASCII ("%itm_off%" + (0x14 * "%index%")) "item"
      PATCH_IF ("%item%" STRING_COMPARE_CASE "_leat04" = 0) BEGIN // find invalid resref
        WRITE_ASCII ("%itm_off%" + (0x14 * "%index%")) "leat04" #8 // corrected resref
      END ELSE
      PATCH_IF ("%item%" STRING_COMPARE_CASE "_sw1h07" = 0) BEGIN // find invalid resref
        WRITE_ASCII ("%itm_off%" + (0x14 * "%index%")) "sw1h07" #8 // corrected resref
      END ELSE
      PATCH_IF ("%item%" STRING_COMPARE_CASE "_potn08" = 0) BEGIN // find invalid resref
        WRITE_ASCII ("%itm_off%" + (0x14 * "%index%")) "potn08" #8 // corrected resref
      END ELSE
      PATCH_IF ("%item%" STRING_COMPARE_CASE "_potn14" = 0) BEGIN // find invalid resref
        WRITE_ASCII ("%itm_off%" + (0x14 * "%index%")) "potn14" #8 // corrected resref
      END
    END

END ELSE BEGIN // Tutu

  // this file does nothing, it just allows other mods to detect this component
  // extra code is to detect walking speeds from tutufix
  COPY_EXISTING ~_montar.cre~ ~override/cdt04090.g3~
    READ_ASCII 0xa4 "soundset" (396)
    READ_BYTE  0x33 "eff_version"
    READ_LONG  0x2c4 "effects_offset"
    READ_LONG  0x2c8 "fx_num"
    PATCH_IF ("%eff_version%" = 0) BEGIN
      FOR (loops = 0 ; loops < fx_num ; loops = loops + 1) BEGIN
        READ_SHORT ("%effects_offset%" + ("%loops%" * 0x30)) "type"
        PATCH_IF (("%type%" = 176) OR ("%type%" = 126)) BEGIN
          SET "loops" = "%fx_num%"
          SET "eff_version" = 4
        END
      END
    END ELSE BEGIN
      FOR (loops = 0 ; loops < fx_num ; loops = loops + 1) BEGIN
        READ_LONG ("%effects_offset%" + 8 + ("%loops%" * 264)) "type"
        PATCH_IF (("%type%" = 176) OR ("%type%" = 126)) BEGIN
          SET "loops" = "%fx_num%"
          SET "eff_version" = 4
        END
      END
    END

  COPY ~BG2_Tweaks/cre/_montar.cre~  ~override~
       ~BG2_Tweaks/cre/_montar2.cre~ ~override~
       ~BG2_Tweaks/cre/_montar4.cre~ ~override~
       ~BG2_Tweaks/cre/_montar6.cre~ ~override~
    SAY NAME1 #2425
    SAY NAME2 #2425
    WRITE_EVALUATED_ASCII 0xa4 "%soundset%" #396
    WRITE_ASCII 0x34 ~_ontars~   // small portrait
    WRITE_ASCII 0x3c ~_ontarl~   // large portrait
    WRITE_ASCII 0x248 ~_ontaron~ // Override Script
    WRITE_ASCII 0x250 ~_shout~   // Class Script
    WRITE_ASCII 0x258 ~~ #8      // Race Script
    WRITE_ASCII 0x260 ~_tasight~ // General Script
    WRITE_ASCII 0x268 ~_dplayer~ // defalt Script
    WRITE_ASCII 0x280 ~montaron~ // Death Variable
    WRITE_ASCII 0x2cc ~_montar~  // Dialogue file
    PATCH_IF ("%eff_version%" = 4) BEGIN
      READ_LONG  0x2b0 "known_offset"
      READ_LONG  0x2b8 "slots_offset"
      READ_LONG  0x2bc "items_offset"
      READ_LONG  0x2c4 "effects_offset"
      READ_LONG  0x2c8 "fx_num"
      WRITE_LONG 0x2c8 ("%fx_num%" + 1)
      PATCH_IF ("%known_offset%" >= "%effects_offset%") BEGIN
        WRITE_LONG 0x2b0 ("%known_offset%" + 264)
      END
      PATCH_IF ("%slots_offset%" >= "%effects_offset%") BEGIN
        WRITE_LONG 0x2b8 ("%slots_offset%" + 264)
      END
      PATCH_IF ("%items_offset%" >= "%effects_offset%") BEGIN
        WRITE_LONG 0x2bc ("%items_offset%" + 264)
      END
      INSERT_BYTES  ("%effects_offset%"     ) 264
        WRITE_LONG  ("%effects_offset%" +  8) 176
        WRITE_LONG  ("%effects_offset%" + 12) 1
        WRITE_LONG  ("%effects_offset%" + 20) 0xfffffffd
        WRITE_LONG  ("%effects_offset%" + 24) 0
        WRITE_LONG  ("%effects_offset%" + 28) 9
        WRITE_SHORT ("%effects_offset%" + 36) 100
    END

END

// if bg-style proficiencies are installed, adjust proficiencies to match new system
ACTION_IF FILE_EXISTS_IN_GAME ~cdt02161.g3~ OR FILE_EXISTS_IN_GAME ~cdt02162.g3~ THEN BEGIN
  OUTER_SPRINT tutu_var ~~
  ACTION_IF (GAME_IS ~tutu tutu_totsc~) BEGIN
    OUTER_SPRINT tutu_var ~_~
  END
  
  COPY_EXISTING ~%tutu_var%montar.cre~  ~override~
                ~%tutu_var%montar2.cre~ ~override~
                ~%tutu_var%montar4.cre~ ~override~
                ~%tutu_var%montar6.cre~ ~override~
    PATCH_IF (SOURCE_SIZE > 0x2d3) BEGIN
      SET_BG2_PROFICIENCY ~PROFICIENCYSPEAR~ 0
      SET_BG2_PROFICIENCY ~PROFICIENCYLARGESWORD~ 1
    END
    BUT_ONLY
  
  ACTION_IF FILE_EXISTS_IN_GAME ~cdt02162.g3~ THEN BEGIN // no weapon styles version
    COPY_EXISTING ~%tutu_var%montar4.cre~ ~override~
                  ~%tutu_var%montar6.cre~ ~override~
      PATCH_IF (SOURCE_SIZE > 0x2d3) BEGIN
        SET_BG2_PROFICIENCY ~UNASSIGNED113~ 0
        SET_BG2_PROFICIENCY ~PROFICIENCYMISSILEWEAPONS~ 1
      END
      BUT_ONLY
  END
END

// if tob-style npcs are installed, then adjust higher level creatures
ACTION_IF FILE_EXISTS_IN_GAME ~cdt04020.g3~ THEN BEGIN

  // load patch macro
  INCLUDE ~bg2_tweaks/lib/tob_style_npcs.tpa~
  
  COPY_EXISTING ~_montar.cre~  ~override/_montar2.cre~
                ~_montar.cre~  ~override/_montar4.cre~
                ~_montar.cre~  ~override/_montar6.cre~
                ~montar.cre~   ~override/montar2.cre~
                ~montar.cre~   ~override/montar4.cre~
                ~montar.cre~   ~override/montar6.cre~
    LAUNCH_PATCH_MACRO ~tob_style_npcs~
    BUT_ONLY_IF_IT_CHANGES

END

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// Change Korgan to NE                              \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @410000 DESIGNATED 4100
GROUP @0
REQUIRE_PREDICATE NOT FILE_EXISTS_IN_GAME ~_sw1h01.itm~ @10 // non-Tutu

// this file does nothing, it just allows other mods to detect this component
COPY_EXISTING ~sw1h01.itm~ ~override/cdt04100.g3~

COPY_EXISTING_REGEXP GLOB ~^korgan[0-9]*.*\.cre$~ ~override~
  WRITE_BYTE 0x27b 35 // neutral evil
  BUT_ONLY_IF_IT_CHANGES

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// Kagain legal CON                                 \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @411000 DESIGNATED 4110
GROUP @0
GROUP @19
REQUIRE_PREDICATE ((FILE_EXISTS_IN_GAME ~_sw1h01.itm~) OR (FILE_EXISTS_IN_GAME ~bg1start.2da~)) @14 // Tutu or BGT only

// this file does nothing, it just allows other mods to detect this component
COPY_EXISTING ~sw1h01.itm~ ~override/cdt04110.g3~

COPY_EXISTING_REGEXP GLOB ~^_?kagain[0-9]*.*\.cre$~ ~override~
  WRITE_BYTE 0x23d 19 // constitution
  BUT_ONLY_IF_IT_CHANGES